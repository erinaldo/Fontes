#Include "RwMake.ch"                            
/*                                
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммямммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  Ё RComA03  ╨ Autor Ё Cesar Moura         ╨ Data Ё 04/07/2006  ╨╠╠
╠╠лммммммммммьммммммммммймммммммомммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Importacao de Pedido de Compra de E-Procurement da Hold     ╨╠╠
╠╠лммммммммммьммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё CSU - CardSystem S.A                                        ╨╠╠
╠╠хммммммммммоммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

User Function RComA03()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                 Declara as Variaveis Utilizadas na Rotina                  Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Private cPerg	  := PADR("RCOMA3",LEN(SX1->X1_GRUPO))
Private cCadastro := "ImportaГЦo de Pedido de Compras"
Private aSay      := {}
Private aButton   := {}
Private cFile1
Private cFile2
Private nErros    := 0
Private lErros    := .F.
Private oProcess  := Nil
Private aCabe     := {}
Private aItem     := {}
Private aBlqForn  := {}
Private nOpc	  := 0


/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё           Verifica se o Grupo de Perguntas existe ou nao                   Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

ValidPerg( cPerg )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё               Carrega o Grupo de Perguntas na Tela                         Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Pergunte( cPerg , .T. )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё           Monta a Janela de Dialogo com as Mensagens Explicativas          Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

aAdd ( aSay , "                                                                                        " )
aAdd ( aSay , "Esta rotina tem como objetivo a realizacao de ImportaГЦo do Pedido de Compra do         " )
aAdd ( aSay , "                sistema da empresa Hold Tecnologia da Informacao.                       " )
aAdd ( aSay , "                                                                                        " )
aAdd ( aSay , "==> Sistemas Envolvidos : Protheus 8 / Hold                                             " )
aAdd ( aSay , "==> Formato do Arquivo  : Texto ( Asc II )                                              " )
aAdd ( aSay , "==> Nome do Arquivo     : PCAAMMDDHHMMSS.Txt                                            " )
aAdd ( aSay , "                                                                                        " )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё              Monta a os Botoes a serem Disponibilizados na Tela            Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

aAdd ( aButton , { 1 , .T. , { || nOpc := 1  , FechaBatch() }} )
aAdd ( aButton , { 2 , .T. , { || FechaBatch()              }} )
aAdd ( aButton , { 5 , .T. , { || Pergunte( cPerg , .T. )   }} )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё              Abre a Tela para o Processamento das Opcoes                   Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

FormBatch( cCadastro , aSay , aButton )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё              Caso o Usuario Tenha acessado o Botao de Ok                   Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

If nOpc == 1
   Processa( { |lEnd| RComA03A() } ,  "Processando o Arquivo : " + MV_PAR01 , "Aguarde" , .F. )
Endif   

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё Fecha os Arquivos Temporarios e Restaura as Areas Antes da Execucao da Rotina  Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

If Select("PED") > 0
   DbSelectArea("PED")
   DbCloseArea()
Endif

If Select("INC")  > 0
   DbSelectArea("INC")
   DbCloseArea()
Endif

Return                      

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммямммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  Ё RComA03A ╨ Autor Ё Cristiano Figueiroa ╨ Data Ё 30/06/2006  ╨╠╠
╠╠лммммммммммьммммммммммймммммммомммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Rotina Principal de Importacao de Pedido de Compras         ╨╠╠
╠╠╨          Ё                                                             ╨╠╠
╠╠лммммммммммьммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё CSU - CardSystem S.A                                        ╨╠╠
╠╠хммммммммммоммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function RComA03A()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё            Declara as Variaveis Locais Utilizadas na Rotina                Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё      Configura a Regua com a quantidade de Rotinas a ser Processadas       Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

ProcRegua( 18 ) //alterar ainda parametro 18 divide  a regra em partes iguais

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё          Verifica se o Arquivo Existe no Caminho Especificado              Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

If !File( Alltrim(MV_PAR01)  ) 
   Aviso( "AtenГЦo !" , "O Arquivo : " + Alltrim(MV_PAR01) + " nЦo foi encontrado !" , {"Ok"} , 1 , "OperaГЦo Cancelada" )
   Return   
Endif

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё              Cria os Arquivos Temporarios a serem utilizados               Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

MontaTrb()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                   Abre o Arquivo TXT a ser Importado                       Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

ImportaTrb() 

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё             Inicia as Validacoes conforme Lay-Out e Necessidade            Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

ValidTrb()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё               Realiza o Bloqueio dos Fornecedores                          Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

u_BloqForn( aBlqForn )
IncProc("Bloqueando os Fornecedores incluМdos pela Rotina" )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё               Caso haja Inconsistencias Exibe as Mesmas em Tela            Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

If INC->( Reccount()) > 0

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё Chama a Rotina que Copia o Arquivo Temporario de Inconsistencias para o Arquivo de Log Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

   CopyLog()

 /*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
   Ё Pergunta se o Usuario quer ou nЦo Visualizar o Relatorio de Inconsistenacias Ё
   юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

   If Aviso("AtenГЦo !" , "Deseja Visualizar o RelatСrio de Inconsistencias ? " , {"Sim","NЦo"} , 1 , "Inconsistencias no Processo" ) ==  1 
      RelInco()
      Return
   Endif

Endif

    /*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      Ё                Exibe mensagem de processamento com sucesso                 Ё
      юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      Aviso( "AtenГЦo !" , "Rotina Executada " , {"Ok"} , 1 , "" )


Return
/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммямммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  Ё MontaTrb ╨ Autor Ё Cristiano Figueiroa ╨ Data Ё 30/06/2006  ╨╠╠
╠╠лммммммммммьммммммммммймммммммомммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Monta o Arquivo Temporario para Importacao dos Dados        ╨╠╠
╠╠╨          Ё                                                             ╨╠╠
╠╠лммммммммммьммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё CSU - CardSystem S.A                                        ╨╠╠
╠╠хммммммммммоммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function MontaTrb()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                    Declara as variaveis utilizadas                         Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Local aFile1 := {}
Local aFile2 := {}

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                   Adiciona a Regua de Processamento                        Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

IncProc("Preparando o Ambiente para Importacao")

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                Monta a Estrutura dos Arquivos Temporarios                  Ё   
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

aFile1 := { ;                                                                  // Arquivo de Pedidos de Compra
             {"FILIAL"  , "C" , 002 , 0 } , {"NUMPED"    , "C" , 006 , 0 } ,; 
             {"DATAE"   , "D" , 008 , 0 } , {"RAZAO"     , "C" , 040 , 0 } ,;
             {"NREDUZ"  , "C" , 020	, 0 } , {"ENDE"      , "C" , 040 , 0 } ,;
             {"BAIRRO"  , "C" , 020 , 0 } , {"MUNIC	"     , "C" , 015 , 0 } ,;          
             {"TIPO"    , "C" , 001 , 0 } , {"CNPJ"      , "C" , 014 , 0 } ,;          
             {"CONDPAG" , "C" , 003 , 0 } , {"CONTATO"   , "C" , 015 , 0 } ,;    
             {"FILENT"  , "C" , 002 , 0 } , {"MOEDA"     , "N" , 002 , 0 } ,;          
             {"ITEMPC"  , "C" , 004 , 0 } , {"CODPROD"   , "C" , 015 , 0 } ,;          
             {"DESCPROD", "C" , 030 , 0 } , {"UNID"      , "C" , 002 , 0 } ,;          
             {"TIPOPROD", "C" , 002 , 0 } , {"QUANT"     , "N" , 012 , 2 } ,;                                           
             {"VLUNIT"  , "N" , 014 , 2 } , {"VLTOTAL"   , "N" , 014 , 2 } ,;                                           
             {"ALIQIPI" , "N" , 014 , 2 } , {"VLFRETE"   , "N" , 012 , 2 } ,;                                            
             {"NUMSC"   , "C" , 006 , 0 } , {"ITEMSC"    , "C" , 004 , 0 } ,;                                           
             {"NUMREQ"  , "C" , 015 , 0 } , {"DTENT"     , "D" , 008 , 0 } ,;                                           
             {"ARMAZEM" , "C" , 002 , 0 } , {"OBS"       , "C" , 030 , 0 } ,;                                           
             {"CCUSTO"  , "C" , 009 , 0 } , {"CONTAC"    , "C" , 020 , 0 } ,;                                           
             {"ITEMC"   , "C" , 009 , 0 } , {"CLVL"      , "C" , 009 , 0 } ,;                                           
             {"VLDESC"  , "N" , 014 , 2 } , {"ALIQICM"   , "N" , 005 , 2 } ,; 
             {"ESTADO"  , "C" , 002 , 0 } }									

aFile2 := { {"LINHA"    , "C" , 006 , 0 } , {"SEQ" , "C" , 001 , 0 } ,;
            {"DESCRICAO " , "C" , 200 , 0 } }                                 // Arquivo de Inconsistencias

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                      Cria area Temporaria no Sistema                       Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

cFile1 := CriaTrab( aFile1 )   // Arquivo de Trabalho
cFile2 := CriaTrab( aFile2 )   // Arquivo de Inconsistencias

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                     Cria o Arquivo Temporario no Sistema                   Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

DbUseArea(.T. , , cFile1 , "PED" , .T. )    // Arquivo de Pedido de Compras
DbUseArea(.T. , , cFile2 , "INC"  , .T. )   // Arquivo de Inconsistencias

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                     Cria o Indice Temporario no Sistema                    Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

IndRegua("PED" , cFile1 , "NUMPED"       ,,, "Criando Arquivo...")  
IndRegua("INC" , cFile2 , "LINHA"        ,,, "Criando Arquivo...")

Return

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммямммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁImportaTrb╨ Autor Ё Cristiano Figueiroa ╨ Data Ё 30/06/2006  ╨╠╠
╠╠лммммммммммьммммммммммймммммммомммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Importa o Arquivo Txt para o Arquivo Dbf Temporario         ╨╠╠
╠╠╨          Ё                                                             ╨╠╠
╠╠лммммммммммьммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё CSU CardSystem S.A                                          ╨╠╠
╠╠хммммммммммоммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function ImportaTrb()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                   Adiciona a Regua de Processamento                        Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

IncProc("Abrindo o Arquivo : " + Alltrim(MV_PAR01) )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё              Executa a Importacao do Txt para o Dbf temporario             Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

DbSelectArea("PED")
Append From &( Alltrim(MV_PAR01) ) SDF

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммямммммммммкмммммммямммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁValidTrb ╨ Autor Ё Cristiano Figueiroa ╨ Data Ё 30/06/2006  ╨╠╠
╠╠лммммммммммьмммммммммймммммммомммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao ЁValida o Arquivo de Trabalho conforme Lay-Out fornecido     ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       ЁCSU CardSystem S.A                                          ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function ValidTrb()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                Declara as Variaveis Utilizadas na Rotina                   Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Private aTemErro   := {}
Private nRecno     := 0
Private lTemErro   := .F.
Private lA2AutoErr := .F.
Private lC7AutoErr := .F.
Private lExistA2   := .F.
Private aDadosForn := {}
Private _cCodfil   := ""
Private _cCodForn  := ""
Private dDtEmissao := Ctod("  /  /  ")
Private dDtNessec  := Ctod("  /  /  ")
Private _cCodProd

         
/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                   Adiciona a Regua de Processamento                        Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

IncProc("Executando Validacoes no Arquivo")

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                  Abre o Arquivo Temporario para a Validacao                Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

DbSelectArea("PED")
DbGoTop()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                  Processa Todos os Registros do Arquivo                    Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Do While PED->( !Eof() )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё              Monta as VariАveis do LaГo                                    Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

   cNumPC     := PED->NUMPED
   lTemErro   := .F. 
   aTemErro   := {}
   aItem      := {}
   aForn	  := {}
   dDtEmissao := Ctod( Substr ( Dtoc ( PED->DATAE ) , 7 , 2 ) + "/" + Substr ( Dtoc ( PED->DATAE ) , 4 , 2 ) + "/" + Substr ( Dtoc ( PED->DATAE ) , 1 , 2 ))  
   dDtNessec  := Ctod( Substr ( Dtoc ( PED->DTENT ) , 7 , 2 ) + "/" + Substr ( Dtoc ( PED->DTENT ) , 4 , 2 ) + "/" + Substr ( Dtoc ( PED->DTENT ) , 1 , 2 ))  	
   _CodProd	  := SUBSTR(PED->CODPROD,12,4)	   

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                 Monta as InformaГУes do Fornecedor (p/ MSEXECAUTO)         Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
       
 		aForn:={  	{ "A2_NOME"  , PED->RAZAO        , Nil },;
					{ "A2_LOJA"  , "01"              , Nil },;				    
				    { "A2_NREDUZ", PED->NREDUZ       , Nil },;
				    { "A2_TIPO"  , PED->TIPO		  , Nil },;
				    { "A2_END"   , PED->ENDE         , Nil },;
				    { "A2_MUN"   , PED->MUNIC        , Nil },;
				    { "A2_CGC"   , PED->CNPJ         , Nil },;
				    { "A2_MSBLQL", "2"               , Nil },;
                    { "A2_EST"   , PED->ESTADO       , Nil } }
       
/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                 Monta o Cabecalho do Pedido de Compras (p/ MSEXECAUTO)     Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

	   aCabe    := { 	{"C7_NUM"     , cNumPC         , Nil },;
	                 	{"C7_EMISSAO" , dDtEmissao     , Nil },;
	                    {"C7_FORNECE" , " "            , Nil },;                    
	                 	{"C7_LOJA"    , " "            , Nil },;
	                 	{"C7_CONTATO" , PED->CONTATO   , Nil },;
						{"C7_COND"    , PED->CONDPAG   , Nil },;
	                 	{"C7_FILENT"  , PED->FILENT    , Nil } }
	        
/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё              Processa todos os itens desse Pedido de Compras               Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
   
   Do While PED->( !Eof() ) .And. PED->NUMPED == cNumPC
 
    /*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      Ё                 Atribui o Numero do Registro Atual                         Ё
      юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      nRecno := PED->( Recno() )
   
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                    Abre o Cadastro de Solicitacoes de Compras                 Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      DbSelectArea("SC1")
      DbSetOrder(1)
   
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                Posiciona no Cadastro de Solicitacoes de Compras               Ё
     Ё                Verifica se Existe a SolicitaГЦo de Compras                    Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If ! DbSeek( xFilial("SC1") + PED->NUMSC + PED->ITEMSC )     //TRATAR ---1
      	  GeraLog( nRecno , "A SolicitaГЦo de Compras NЗmero : " + Alltrim( PED->NUMSC ) + " na filial" + PED->FILIAL + " nЦo foi encontrado !" )
          lTemErro := .T.
      
      Else

    	  If  SC1->C1_QTDORIG - SC1->C1_QUJE  < PED->QUANT
              GeraLog( nRecno , "A Quantidade informada no " + PED->NUMPED + " И maior que a Quantidade disponМvel na SolicitaГЦo de Compras NЗmero : " + Alltrim( PED->NUMSC ))              
              lTemErro := .T.
       	  EndIf	  

      Endif

   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                        Abre o Cadastro de Produtos                            Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      DbSelectArea("SB1")
      DbSetOrder(1)
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                        Posiciona no Cadastro de Produtos                      Ё
     Ё                        Verifica se o Produto Existe no SB1                    Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If ! DbSeek( xFilial("SB1") + _CodProd )
         GeraLog( nRecno , "O produto de cСdigo : " + _CodProd + " nЦo foi encontrado !" )
         lTemErro := .T.
      Endif

   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                     Abre o Cadastro de Plano de Contas                        Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      DbSelectArea("CT1")
      DbSetOrder(1)             
     
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                  Posiciona no Cadastro de Plano de Contas                     Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If ! DbSeek( xFilial("CT1") + PED->CONTAC ) .And. !Empty( PED->CONTAC )
         GeraLog( nRecno , "A conta contАbil : " + Alltrim( PED->CONTAC ) + " nЦo foi encontrada !" )
         lTemErro := .T.
      Endif

   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                        Abre o Cadastro de Centro de Custo                     Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      DbSelectArea("CTT")
      DbSetOrder(1)             
   
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                   Posiciona no Cadastro de Centro de Custo                    Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If ! DbSeek( xFilial("CTT") + PED->CCUSTO ) .And. !Empty( PED->CCUSTO )
         GeraLog( nRecno , "O centro de custo : " + Alltrim( PED->CCUSTO ) + " nЦo foi encontrado !" )
         lTemErro := .T.      
      Endif

   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                        Abre o Cadastro de Itens Contabeis                     Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      DbSelectArea("CTD")
      DbSetOrder(1)             
     
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                   Posiciona no Cadastro de Itens Contabeis                    Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If ! DbSeek( xFilial("CTD") + PED->ITEMC ) .And. !Empty( PED->ITEMC )   //TRATAR ---1
         GeraLog( nRecno , "O item contabil : " + Alltrim( PED->ITEMC ) + " nЦo foi encontrado !" )
         lTemErro := .T.
      Endif

   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                       Abre o Cadastro de Classe de Valor                      Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      DbSelectArea("CTH")
      DbSetOrder(1)             
   
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                   Posiciona no Cadastro de Classe de Valor                    Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If ! DbSeek( xFilial("CTH") + PED->CLVL ) .And. !Empty( PED->CLVL )
         GeraLog( nRecno , "A classe de valor : " + Alltrim( PED->CLVL ) + " nЦo foi encontrada !" )
         lTemErro := .T.
      Endif
                    
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                       Valida a Quantidade Informada                           Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If PED->QUANT <= 0
         GeraLog( nRecno , "A quantidade nЦo pode ser menor ou igual a zero !" )
         lTemErro := .T.
      Endif

   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                       Valida o Valor UnitАrio                                 Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If PED->VLUNIT <= 0
         GeraLog( nRecno , "O Valor unitАrio nЦo pode ser menor ou igual a zero !" )
         lTemErro := .T.
      Endif

   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                       Valida o Valor Total                                    Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If PED->VLTOTAL <= 0
         GeraLog( nRecno , "O Valor Total do Pedido de Compra nЦo pode ser menor ou igual a zero !" )
         lTemErro := .T.
      Endif

   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                           Valida a Data de Emissao                            Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If Empty( PED->DATAE )
         GeraLog( nRecno , "A data de emissao estА em branco !" )
         lTemErro := .T.
      Endif

   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                           Valida o Armazem Informado                          Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If Empty( PED->ARMAZEM )
         GeraLog( nRecno , "O ArmazИm estА em branco !" )
         lTemErro := .T.
      Endif

   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                           Valida o Tipo do Fornecedor                         Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If !( PED->TIPO $ "FJX" )
         GeraLog( nRecno , "O tipo do Fornecedor deve ser F , J ou X !" )
         lTemErro := .T.
      Endif
   
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                           Valida o CNPJ do Fornecedor                         Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
      
      If !(Cgc(PED->CNPJ)) 
         GeraLog( nRecno , "CNPJ " + PED->CNPJ + " do Fornecedor " + PED->NREDUZ + " InvАlido " )
         lTemErro := .T.
      Endif
   
   
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                       Abre o Cadastro de Fornecedores                         Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      DbSelectArea("SA2")
      DbSetOrder(3)             
   
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                   Realiza Busca do Fornecedor pelo CNPJ/CPF                   Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If ! DbSeek( xFilial("SA2") + PED->CNPJ )
                  
      /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
        Ё  Chama a Funcao que Inclui Fornecedores Novos e Retorna o Codigo , Loja e se  Ё
        Ё  a gravacao ocorreu com sucesso.                                              Ё
        юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

         aDadosForn  := GeraForn( aForn , 3 )

      /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
        Ё      Atualiza o Flag que avisa se a gravacao ocorreu ou nao com sucesso       Ё
        юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

         lA2AutoErr  := aDadosForn[3]
         
      /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
        Ё         Caso tenha existido algum tipo de erro , gera o log de erros          Ё
        юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
         
         If lA2AutoErr
            GeraLog( nRecno , "Ocorreram erros na InclusЦo do Fornecedor" )
         	lTemErro := .T.
         Else

         /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
           Ё         Caso o fornecedor tenha sido gravado com sucesso retorna o Codigo     Ё
           Ё         e Loja do Fornecedor e atualiza o Cabecalho do Pedido.                Ё
           юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

            _cCodForn  := aDadosForn[1]
        	_cCodFil   := aDadosForn[2]

         	aCabe[3,2] := Alltrim(_cCodForn)
         	aCabe[4,2] := Alltrim(_cCodFil)         

         Endif
            
      Else
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё        Caso encontre o Fornecedor , verifica se o mesmo esta bloqueado        Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
      
         If SA2->A2_MSBLQL == "1"
         	GeraLog( nRecno , "Fornecedor" + SA2->A2_COD + " Bloqueado, solicite o desbloqueio do cadastro !!!" )
         	lTemErro := .T.

		 Else

		 	aCabe[3,2] := SA2->A2_COD
         	aCabe[4,2] := SA2->A2_LOJA         

		 EndIf

	  EndIf      
      
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                           Valida a Data de Necessidade                        Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If Empty( PED->DTENT )
         GeraLog( nRecno , "A data da necessidade estА em branco !" )
         lTemErro := .T.
      Endif
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                           Abre Cadastro de Empresas                           Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      DbSelectArea("SM0")
      DbSetOrder(1)             
   
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                           Valida a Filial de Entrega                          Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
      If ! DbSeek( SM0->M0_CODIGO + PED->FILIAL ) .And. !Empty( PED->FILIAL )
         GeraLog( nRecno , "A Filial informada no arquivo : " + Alltrim( PED->FILIAL ) + " nЦo foi encontrada no Cadastro de Empresas " )
         lTemErro := .T.
      Endif
      
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                       Abre o Cadastro de Unidades de Medidas                  Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      DbSelectArea("SAH")
      DbSetOrder(1)             
   
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                           Valida a Unidade de Medida                          Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If ! DbSeek( xFilial("SAH") + PED->UNID ) .And. !Empty( PED->UNID )
         GeraLog( nRecno , "A Unidade de Medida do Produto : " + Alltrim( PED->DESCPROD ) + " nЦo foi encontrada no Cadastro de Unidades de Medidas (SAH) " )
         lTemErro := .T.
      Endif

   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                       Abre o Cadastro de Tipos                                Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

	  DbSelectArea("SX5")
      DbSetOrder(1)

   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                           Valida o Tipo de Produto                            Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
      
      If ! DbSeek(xFilial("SX5")+"02" + PED->TIPOPROD) .And. !Empty( PED->TIPOPROD)
         GeraLog( nRecno , "O Tipo de Produto : " + Alltrim( PED->TIPO ) + " nЦo foi encontrada no Cadastro de Tipos (SX5 - Tabela 02) " )
         lTemErro := .T.	
      EndIf		
      		
      		
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                           Atualiza o Flag de Erro                             Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
  
      If lTemErro .Or. Ascan( aTemErro , PED->NUMPED ) > 0 
         Aadd ( aTemErro , PED->NUMPED )     
      Else

      /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
        Ё             Monta os Itens do Pedido de Compras                     Ё
        юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

         Aadd ( aItem,{{"C7_ITEM"    , Alltrim(PED->ITEMPC)	,Nil } ,;     //TRATAR ---1
                       {"C7_PRODUTO" , _CodProd	                ,Nil } ,;
                       {"C7_QUANT"   , PED->QUANT				,Nil } ,;
                       {"C7_PRECO"   , PED->VLUNIT				,Nil },;
                       {"C7_DATPRF"  , dDtNessec				,Nil } ,;
                       {"C7_FLUXO"   , "S"						,Nil } ,;
                       {"C7_NUMSC"   , Alltrim(PED->NUMSC)		,Nil } ,;     //TRATAR 0 A ESQUERDA
                       {"C7_ITEMSC"  , Alltrim(PED->ITEMSC)		,Nil } ,;     //TRATAR ---1
                       {"C7_QTDSOL"  , PED->QUANT				,Nil } ,;
                       {"C7_LOCAL"   , Alltrim(PED->ARMAZEM)	,Nil } ,;
                       {"C7_OBS"     , PED->OBS					,Nil } ,;
                       {"C7_CC"      , PED->CCUSTO				,Nil } ,;
                       {"C7_CONTA"   , PED->CONTAC				,Nil } ,;
                       {"C7_ITEMCTA" , PED->ITEMC				,Nil } ,;
                       {"C7_CLVL"    , PED->CLVL				,Nil } ,;
                       {"C7_VLDESC"  , PED->VLDESC				,Nil } ,;   
                       {"C7_IPI"     , PED->ALIQIPI				,Nil } ,;   
                       {"C7_PICM"    , PED->ALIQICM				,Nil } } )
 
      Endif

    /*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      Ё            Processa o Proximo Registro do Arquivo Temporario               Ё
      юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
 
      DbSelectArea("PED")
      DbSkip()
      Loop

   Enddo

 /*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
   Ё        Vericando se existe cabecalho ou item do Pedido de compras          Ё
   юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
   
   If Len( aItem ) > 0 .And. Len( aCabe ) > 0 .And. Len( aTemErro) == 0

   /*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                        Gera a Pedido de Compras                            Ё
     юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
      
      lC7AutoErr := GeraPedExecAuto( aCabe , aItem )

   /*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                        Caso haja erros , gera o Log                        Ё
     юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
      
      If lC7AutoErr
         GeraLog( nRecno , "Ocorreram Erros na GravaГЦo do Pedido de Compras NЗmero : " + cNumPC   )
         lTemErro := .T.
      EndIf
      	 
   Endif
   
Enddo

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммямммммммммммммммммкмммммммяммммммммммммммммммммкммммммямммммммммммм╩╠╠
╠╠╨Programa  Ё GeraForn        ╨ Autor ЁCesar Moura         ╨ Data Ё 04/07/2006 ╨╠╠
╠╠лммммммммммьмммммммммммммммммймммммммоммммммммммммммммммммйммммммомммммммммммм╧╠╠
╠╠╨Descricao Ё Cria Fornecedor via MsExecAuto  	                                ╨╠╠
╠╠╨          Ё                                                                  ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё CSU CardSystem S.A                                               ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function GeraForn( aForn, Opcao)                      

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                 Declara as Variaveis Utilizadas na Rotina                  Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Local aArea 	:= GetArea()
Local lComErro  := .F.
Local _cForn
Local _cLoja

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё           Adiciona no Array os Produtos a serem Bloqueados          Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
//MsgAlert(aforn[7,2])

If Ascan ( aBlqForn , aForn[7,2]) == 0
   Aadd  ( aBlqForn , aForn[7,2])
Endif   

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                 Limpa as Variaveis referentes a MsExec                     Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

lMsErroAuto 	:= .F. 
lMsHelpAuto 	:= .F.	
		            
/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё               Executa a Gravacao do Fornecedor                      Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
 
MsExecAuto( { |x,y| Mata020( x , y ) } , aForn , Opcao )
	 
/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё Carrega o Grupo de Perguntas Especifico da Rotina                   Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Pergunte( cPerg , .F. )

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё Caso exista erro de Gravacao                                        Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
						
If lMsErroAuto
   MostraErro()
   lComErro    := .T.   
   lMsErroAuto := .F. 
   lMsHelpAuto := .F.	
Endif         

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё Atribui valores as variАveis                                        Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

_cForn := SA2->A2_COD
_cLoja := SA2->A2_LOJA

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё Restaura a Area Antes da Execucao da Rotina                         Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

RestArea( aArea )


Return { _cForn, _cLoja , lComErro} 
   
/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммямммммммммммммммммкмммммммяммммммммммммммммммммкммммммямммммммммммм╩╠╠
╠╠╨Programa  Ё GeraPedExecAuto ╨ Autor ЁCristiano Figueiroa ╨ Data Ё 30/06/2006 ╨╠╠
╠╠лммммммммммьмммммммммммммммммймммммммоммммммммммммммммммммйммммммомммммммммммм╧╠╠
╠╠╨Descricao Ё Gera Pedido de Compras via ExecAuto                              ╨╠╠
╠╠╨          Ё                                                                  ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё CSU CardSystem S.A                                               ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function GeraPedExecAuto( aCabe , aItem )

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё        Declara as Variaveis Internas das Rotinas Automaticas        Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Local aArea 	:= GetArea()
Local lComErr   := .F.

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                 Limpa as variaveis da MsExecAuto                    Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

lMsErroAuto 	:= .F. 
lMsHelpAuto 	:= .F.	

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё Chama a Rotina de Gravacao Automatica - ExecAuto                    Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

MSExecAuto( { |v,x,y,z| MATA120(v,x,y,z) } , 1 , aCabe , aItem , 3 )    

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё Carrega o Grupo de Perguntas Especifico da Rotina                   Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Pergunte( cPerg , .F. )

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё Caso exista erro de Gravacao                                        Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
					
If lMsErroAuto
   MostraErro()
   lComErro    := .T.   
   lMsErroAuto := .F. 
   lMsHelpAuto := .F.	
Endif         


RestArea( aArea )

Return lComErr 

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммямммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  Ё BloqForn ╨ Autor Ё Analistas Microsiga ╨ Data Ё 29/08/2006  ╨╠╠
╠╠лммммммммммьммммммммммймммммммомммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Rotina para Bloquear os Fornecedores Incluidos pela Rotina  ╨╠╠
╠╠╨          Ё                                                             ╨╠╠
╠╠лммммммммммьммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨ParametrosЁ Array com os Fornecedores a serem Bloqueados                ╨╠╠
╠╠╨          Ё                                                             ╨╠╠
╠╠лммммммммммьммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё CSU CardSystem S.A                                          ╨╠╠
╠╠хммммммммммоммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

User Function BloqForn( aMsBlqlForn )

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                    Declara as Variaveis da Rotina                   Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Local aArea    := GetArea()
Local e        := 1

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                        Abre o Cadastro de Fornecedores                        Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

DbSelectArea("SA2")
DbSetOrder(3)

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                    Bloqueia todos os Fornecedores do Array                    Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

For e := 1 to Len ( aMsBlqlForn )
  
/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                        Posiciona no Cadastro de Fornecedores                  Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
   
   If DbSeek( xFilial("SA2") + aMsBlqlForn[e]  )
                                    
   /*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                              Realiza o Bloqueio                            Ё
     юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      Reclock("SA2" , .F. )     
      	SA2->A2_MSBLQL := "1"
      MsUnlock()   

   /*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё    Chama a Funcao que ira Disparar o WorkFlow de Desbloqueio do Fornecedor Ё
     юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      u_Rwfwa03()
      
   Else
   
   
   
   Endif
   
   
Next e   

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё         Restaura as Areas Utilizadas antes da Execucao da Rotina               Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

RestArea(aArea)



Return
     
/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммямммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁValidPerg ╨ Autor Ё Analistas Microsiga ╨ Data Ё 13/02/2006  ╨╠╠
╠╠лммммммммммьммммммммммймммммммомммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Cria o Grupo de Perguntas conforme Array                    ╨╠╠
╠╠╨          Ё                                                             ╨╠╠
╠╠лммммммммммьммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё CSU 					                                       ╨╠╠
╠╠хммммммммммоммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function ValidPerg( cPerg )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё            Declara as Variaveis Locais Utilizadas na Rotina                Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Local aRegs := {}

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё            Adiciona as Perguntas no Array a ser Gravado no SX1             Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

aAdd( aRegs , { cPerg , "01" , "Arquivo de Importacao ?"      , "         	   " , "            " , "MV_CH1" , "C" , 24 , 0 , 0 , "G" , "u_SelArquivo()" , "MV_PAR01" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , ""})
aAdd( aRegs , { cPerg , "02" , "Caminho dos Arquivos de Log ?" , "            " , "            " , "MV_CH2" , "C" , 50 , 0 , 0 , "G" , "" , "MV_PAR02" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , ""})


/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё            Verifica se Ja Existe Registro no SX1 e Grava                   Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

DbSelectArea("SX1")
DbSetOrder(1)

For i := 1 to Len( aRegs )
   If !SX1->(dbSeek(cPerg+aRegs[i,2]))
      RecLock("SX1",.T.)
	  For j:=1 to FCount()
	     If j <= Len(aRegs[i])
		    FieldPut(j,aRegs[i,j])
		 Endif
	  Next
	  SX1->(MsUnlock())
   Endif
Next  

Return

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁRelIncons ╨ Autor Ё Cristiano Figueiroa╨ Data Ё 30/06/2006  ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao ЁImprime o Relatorio de Inconsistencias do Processo de       ╨╠╠
╠╠╨          ЁImportacao dO Pedido de Compras.                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       ЁProtheus 8.11                                               ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/ 

Static Function RelInco()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё Define as variaveis necessarias                                      Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

cString  := "PED"
cDesc1   := "Esta rotina tem como Objetivo Imprimir os Registros Inconsistentes "
cDesc2   := "do Processo de ImportaГao de Pedido de Compras existentes nos Arquivos "
cDesc3   := "Gerados pelo Sistema da Empresa Hold - EspecМfico CSU CardSystem S.A"
tamanho  := "G"
aReturn  := { "Zebrado", 1 , "Contabilidade" , 1 , 1 , 2 , "" , 1 }
Nomeprog := "Incons"
Wnrel    := "Incons"
nLastKey := 0
m_pag    := 1
nLin     := 80
cTitulo  := "Relatorio de Inconsistencias - Processo de ImportaГЦo do Pedido de Compras do Sistema Hold "
cCabec2  := ""
cCabec1  := "Linha   Inconsistencia "

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                   Adiciona a Regua de Processamento                        Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

 IncProc("Gerando RelatСrio de Inconsistencias")
 
/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё               Envia Controle para a Funcao SetPrint                   Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

wnrel := SetPrint( cString , Wnrel , , cTitulo , cDesc1 , cDesc2 , cDesc3 , .F. , "" , .F. , Tamanho )

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё               Verifica o Cancelamento do Relatorio                    Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

If nLastKey == 27
   Return
Endif

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                           Chama a SetDefault                          Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

SetDefault(aReturn,cString)

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё               Verifica o Cancelamento do Relatorio                    Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

If nLastKey == 27
   Return
Endif

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё            Chama a Rotina de Processamento do Relatorio               Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

RptStatus( { || PrintInco() }, "Processando o Relatorio de Inconsistencias" )

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤Ao    ЁPrintInco Ё Autor Ё Cristiano Figueiroa   Ё Data Ё30/06/2006Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁProcessa os Itens do Relatorio de Inconsistencias           Ё╠╠
╠╠Ё			 Ё 														      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё CSU CardSystem S.A                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function PrintInco()

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                  Abre o Arquivo de Inconsistencias                    Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

DbSelectArea("INC")
DbGotop()
SetRegua(Reccount())

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё               Processa as Inconsistencias no Relatorio                Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Do While INC->( !Eof() )
	
  /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Imprime o cabecalho                                               Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	
	If nLin >= 60
	   nLin := Cabec( cTitulo, cCabec1, cCabec2, Nomeprog , Tamanho , 15 ) + 1
	   nLin ++
	Endif
	
  /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Imprime a Linha de Detalhe                                        Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	
	@ nLin , 000  PSay INC->LINHA
	@ nLin , 008  PSay INC->DESCRICAO
	nLin ++

  /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё             Processa o Proximo Registro Inconsistente             Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

	DbSelectarea("INC")
	IncRegua()
	DbSkip()
	
Enddo

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё Imprime o Rodape do Relatorio                                     Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Roda( 0 , "" , Tamanho )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё Caso a Impressao seja realizada em Disco , Exibe na Tela o Relatorio Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

If aReturn[5] == 1
   Set Printer To
   Commit
   OurSpool(Wnrel)
Endif

MS_FLUSH()

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤Ao    Ё CopyLog  Ё Autor Ё Cristiano Figueiroa   Ё Data Ё30/06/2006Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁCopia o Arquivo Temporario de Inconsistencias para o        Ё╠╠
╠╠Ё			 ЁDiretorio de Log                    		                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё CSU CardSystem S.A                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function CopyLog()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                  Declara as Variaveis Utilizadas na Rotina                 Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Local cDiretorio := Alltrim(MV_PAR02)
Local cDestino   := DtoS(Date()) + StrTran( Time() , ":" , "" )  + ".XLS"

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                  Gera o Cabecalho do Arquivo de Log                        Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
GeraLog( 0 , Replicate("=" , 220 )   , "0" )
GeraLog( 0 , "Arquivo : " + MV_PAR01 , "0" )
GeraLog( 0 , "Data    : " + Dtoc(Date()) , "0" )
GeraLog( 0 , "Hora    : " + Time() , "0" )
GeraLog( 0 , "Usuario : " + Substr(cUsuario , 7 , 15 ) , "0" )
GeraLog( 0 , "Empresa : " + SM0->M0_NOME + " - " + SM0->M0_FILIAL , "0" )
GeraLog( 0 , "" , "0" )
GeraLog( 0 , Replicate("=" , 220 ) , "0" )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё   Copia o Arquivo Temporario de Inconsistencias para o Diretorio de Log    Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

DbSelectArea("INC")
Copy to &(Alltrim(cDiretorio) + Alltrim(cDestino)) SDF

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммямммммммммкмммммммямммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  Ё GeraLog ╨ Autor Ё Cristiano Figueiroa ╨ Data Ё 30/06/2006  ╨╠╠
╠╠лммммммммммьмммммммммймммммммомммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao ЁGrava o Arquivo de Log a Inconsistencia encontrada no       ╨╠╠
╠╠╨          ЁProcessamento.                                              ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       ЁCSU CardSystem S.A                                          ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function GeraLog( nLinha , cDescricao , cSequencia )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё            Declara as Variaveis  Utilizadas na Rotina            Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Local aArea := GetArea()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё               Abre a Tabela de Inconsistencias                   Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

DbSelectArea("INC")

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё               Grava o Registro de Inconsistencias                Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Reclock( "INC" , .T. )
INC->LINHA     := Iif ( nLinha == 0 , "" , StrZero( nLinha , 5 ) )
INC->DESCRICAO := cDescricao
INC->SEQ       := Iif ( cSequencia == Nil , "2" , cSequencia )
Msunlock()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                 Restaura as Areas Utilizadas                     Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

RestArea(aArea)

Return