#Include "RwMake.ch"                              
/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммямммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  Ё RComA00  ╨ Autor Ё Cristiano Figueiroa ╨ Data Ё 30/06/2006  ╨╠╠
╠╠╨          Ё          ╨ Alter Ё Marcelo Varella     ╨ Data Ё 05/07/2006  ╨╠╠
╠╠лммммммммммьммммммммммймммммммомммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Importacao de Cadastro de Produto Geradas no Sistema de     ╨╠╠
╠╠╨          Ё E-Procurement da Hold.                                      ╨╠╠
╠╠лммммммммммьммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё CSU - CardSystem S.A                                        ╨╠╠
╠╠хммммммммммоммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

User Function RComA00()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                 Declara as Variaveis Utilizadas na Rotina                  Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Private cPerg     := PADR("RCOMA0",LEN(SX1->X1_GRUPO))
Private nOpc      := 0
Private cCadastro := "ImportaГЦo de Produtos Cadastrados"
Private aSay      := {}
Private aButton   := {}
Private cFile1
Private cFile2
Private nErros    := 0
Private lErros    := .F.
Private oProcess  := Nil

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё           Verifica se o Grupo de Perguntas existe ou nao                   Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

ValidPerg( cPerg )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                   Cria os Parametros Necessarios a Rotina                  Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

CriaSX6()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё               Carrega o Grupo de Perguntas na Tela                         Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Pergunte( cPerg , .T. )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё           Monta a Janela de Dialogo com as Mensagens Explicativas          Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

aAdd ( aSay , "                                                                                        " )
aAdd ( aSay , "  Esta rotina tem como objetivo a realizacao de ImportaГЦo dos Produtos cadastrados no  " )
aAdd ( aSay , "                sistema da empresa Hold Tecnologia da Informacao.                       " )
aAdd ( aSay , "                                                                                        " )
aAdd ( aSay , "==> Sistemas Envolvidos : Protheus 8 / Hold                                             " )
aAdd ( aSay , "==> Formato do Arquivo  : Texto ( Asc II )                                              " )
aAdd ( aSay , "==> Nome do Arquivo     : SB1AAMMDD.Txt                                                 " )
aAdd ( aSay , "                                                                                        " )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё              Monta a os Botoes a serem Disponibilizados na Tela            Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

aAdd ( aButton , { 1 , .T. , { || nOpc := 1  , FechaBatch() }} )
aAdd ( aButton , { 2 , .T. , { || FechaBatch()              }} )
aAdd ( aButton , { 5 , .T. , { || Pergunte( cPerg , .T. )   }} )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё              Abre a Tela para o Processamento das Opcoes                   Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

FormBatch( cCadastro , aSay , aButton )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё              Caso o Usuario Tenha acessado o Botao de Ok                   Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

If nOpc == 1
   Processa( { |lEnd| RComA00A() } ,  "Processando o Arquivo : " + MV_PAR01 , "Aguarde" , .F. )
Endif   

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё Fecha os Arquivos Temporarios e Restaura as Areas Antes da Execucao da Rotina  Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

If Select("PRD") > 0
   DbSelectArea("PRD")
   DbCloseArea()
Endif

If Select("INC")  > 0
   DbSelectArea("INC")
   DbCloseArea()
Endif

Return                      

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммямммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  Ё RComA00a ╨ Autor Ё Cristiano Figueiroa ╨ Data Ё 30/06/2006  ╨╠╠
╠╠           Ё          ╨ Alt   Ё Marcelo Varella     ╨ Data Ё 05/07/2006  ╨╠╠
╠╠лммммммммммьммммммммммймммммммомммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Rotina Principal de Importacao de Produtos Cadastrados      ╨╠╠
╠╠╨          Ё                                                             ╨╠╠
╠╠лммммммммммьммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё CSU - CardSystem S.A                                        ╨╠╠
╠╠хммммммммммоммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function RComA00a()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё            Declara as Variaveis Locais Utilizadas na Rotina                Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё      Configura a Regua com a quantidade de Rotinas a ser Processadas       Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

ProcRegua( 18 )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё          Verifica se o Arquivo Existe no Caminho Especificado              Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

If !File( Alltrim(MV_PAR01)  ) 
   Aviso( "AtenГЦo !" , "O Arquivo : " + Alltrim(MV_PAR01) + " nЦo foi encontrado !" , {"Ok"} , 1 , "OperaГЦo Cancelada" )
   Return   
Endif

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё              Cria os Arquivos Temporarios a serem utilizados               Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

MontaTrb()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                   Abre o Arquivo TXT a ser Importado                       Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

ImportaTrb() 

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё             Inicia as Validacoes conforme Lay-Out e Necessidade            Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

ValidTrb()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё               Caso haja Inconsistencias Exibe as Mesmas em Tela            Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

If INC->( Reccount()) > 0

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё Chama a Rotina que Copia o Arquivo Temporario de Inconsistencias para o Arquivo de Log Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

   CopyLog()

 /*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
   Ё Pergunta se o Usuario quer ou nЦo Visualizar o Relatorio de Inconsistenacias Ё
   юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

   If Aviso("AtenГЦo !" , "Deseja Visualizar o RelatСrio de Inconsistencias ? " , {"Sim","NЦo"} , 1 , "Inconsistencias no Processo" ) ==  1 
      RelInco()
      Return
   Endif

Endif

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
 Ё                Exibe mensagem de processamento com sucesso                 Ё
 юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Aviso( "AtenГЦo !" , "Arquivo foi Importado com Sucesso !" , {"Ok"} , 1 , "ImportaГЦo Finalizada !" )

Return


/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммямммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  Ё MontaTrb ╨ Autor Ё Cristiano Figueiroa ╨ Data Ё 30/06/2006  ╨╠╠
╠╠╨          Ё          ╨ Alt   Ё Marcelo Varella     ╨ Data Ё 05/07/2006  ╨╠╠
╠╠лммммммммммьммммммммммймммммммомммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Monta o Arquivo Temporario para Importacao dos Dados        ╨╠╠
╠╠╨          Ё                                                             ╨╠╠
╠╠лммммммммммьммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё CSU - CardSystem S.A                                        ╨╠╠
╠╠хммммммммммоммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function MontaTrb()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                    Declara as variaveis utilizadas                         Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Local aFile1 := {}
Local aFile2 := {}

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                   Adiciona a Regua de Processamento                        Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

IncProc("Preparando o Ambiente para Importacao")

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                Monta a Estrutura dos Arquivos Temporarios                  Ё   
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

aFile1 := { ;                                                                 // Arquivo de Produtos
             {"FILIAL  " , "C" , 002 , 0 } , {"PRODUTO  " , "C" , 015 , 0 } ,;
             {"DESCPRD " , "C" , 030 , 0 } , {"TIPOPRD  " , "C" , 002 , 0 } ,;
             {"UNID    " , "C" , 002 , 0 } , {"LOCALPADR" , "C" , 002 , 0 } ,;          
             {"GRUPOPRD" , "C" , 004 , 0 } , {"BLOQUEADO" , "C" , 001 , 0 } }          

aFile2 := { {"LINHA     " , "C" , 006 , 0 } , {"SEQ" , "C" , 001 , 0 } ,;
            {"DESCRICAO " , "C" , 200 , 0 } }                                 // Arquivo de Inconsistencias

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                      Cria area Temporaria no Sistema                       Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

cFile1 := CriaTrab( aFile1 )   // Arquivo de Trabalho
cFile2 := CriaTrab( aFile2 )   // Arquivo de Inconsistencias

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                     Cria o Arquivo Temporario no Sistema                   Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

DbUseArea(.T. , , cFile1 , "PRD" , .T. )  // Arquivo de Produto
DbUseArea(.T. , , cFile2 , "INC"  , .T. ) // Arquivo de Inconsistencias

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                     Cria o Indice Temporario no Sistema                    Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

IndRegua("PRD" , cFile1 , "PRODUTO"   ,,, "Criando Arquivo...")  
IndRegua("INC" , cFile2 , "LINHA  "   ,,, "Criando Arquivo...")

Return

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммямммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁImportaTrb╨ Autor Ё Cristiano Figueiroa ╨ Data Ё 30/06/2006  ╨╠╠
╠╠╨          Ё          ╨ Adapt Ё Marcelo Varella     ╨ Data Ё 05/07/2006  ╨╠╠
╠╠лммммммммммьммммммммммймммммммомммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Importa o Arquivo Txt para o Arquivo Dbf Temporario         ╨╠╠
╠╠╨          Ё                                                             ╨╠╠
╠╠лммммммммммьммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё CSU CardSystem S.A                                          ╨╠╠
╠╠хммммммммммоммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function ImportaTrb()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                   Adiciona a Regua de Processamento                        Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

IncProc("Abrindo o Arquivo : " + Alltrim(MV_PAR01) )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё              Executa a Importacao do Txt para o Dbf temporario             Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

DbSelectArea("PRD")
Append From &( Alltrim(MV_PAR01) ) SDF

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммямммммммммкмммммммямммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁValidTrb ╨ Autor Ё Cristiano Figueiroa ╨ Data Ё 30/06/2006  ╨╠╠
╠╠╨          Ё         ╨ Adapt Ё Marcelo Varella     ╨ Data Ё 05/07/2006  ╨╠╠
╠╠лммммммммммьмммммммммймммммммомммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao ЁValida o Arquivo de Trabalho conforme Lay-Out fornecido     ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       ЁCSU CardSystem S.A                                          ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function ValidTrb()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                Declara as Variaveis Utilizadas na Rotina                   Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Private nAcao     	 	:= 3
Private aTemErro   		:= {}
Private nRecno     		:= 0
Private lTemErro   		:= .F.
Private lB1AutoErr 		:= .F.
Private cCodPRD    		:= ""


/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                   Adiciona a Regua de Processamento                        Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

IncProc("Executando Validacoes no Arquivo")

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                  Abre o Arquivo Temporario para a Validacao                Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

DbSelectArea("PRD")
DbGoTop()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                  Processa Todos os Registros do Arquivo                    Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Do While PRD->( !Eof() )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё              Atribui o numero da solicitacao de compras a variavel         Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

   cCodPRD  := PRD->PRODUTO
   lTemErro := .F. 
   aTemErro := {}
   aItem    := {}
   nRecno   := PRD->( Recno() ) 
   cLocPad  := ""
   
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                        Valida Filial                                          Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If !Empty( PRD->FILIAL )
         GeraLog( nRecno , "O Campo Filial deve vir em Branco !" )
         lTemErro := .T.
      Endif

   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                    Abre o Cadastro de Produtos                                Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      DbSelectArea("SB1")
      DbSetOrder(1)
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                    Valida se Produtos ja Existe no Protheus                   Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If DbSeek( PRD->FILIAL + PRD->PRODUTO)
         GeraLog( nRecno , " O Produto Cadastrado: " + Alltrim( PRD->PRODUTO ) + " na filial: " + (PRD->FILIAL) + " jА existe no sistema !" )
         lTemErro := .T.
      Endif

   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                        Valida Campo Codigo do Produto                         Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If Empty( PRD->PRODUTO )
         GeraLog( nRecno , "O Codigo do Produto estА em branco !" )
         lTemErro := .T.
      Endif


   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                        Valida DescriГЦo do Produto                            Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If Empty( PRD->DESCPRD )
         GeraLog( nRecno , "A DescriГЦo do Produto estА em branco !" )
         lTemErro := .T.
      Endif


   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                       Abre o Cadastro de Tipo de Produto                      Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      DbSelectArea("SX5")
      DbSetOrder(1)

   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                           Valida o Tipo de Produto                            Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If ! DbSeek(xFilial("SX5")+"02" + PRD->TIPOPRD) .And. !Empty(PRD->TIPOPRD)
         GeraLog( nRecno , "O Tipo de Produto : " + Alltrim( PRD->TIPOPRD ) + " nЦo foi encontrada no Cadastro de Tipos (SX5 - Tabela 02) " )
         lTemErro := .T.     
      EndIf                      

   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                       Abre o Cadastro de Unidades de Medidas                  Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      DbSelectArea("SAH")
      DbSetOrder(1)             

   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                           Valida a Unidade de Medida                          Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If ! DbSeek( xFilial("SAH") + PRD->UNID ) .And. !Empty( PRD->UNID )
         GeraLog( nRecno , "A Unidade de Medida do Produto : " + Alltrim( PRD->DESCPRD  ) + " nЦo foi encontrada no Cadastro de Unidades de Medidas (SAH) " )
         lTemErro := .T.
      Endif

   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                              Valida Local Padrao                              Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If Empty( PRD->LOCALPADR)
         cLocPad := "01"
      else
         cLocPad := PRD->LOCALPADR
      Endif

   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                              Valida Grupo de Produto                          Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If !Empty( PRD->GRUPOPRD )
         GeraLog( nRecno , "O Grupo do Produto deve estar em branco !" )
         lTemErro := .T.
      Endif


   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё                              Valida Situacao Bloqueado                        Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

      If !Empty( PRD->BLOQUEADO)
         GeraLog( nRecno , "A Situacao 'Bloqueado' do Produto deve estar em branco !" )
         lTemErro := .T.
      Endif

               
   /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
     Ё              Checa Erro e Monta itens para Cadastro do Produto                Ё
     юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
  
      If !lTemErro 
        
      /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
        Ё               Caso nao Exista , Chama a ExecAuto e Cria o Produto             Ё
        юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

        lB1AutoErr := IncluiProduto( PRD->FILIAL , PRD->PRODUTO , PRD->DESCPRD , PRD->TIPOPRD , PRD->UNID , cLocPad , PRD->GRUPOPRD , PRD->BLOQUEADO ) 

      /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
        Ё            Verifica se a inclusao do Produto deu Erro ou nao                  Ё
        юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

         If lB1AutoErr
            GeraLog( nRecno , "Ocorreram erros na InclusЦo do Produto : " + Alltrim( PRD->PRODUTO ) + " " + Alltrim( PRD->DESCPRD )  + " !" )
            lTemErro := .T.
         Endif           
       
        
/*      Reclock("SB1", .T.)
			 SB1->B1_FILIAL 	:=	PRD->FILIAL
             SB1->B1_COD    	:=  PRD->PRODUTO   
             SB1->B1_DESC   	:=  PRD->DESCPRD   
             SB1->B1_TIPO   	:=  PRD->TIPOPRD   
             SB1->B1_UM     	:=  PRD->UNID      
             SB1->B1_LOCPAD 	:=  cLocPad
             SB1->B1_GRUPO  	:=  PRD->GRUPOPRD  
             SB1->B1_MSBLQL 	:=  PRD->BLOQUEADO  
		MsUnlock()*/

      Endif 

    /*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      Ё            Processa o Proximo Registro do Arquivo Temporario               Ё
      юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
 
      DbSelectArea("PRD")
      DbSkip()
      Loop

Enddo

Return


/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммямммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁValidPerg ╨ Autor Ё Analistas Microsiga ╨ Data Ё 13/02/2006  ╨╠╠
╠╠╨          Ё          ╨ Adapt Ё Marcelo Varella     ╨ Data Ё 05/07/2006  ╨╠╠
╠╠лммммммммммьммммммммммймммммммомммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Cria o Grupo de Perguntas conforme Array                    ╨╠╠
╠╠╨          Ё                                                             ╨╠╠
╠╠лммммммммммьммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё CSU Card System                                             ╨╠╠
╠╠хммммммммммоммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function ValidPerg( cPerg )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё            Declara as Variaveis Locais Utilizadas na Rotina                Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Local aRegs := {}

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё            Adiciona as Perguntas no Array a ser Gravado no SX1             Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

aAdd( aRegs , { cPerg , "01" , "Arquivo de Importacao ?"      , "         	   " , "            " , "MV_CH1" , "C" , 24 , 0 , 0 , "G" , "u_SelArquivo()" , "MV_PAR01" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , ""})
aAdd( aRegs , { cPerg , "02" , "Caminho dos Arquivos de Log ?" , "            " , "            " , "MV_CH2" , "C" , 50 , 0 , 0 , "G" , "" , "MV_PAR02" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , "" , ""})

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё            Verifica se Ja Existe Registro no SX1 e Grava                   Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

DbSelectArea("SX1")
DbSetOrder(1)

For i := 1 to Len( aRegs )
   If !SX1->(dbSeek(cPerg+aRegs[i,2]))
      RecLock("SX1",.T.)
	  For j:=1 to FCount()
	     If j <= Len(aRegs[i])
		    FieldPut(j,aRegs[i,j])
		 Endif
	  Next
	  SX1->(MsUnlock())
   Endif
Next  

Return

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁRelIncons ╨ Autor Ё Cristiano Figueiroa╨ Data Ё 30/06/2006  ╨╠╠
╠╠           Ё          ╨ Adapt Ё Marcelo Varella    ╨ Data Ё 05/07/2006  ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao ЁImprime o Relatorio de Inconsistencias do Processo de       ╨╠╠
╠╠╨          ЁImportacao das Solicitacoes de Compras.                     ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       ЁProtheus 8.11                                               ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/ 

Static Function RelInco()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё Define as variaveis necessarias                                      Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

cString  := "PRD"
cDesc1   := "Esta rotina tem como Objetivo Imprimir os Registros Inconsistentes "
cDesc2   := "do Processo de ImportaГao do Cadastro do Produto existentes nos Arquivos "
cDesc3   := "Gerados pelo Sistema da Empresa Hold - EspecМfico CSU CardSystem S.A"
tamanho  := "G"
aReturn  := { "Zebrado", 1 , "Contabilidade" , 1 , 1 , 2 , "" , 1 }
Nomeprog := "Incons"
Wnrel    := "Incons"
nLastKey := 0
m_pag    := 1
nLin     := 80
cTitulo  := "Relatorio de Inconsistencias - Processo de ImportaГЦo do Cadastro do Produto do Sistema Hold "
cCabec2  := ""
cCabec1  := "Linha   Inconsistencia "

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                   Adiciona a Regua de Processamento                        Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

 IncProc("Gerando RelatСrio de Inconsistencias")
 
/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё               Envia Controle para a Funcao SetPrint                   Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

wnrel := SetPrint( cString , Wnrel , , cTitulo , cDesc1 , cDesc2 , cDesc3 , .F. , "" , .F. , Tamanho )

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё               Verifica o Cancelamento do Relatorio                    Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

If nLastKey == 27
   Return
Endif

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                           Chama a SetDefault                          Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

SetDefault(aReturn,cString)

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё               Verifica o Cancelamento do Relatorio                    Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

If nLastKey == 27
   Return
Endif

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё            Chama a Rotina de Processamento do Relatorio               Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

RptStatus( { || PrintInco() }, "Processando o Relatorio de Inconsistencias" )

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤Ao    ЁPrintInco Ё Autor Ё Cristiano Figueiroa   Ё Data Ё30/06/2006Ё╠╠
╠╠Ё                     Ё Adapt Ё Marcelo Varella       Ё Data Ё05/07/2006Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁProcessa os Itens do Relatorio de Inconsistencias           Ё╠╠
╠╠Ё			 Ё 														      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё CSU CardSystem S.A                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function PrintInco()

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                  Abre o Arquivo de Inconsistencias                    Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

DbSelectArea("INC")
DbGotop()
SetRegua(Reccount())

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё               Processa as Inconsistencias no Relatorio                Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Do While INC->( !Eof() )
	
  /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Imprime o cabecalho                                               Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	
	If nLin >= 60
	   nLin := Cabec( cTitulo, cCabec1, cCabec2, Nomeprog , Tamanho , 15 ) + 1
	   nLin ++
	Endif
	
  /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Imprime a Linha de Detalhe                                        Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	
	@ nLin , 000  PSay INC->LINHA
	@ nLin , 008  PSay INC->DESCRICAO
	nLin ++

  /*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё             Processa o Proximo Registro Inconsistente             Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

	DbSelectarea("INC")
	IncRegua()
	DbSkip()
	
Enddo

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё Imprime o Rodape do Relatorio                                     Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Roda( 0 , "" , Tamanho )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё Caso a Impressao seja realizada em Disco , Exibe na Tela o Relatorio Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

If aReturn[5] == 1
   Set Printer To
   Commit
   OurSpool(Wnrel)
Endif

MS_FLUSH()

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤Ao    Ё CopyLog  Ё Autor Ё Cristiano Figueiroa   Ё Data Ё30/06/2006Ё╠╠
╠╠Ё                     Ё Adapt Ё Marcelo Varella       Ё Data Ё05/07/2006Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁCopia o Arquivo Temporario de Inconsistencias para o        Ё╠╠
╠╠Ё			 ЁDiretorio de Log                    		                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё CSU CardSystem S.A                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function CopyLog()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                  Declara as Variaveis Utilizadas na Rotina                 Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Local cDiretorio := Alltrim(MV_PAR02)
Local cDestino   := DtoS(Date()) + StrTran( Time() , ":" , "" )  + ".XLS"

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                  Gera o Cabecalho do Arquivo de Log                        Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
GeraLog( 0 , Replicate("=" , 220 )   , "0" )
GeraLog( 0 , "Arquivo : " + MV_PAR01 , "0" )
GeraLog( 0 , "Data    : " + Dtoc(Date()) , "0" )
GeraLog( 0 , "Hora    : " + Time() , "0" )
GeraLog( 0 , "Usuario : " + Substr(cUsuario , 7 , 15 ) , "0" )
GeraLog( 0 , "Empresa : " + SM0->M0_NOME + " - " + SM0->M0_FILIAL , "0" )
GeraLog( 0 , "" , "0" )
GeraLog( 0 , Replicate("=" , 220 ) , "0" )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё   Copia o Arquivo Temporario de Inconsistencias para o Diretorio de Log    Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

DbSelectArea("INC")
Copy to &(Alltrim(cDiretorio) + Alltrim(cDestino)) SDF

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммямммммммммкмммммммямммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  Ё GeraLog ╨ Autor Ё Cristiano Figueiroa ╨ Data Ё 30/06/2006  ╨╠╠
╠╠╨          Ё         ╨ Adapt Ё Marcelo Varella     ╨ Data Ё 05/07/2006  ╨╠╠
╠╠лммммммммммьмммммммммймммммммомммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao ЁGrava o Arquivo de Log a Inconsistencia encontrada no       ╨╠╠
╠╠╨          ЁProcessamento.                                              ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       ЁCSU CardSystem S.A                                          ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function GeraLog( nLinha , cDescricao , cSequencia )

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё            Declara as Variaveis  Utilizadas na Rotina            Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Local aArea := GetArea()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё               Abre a Tabela de Inconsistencias                   Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

DbSelectArea("INC")

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё               Grava o Registro de Inconsistencias                Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Reclock( "INC" , .T. )
INC->LINHA     := Iif ( nLinha == 0 , "" , StrZero( nLinha , 5 ) )
INC->DESCRICAO := cDescricao
INC->SEQ       := Iif ( cSequencia == Nil , "2" , cSequencia )
Msunlock()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                 Restaura as Areas Utilizadas                     Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

RestArea(aArea)

Return

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммямммммммммммм╩╠╠
╠╠╨Programa  Ё CriaSX6  ╨ Autor ЁCristiano Figueiroa ╨ Data Ё 06/07/2006 ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммомммммммммммм╧╠╠
╠╠╨Descricao Ё Inclui um Novo Parametro no SX6                           ╨╠╠
╠╠лммммммммммьммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё CSU CardSystem S.A                                        ╨╠╠
╠╠хммммммммммоммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function CriaSX6()

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                Abre a tabela de parametros do sistema                      Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

DbSelectArea("SX6")
DbSetOrder(1)


/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё        Verifica se o parametro existe , caso contrario grava o mesmo       Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

If !DbSeek( "  " + "MV_LPPROD" )
   CriarSX6( "MV_LPPROD" , "C" , "Determina o Local Padrao a ser Gravado nas Interfaces de Importacao do Cadastro de Produtos " , "01" , "" )
Endif

Return

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммямммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁIncluiProd╨ Autor Ё Analistas Microsiga ╨ Data Ё 06/07/2006  ╨╠╠
╠╠лммммммммммьммммммммммймммммммомммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё ExecAuto para a Gravacao de Novos Produtos                  ╨╠╠
╠╠╨          Ё                                                             ╨╠╠
╠╠лммммммммммьммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨ParametrosЁ Filial , Codigo , Descricao , Tipo , Unidade , Local Padrao ╨╠╠
╠╠╨          Ё Grupo , Bloqueio                                            ╨╠╠
╠╠лммммммммммьммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё CSU CardSystem S.A                                          ╨╠╠
╠╠хммммммммммоммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function IncluiProduto( cCodFil , cCodPrd , cDescPrd , cTipoPrd , cUn , cLocPadr , cGrupo , cBloqueio )

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                    Declara as Variaveis da Rotina                   Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Local aArea    := GetArea()
Local aProduto := {}
Local lComErro := .F.   

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё        Declara as Variaveis Internas das Rotinas Automaticas        Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

lMsErroAuto    := .F. 
lMsHelpAuto    := .F.	

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё              Carrega o Array com os Dados do Produto                Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

aProduto:= {  { "B1_FILIAL" , cCodFil    , Nil },;
              { "B1_COD"    , cCodPrd    , Nil },;
	          { "B1_DESC"   , cDescPrd   , Nil },;
	          { "B1_TIPO"   , cTipoPrd   , Nil },;
	          { "B1_UM"     , cUn        , Nil },;
	          { "B1_LOCPAD" , cLocPadr   , Nil },;
  	          { "B1_GRUPO"  , cGrupo     , Nil },;
   	          { "B1_MSBLQL" , cBloqueio  , Nil } }

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё              Chama a ExecAuto que Inclui os Produtos                Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

MSExecAuto( { |x , y | Mata010( x , y ) } , aProduto , 3 ) 

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё                      Atualiza o Flag de Erro                        Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

If lMsErroAuto
   lComErro    := .F.   
   lMsErroAuto := .F. 
   lMsHelpAuto := .F.	
Endif   

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё               Carrega o Grupo de Perguntas na Tela                         Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Pergunte( cPerg , .F. )

/*зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  Ё    Retorna a Ocorrencia ou Nao de erros no Processo de Gravacao     Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Return lComErro