#INCLUDE "FINR150.CH"
#Include "PROTHEUS.Ch"

#DEFINE QUEBR				1
#DEFINE FORNEC				2
#DEFINE TITUL				3
#DEFINE TIPO				4
#DEFINE NATUREZA			5
#DEFINE EMISSAO			6
#DEFINE VENCTO				7
#DEFINE VENCREA			8
#DEFINE VL_ORIG			9
#DEFINE VL_NOMINAL		10
#DEFINE VL_CORRIG			11
#DEFINE VL_VENCIDO		12
#DEFINE PORTADOR			13
#DEFINE VL_JUROS			14
#DEFINE ATRASO				15
#DEFINE HISTORICO			16
#DEFINE VL_SOMA			17

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³xFIN150	³ Autor ³ Daniel G.Jr.TI1239    ³ Data ³ 30.06.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Posi‡„o dos Titulos a Pagar			 	                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ FINR150(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
User Function xFINR150()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Vari veis ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cDesc1 :=OemToAnsi("Imprime a posição dos títulos a pagar por data ")
Local cDesc2 :=OemToAnsi("contábil.")
LOCAL cDesc3 :=""
LOCAL wnrel
LOCAL cString:="SE2"
LOCAL nRegEmp := SM0->(RecNo())
Local dOldDtBase := dDataBase
Local dOldData := dDataBase

PRIVATE aReturn := { OemToAnsi(STR0003), 1,OemToAnsi(STR0004), 1, 2, 1, "",1 }  //"Zebrado"###"Administracao"
PRIVATE nomeprog:="XFINR150"
PRIVATE aLinha  := { },nLastKey := 0
PRIVATE cPerg	 :=PADR("FIN150",LEN(SX1->X1_GRUPO))
PRIVATE nJuros  :=0
PRIVATE tamanho:="M"

PRIVATE titulo  := ""
PRIVATE cabec1
PRIVATE cabec2
PRIVATE _nSaldoAnt := 0
PRIVATE _dDataIni  := CtoD("")
PRIVATE _dDataFim  := CtoD("")
PRIVATE _dDataProc := CtoD("")
Private aPosicaoCP := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao dos cabe‡alhos ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
titulo := OemToAnsi(STR0005)  //"Posicao dos Titulos a Pagar"
cabec1 := ""
cabec2 := ""

//Nao retire esta chamada. Verifique antes !!!
//Ela é necessaria para o correto funcionamento da pergunte 36 (Data Base)
PutDtBase()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AjustaSx1()
pergunte("FIN150",.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros ³
//³ mv_par01	  // do Numero 			  ³
//³ mv_par02	  // at‚ o Numero 		  ³
//³ mv_par03	  // do Prefixo			  ³
//³ mv_par04	  // at‚ o Prefixo		  ³
//³ mv_par05	  // da Natureza  	     ³
//³ mv_par06	  // at‚ a Natureza		  ³
//³ mv_par07	  // do Vencimento		  ³
//³ mv_par08	  // at‚ o Vencimento	  ³
//³ mv_par09	  // do Banco			     ³
//³ mv_par10	  // at‚ o Banco		     ³
//³ mv_par11	  // do Fornecedor		  ³
//³ mv_par12	  // at‚ o Fornecedor	  ³
//³ mv_par13	  // Da Emiss„o			  ³
//³ mv_par14	  // Ate a Emiss„o		  ³
//³ mv_par15	  // qual Moeda			  ³
//³ mv_par16	  // Imprime Provis¢rios  ³
//³ mv_par17	  // Reajuste pelo vencto ³
//³ mv_par18	  // Da data contabil	  ³
//³ mv_par19	  // Ate data contabil	  ³
//³ mv_par20	  // Imprime Rel anal/sint³
//³ mv_par21	  // Considera  Data Base?³
//³ mv_par22	  // Cons filiais abaixo ?³
//³ mv_par23	  // Filial de            ³
//³ mv_par24	  // Filial ate           ³
//³ mv_par25	  // Loja de              ³
//³ mv_par26	  // Loja ate             ³
//³ mv_par27 	  // Considera Adiantam.? ³
//³ mv_par28	  // Imprime Nome 		  ³
//³ mv_par29	  // Outras Moedas 		  ³
//³ mv_par30     // Imprimir os Tipos    ³
//³ mv_par31     // Nao Imprimir Tipos	  ³
//³ mv_par32     // Consid. Fluxo Caixa  ³
//³ mv_par33     // DataBase             ³
//³ mv_par34     // Tipo de Data p/Saldo ³
//³ mv_par35     // Quanto a taxa		  ³
//³ mv_par36     // Tit.Emissao Futura	  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

wnrel := "XFINR150"            //Nome Default do relatorio em Disco
//aOrd	:= {OemToAnsi(STR0008),OemToAnsi(STR0009),OemToAnsi(STR0010) ,;  //"Por Numero"###"Por Natureza"###"Por Vencimento"
//OemToAnsi(STR0011),OemToAnsi(STR0012),OemToAnsi(STR0013),OemToAnsi(STR0014) }  //"Por Banco"###"Fornecedor"###"Por Emissao"###"Por Cod.Fornec."
wnrel := SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,/*aOrd*/,,Tamanho)

If nLastKey == 27
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey == 27
	Return
Endif

_dDataIni := MV_PAR18
_dDataFim := MV_PAR19

bProcessa := { |lFim| ImpRelato(@lFim) }
cTitulo   := "Relatório de Posição Diária a Pagar"
cMsg      := "Processandos dados... Aguarde..."
lAborta   := .T.
Processa( bProcessa, cTitulo, cMsg, lAborta )

RptStatus({|lEnd| Fa150Imp(@lEnd,wnRel,cString)},Titulo)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura empresa / filial original    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SM0->(dbGoto(nRegEmp))
cFilAnt := SM0->M0_CODFIL

//Acerta a database de acordo com a database real do sistema
If mv_par21 == 1    // Considera Data Base
	dDataBase := dOldDtBase
Endif

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ FA150Imp ³ Autor ³ Daniel G.Jr.TI1239    ³ Data ³ 30.06.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Posi‡„o dos Titulos a Pagar								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ FA150Imp(lEnd,wnRel,cString)								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ lEnd	  - A‡Æo do Codeblock								  ³±±
±±³			 ³ wnRel   - T¡tulo do relat¢rio 							  ³±±
±±³			 ³ cString - Mensagem							 			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Gen‚rico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ImpRelato(lEnd)

LOCAL nTit0:=0,nTit1:=0,nTit2:=0,nTit3:=0,nTit4:=0,nTit5:=0,nTit6:=0,nTotJur:=0
LOCAL nSaldo:=0,nAtraso:=0
LOCAL dDataReaj
LOCAL dDtContab
LOCAL dDtIniCtb := CtoD("01/01/01")
LOCAL cFilDe,cFilAte
Local nTotsRec := SE2->(RecCount())
Local lFr150Flt := EXISTBLOCK("FR150FLT")
Local cFr150Flt
Local nDecs := Msdecimais(mv_par15)

Local nI := 0
Local _dI:= mv_par18-1
Local aStru := SE2->(dbStruct())

Private nRegSM0 := SM0->(Recno())
Private nAtuSM0 := SM0->(Recno())
PRIVATE dBaixa := dDataBase

While _dI <= mv_par19
	aAdd(aPosicaoCP, {_dI,0,0,0,0,0})
	_dI += 1
End
aPosicaoCP := aSort( aPosicaoCP,,, { |x,y| DtoS(x[1]) < DtoS(y[1]) } )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada para Filtrar 										  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ Localiza‡”es ArgentinaÄÙ
If lFr150Flt
	cFr150Flt := EXECBLOCK("Fr150FLT",.f.,.f.)
Endif

dbSelectArea ( "SE2" )
Set Softseek On

cFilDe := "  "		//mv_par23
cFilAte:= "zz"		//mv_par24

//Acerta a database de acordo com o parametro
If mv_par21 == 1    // Considera Data Base
	dDataBase := mv_par33
Endif

dbSelectArea("SM0")
dbSeek(cEmpAnt+cFilDe,.T.)

nRegSM0 := SM0->(Recno())
nAtuSM0 := SM0->(Recno())

cFilAnt := SM0->M0_CODFIL

cQuery := "SELECT * "
cQuery += "  FROM "+	RetSqlName("SE2")
cQuery += " WHERE E2_FILIAL = '" + xFilial("SE2") + "'"
cQuery += "   AND D_E_L_E_T_ <> '*' "

dbSelectArea("SE2")
SE2->(dbSetOrder(5))
cOrder := SqlOrder(indexkey())
nQualIndice := 1

dbSelectArea("SE2")
cFilterUser:=aReturn[7]

Set Softseek Off

cQuery += " AND E2_NUM     BETWEEN '"+ mv_par01+ "' AND '"+ mv_par02 + "'"
cQuery += " AND E2_PREFIXO BETWEEN '"+ mv_par03+ "' AND '"+ mv_par04 + "'"
cQuery += " AND (E2_MULTNAT = '1' OR (E2_NATUREZ BETWEEN '"+MV_PAR05+"' AND '"+MV_PAR06+"'))"
cQuery += " AND E2_VENCREA BETWEEN '"+ DTOS(mv_par07)+ "' AND '"+ DTOS(mv_par08) + "'"
cQuery += " AND E2_PORTADO BETWEEN '"+ mv_par09+ "' AND '"+ mv_par10 + "'"
cQuery += " AND E2_FORNECE BETWEEN '"+ mv_par11+ "' AND '"+ mv_par12 + "'"
cQuery += " AND E2_EMISSAO BETWEEN '"+ DTOS(mv_par13)+ "' AND '"+ DTOS(mv_par14) + "'"
cQuery += " AND E2_LOJA    BETWEEN '"+ mv_par25 + "' AND '"+ mv_par26 + "'"

//Considerar titulos cuja emissao seja maior que a database do sistema
If mv_par36 == 2
	cQuery += " AND E2_EMISSAO <= '" + DTOS(dDataBase) +"'"
Endif

If !Empty(mv_par30) // Deseja imprimir apenas os tipos do parametro 30
	cQuery += " AND E2_TIPO IN "+FormatIn(mv_par30,";")
ElseIf !Empty(Mv_par31) // Deseja excluir os tipos do parametro 31
	cQuery += " AND E2_TIPO NOT IN "+FormatIn(mv_par31,";")
EndIf
If mv_par32 == 1
	cQuery += " AND E2_FLUXO <> 'N'"
Endif
cQuery += " ORDER BY "+ cOrder

cQuery := ChangeQuery(cQuery)

dbSelectArea("SE2")
dbCloseArea()
dbSelectArea("SA2")

dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), 'SE2', .F., .T.)

For ni := 1 to Len(aStru)
	If aStru[ni,2] != 'C'
		TCSetField('SE2', aStru[ni,1], aStru[ni,2],aStru[ni,3],aStru[ni,4])
	Endif
Next

_nDias := Len(aPosicaoCP)		    	// seta a quantidade de dias

For _nX:=1 to _nDias					// executa a geração de dados dia-a-dia
	
	_dDataProc := aPosicaoCP[_nX][1]
	dDataBase  := _dDataProc
	dBaixa	   := _dDataProc
	Store 0 To nTit1,nTit2,nTit3,nTit4,nTit5,nTotJur
	
	dbSelectArea("SE2")
	SE2->(dbGoTop())
	
	ProcRegua(nTotsRec)
	
	While SE2->(!Eof()) .And. !lEnd
		
		IF lEnd
			Aviso("Atenção!",OemToAnsi(STR0025),{'Ok'})	  //"CANCELADO PELO OPERADOR"
			Exit
		End
		
		IncProc("Processando DataBase: "+DtoC(dDataBase)+" ("+DtoC(SE2->E2_EMISSAO)+")")
		
		dbSelectArea("SE2")
				
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Considera filtro do usuario                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(cFilterUser).and.!(&cFilterUser)
			dbSkip()
			Loop
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Considera filtro do usuario no ponto de entrada.             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lFr150flt
			If &cFr150flt
				DbSkip()
				Loop
			Endif
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se trata-se de abatimento ou provisorio, ou ³
		//³ Somente titulos emitidos ate a data base				   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IF SE2->E2_TIPO $ MVABATIM .Or. (SE2 -> E2_EMISSAO > dDataBase .and. mv_par36 == 2)
			dbSkip()
			Loop
		EndIF
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se ser  impresso titulos provis¢rios		   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IF E2_TIPO $ MVPROVIS .and. mv_par16 == 2
			dbSkip()
			Loop
		EndIF
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se ser  impresso titulos de Adiantamento	 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IF SE2->E2_TIPO $ MVPAGANT+"/"+MV_CPNEG .and. mv_par27 == 2
			dbSkip()
			Loop
		EndIF
		
		// dDtContab para casos em que o campo E2_EMIS1 esteja vazio
		// compatibilizando com a vers„o 2.04 que n„o gerava para titulos
		// de ISS e FunRural
		
		dDtContab := Iif(Empty(SE2->E2_EMIS1),SE2->E2_EMISSAO,SE2->E2_EMIS1)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se esta dentro dos parametros ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IF E2_NUM < mv_par01      .OR. E2_NUM > mv_par02 .OR. ;
			E2_PREFIXO < mv_par03  .OR. E2_PREFIXO > mv_par04 .OR. ;
			E2_NATUREZ < mv_par05  .OR. E2_NATUREZ > mv_par06 .OR. ;
			E2_VENCREA < mv_par07  .OR. E2_VENCREA > mv_par08 .OR. ;
			E2_PORTADO < mv_par09  .OR. E2_PORTADO > mv_par10 .OR. ;
			E2_FORNECE < mv_par11  .OR. E2_FORNECE > mv_par12 .OR. ;
			E2_EMISSAO < mv_par13  .OR. E2_EMISSAO > mv_par14 .OR. ;
			(E2_EMISSAO > dDataBase .and. mv_par36 == 2) .OR. dDtContab  < dDtIniCtb .OR. ;
			E2_LOJA    < mv_par25  .OR. E2_LOJA    > mv_par26 .OR. ;
			dDtContab  > _dDataProc  .Or. !&(fr150IndR())
			
			dbSkip()
			Loop
		Endif
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se titulo, apesar do E2_SALDO = 0, deve aparecer ou ³
		//³ nÆo no relat¢rio quando se considera database (mv_par21 = 1) ³
		//³ ou caso nÆo se considere a database, se o titulo foi totalmen³
		//³ te baixado.																  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SE2")
		IF !Empty(SE2->E2_BAIXA) .and. Iif(mv_par21 == 2 ,SE2->E2_SALDO == 0 ,;
			IIF(mv_par34 == 1,(SE2->E2_SALDO == 0 .and. SE2->E2_BAIXA <= dDataBase),.F.))
			dbSkip()
			Loop
		EndIF
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se deve imprimir outras moedas³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If mv_par29 == 2 // nao imprime
			if SE2->E2_MOEDA != mv_par15 //verifica moeda do campo=moeda parametro
				dbSkip()
				Loop
			endif
		Endif
		
		dbSelectArea("SA2")
		MSSeek(xFilial("SA2")+SE2->E2_FORNECE+SE2->E2_LOJA)
		dbSelectArea("SE2")
		
		// Verifica se existe a taxa na data do vencimento do titulo, se nao existir, utiliza a taxa da database
		If SE2->E2_VENCREA < dDataBase
			If mv_par17 == 2 .And. RecMoeda(SE2->E2_VENCREA,cMoeda) > 0
				dDataReaj := SE2->E2_VENCREA
			Else
				dDataReaj := dDataBase
			EndIf
		Else
			dDataReaj := dDataBase
		EndIf
		
		If mv_par21 == 1
			nSaldo := SaldoTit(SE2->E2_PREFIXO,SE2->E2_NUM,SE2->E2_PARCELA,SE2->E2_TIPO,SE2->E2_NATUREZ,"P",SE2->E2_FORNECE,mv_par15,dDataReaj,,SE2->E2_LOJA,,If(mv_par35==1,SE2->E2_TXMOEDA,Nil),IIF(mv_par34 == 2,3,1)) // 1 = DT BAIXA    3 = DT DIGIT
			//Verifica se existem compensações em outras filiais para descontar do saldo, pois a SaldoTit() somente
			//verifica as movimentações da filial corrente.
			If !Empty(xFilial("SE2")) .And. !Empty(xFilial("SE5"))
				nSaldo -= xMoeda(VlrCompFil(SE2->E2_PREFIXO,SE2->E2_NUM,SE2->E2_PARCELA,SE2->E2_TIPO,SE2->E2_FORNECE,SE2->E2_LOJA,IIF(mv_par34 == 2,3,1));
				,SE2->E2_MOEDA,mv_par15,dDataReaj,ndecs+1,If(mv_par35==1,SE2->E2_TXMOEDA,Nil))
			EndIf
			// Subtrai decrescimo para recompor o saldo na data escolhida.
			If Str(SE2->E2_VALOR,17,2) == Str(nSaldo,17,2) .And. SE2->E2_DECRESC > 0 .And. SE2->E2_SDDECRE == 0
				nSAldo -= SE2->E2_DECRESC
			Endif
			// Soma Acrescimo para recompor o saldo na data escolhida.
			If Str(SE2->E2_VALOR,17,2) == Str(nSaldo,17,2) .And. SE2->E2_ACRESC > 0 .And. SE2->E2_SDACRES == 0
				nSAldo += SE2->E2_ACRESC
			Endif
		Else
			nSaldo := xMoeda((SE2->E2_SALDO+SE2->E2_SDACRES-SE2->E2_SDDECRE),SE2->E2_MOEDA,mv_par15,dDataReaj,ndecs+1,If(mv_par35==1,SE2->E2_TXMOEDA,Nil))
		Endif
		If ! (SE2->E2_TIPO $ MVPAGANT+"/"+MV_CPNEG) .And. ;
			! ( MV_PAR21 == 2 .And. nSaldo == 0 ) // nao deve olhar abatimento pois e zerado o saldo na liquidacao final do titulo
			
			//Quando considerar Titulos com emissao futura, eh necessario
			//colocar-se a database para o futuro de forma que a Somaabat()
			//considere os titulos de abatimento
			If mv_par36 == 1
				dOldData := dDataBase
				dDataBase := CTOD("31/12/40")
			Endif
			
			nSaldo-= SumAbatPag(SE2->E2_PREFIXO,SE2->E2_NUM,SE2->E2_PARCELA,SE2->E2_FORNECE,SE2->E2_MOEDA,"V",dDataBase,SE2->E2_LOJA,"2",mv_par18,mv_par19)
			
			If mv_par36 == 1
				dDataBase := dOldData
			Endif
		EndIf
		
		nSaldo:=Round(NoRound(nSaldo,3),2)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Desconsidera caso saldo seja menor ou igual a zero   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nSaldo <= 0
			dbSkip()
			Loop
		Endif
		
		If dDataBase > SE2->E2_VENCREA 		//vencidos
			nJuros:=0
			dBaixa:=_dDataProc
			fa080Juros(mv_par15)
			dbSelectArea("SE2")
			If SE2->E2_TIPO $ MVPAGANT+"/"+MV_CPNEG
				nTit0 -= xMoeda(SE2->E2_VALOR,SE2->E2_MOEDA,mv_par15,SE2->E2_EMISSAO,ndecs+1,If(mv_par35==1,SE2->E2_TXMOEDA,Nil))
				nTit1 -= nSaldo
				nTit2 -= nSaldo+nJuros
			Else
				nTit0 += xMoeda(SE2->E2_VALOR,SE2->E2_MOEDA,mv_par15,SE2->E2_EMISSAO,ndecs+1,If(mv_par35==1,SE2->E2_TXMOEDA,Nil))
				nTit1 += nSaldo
				nTit2 += nSaldo+nJuros
			Endif
			nTotJur += (nJuros)
		Else				  //a vencer
			If SE2->E2_TIPO $ MVPAGANT+"/"+MV_CPNEG
				nTit0 -= xMoeda(SE2->E2_VALOR,SE2->E2_MOEDA,mv_par15,SE2->E2_EMISSAO,ndecs+1,If(mv_par35==1,SE2->E2_TXMOEDA,Nil))
				nTit3 -= nSaldo
				nTit4 -= nSaldo
			Else
				nTit0 += xMoeda(SE2->E2_VALOR,SE2->E2_MOEDA,mv_par15,SE2->E2_EMISSAO,ndecs+1,If(mv_par35==1,SE2->E2_TXMOEDA,Nil))
				nTit3 += nSaldo
				nTit4 += nSaldo
			Endif
		Endif
		nJuros := 0
		
		IF dDataBase > E2_VENCREA
			nAtraso:=dDataBase-E2_VENCTO
			IF Dow(E2_VENCTO) == 1 .Or. Dow(E2_VENCTO) == 7
				IF Dow(dBaixa) == 2 .and. nAtraso <= 2
					nAtraso:=0
				EndIF
			EndIF
			nAtraso:=IIF(nAtraso<0,0,nAtraso)
		EndIF
		
		dbSkip()
		nTit5 ++
		
	Enddo
	IF nTit5 > 0
		nTit6 := nTit2+nTit3
		aPosicaoCP[_nX][2] := nTit1
		aPosicaoCP[_nX][3] := nTit2
		aPosicaoCP[_nX][4] := nTit3
		aPosicaoCP[_nX][5] := nTotJur
		aPosicaoCP[_nX][6] := nTit6
	EndIf
	
Next _nX

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ FA150Imp ³ Autor ³ Daniel G.Jr.TI1239    ³ Data ³ 30.06.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Posi‡„o dos Titulos a Pagar								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ FA150Imp(lEnd,wnRel,cString)								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ lEnd	  - A‡Æo do Codeblock								  ³±±
±±³			 ³ wnRel   - T¡tulo do relat¢rio 							  ³±±
±±³			 ³ cString - Mensagem							 			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Gen‚rico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function FA150Imp(lEnd,wnRel,cString)

LOCAL CbCont
LOCAL CbTxt
Local nDecs := Msdecimais(mv_par15)
Local li:=80

Private nRegSM0 := SM0->(Recno())
Private nAtuSM0 := SM0->(Recno())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para Impress„o do Cabe‡alho e Rodap‚ ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cbtxt  := OemToAnsi(STR0015)  //"* indica titulo provisorio, P indica Saldo Parcial"
cbcont := 0
li 	 := 80
m_pag  := 1

cMoeda := LTrim(Str(mv_par15))
Titulo += OemToAnsi(STR0035)+GetMv("MV_MOEDA"+cMoeda)  //" em "

//Acerta a database de acordo com o parametro
If mv_par21 == 1    // Considera Data Base
	dDataBase := mv_par33
Endif

titulo += OemToAnsi(STR0024)  // " - Sintetico"
cabec1 := OemToAnsi("  Data   |        Titulos vencidos        | Titulos a vencer | Valor dos juros ou | (Vencidos+Vencer)")
cabec2 := OemToAnsi("         | Valor nominal  Valor corrigido |   Valor nominal  |   com. permanencia |                  ")
// "dd/mm/aa  999.999.999,99   999.999.999,99    999.999.999,99        999.999.999,99    9999.999.999,99
// "123456789.123456789.123456789.123456789.123456789.123456789.123456789.123456789.123456789.123456789.123456789.123456789
// "01        11               28                46                    68                86
titulo += OemToAnsi(STR0016)  //" - Por Numero"

// Imprime os valores dia a dia
SubTot150(aPosicaoCP,nDecs)

SM0->(dbGoTo(nRegSM0))
cFilAnt := SM0->M0_CODFIL

Set Device To Screen

dbSelectArea("SE2")
dbCloseArea()
ChKFile("SE2")
dbSetOrder(1)

If aReturn[5] = 1
	Set Printer To
	dbCommitAll()
	ourspool(wnrel)
Endif
MS_FLUSH()

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³SubTot150 ³ Autor ³ Daniel G.Jr.TI1239    ³ Data ³ 30.06.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³IMPRIMIR VALORES DIA A DIA     							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ SubTot150() 												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ 															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function SubTot150(aPosCP,nDecs)

DEFAULT nDecs := Msdecimais(mv_par15) 
li:=80
                               
IF li > 58
	nAtuSM0 := SM0->(Recno())
	SM0->(dbGoto(nRegSM0))
	cabec(titulo,cabec1,cabec2,nomeprog,tamanho,GetMv("MV_COMP"))
	SM0->(dbGoTo(nAtuSM0))
EndIF
         
_nSaldoAnt := aPosCP[1][6]

@li,000 PSAY "SALDO ANTERIOR ---->"
@li,086 PSAY _nSaldoAnt Picture TM(_nSaldoAnt,16,nDecs)
_nSaldo := _nSaldoAnt
li+=2

For _nI:=2 to Len(aPosCP)
	
	IF li > 58
		nAtuSM0 := SM0->(Recno())
		SM0->(dbGoto(nRegSM0))
		cabec(titulo,cabec1,cabec2,nomeprog,tamanho,GetMv("MV_COMP"))
		SM0->(dbGoTo(nAtuSM0))
	EndIF
	If aPosCP[_nI][6]==0
		aPosCP[_nI][6]:=_nSaldo
	Else
		_nSaldo := aPosCP[_nI][6]
	EndIf
	@li,001 PSAY aPosCP[_nI][1]
	@li,011 PSAY aPosCP[_nI][2]		 Picture TM(aPosCP[_nI][2],14,nDecs)
	@li,028 PSAY aPosCP[_nI][3]		 Picture TM(aPosCP[_nI][3],14,nDecs)
	@li,046 PSAY aPosCP[_nI][4]		 Picture TM(aPosCP[_nI][4],14,nDecs)
	If aPosCP[_nI][5] > 0
		@li,068 PSAY aPosCP[_nI][5] 	 Picture TM(aPosCP[_nI][5],12,nDecs)
	Endif
	@li,086 PSAY aPosCP[_nI][6] Picture TM(aPosCP[_nI][6],16,nDecs)
	li++
Next _nI

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³fr150Indr ³ Autor ³ Daniel G.Jr.TI1239    ³ Data ³ 30.06.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Monta Indregua para impressao do relat¢rio				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fr150IndR()
Local cString
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ ATENCAO !!!!                                               ³
//³ N„o adiconar mais nada a chave do filtro pois a mesma est  ³
//³ com 254 caracteres.                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cString := 'E2_FILIAL="'+xFilial()+'".And.'
cString += '(E2_MULTNAT="1" .OR. (E2_NATUREZ>="'+mv_par05+'".and.E2_NATUREZ<="'+mv_par06+'")).And.'
cString += 'E2_FORNECE>="'+mv_par11+'".and.E2_FORNECE<="'+mv_par12+'".And.'
cString += 'DTOS(E2_VENCREA)>="'+DTOS(mv_par07)+'".and.DTOS(E2_VENCREA)<="'+DTOS(mv_par08)+'".And.'
cString += 'DTOS(E2_EMISSAO)>="'+DTOS(mv_par13)+'".and.DTOS(E2_EMISSAO)<="'+DTOS(mv_par14)+'"'
If !Empty(mv_par30) // Deseja imprimir apenas os tipos do parametro 30
	cString += '.And.E2_TIPO$"'+mv_par30+'"'
ElseIf !Empty(Mv_par31) // Deseja excluir os tipos do parametro 31
	cString += '.And.!(E2_TIPO$'+'"'+mv_par31+'")'
EndIf
IF mv_par32 == 1  // Apenas titulos que estarao no fluxo de caixa
	cString += '.And.(E2_FLUXO!="N")'
Endif

Return cString

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ PutDtBase³ Autor ³ Daniel G.Jr.TI1239    ³ Data ³ 30.06.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Ajusta parametro database do relat[orio.                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Finr150.                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function PutDtBase()
Local _sAlias	:= Alias()

dbSelectArea("SX1")
dbSetOrder(1)
If MsSeek("FIN15033")
	//Acerto o parametro com a database
	RecLock("SX1",.F.)
	Replace x1_cnt01		With "'"+DTOC(dDataBase)+"'"
	MsUnlock()
Endif

dbSelectArea(_sAlias)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AjustaSX1 ºAutor  ³Daniel G.Jr.TI1239    ³ Data ³ 30.06.08  ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Insere novas perguntas ao sx1                               º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ FINR150                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AjustaSX1()

Local aHelpPor	:= {}
Local aHelpEng	:= {}
Local aHelpSpa	:= {}

Aadd( aHelpPor, "Indica que o valor será convertido de  "  )
Aadd( aHelpPor, 'acordo com a "Taxa contradada" ou  de  '  )
Aadd( aHelpPor, 'acordo com a "Taxa normal". Se for esco-' )
Aadd( aHelpPor, 'lhida "Taxa contratada", o valor será  '  )
Aadd( aHelpPor, "convertido pela taxa contradada para o "  )
Aadd( aHelpPor, 'titulo. Se for escolhida "Taxa Normal" '  )
Aadd( aHelpPor, 'o valor será convertido pela taxa da   '  )
Aadd( aHelpPor, 'data base do sistema, ou se o titulo   '  )
Aadd( aHelpPor, 'estiver vencido, pela taxa da data     '  )
Aadd( aHelpPor, 'informada na pergunta'             		  )
Aadd( aHelpPor, '"Converte Venci Por?"'  		              )

Aadd( aHelpSpa, "Indica que el valor se convertira de "	)
Aadd( aHelpSpa, 'acuerdo con la "Tasa contratada" o la' 	)
Aadd( aHelpSpa, '"Tasa normal". Si se elige la ' 			)
Aadd( aHelpSpa, '"Tasa contratada", el valor se  '     	)
Aadd( aHelpSpa, "convertira por la tasa contratada para"	)
Aadd( aHelpSpa, 'el titulo. Si se elige la "Tasa Normal"')
Aadd( aHelpSpa, 'el valor se convertira por la tasa de la')
Aadd( aHelpSpa, 'fecha base del sistema o, si el titulo'	)
Aadd( aHelpSpa, 'estuviera vencido, por la tasa de la '	)
Aadd( aHelpSpa, 'fecha informada en la pregunta '			)
Aadd( aHelpSpa, '"¿Convierte Venc Por?"' )

Aadd( aHelpEng, "Determine if the value will be converted "	)
Aadd( aHelpEng, 'according to the "Hired rate" or to the  '	)
Aadd( aHelpEng, '"Normal rate". If a "Hired rate" is '  		)
Aadd( aHelpEng, 'chosen, the value will be converted by'		)
Aadd( aHelpEng, "the rate hired for the bill. However, if "	)
Aadd( aHelpEng, 'a "Normal rate" is chosen, the value will'	)
Aadd( aHelpEng, 'be converted by the rate of the system   '	)
Aadd( aHelpEng, 'base date, or if the bill is due, this   '	)
Aadd( aHelpEng, 'will be converted by the rate of the '		)
Aadd( aHelpEng, 'informed date found in query'					)
Aadd( aHelpEng, '"Convert Due Date By?"'							)

PutSx1( "FIN150", "35","Quanto a Taxa?","¿Cuanto a tasa?","How about rate?","mv_cht","N",1,0,1,"C","","","","",;
"mv_par35","Taxa Contratada","Tasa contratada","Hired Rate","","Taxa Normal","Tasa Normal","Normal Rate","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

aHelpPor	:= {}
aHelpEng	:= {}
aHelpSpa	:= {}

Aadd( aHelpPor, 'Selecione "Sim" para que sejam         '  )
Aadd( aHelpPor, 'considerados no relatório, títulos cuja'  )
Aadd( aHelpPor, 'emissão seja em data posterior a database' )
Aadd( aHelpPor, 'do relatório, ou "Não" em caso contrário'  )

Aadd( aHelpSpa, 'Seleccione "Sí" para que se consideren en'	)
Aadd( aHelpSpa, 'el informe los títulos cuya emisión sea en')
Aadd( aHelpSpa, 'fecha posterior a la fecha base de dicho '	)
Aadd( aHelpSpa, 'informe o "No" en caso contrario'	)

Aadd( aHelpEng, 'Choose "Yes" for bills which issue date '	)
Aadd( aHelpEng, 'is posterior to the report base date in ' 	)
Aadd( aHelpEng, 'the report, otherwise "No"' 			)

PutSx1( "FIN150", "36","Tit. Emissao Futura?","Tit.Emision Futura  ","Future Issue Bill   ","mv_chu","N",1,0,2,"C","","","","","mv_par36","Sim","Si","Yes","","Nao","No","No","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³VlrCompFilºAutor  ³ Daniel G.Jr.TI1239    ³ Data ³ 30.06.08 ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao que retorna o valor da compensacao de um título que  º±±
±±º          ³que foi compensado em filiais diferentes.				      º±±
±±º          ³                                  						  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Financeiro                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function VlrCompFil(cPrefixo,cNumero,cParcela,cTipo,cFornece,cLoja,nTipoData)

Local aArea     := GetArea()
Local nValor    := 0
Local cTipoData := "0"
Local nX        := 0
Local aFiliais 	:= {}
Local nRegEmp	:= SM0->(Recno())
Local nRegAtu	:= SM0->(Recno())
Local cEmpAnt	:= SM0->M0_CODIGO
Local cFilSE5	:= xFilial("SE5")

Local aStruSE5
Local nSE5

nTipoData  := IIF(nTipoData == Nil, 0, nTipoData)

//Tipos de Data (cTipoData)
// 0 = Data Da Baixa (E5_DATA)
// 1 = Data de Disponibilidade (E5_DTDISPO)
// 2 = Data de Contabilidação (E5_DTDIGIT)

If nTipoData == 1
	cTipoData := "0"
ElseIf nTipodata == 2
	cTipoData := "1"
Else
	cTipoData := "2"
Endif

cQuery := "SELECT SE5.E5_FILORIG,SE5.E5_FILIAL,SE5.E5_TIPODOC,SE5.E5_RECPAG,SE5.E5_PREFIXO,SE5.E5_NUMERO,SE5.E5_PARCELA,"
cQuery += "SE5.E5_TIPO,SE5.E5_DATA,SE5.E5_CLIFOR,SE5.E5_LOJA,SE5.E5_SEQ,SE5.E5_DOCUMEN,SE5.E5_SEQ,SE5.E5_VALOR "
cQuery += "FROM "+RetSqlName("SE5") + " SE5, "
cQuery += RetSqlName("SE2") + " SE2 "
cQuery += "WHERE "
cQuery += "SE2.E2_FILIAL = '"+xFilial("SE2")+"' AND "
cQuery += "SE5.E5_FILIAL <> '"+xFilial("SE5")+"' AND "
cQuery += "SE5.E5_FILORIG = '"+xFilial("SE5")+"' AND "
cQuery += "SE5.E5_PREFIXO = SE2.E2_PREFIXO AND "
cQuery += "SE5.E5_NUMERO = SE2.E2_NUM AND "
cQuery += "SE5.E5_PARCELA = SE2.E2_PARCELA AND "
cQuery += "SE5.E5_TIPO = SE2.E2_TIPO AND "
cQuery += "SE5.E5_CLIFOR = SE2.E2_FORNECE AND "
cQuery += "SE5.E5_LOJA = SE2.E2_LOJA AND "
cQuery += "SE5.E5_MOTBX = 'CMP' AND "
cQuery += "SE5.E5_PREFIXO ='"+cPrefixo+"' AND "
cQuery += "SE5.E5_NUMERO ='"+cNumero+"' AND "
cQuery += "SE5.E5_PARCELA ='"+cParcela+"' AND "
cQuery += "SE5.E5_TIPO = '"+cTipo+"' AND "
cQuery += "SE5.E5_CLIFOR = '"+cFornece+"' AND "
cQuery += "SE5.E5_LOJA = '"+cLoja+"' AND "
//Defino qual o tipo de data a ser utilizado para compor o saldo do titulo
If cTipoData == "0"
	cQuery += "SE5.E5_DATA <= '"+DTOS(dDataBase)+"' AND "
ElseIf cTipoData == "1"
	cQuery += "SE5.E5_DTDISPO <= '"+DTOS(dDataBase)+"' AND "
Else
	cQuery += "SE5.E5_DTDIGIT <= '"+DTOS(dDataBase)+"' AND "
Endif
cQuery += "SE5.D_E_L_E_T_ = ' ' AND "
cQuery += "SE2.D_E_L_E_T_ = ' ' "
cQuery += "ORDER BY SE5.E5_FILIAL, SE5.E5_TIPODOC, SE5.E5_PREFIXO, SE5.E5_NUMERO, SE5.E5_PARCELA, SE5.E5_TIPO, SE5.E5_DATA, SE5.E5_SEQ "

cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"TRB",.T.,.T.)

aStruSE5:= SE5->(dbStruct())

For nSE5 := 1 To Len(aStruSE5)
	If aStruSE5[nSE5][2] <> "C" .and.  FieldPos(aStruSE5[nSE5][1]) > 0
		TcSetField("TRB",aStruSE5[nSE5][1],aStruSE5[nSE5][2],aStruSE5[nSE5][3],aStruSE5[nSE5][4])
	EndIf
Next nSE5

While TRB->(!EOF())
	
	If TRB->E5_TIPO == MVPAGANT
		If  TRB->E5_RECPAG == "P" .And. TRB->E5_TIPODOC $ "BA|VL"
			nValor += TRB->E5_VALOR
		EndIf
	Else
		If  TRB->E5_RECPAG == "P" .And. TRB->E5_TIPODOC $ "CP"
			nValor += TRB->E5_VALOR
		EndIf
	EndIf
	
	If TRB->E5_RECPAG == "R" .And. TRB->E5_TIPODOC == "ES"
		nValor -= TRB->E5_VALOR
	EndIf
	
	TRB->(dbSkip())
EndDo

dbSelectArea("SM0")
dbSeek(cEmpAnt,.T.)
nRegAtu := SM0->(RECNO())
While !Eof() .and. SM0->M0_CODIGO == cEmpAnt
	AADD(aFiliais,SM0->M0_CODFIL)
	DbSkip()
Enddo
SM0->(dbGoto(nRegAtu))

For nX := 1 to Len(aFiliais)
	
	cFilAtu := aFiliais[nX]
	cEmpAnt := SM0->M0_CODIGO
	
	While !Eof() .and. M0_CODIGO == cEmpAnt .and. M0_CODFIL == cFilAtu
		
		dbSelectArea("SE2")
		cFilAnt := SM0->M0_CODFIL
		dbSelectArea("SE5")
		dbSetOrder(7)
		
		SE5->(MsSeek(cFilAtu+cPrefixo+cNumero+cParcela+cTipo+cFornece+cLoja))
		While SE5->(!Eof()) .And. SE5->(E5_FILIAL+E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA) ==;
			cFilAtu+cPrefixo+cNumero+cParcela+cTipo+cFornece+cLoja
			
			If SE5->E5_MOTBX != "CMP" .And. SE5->E5_RECPAG != "P"
				SE5->(dbSkip())
				Loop
			Endif
			//Defino qual o tipo de data a ser utilizado para compor o saldo do titulo
			If cTipoData == "0"
				dDtFina := SE5->E5_DATA
			ElseIf cTipoData == "1"
				dDtFina := SE5->E5_DTDISPO
			Else
				dDtFina := SE5->E5_DTDIGIT
			Endif
			If dDtFina > dDataBase
				SE5->(dbSkip())
				Loop
			EndIf
			If SE5->E5_FILORIG == cFilSE5
				If SE5->E5_TIPO == MVPAGANT
					If  SE5->E5_RECPAG == "P" .And. SE5->E5_TIPODOC $ "BA|VL"
						nValor += SE5->E5_VALOR
					EndIf
				Else
					If  SE5->E5_RECPAG == "P" .And. SE5->E5_TIPODOC $ "CP"
						nValor += SE5->E5_VALOR
					EndIf
				EndIf
				If SE5->E5_RECPAG == "R" .And. SE5->E5_TIPODOC == "ES"
					nValor -= SE5->E5_VALOR
				EndIf
			Endif
			dbSelectArea("SE5")
			SE5->(dbSkip())
		EndDo
		dbSelectArea("SM0")
		SM0->(dbSkip())
	EndDo
Next
SM0->(dbGoTo(nRegEmp))
cFilAnt := SM0->M0_CODFIL

dbSelectArea("TRB")
dbCloseArea()

RestArea(aArea)

Return nValor
