#INCLUDE "rwmake.ch"
/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFA430CIT  บ Autor ณ Felipe Raposo      บ Data ณ  28/02/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบPonto de  ณ Programa: FINA430 - Recepcao do arquivo cnab de retorno do บฑฑ
ฑฑบEntrada   ณ contas a pagar.                                            บฑฑ
ฑฑบ          ณ Eh executado no inicio do processamento, antes do arquivo  บฑฑ
ฑฑบ          ณ ter sido aberto.                                           บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบDescricao ณ Desfaz a criptografia do arquivo do cnab a pagar de retor- บฑฑ
ฑฑบ          ณ no para que possa ser processado pelo sistema.             บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ Nenhum.                                                    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณ Nenhum.                                                    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบObs.      ณ O arquivo decriptografado tem de ser apagado apos o proces-บฑฑ
ฑฑบ          ณ samento para que nenhuma pessoa nao autorizada possa ter   บฑฑ
ฑฑบ          ณ acesso a dados restritos da empresa. Para isso deve ser u- บฑฑ
ฑฑบ          ณ tilizado algum ponto de entrada no final do processamento  บฑฑ
ฑฑบ          ณ para tal finalidade.                                       บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ CIEE.                                                      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
User Function TESTE01()

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Declaracao de variaveis.                                            ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
Local _nRet := 0
Private _cArq1, _nArq1
Private _cArq2, _nArq2
Private _cDll1, _nDll1

_cDll1 := "CIEEBrad.dll" // "Bradesco2Microsiga.dll"   
_cArq1 := "\TEMP\Arq.crp"
_cArq2 := "\TEMP\ArqFim.TXT"

// Tenta abrir o arquivo texto puro.
If (_nArq1 := fOpen(_cArq1, 68)) == -1
	_cMsg := "Nใo foi possํvel abrir o arquivo " + _cArq1 + "! Verifique os parโmetros."
	MsgAlert(_cMsg, "Aten็ใo!")
	_nRet -= 1
Endif

// Tenta abrir a DLL que faz o tratamento do arquivo texto.
If (_nDll1 := ExecInDllOpen(_cDll1)) == -1
	_cMsg := "Nใo foi possํvel abrir o arquivo " + _cDll1 + "."
	MsgAlert(_cMsg, "Aten็ใo!")
	_nRet -= 1
Endif

// Tenta criar o arquivo de saida (a ser decriptografado).
If (_nArq2 := fCreate(_cArq2)) == -1
	_cMsg := "Nใo foi possํvel criar o arquivo de saํda " + _cArq2 + "."
	MsgAlert(_cMsg, "Aten็ใo!")
	_nRet -= 1
Endif

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Inicializa a regua de processamento                                 ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If _nRet == 0
	_cMsg := "Aguarde. Decriptografando o arquivo de retorno..."
	Processa({|| _nRet := DeCripta()}, _cMsg)
Endif

// Fecha o arquivo criptografado e o arquivo de saida.
fClose(_nArq1); fClose(_nArq2)

// Fecha a DLL.
ExecInDllClose(_nDll1)

Return(_nRet)


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณDECRIPTA  บ Autor ณ Felipe Raposo      บ Data ณ  28/02/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Desfaz a criptografia do arquivo ja aberto.                บฑฑ
ฑฑบ          ณ Funcao auxiliar para a geracao da regua de progressao.     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ CIEE.                                                      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function DeCripta()

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Declaracao de variaveis.                                            ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
Local _nTamLin, _nTamArq, _nBtLidos
Local _cBufNor, _cBuffCrip, _nDLLRun

_nTamLin   := 4096  // 4 Kb - Tamanho da linha.
_nTamArq   := fSeek(_nArq1, 0, 2)  // Tamanho do arquivo.
_cBuffCrip := Space(_nTamLin)

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Configura a regua de progressao.                                    ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
ProcRegua(_nTamArq / _nTamLin)  // Numero de registros a processar

fSeek(_nArq1, 0, 0)  // Posiciona no primeiro caractere do arquivo.
_nBtLidos := fRead(_nArq1, @_cBuffCrip, _nTamLin)  // Le a primeira linha.
Do While _nBtLidos != 0  // Enquanto nao for fim de arquivo.
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Incrementa a regua.                                                 ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	IncProc()
	
	// Utiliza a DLL para desfazer a criptografia.
	_cBufNor := _cBuffCrip
	_nDLLRun := ExeDllRun2(_nDll1, 2, @_cBufNor)
	//_cBufNor := ExecInDllRun(_nDll, 2, _cBuffCrip)
	
	Do Case
		Case _nDLLRun == -1
			_cMsg := "O tamanho do texto a ser decriptografado ้ invแlido!" + _EOL +;
			"O sistema irแ abandonar o processamento."
			MsgAlert(_cMsg, "Aten็ใo")
			Return
			
		Case _nDLLRun == -2
			_cMsg := "Erro no processamento da decriptografia do arquivo." + _EOL +;
			"O sistema irแ abandonar o processamento."
			MsgAlert(_cMsg, "Aten็ใo")
			Return
	EndCase
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Grava a string decriptografada no arquivo de saida.                 ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If fWrite(_nArq2, _cBufNor, len(_cBufNor)) != len(_cBufNor)
		MsgAlert("Ocorreu um erro na grava็ใo do arquivo.", "Aten็ใo!")
		Return -1
	Endif
	
	// Leitura da proxima linha do arquivo texto.
	_nBtLidos := fRead(_nArq1, @_cBuffCrip, _nTamLin)
EndDo
Return 0