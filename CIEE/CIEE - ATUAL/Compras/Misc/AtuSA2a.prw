#INCLUDE "rwmake.ch"
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณAtuSA2a   บ Autor ณ Felipe Raposo      บ Data ณ  24/07/02   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Gera o arquivo de envio para a interface do cadastro de    บฑฑ
ฑฑบ          ณ fornecedores com a informatica.                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ CIEE                                                       บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
User Function AtuSA2a()

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Declaracao de variaveis.                                            ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
Local _lRet
Private cArqTxt, nHdl, cEOL
Private oGeraTxt
Private cString := "SA2"
// Campos que serao exportados.
Private _aCpo := {"A2_COD", "A2_CGC", "A2_CONV", "A2_ESTNUM"}
// Caractere delimitador.
Private _cDelim  := "|"
// Extensao padrao do arquivo de envio.
Private _cExtArq := "env"
// Diretorio padrao do arquivo de envio.
Private _cPath := "\AtuForn\"

cArqTxt := _cPath + dtos(dDataBase) + "." + _cExtArq + space(60)
cEOL    := chr(13) + chr(10)
_lRet   := .F.

dbSelectArea(cString)
dbSetOrder(1)

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Montagem da tela de processamento.                                  ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
@ 200, 001 to 380, 380 dialog oGeraTxt Title OemToAnsi("Geraใo de arquivo texto")
@ 002, 002 to 073, 188
@ 018, 018 Say "Arquivo"
@ 018, 060 Get cArqTxt Picture "@S70" Valid .T.
@ 040, 018 Say OemToAnsi("Este programa irแ gerar um arquivo texto, conforme os parโme-")
@ 048, 018 Say OemToAnsi("tros definidos pelo usuแrio.                                 ")
@ 075, 128 BmpButton Type 01 Action _lRet := OkGeraTxt()
@ 075, 158 BmpButton Type 02 Action Close(oGeraTxt)
Activate Dialog oGeraTxt Centered
Return (_lRet)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuno    ณOKGERATXT บ Autor ณ Felipe Raposo      บ Data ณ  24/07/02   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescrio ณ Funcao chamada pelo botao OK na tela inicial de processamenบฑฑ
ฑฑบ          ณ to. Executa a geracao do arquivo texto.                    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ CIEE                                                       บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function OkGeraTxt()
Local _lRet
_lRet := .F.
// Adiciona a extensao ao arquivo, caso nao tenha sido informado.
If at(".", cArqTxt) == 0
	cArqTxt := AllTrim(cArqTxt) + "." + _cExtArq
Endif
// Adiciona o diretorio, caso nao tenha sido informado.
If at("\", cArqTxt) == 0
	cArqTxt := _cPath + AllTrim(cArqTxt)
Endif
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Cria o arquivo texto.                                               ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
_cMsg := "O arquivo ja existente: " + AllTrim(cArqTxt) + cEOL +;
"Deseja sobrepor?"
If !file(cArqTxt) .or. MsgBox(OemToAnsi(_cMsg), OemToAnsi("Aten็ใo!"), "YESNO")
	If (nHdl := fCreate(cArqTxt)) == -1
		_cMsg := "O arquivo nใo pode ser executado: " + AllTrim(cArqTxt) + cEOL +;
		"Verifique os parโmetros."
		MsgBox(OemToAnsi(_cMsg), OemToAnsi("Aten็ใo!"), "ALERT")
	Else
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Inicializa a regua de processamento                                 ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		Processa({|| _lRet := RunCont() },"Processando...")
		Close(oGeraTxt)
	Endif
Endif
Return (_lRet)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuno    ณ RUNCONT  บ Autor ณ Felipe Raposo      บ Data ณ  24/07/02   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescrio ณ Funcao auxiliar chamada pela PROCESSA.  A funcao PROCESSA  บฑฑ
ฑฑบ          ณ monta a janela com a regua de processamento.               บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ CIEE                                                       บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function RunCont()
Local nTamLin, cLin, _aTamX3, _nAux1
Local _cMsg, _cTit, _aCpoAux, _nCnt
Local _nTamInt, _nTamDec, _lRet
Local _cAlsAnt, _aAreaAnt, _aAreaStr

// Salva o estado dos arquivos abertos.
_cAlsAnt  := Alias()
_aAreaAnt := GetArea()
dbSelectArea(cString)
_aAreaStr := GetArea()

// Conta o tamanho da linha.
_aCpoAux := {}
nTamLin  := 0
For _nAux1 := 1 to len(_aCpo)
	_cCampo := _aCpo[_nAux1]
	_cTipo  := Type(_cCampo)
	_aTamX3 := TamSX3(_cCampo)
	aAdd(_aCpoAux, {_cCampo, _cTipo, _aTamX3[1], _aTamX3[2]})
	nTamLin += (_aTamX3[1] + 1)
Next _nAux1
nTamLin += len(cEOL)

_nCnt := 0
dbSelectArea(cString)
Set Filter to A2_TPFOR == "1" // Somente fornecedores, nao funcionarios.
dbGoTop()
ProcRegua(RecCount()) // Numero de registros a processar.
Do While !eof()
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Incrementa a regua                                                  ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	IncProc()
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Monta a linha que sera gravada no arquivo texto.                    ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	cLin := ""
	For _nAux1 := 1 to len(_aCpoAux)
		_cCampo := _aCpoAux[_nAux1][1]
		Do Case
			Case Type(_cCampo) == "C"
				_cCampo := &_cCampo
				
			Case Type(_cCampo) == "N"
				_nTamInt := _aCpoAux[_nAux1][3] - _aCpoAux[_nAux1][4]
				_nTamDec := _aCpoAux[_nAux1][4]
				_nTamInt += IIf (_nTamDec != 0, 1, 0)  // O ponto decimal.
				_cCampo := StrZero(&_cCampo, _nTamInt, _nTamDec)
				_cCampo := StrTran(_cCampo, ".", "")  // Exclui o ponto da string.
				
			Case Type(_cCampo) == "D"
				_cCampo := dtos(&_cCampo)
				
			Case Type(_cCampo) == "L"
				_cCampo := If(&_cCampo, "T", "F")
				
			OtherWise
				_cMsg := "O campo " + _cCampo + " ้ do tipo " + Type(_cCampo) +;
				" e nใo pode ser importado. Entre em contato com o administrador " +;
				"do sistema."
				MsgBox(OemToAnsi(_cMsg), OemToAnsi("Atencใo!"), "ALERT")
				Return
		EndCase
		cLin += (_cCampo + _cDelim)
	Next _nAux1
	cLin += cEOL
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Gravacao no arquivo texto. Testa por erros durante a gravacao da    ณ
	//ณ linha montada.                                                      ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If (_nAux1 := fWrite(nHdl, cLin, Len(cLin))) != nTamLin // Len(cLin)
		_cMsg := "Ocorreu um erro na grava็ใo do arquivo. Continua?"
		_cTit := "Erro: " + AllTrim(str(_nAux1)) + AllTrim(str(nTamLin))
		If !MsgBox(OemToAnsi(_cMsg), OemToAnsi(_cTit), "YESNO")
			Exit
		Endif
	Endif
	_nCnt ++
	dbSkip()
EndDo

// Restaura o estado dos arquivos abertos.
Set Filter to // Limpa filtro.
RestArea(_aAreaStr)
dbSelectArea(_cAlsAnt)
RestArea(_aAreaAnt)

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ O arquivo texto deve ser fechado, bem como o dialogo criado na fun- ณ
//ณ cao anterior.                                                       ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
fClose(nHdl)
If (_lRet := file(cArqTxt))
	_cMsg := "Arquivo gerado com sucesso: " + AllTrim(cArqTxt) + cEOL +;
	"Total: " + AllTrim(str(_nCnt)) + " registro(s) gerado(s)."
	MsgBox(OemToAnsi(_cMsg), OemToAnsi("Gera็ใo do arquivo"), "INFO")
Endif
Return (_lRet)