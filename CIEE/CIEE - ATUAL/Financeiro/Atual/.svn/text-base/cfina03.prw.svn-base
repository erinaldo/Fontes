#include "rwmake.ch"
/*/


Ŀ
Programa   CFINA580  Autor  Felipe Raposo          Data  24/07/02 
Ĵ
Descrio  Liberacao dos titulos a pagar.                    		  
           Programa personalizado para tratar a liberacao de titulos  
           a pagar. O usuario quer que o titulo seja liberado por dois
           usuarios antes de ser baixado no sistema ou enviado ao     
           banco.                                                     
Ĵ
Sintaxe    CFINA580()                                                 
Ĵ
Uso        CIEE                                                       
ٱ


/*/
/*/


Ŀ
Funo     FiNA580   Autor  Fernando Dourado       Data  19/03/01 
Ĵ
Descrio  Liberacao dos  Titulos a Pagar                     		  
Ĵ
Sintaxe    FINA580()                                                  
Ĵ
 Uso       Genrico                                                   
ٱ


/*/
User Function cfina03()

Private aRotina, cMarca, cCadastro, cAlias

aRotina	:= {;
{"Pesquisar ", "AxPesqui",       0, 1},;
{"Manual    ", "U_fina03Man(2)", 0, 2},;
{"Automtica", "U_fina03Aut(2)", 0, 2},;
{"Cancelar  ", "U_fina03Can(2)", 0, 2},;
{"Legenda   ", "U_fina03Leg(1)", 0, 2}}

cMarca    := GetMark()
cCadastro := "Liberao dos pagamentos"
cAlias    := "SE2"

If !GetMv("MV_CTLIPAG")
	MsgAlert(OemToAnsi("Esta rotina s poder ser executada, quando o parmetro MV_CTLIPAG for 'T'"))
	Return
Endif

//Ŀ
// Ponto de entrada para ser utilizado antes do browse.         
//
If ExistBlock("F580BROW")
	ExecBlock("F580BROW", .F., .F.)
EndIf

//Ŀ
// Endereca a Funo de BROWSE                                  
//
mBrowse(06, 01, 22, 75, cAlias,,,,,, U_fina03Leg(0))

//Ŀ
// Recupera a Integridade dos dados                             
//
RetIndex("SE2")
dbSetOrder(1)
Set Filter to
dbSeek(xFilial())
Return


/*/


Ŀ
Funo     fA580Man  Autor  Fernando Dourado       Data  19/03/01 
Ĵ
Descrio  Liberacao Manual                                           
Ĵ
Sintaxe    Fa580Man()                                                 
Ĵ
 Uso       Genrico                                                   
ٱ


*/
User Function fina03Man(nOpc)
Local nOpcA := 0
If Empty(SE2->E2_DATALIB) .or. SE2->E2_VALOR > GetMV("MV_VLMINPG")
	dbSelectArea(cAlias)
	nOpcA := AxVisual(cAlias, RecNo(), nOpc)
	If nOpcA == 1
		dbSelectArea("SE2"); Libera()
	EndIf
Endif
Return


/*/


Ŀ
Funo     fA580Aut  Autor  Fernando Dourado       Data  19/03/01 
Ĵ
Descrio  Liberacao Automtica                                       
Ĵ
Sintaxe    Fa580Aut()                                                 
Ĵ
 Uso       Genrico                                                   
ٱ


*/
User Function fina03Aut(nOpc)
//Ŀ
// Define Vari veis                                             
//
Local oDlg
Local oDlg1
Local oCbx
Local lInverte := .F.
Local nProxReg
Local lEof
Local nOpcA    := 0
Local nC       := 0
Local cIndex   := ""
Local cChave   := ""
Local nSavOrd1 := SE2->(IndexOrd() - 1)
Local cArquivo
Local nX := 0
Local nRec
Local nRegAtu
Local nIndex
Local aCampos := {}

Local lOk := Pergunte("FIN580", .T.)
If !lOk
	Return .T.
Endif

Do While .T.
	nRec := SE2->(RecNo())
	
	dbSelectArea(cAlias)
	//Ŀ
	// Monta expresso do Filtro para seleo                       
	//
	cIndex    := CriaTrab(nil, .F.)
	// cChave    := "E2_FILIAL + DTOS(E2_VENCREA) + E2_NOMFOR + E2_PREFIXO + E2_NUM + E2_PARCELA + E2_TIPO"
	cChave    := SE2->(IndexKey())
	nOldIndex := IndexOrd()
	IndRegua("SE2", cIndex, cChave,, FilChecF(), "Selecionando Registros..." )
	nIndex := RetIndex("SE2")
	dbSelectArea("SE2")
	#IFNDEF TOP
		dbSetIndex(cIndex + OrdBagExt())
	#ENDIF
	dbSetOrder(nIndex + 1)
	dbGoTop()
	
	If bof() .and. eof()
		Help(" ", 1, "RECNO")
		RetIndex("SE2")
		Set Filter to
		Exit
	EndIf
	nOpcA	:= 0
	aCampos := {}
	aAdd(aCampos,{"E2_OK", "", "  ", ""})
	dbSelectArea("SX3")
	dbSeek ("SE2")
	Do While !eof() .and. (X3_ARQUIVO == "SE2")
		If X3USO(X3_USADO)  .and. cNivel >= X3_NIVEL .and. X3_CONTEXT != "V" .or. ;
			AllTrim(X3_CAMPO) == "E2_DATALIB"
			AADD(aCampos,{X3_CAMPO, "", X3Titulo(), X3_PICTURE})
		Endif
		dbSkip()
	EndDo
	dbSelectArea("SE2")
	If mv_par11 <> 2
		MarcaRegs("SE2", cMarca)
	Endif
	
	Define MsDialog oDlg1 Title OemToAnsi("Liberao dos Pagamentos") From 09, 00 to 28, 80 of oMainWnd
	oMark := MsSelect():New(cAlias	,;
	"E2_OK"			,;
	"E2_DATALIB"	,;
	aCampos			,;
	@lInverte		,;
	@cMarca			,;
	{035, 001, 143, 315})
	
	oMark:oBrowse:lhasMark    := .T.
	oMark:oBrowse:lCanAllmark := .T.
	oMark:oBrowse:bAllMark    := {|| FA580Inverte(cMarca,,)}
	
	Activate MsDialog oDlg1 on init EnchoiceBar(oDlg1,{|| nOpcA := 1,oDlg1:End()},;
	{|| nOpcA := 000, ODlg1:End()}) Center
	
	dbSelectArea("SE2")
	If nOpcA == 1
		dbSelectArea("SE2")
		MsUnlockAll()
		dbGoTop()
		Do While !Eof() // .and. E2_FILIAL == cFilial
			// If marked(E2_OK) .and. empty(E2_DATALIB)
			If E2_OK == cMarca .and. empty(E2_DATALIB)
				dbSelectArea("SE2"); Libera()
			EndIf
			SE2->(dbSKip())
		EndDo
	EndIf
	dbSelectArea("SE2")
	RetIndex("SE2")
	Set Filter to
	Exit
EndDo
dbSelectArea("SE2")
RetIndex("SE2")
dbSetOrder(1)
dbGoTop()
#IFNDEF TOP
	FErase (cIndex + OrdBagExt())
#ENDIF
Return (.T.)


/*/


Ŀ
Funo     fA580Can  Autor  Fernando Dourado       Data  19/03/01 
Ĵ
Descrio  Cancelar a liberacao.                                      
Ĵ
Sintaxe    Fa580Can()                                                 
Ĵ
 Uso       Genrico                                                   
ٱ


*/
User Function fina03Can(nOpc)
Local _cUserCod, _aAreaZ2
Local nOpcC := 0
// Verifica se o titulo pode sofrer o cancelamento da liberacao de baixa.
If !empty(SE2->E2_BAIXA)
	MsgAlert(OemToAnsi("Ateno!!! Ttulo j baixado."))
ElseIf empty(SE2->E2_USUALIB)
	MsgAlert(OemToAnsi("Ateno!!! Ttulo no liberado."))
ElseIf empty(SE2->E2_DTLIB1) .and. empty(SE2->E2_DTLIB2)
	MsgAlert(OemToAnsi("Ateno!!! Ttulo com liberao j cancelada."))
Else
	dbSelectArea(cAlias)
	nOpcC := AxVisual(cAlias, RecNo(), nOpc)
	If nOpcC == 1
		dbSelectArea(cAlias)
		_cUserCod := RetCodUsr()  // USRRETNAME(RETCODUSR())
		_aAreaZ2 := SZ2->(GetArea())
		SZ2->(dbSetOrder(1))  // Z2_FILIAL+Z2_CODUSR
		If SZ2->(dbSeek(xFilial("SZ2") + _cUserCod,.F.))
			// Verifica se o liberador tem o privilegio para cancelar a lieracao.
			If SZ2->Z2_CANCLIB == "S"
				// Efetua o cancelamento da baixa.
				Begin Transaction
				RecLock("SE2", .F., .T.)
				SE2->E2_USUALIB := Subs(cUsuario, 07, 15)
				SE2->E2_DATALIB := ctod("  /  /  ")
				SE2->E2_USRLIB1 := ""
				SE2->E2_DTLIB1  := ctod("  /  /  ")
				SE2->E2_USRLIB2 := ""
				SE2->E2_DTLIB2  := ctod("  /  /  ")
				SE2->(msUnlock())
				End Transaction
			Else
				MsgAlert(OemToAnsi("Ateno!!! Esse liberador no pode efetuar o cancelamento."))
			Endif
		Else
			MsgAlert(OemToAnsi("Ateno!!! Liberador no cadastrado."))
		Endif
		SZ2->(RestArea(_aAreaZ2))
	EndIf
Endif
dbSelectArea(cAlias)
Return


/*/


Ŀ
Funo    FA580Leg     Autor  Mauricio Pequim Jr.  Data  04.12.01 
Ĵ
Descrio  Cria uma janela contendo a legenda da mBrowse ou retorna a 
           para o BROWSE                                              
Ĵ
 Uso       Fina580                                                    
ٱ


/*/
User Function fina03Leg(nOpcLeg)

Local aLegenda, uRetorno

aLegenda := {;
{"BR_VERDE",    "Titulo liberado para pagamento."},;
{"BR_AZUL",     "Titulo aguardando liberacao."},;
{"BR_VERMELHO", "Titulo totalmente baixado."}}

uRetorno := .T.

If nOpcLeg == 0 // Via menu, a opcao passada eh 1.
	uRetorno := {;
	{' E2_SALDO <= 0', aLegenda[3][1]},;
	{'!EMPTY(E2_DATALIB) .or.  E2_VALOR < GETMV("MV_VLMINPG")', aLegenda[1][1]},;
	{' EMPTY(E2_DATALIB) .and. E2_VALOR > GETMV("MV_VLMINPG")', aLegenda[2][1]}}
Else
	BrwLegenda(cCadastro, "Legenda", aLegenda)
Endif
Return (uRetorno)


/*/


Ŀ
Funo    fa580ChecF Autor  Fernando Dourado       Data  21/03/01 
Ĵ
Descrio Retorna condio para indice condicional.                   
Ĵ
Sintaxe	 fa580ChecF()                                                
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


*/
Static Function FilChecF()
Local cFiltro
cFiltro  :=;
"(E2_FORNECE       >= '" + mv_par01 + "' .and. E2_FORNECE <= '" + mv_par02 + "'" +;
" .and. E2_PORTADO >= '" + mv_par03 + "' .and. E2_PORTADO <= '" + mv_par04 + "'" +;
" .and. DTOS(E2_VENCTO) >= '" + DTOS(mv_par05) + "'" +;
" .and. dtos(E2_VENCTO) <= '" + Dtos(mv_par06) + "'" +;
" .and. E2_VALOR >= " + ALLTRIM(STR(mv_par07,17,2)) +;
" .and. E2_VALOR <= " + ALLTRIM(STR(mv_par08,17,2)) +;
" .and. E2_SALDO > 0" +;
" .and. !empty(E2_NUMBOR) " +;
" .and. empty(E2_DATALIB))" +;
" .or. (E2_SALDO + E2_SDACRES - E2_SDDECRE <= " + AllTrim(str(GetMv('MV_VLMINPG'), 17, 02)) +;
" .and. E2_SALDO > 0" +;
" .and. empty(E2_DATALIB))"
Return (cFiltro)


/*


Ŀ
Funo	 Fa580Marca Autor  Alessandro B. Freire   Data  21/11/95 
Ĵ
Descrio  Trata o valor	para marcar e desmarcar item.             
Ĵ
Sintaxe    Fa580Marca(ExpN1, ExpD1, ExpD2)                            
Ĵ
 Uso		  FINA580                                                    
ٱ


*/
Static Function MarcaRegs(cAlias, cMarca)
Local cAliasAnt, _aArea
cAliasAnt := Alias()
dbSelectArea(cAlias)
_aArea := GetArea()
dbGoTop()
Do While !eof()
	RecLock(cAlias, .F.)
	SE2->E2_OK := cMarca
	msUnLock()
	dbSkip()
EndDo
RestArea(_aArea)
dbSelectArea(cAliasAnt)
Return


/*/


Ŀ
Funo    FA580Inver Autor  Fernando Dourado       Data  19/03/01 
Ĵ
Descrio  Marca / Desmarca todos os titulos.                         
Ĵ
 Uso       FINA580                                                    
ٱ


*/
Static Function Fa580Inverte(cMarca)
Local _aAreaE2
_aAreaE2 := SE2->(GetArea())
SE2->(dbGoTop())
Do While SE2->(!eof()) .and. xFilial("SE2") == SE2->E2_FILIAL
	RecLock("SE2", .F.)
	SE2->E2_OK := If (SE2->E2_OK == cMarca, "  ", cMarca)
	SE2->(MsUnlock())
	SE2->(dbSkip())
EndDo
SE2->(RestArea(_aAreaE2))
oMark:oBrowse:Refresh(.T.)
Return Nil


Static Function Libera()
RecLock("SE2", .F.)
SE2->E2_DATALIB := dDataBase
SE2->E2_USUALIB := Subs(cUsuario, 07, 15)
MsUnlock()
If ExistBlock("FINA580")
	Execblock("FINA580", .F., .F.)
Endif
Return .T.