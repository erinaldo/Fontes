/*/


Ŀ
  DATA   BOPS Program.					ALTERACAO				  
Ĵ
03.08.9817223AEduardo  Alterado o Calc.do LE para considerar o PE   
22.09.98XXXXXXRodrigo  Alterado o Calc.do LE para desconsiderar o PE
05.10.9803771ARodrigo  Incluida chamada da funcao ListBoxAll        
24.10.98XXXXXXFabio C. Incluida tratamento para a Shell com IFDEF   
06.11.9815652ARodrigo  Acerto no calculo com material aprp. indireta
30.06.98PROTHECesarVal Acerto no tamanho dos MSGET                  
24/05/00XXXXXXPatricia Alt. A290MENU(), da tabela "03" para "SBM".  
08/05/02XXXXXXFelipe R Especifico CIEE.                             
ٱ


/*/
#INCLUDE "MATA290.CH"
//#include "fivewin.ch"
/*  Cabecalho padrao.


Ŀ
Funo     MATA290   Autor  Eveli Morasco          Data  31/01/92 
Ĵ
Descrio  Calculo do lote economico                                  
Ĵ
Sintaxe e  MATA290(void)                                              
Ĵ
 Uso       Generico                                                   
ٱ


*/

/*


ͻ
Programa  MATA290A  Autor  Microsiga            Data   05/08/02   
͹
Desc.      O usuario necessita da inclusao de                         
                                                                      
͹
Uso        Especifico CIEE.                                           
ͼ


*/
Function MATA290
PRIVATE aOpcoes,nTotRegs:=0,lEnd:=.F.

#IFDEF TOP
	TCInternal(5,"*OFF")   // Desliga Refresh no Lock do Top
#ENDIF
aOpcoes := A290Menu()
IF aOpcoes == NIL
	Return
Endif	

//Ŀ
// Variaveis utilizadas para desenhar cursor                    
//
dbSelectArea("SB1")
nTotRegs := nTotRegs + RecCount()
nTotRegs := nTotRegs + RecCount()

dbSelectArea("SB3")
nTotRegs := nTotRegs + RecCount()
nTotRegs := nTotRegs + RecCount()
nTotRegs := nTotRegs + RecCount()
nTotRegs := nTotRegs + RecCount()
nTotRegs := nTotRegs + RecCount()

dbSelectArea("SD1")
nTotRegs := nTotRegs + RecCount()

dbSelectArea("SD2")
nTotRegs := nTotRegs + RecCount()
	
dbSelectArea("SD3")
nTotRegs := nTotRegs + RecCount()
	
Processa({|lEnd| MA290Process(aOpcoes,@lEnd)},OemToAnsi(STR0001),OemToAnsi(STR0002),.F.)    //"Clculo do Lote Econmico"###"Efetuando Clculo do Lote Econmico..."

Return

/*

Ŀ
Funo    A290CalCon Autor  Eveli Morasco          Data  10/02/92 
Ĵ
Descrio  Recalcula o consumo medio do mes                           
Ĵ
Sintaxe e  A290CalCon()                                               
Ĵ
 Uso       MATA290                                                    
ٱ


*/
STATIC Function A290CalCon
Static l290Cons

LOCAL cCod,nTotCod:=0
LOCAL cMes:="B3_Q"+StrZero(Month(dDataBase),2) , cDataIni, cDataFim
LOCAL nCons:=0

l290Cons := If(l290Cons==NIL,ExistBlock("A290CONS"),l290Cons)

//Ŀ
// Zera o arquivo de consumos antes de recalcular               
//
dbSelectArea("SB3")
dbSeek(cFilial)
While !EOF() .And. B3_FILIAL == cFilial
	RecLock("SB3",.F.) // tentar ver replace all para TOPCONN
	Replace &(cMes) With 0
	MsUnlock()
	dbSkip()
	IncProc()
   MsUnlock()
EndDo

//Ŀ
// Processa o arquivo SD2 -> Itens das notas fiscais de entrada 
//
dbSelectArea("SD2")           // itens das notas fiscais de saida

#IFDEF TOP
	cDataINI := StrZero(Year(dDataBase),4,0)+StrZero(Month(dDataBase),2,0)+"01"
	cDataFim := Dtos(LastDay(dDataBAse))
	cFilSD2 := 'DTOS(D2_EMISSAO) >= "'+cDataINI+'" .and. DTOS(D2_EMISSAO) <= "'+cDataFIM+'"'
	MsFilter(cFILSD2)
#ENDIF
dbSetOrder(1)
dbSeek(cFilial)
While !EOF() .And. D2_FILIAL == cFilial
	nTotCod := 0
	cCod    := D2_COD
	While !EOF() .And. D2_FILIAL+D2_COD == cFilial+cCod

		//Ŀ
		// Despreza Notas Fiscais Lancadas Pelo Modulo do Livro Fiscal  
		//
		If D2_ORIGLAN == "LF" .or. D2_TIPO $ "DB"
			dbSkip()
			Loop
		EndIf

		#IFDEF SHELL
			//Ŀ
			// Despreza Notas Fiscais Canceladas.                           
			//
			If SD2->D2_CANCEL == "S"
				dbSkip()
				Loop
			EndIf
		#ENDIF

		If Month(D2_EMISSAO) == Month(dDataBase) .And. Year(D2_EMISSAO) == Year(dDataBase)
			dbSelectArea("SF4")
			dbSeek(cFilial+SD2->D2_TES)
			dbSelectArea("SD2")
			If SF4->F4_ESTOQUE == "S"
				If D2_TES <= "500"
					nTotCod := nTotCod - D2_QUANT
				Else
					nTotCod := nTotCod + D2_QUANT
				EndIf
			EndIf
		EndIf
		dbSkip()
		//Ŀ
		// Movimentacao do Cursor                                       
		//
		IncProc()
   EndDo
	dbSelectArea("SB3")
	dbSeek(cFilial+cCod)
	If EOF()
		RecLock("SB3",.T.)
		Replace B3_FILIAL With cFilial, B3_COD With cCod
	Else
		RecLock("SB3",.F.)
	EndIf
	Replace &(cMes) With &(cMes) + nTotCod
	
	MsUnlock()
	
	dbSelectArea("SD2")
EndDo

//Ŀ
// Processa o arquivo SD1 -> Itens das notas fiscais de Dev.Vdas
//
dbSelectArea("SD1")           // itens das notas fiscais de entrada
#IFDEF TOP
	cDataINI := StrZero(Year(dDataBase),4,0)+StrZero(Month(dDataBase),2,0)+"01"
	cDataFim := Dtos(LastDay(dDataBAse))
	cFilSD1 := 'DTOS(D1_DTDIGIT) >= "'+cDataINI+'" .and. DTOS(D1_DTDIGIT) <= "'+cDataFIM+'" .and. '
	cFILSD1 += 'D1_TIPO == "D"'
	MsFilter(cFILSD1)
#ENDIF
dbSetOrder(2)
dbSeek(cFilial)
While !EOF() .And. D1_FILIAL == cFilial
	nTotCod := 0
    cCod    := D1_COD
    While !EOF() .And. D1_FILIAL+D1_COD == cFilial+cCod

		//Ŀ
		// Despreza Notas Fiscais Lancadas Pelo Modulo do Livro Fiscal  
		//
		If D1_ORIGLAN $ "LF"
			dbSkip()
			Loop
		EndIf
		//Ŀ
		// Especifico para a SHELL                                      
		//
		#IFDEF SHELL
			If D1_CANCEL == "S"
				dbSkip()
				Loop
			EndIf
		#ENDIF

		If Month(D1_DTDIGIT) == Month(dDataBase) .and. D1_TIPO="D" .And.;
		   Year(D1_DTDIGIT) == Year(dDataBase)
			dbSelectArea("SF4")
			dbSeek(cFilial+SD1->D1_TES)
			dbSelectArea("SD1")
			If SF4->F4_ESTOQUE == "S"
				If D1_TES <= "500"
					nTotCod := nTotCod - D1_QUANT
				Else
					nTotCod := nTotCod + D1_QUANT
				EndIf
			EndIf
		EndIf
		dbSkip()
		//Ŀ
		// Movimentacao do Cursor                                       
		//
		IncProc()
	EndDo
	dbSelectArea("SB3")
	dbSeek(cFilial+cCod)
	If EOF()
		RecLock("SB3",.T.)
		Replace B3_FILIAL With cFilial, B3_COD With cCod
	Else
		RecLock("SB3",.F.)
	EndIf
	Replace &(cMes) With &(cMes) + nTotCod
	
	MsUnlock()

	dbSelectArea("SD1")
EndDo

//Ŀ
// Processa o arquivo SD3 -> Movimentacoes internas             
//
dbSelectArea("SD3")           // movimentacoes internas

#IFDEF TOP
	cDataINI := StrZero(Year(dDataBase),4,0)+StrZero(Month(dDataBase),2,0)+"01"
	cDataFim := Dtos(LastDay(dDataBAse))
	cFilSD3 := 'DTOS(D3_EMISSAO) >= "'+cDataINI+'" .and. DTOS(D3_EMISSAO) <= "'+cDataFIM+'"'
	cFilSD3 += '.And.!(D3_CF=="DE8".Or.D3_CF=="RE8")'
	SET FILTER TO &cFILSD3
#ENDIF

dbSetOrder(3)       // ordem por codigo de produto
dbSeek(cFilial)
While !EOF() .And. D3_FILIAL == cFilial
	nTotCod := 0
	cCod    := D3_COD
	While !EOF() .And. D3_FILIAL+D3_COD == cFilial+cCod
		If Month(D3_EMISSAO) == Month(dDataBase) .And. Subs(D3_CF,2,1) == "E" .and.;
		   Year(D3_EMISSAO) == Year(dDataBase)
			If !(Substr(D3_CF,3,1) $ "3478")
				If D3_TM <= "500"
					nTotCod := nTotCod - D3_QUANT
				Else
					nTotCod := nTotCod + D3_QUANT
				EndIf
			EndIf
		EndIf
		dbSkip()
		//Ŀ
		// Movimentacao do Cursor                                       
		//
		IncProc()
   EndDo
	If l290Cons 
		nCons:=ExecBlock("A290CONS",.F.,.F.,{cCod,nTotCod})
		If ValType(nCons) == "N"
			nTotCod:=nCons
		EndIf
	EndIf
	dbSelectArea("SB3")
	dbSeek(cFilial+cCod)
	If EOF()
		RecLock("SB3",.T.)
		Replace B3_FILIAL With cFilial, B3_COD With cCod
	Else
		RecLock("SB3",.F.)
	EndIf
	Replace &(cMes) With &(cMes) + nTotCod
	
	MsUnlock()
	
	dbSelectArea("SD3")
EndDo
//Ŀ
// Devolve ordem principal dos arquivos                         
//
RETINDEX("SD1")
Set FIlter to
RETINDEX("SD2")
Set FIlter to
RETINDEX("SD3")
Set FIlter to

/*

Ŀ
Funo    A290CalNor Autor  Eveli Morasco          Data  10/02/92 
Ĵ
Descrio  Calculo normal da media de consumos (utiliza os pesos)     
Ĵ
Sintaxe e  A290CalNor()                                               
Ĵ
 Uso       MATA290                                                    
ٱ


*/
STATIC Function A290CalNor(nPorInc)
LOCAL nX,aPesos[12],cPesos,nTPesos:=0,nMeses,aPesoAux,nI,nConsid
LOCAL aTamSX3:={}

AFILL(aPesos,0)

//Ŀ
// Pega a configuracao de pesos do cliente                      
//
cPesos := GetMv("MV_PESOS")
cPesos := SubStr(cPesos,1,12)
cPesos := cPesos+Space(12-Len(cPesos))

For nX := 1 To 12
	aPesos[nX] := Val(SubStr(cPesos,nX,1))
Next nX

aPesoAux := aClone(aPesos)
//Ŀ
// Varre o arquivo de demandas e verifica filtro no SB1         
//
dbSelectArea("SB3")
dbSeek(cFilial)
While !EOF() .And. B3_FILIAL == cFilial
	dbSelectArea("SB1")
	dbSeek(cFilial+SB3->B3_COD)
	nMeses := CalcMeses(SB1->B1_CONINI)
	aPesos := aClone(aPesoAux)
	nI := nMeses

	If nMeses < 12
		nX := (Month(dDataBase)+1) - nMeses
		If nX <= 0
			nX += 13
		Endif
		If nX = 13
			nX := 1
		Endif
		aPesos := {0,0,0,0,0,0,0,0,0,0,0,0}
		If nMeses > 0
			nConsid := Month(dDataBase)+1
			nConsid := IIF(nConsid >12,1,nConsid)
			While nX != nConsid
				aPesos[nX] := aPesoAux[nX]
				nX++
				nX := IIF(nX>12,1,nX)
			End
		Endif
	Endif
	nTpesos := 0
	For nX := 1 To 12
		nTpesos += aPesos[nX]
	Next nX
	
	dbSelectArea("SB3")
	If MA290Filtro()
		RecLock("SB3",.F.)
		Replace  B3_MEDIA With ((B3_Q01*aPesos[01]+B3_Q02*aPesos[02]+B3_Q03*aPesos[03]+;
										 B3_Q04*aPesos[04]+B3_Q05*aPesos[05]+B3_Q06*aPesos[06]+;
										 B3_Q07*aPesos[07]+B3_Q08*aPesos[08]+B3_Q09*aPesos[09]+;
										 B3_Q10*aPesos[10]+B3_Q11*aPesos[11]+B3_Q12*aPesos[12])/;
										 nTpesos)*(1+nPorInc/100)
		Replace	B3_TOTAL With B3_MEDIA*SB1->B1_CUSTD
		aTamSX3:=TamSX3("B3_CTOTAL")
		Replace	B3_CTOTAL With Transform(B3_TOTAL,PesqPict("SB3","B3_CTOTAL",aTamSX3[1]))
		MsUnlock()
	EndIf
	dbSkip()
   IncProc()
EndDo

/*

Ŀ
Funo    A290CalMin Autor  Eveli Morasco          Data  11/02/92 
Ĵ
Descrio  Calculo da media de consumos que utiliza o conceito dos    
           minimos quadrados.                                         
Ĵ
Sintaxe e  A290CalMin()                                               
Ĵ
 Uso       MATA290                                                    
ٱ


*/
STATIC Function A290CalMin(nQtdMes)
LOCAL nX,cMesAux2,cMeses:="010203040506070809101112"
LOCAL nTotx:=0,nMpx:=0,nToty:=0,nMpy:=0,nTotXaYa:=0,nTotXaXa:=0,nTotYaYa:=0
LOCAL aX[nQtdMes],aY[nQtdMes],aXa[nQtdMes],aYa[nQtdMes]
LOCAL aXaYa[nQtdMes],aXaXa[nQtdMes],aYaYa[nQtdMes]
LOCAL nMesProj,nSX,nSY,nRXY,nK1,nK2,nYP,nMeses,nMesAux
LOCAL aTamSX3:={}
nMesAux := nQtdMes

For nX := 1 To nQtdMes
	 aX[nX] := nX
	 aY[nX] := 0
	aXa[nX] := 0
	aYa[nX] := 0
 aXaYa[nX] := 0
 aXaXa[nX] := 0
 aYaYa[nX] := 0
Next nX

//Ŀ
// Varre o arquivo de demandas e verifica filtro no SB1         
//
dbSelectArea("SB3")
dbSeek(cFilial)
STORE nQtdMes+1 TO nMesProj

// DEFINICOES DO X  --> MESES

FOR nX := 1 TO nQtdMes
	nTotx := nTotx + aX[nX]
NEXT
nMpx := nTotx/nQtdMes               // MEDIA PONDERADA DE X

While !EOF() .And. B3_FILIAL == cFilial
	nToty    := 0
	nTotXaYa := 0
	nTotXaXa := 0
	nTotYaYa := 0
	dbSelectArea("SB1")
	dbSeek(cFilial+SB3->B3_COD)
	nQtdMes := CalcMeses(SB1->B1_CONINI,nMesAux)
	dbSelectArea("SB3")
	If MA290Filtro()

		// DEFINICOES DO Y  --> QTDES
		For nX := 1 To nQtdMes
			STORE "B3_Q"+SUBS(cMeses+cMeses,(24+Month(dDataBase)*2)-1-(nQtdMes-nX)*2,2) TO cMesAux2
			aY[nX] := &(cMesAux2)
			nToty  := nToty + aY[nX]
		Next nX
		nMpy := nToty/nQtdMes               // MEDIA PONDERADA DE Y

		For nX := 1 To nQtdMes

			// CALCULO DO XA
			aXa[nX] := aX[nX] - nMpx

			// CALCULO DO YA
			aYa[nX] := aY[nX] - nMpy

			// CALCULO DO XA*YA
			aXaYa[nX] := aXa[nX]  * aYa[nX]
			nTotXaYa  := nTotXaYa + aXaYa[nX]

			// CALCULO DO XA*XA
			aXaXa[nX] := aXa[nX]  * aXa[nX]
			nTotXaXa  := nTotXaXa + aXaXa[nX]

			// CALCULO DO YA*YA
			aYaYa[nX] := aYa[nX]  * aYa[nX]
			nTotYaYa  := nTotYaYa + aYaYa[nX]
		Next nX

		nSX  := Abs( Sqrt( nTotXaXa / nQtdMes ) )
		nSY  := Abs( Sqrt( nTotYaYa / nQtdMes ) )
		nRXY := nTotXaYa / ( nQtdMes * nSX * nSY )
		nK1  := nRXY * ( nSX / nSY )
		nK2  := nRXY * ( nSY / nSX )
		nYP  := ( nK2 * nMesProj ) + ( nMpy - ( nK2 * nMpx ) )

		RecLock("SB3",.F.)
		Replace B3_MEDIA WITH nYP,B3_TOTAL WITH B3_MEDIA*SB1->B1_CUSTD
		aTamSX3:=TamSX3("B3_CTOTAL")
		Replace	B3_CTOTAL With Transform(B3_TOTAL,PesqPict("SB3","B3_CTOTAL",aTamSX3[1]))
		MsUnlock()
	EndIf
	dbSkip()
	IncProc()
EndDo

/*

Ŀ
Funo    A290CalLot Autor  Eveli Morasco          Data  12/02/92 
Ĵ
Descrio  Calcula o lote economico                                   
Ĵ
Sintaxe e  A290CalLot()                                               
Ĵ
 Uso       MATA290                                                    
ٱ


*/
STATIC Function A290CalLot(cCalPon,nPerA,nPerB,nPerC,nPorA,nPorB,nPorC)
LOCAL nTotCon:=0,nTotA:=0,nTotB:=0,nTotC:=0,nLote,cClasse,nPrazo
LOCAL cIndSB3	:= CriaTrab(NIL,.F.), nIndSB3
dbSelectArea("SB3")
nIndSB3 := RetIndex("SB3")
IndRegua("SB3",cIndSB3,"B3_FILIAL+B3_CTOTAL",,".T.", OemToAnsi(STR0003))    //"Selecionando Registros..."

dbSeek(cFilial+"z",.t.)
dbSkip(-1)
//Ŀ
// Soma o valor total das medias de consumo                     
//

While !Bof() .And. B3_FILIAL == cFilial
	dbSelectArea("SB1")
	dbSeek(cFilial+SB3->B3_COD)
	dbSelectArea("SB3")
	If MA290Filtro()
		nTotCon := nTotCon + B3_TOTAL
	EndIf
	dbSkip(-1)
	IncProc()
EndDo
nTotA   :=  nPorA * nTotCon / 100
nTotB   := (nPorB * nTotCon / 100) + nTotA
nTotC   := (nPorC * nTotCon / 100) + nTotB
nTotCon := 0

dbSeek(cFilial+"z",.t.)
dbSkip(-1)

While !Bof() .And. B3_FILIAL == cFilial
	dbSelectArea("SB1")
	If	dbSeek(cFilial+SB3->B3_COD)
		dbSelectArea("SB3")
		If MA290Filtro()
			Do Case
				Case nTotCon < nTotA
					nLote   := B3_MEDIA * nPerA
					cClasse := 'A'
				Case nTotCon < nTotB
					nLote   := B3_MEDIA * nPerB
					cClasse := 'B'
				OtherWise
					nLote   := B3_MEDIA * nPerC
					cClasse := 'C'
			EndCase
			nLote := IIF(nLote < SB1->B1_QE,SB1->B1_QE,Round(nLote / IIF(SB1->B1_QE==0,1,SB1->B1_QE), 0) * IIF(SB1->B1_QE==0,1,SB1->B1_QE))
			dbSelectArea("SB1")
			nPrazo := CalcPrazo(B1_COD,B1_LE)
			RecLock("SB1",.F.)
			Replace B1_LE With nLote
			If cCalPon == "x"
				Replace B1_EMIN WITH (SB3->B3_MEDIA * (nPrazo / 30 ))+CalcEstSeg( SB1->B1_ESTFOR )
			EndIf
			MsUnlock()		
			dbSelectArea("SB3")
			RecLock("SB3",.F.)
			Replace B3_CLASSE With cClasse
			MsUnlock()
			nTotCon := nTotCon + B3_TOTAL
		EndIf
	EndIf
	dbSelectArea("SB3")
	dbSkip(-1)
	IncProc()
EndDo

RetIndex("SB3")
Ferase(cIndSB3+OrdBagExt())
dbSetOrder(1)

/*

Ŀ
Funo    A290AjuLot Autor  Eveli Morasco          Data  13/02/92 
Ĵ
Descrio  Ajusta o lote economico pelo valor disponivel para compras 
Ĵ
Sintaxe e  A290AjuLot()                                               
Ĵ
 Uso       MATA290                                                    
ٱ


*/
STATIC Function A290AjuLot(nValDisp)
LOCAL nQatu:=0,nPedido:=0,nTotVal:=0,nFalta:=0,nDif:=0

//Ŀ
// Seleciona ordem por produto para o SC1 e o SC7               
//
dbSelectArea("SC1")
dbSetOrder(2)

dbSelectArea("SC7")
dbSetOrder(4)

//Ŀ
// Soma o valor total das compras do mes                        
//
dbSelectArea("SB1")
dbSeek(cFilial)
While !EOF() .And. B1_FILIAL == cFilial
	If MA290Filtro()
		dbSelectArea("SB2")
		dbSeek(cFilial+SB1->B1_COD)
		nQatu   := 0
		nPedido := 0
		While !EOF() .And. B2_FILIAL+B2_COD == cFilial+SB1->B1_COD
			nQatu := nQatu + B2_QATU
			dbSkip()
		EndDo
		dbSelectArea("SC1")
		dbSeek(cFilial+SB1->B1_COD)
		While !EOF() .And. C1_FILIAL+C1_PRODUTO == cFilial+SB1->B1_COD
			If C1_QUANT > C1_QUJE .And. Month(C1_DATPRF) == Month(dDataBase) .And.;
			   Year(C1_DATPRF) == Year(dDataBase)
				nPedido := nPedido + (C1_QUANT - C1_QUJE)
			EndIf
			dbSkip()
		EndDo
		dbSelectArea("SC7")
		dbSeek(cFilial+SB1->B1_COD)
		While !EOF() .And. C7_FILIAL+C7_PRODUTO == cFilial+SB1->B1_COD
			If C7_QUANT > C7_QUJE .And. Month(C7_DATPRF) == Month(dDataBase) .And. Empty(C7_RESIDUO);
			   .And. Year(C7_DATPRF) == Year(dDataBase)
				nPedido := nPedido + (C7_QUANT-C7_QUJE)
			EndIf
			dbSkip()
		EndDo
		dbSelectArea("SB3")
		dbSeek(cFilial+SB1->B1_COD)
		dbSelectArea("SB1")
		nFalta := (B1_EMIN+SB3->B3_MEDIA) - (nQatu+nPedido)
		If nFalta > 0
			If nFalta < B1_LE
				nFalta := B1_LE
			EndIf
			nTotVal := nTotVal + (nFalta * IIF(B1_UPRC > 0,B1_UPRC,B1_CUSTD))
		EndIf
	EndIf
	dbSkip()
	IncProc()
EndDo
If nTotVal <= 0
	nTotVal := 1
EndIf
nDif := (nValDisp/nTotVal)
//Ŀ
// Somente ajusta se o valor informado nao conseguir comprar    
// pelo menos 85% da necessidade calculada.                     
//
If nDif < 0.85
	dbSelectArea("SB1")
	dbSeek(cFilial)
	While !EOF() .And. B1_FILIAL == cFilial
		If MA290Filtro()
			RecLock("SB1",.F.)
			Replace B1_LE With B1_LE * nDif
			MsUnlock()
		EndIf
		dbSkip()
		IncProc()
   EndDo
EndIf
//Ŀ
// Devolve a ordem principal dos arquivos                       
//
dbSelectArea("SC1")
dbSetOrder(1)

dbSelectArea("SC7")
dbSetOrder(1)

/*

Ŀ
Funo     A290Menu  Autor  Jorge Queiroz          Data  06/01/92 
Ĵ
Descrio  Processa a string das opcoes de processamento              
Ĵ
Sintaxe e  ExpA1 := A290Menu()                                        
Ĵ
Parametros ExpA1 := Array devolvido pela escolha                      
           [1][1]-> Atualiza Consumo do Mes (x ou Branco)             
           [2][1]-> Calcula por Peso        (x ou Branco)             
              [2]-> Incremento Percentual   (Percentual N 3)          
           [3][1]-> Calculo pela Tendencia  (x ou Branco)             
              [2]-> Numero de Meses         (Meses N 2)               
           [4][1]-> Calculo Lote Economico  (x ou Branco)             
              [2]-> Calculo Ponto Pedido    (x ou Branco)             
           [5][1]-> Ajusta Lote Economico   (x ou Branco)             
              [2]-> Disponibilidade         (Valor N 11)              
           [6][1]-> Periodo Aquisicao A     (Valor N  4,1)            
           [6][2]-> Periodo Aquisicao B     (Valor N  4,1)            
           [6][3]-> Periodo Aquisicao C     (Valor N  4,1)            
           [7][1]-> %Distribuicao     A     (Percentual N 5,2)        
           [7][2]-> %Distribuicao     B     (Percentual N 5,2)        
           [7][3]-> %Distribuicao     C     (Percentual N 5,2)        
           [8][1]-> Tipo de Material  (** Todos)                      
           [9][1]-> Grupos de Material  (** Todos)                    
Ĵ
 Uso       MATA290                                                    
ٱ


*/
STATIC FUNCTION A290MENU()
LOCAL oDlg,oQual,oQual2,oUsado,nCalculo:=1,oChk1,oChk2,oChk3,oChk4,lCalc,lLote,;
	lPedido,lDispoF,aTipo:={},aGrupo:={},nIncre:=nMeses:=nDispoF:=0,;
   nA1:=nB1:=nC1:=1,nA2:=nB2:=30,nC2:=40,oGet1,oGet2,nOpca:=2,aOpt[9][3],;
   oChkQual,oChkQual2,lQual:=.T.,lQual2:=.T.
LOCAL oOk := LoadBitmap( GetResources(), "LBOK")
LOCAL oNo := LoadBitmap( GetResources(), "LBNO")
LOCAL cVarQ := cVarQ2:="  "
LOCAL cCapital
Private cCadastro := OemToAnsi(STR0004)   //"Lote Econmico"

//Ŀ
//Monta a Tabela de Tipos                                      
//
dbSelectArea("SX5")
dbSeek(cFilial+"02")
While (X5_filial == cFilial) .AND. (X5_tabela == "02")
	cCapital := OemToAnsi(Capital(X5Descri()))
	AADD(aTipo,{.T.,SubStr(X5_chave,1,3)+cCapital})
	dbSkip()
EndDo

//Ŀ
//Monta a Tabela de Grupos                                      
//
dbSelectArea("SBM")
dbSeek(xFilial())
While !EOF() .And. BM_FILIAL == cFilial
	cCapital := OemToAnsi(Capital(BM_DESC))
	AADD(aGrupo,{.T.,SubStr(BM_GRUPO,1,5)+" "+cCapital})
	dbSkip()
EndDo

DEFINE MSDIALOG oDlg TITLE cCadastro From 145,0 To 455,628 OF oMainWnd PIXEL
	@ 02,15 TO 25,120 LABEL "" OF oDlg  PIXEL
	@ 10,20 CHECKBOX oChk1 VAR lCalc PROMPT OemToAnsi(STR0005) SIZE 90, 10 OF oDlg PIXEL ;oChk1:oFont := oDlg:oFont   										//"Atualizao do Consumo do Ms"
	@ 28,15 TO 92,120 LABEL OemToAnsi(STR0006) OF oDlg  PIXEL    //"Clculos"
	@ 38,20 RADIO oUsado VAR nCalculo 3D SIZE 70,10 PROMPT OemToAnsi(STR0007),OemToAnsi(STR0008) OF oDlg PIXEL      											//"Por Peso"###"Pela Tendncia"
    oUsado:bChange := { || IIF(nCalculo=1,(oGet1:Enable(),oGet2:Disable()),(oGet1:Disable(),oGet2:Enable())) }
	@ 62,20 Say OemToAnsi(STR0009) SIZE 40,10 OF oDlg PIXEL    																													//"Incremento:"
	@ 62,67 MSGET oGet1 VAR nIncre When IIF(nCalculo=1,.T.,.F.) Picture "999" SIZE 15,10 OF oDlg PIXEL
	@ 78,20 Say OemToAnsi(STR0010) SIZE 60,10 OF oDlg PIXEL     																												//"Nmero de Meses:"
	@ 78,67 MSGET oGet2 VAR nMeses When IIF(nCalculo=2,.T.,.F.) Picture "999" SIZE 15,10 VALID (nMeses < 13 .And. nMeses > 0) OF oDlg PIXEL
	@ 95,15 TO 148,120 LABEL "" OF oDlg  PIXEL
	@ 103,20 CHECKBOX oChk2 VAR lLote PROMPT OemToAnsi(STR0011) SIZE 80, 10 OF oDlg PIXEL ;oChk2:oFont := oDlg:oFont    										//"Clculo do Lote Econmico"
	@ 113,20 CHECKBOX oChk3 VAR lPedido When lLote PROMPT OemToAnsi(STR0012) SIZE 80, 10 OF oDlg PIXEL ;oChk3:oFont := oDlg:oFont   	//"Clculo do Ponto de Pedido"
	@ 123,20 CHECKBOX oChk4 VAR lDispoF PROMPT OemToAnsi(STR0013) SIZE 90, 10 OF oDlg PIXEL ;oChk4:oFont := oDlg:oFont    									//"Ajusta Lote Econmico pela dispo-"
    @ 133,20 Say OemToAnsi(STR0014) SIZE 80,10 OF oDlg PIXEL    																												//"nibilidade financeira"
	@ 135,70 MSGET nDispoF When lDispoF Picture "@E 99,999,999,999" VALID nDispoF > 0 SIZE 45,10 OF oDlg PIXEL
	@ 02,130 TO 68,300 LABEL OemToAnsi(STR0015) OF oDlg PIXEL    																												//"Classificao ABC"
	@ 15,182 TO 60,219 LABEL "A" OF oDlg  PIXEL
	@ 15,220 TO 60,257 LABEL "B" OF oDlg  PIXEL
	@ 15,258 TO 60,295 LABEL "C" OF oDlg  PIXEL
	@ 23,135 Say OemToAnsi(STR0016) SIZE 45,10 OF oDlg PIXEL   //"Perodo de"
	@ 30,135 Say OemToAnsi(STR0017) SIZE 45,10 OF oDlg PIXEL   //"Aquisio(meses)"
	@ 43,135 Say OemToAnsi(STR0018) SIZE 45,10 OF oDlg PIXEL   //"Distribuio"
	@ 50,135 Say OemToAnsi(STR0019) SIZE 45,10 OF oDlg PIXEL   //"Percentual (%)"
	@ 25,185 MSGET nA1 Picture "99.9" VALID nA1 >0 SIZE 17,10 OF oDlg PIXEL
	@ 25,223 MSGET nB1 Picture "99.9" VALID nB1 >0 SIZE 17,10 OF oDlg PIXEL
	@ 25,261 MSGET nC1 Picture "99.9" VALID nC1 >0 SIZE 17,10 OF oDlg PIXEL
  	@ 45,185 MSGET nA2 Picture "99.9" VALID nA2 >0 SIZE 17,10 OF oDlg PIXEL
	@ 45,223 MSGET nB2 Picture "99.9" VALID nB2 >0 SIZE 17,10 OF oDlg PIXEL
	@ 45,261 MSGET nC2 Picture "99.9" VALID nC2 >0 SIZE 17,10 OF oDlg PIXEL
	@ 76,130 LISTBOX oQual VAR cVarQ Fields HEADER "",OemToAnsi(STR0020) SIZE 75,50 ON DBLCLICK (aTipo:=ca290troca(oQual:nAt,aTipo),oQual:Refresh()) ON RIGHT CLICK ListBoxAll(nRow,nCol,@oQual,oOk,oNo,@aTipo) NOSCROLL OF oDlg PIXEL  //"Tipos de Material"
	oQual:SetArray(aTipo)
	oQual:bLine := { || {if(aTipo[oQual:nAt,1],oOk,oNo),aTipo[oQual:nAt,2]}}
	@ 76,225 LISTBOX oQual2 VAR cVarQ2 Fields HEADER "",OemToAnsi(STR0021) SIZE 75,50 ON DBLCLICK (aGrupo:=ca290troca(oQual2:nAt,aGrupo),oQual2:Refresh()) ON RIGHT CLICK ListBoxAll(nRow,nCol,@oQual2,oOk,oNo,@aGrupo) NOSCROLL OF oDlg  PIXEL   //"Grupos de Material"
	oQual2:SetArray(aGrupo)
	oQual2:bLine := { || {if(aGrupo[oQual2:nAt,1],oOk,oNo),aGrupo[oQual2:nAt,2]}}
	
	@ 127,130 CHECKBOX oChkQual  VAR lQual  PROMPT OemToAnsi(STR0024) SIZE 66, 10 OF oDlg PIXEL ON CLICK (AEval(aTipo , {|z| z[1] := If(z[1]==.T.,.F.,.T.)}), oQual:Refresh(.F.)) //"Inverter Selecao"
	@ 127,225 CHECKBOX oChkQual2 VAR lQual2 PROMPT OemToAnsi(STR0024) SIZE 66, 10 OF oDlg PIXEL ON CLICK (AEval(aGrupo, {|z| z[1] := If(z[1]==.T.,.F.,.T.)}),oQual2:Refresh(.F.)) //"Inverter Selecao"
	
    DEFINE SBUTTON FROM 140,240 TYPE 1 ACTION IIF(A290VldPer(nA2,nB2,nC2),(nOpca:=1,oDlg:End()),Help(" ",1,"A290PERC")) ENABLE OF oDlg
	DEFINE SBUTTON FROM 140,270 TYPE 2 ACTION oDlg:End() ENABLE OF oDlg
	oChk2:BlClicked :=	{|| oChk2:Refresh()}
ACTIVATE MSDIALOG oDlg CENTERED

If nOpca = 1
	//Ŀ
	// Grava Atualizacao do Consumo do Mes no array
	//
	If lCalc
	   aOpt[1,1]:= "x"
	Else
	   aOpt[1,1]:=" "
	EndIf

	//Ŀ
	// Grava Tipo de Calculo e valor p/ formula de calculo no array
	//
	If nCalculo=1
	   aOpt[2,1]:="x"
	   aOpt[2,2]:=nIncre
	   aOpt[3,1]:=" "
	   aOpt[3,2]:=0
	Else
	   aOpt[2,1]:=" "
   	aOpt[2,2]:=0
   	aOpt[3,1]:="x"
   	aOpt[3,2]:=nMeses
	EndIf

	//Ŀ
	// Grava Calculo do Lote Economico no array
	//
	If lLote
	   aOpt[4,1]:="x"
   	If lPedido
	      aOpt[4,2]:="x"
	   EndIf
	Else
   	aOpt[4,1]:=" "
	   aOpt[4,2]:=" "
	EndIf

	//Ŀ
	// Grava disponibilidade financeira no array
	//
	If lDispoF
	   aOpt[5,1]:="x"
	   aOpt[5,2]:=nDispoF
	Else
	   aOpt[5,1]:=" "
	   aOpt[5,2]:=0
	EndIf

	//Ŀ
	// Grava Periodos de Aquisicao no array
	//
	aOpt[6,1]:=nA1
	aOpt[6,2]:=nB1
	aOpt[6,3]:=nC1

	//Ŀ
	// Grava percentuais no array      
	//
	aOpt[7,1]:=nA2
	aOpt[7,2]:=nB2
	aOpt[7,3]:=nC2

	//Ŀ
	// Move aTipo p/aOpt               
	//
	aOpt[8][1]:=""
	nArr:=0
	FOR i:=1 TO LEN(aTipo)
		IF aTipo[i][1]
			nArr++
			aOpt[8][1] := aOpt[8][1]+SubStr(aTipo[i,2],1,2)+"|"
		ENDIF
	Next i
	IF nArr == Len(aTipo)
		aOpt[8][1]:="**"
	End

	//Ŀ
	// Move aGrupo p/aOpt              
	//
	aOpt[9][1]:=""
	nArr:=0
	FOR i:=1 TO LEN(aGrupo)
		IF aGrupo[i][1]
			nArr++
			aOpt[9][1] := aOpt[9][1]+SubStr(aGrupo[i,2],1,4)+"|"
		ENDIF
	Next i
	IF nArr == Len(aGrupo)
		aOpt[9][1]:="**"
	EndIf
Else
	aOpt:=NIL
Endif
DeleteObject(oOk)
DeleteObject(oNo)
Return aOpt

/*

Ŀ
Funo     CalcMeses Autor  Wilson Junior          Data  28/03/95 
Ĵ
Descrio  Retorna o Numero de Meses para calculo                     
Ĵ
 Uso       MATA290                                                    
ٱ


*/
Static Function CalcMeses(dInicio,nMaximo)
Local nAnoIni := Year(dInicio),nMesIni:=Month(dInicio)
Local nAnoFim := Year(dDataBase),nMesFim:=Month(dDataBase)
Local nMeses
nMaximo := IIF(nMaximo == Nil,12,nMaximo)
nMeses := ((nAnoFim-nAnoIni)*12) + (nMesFim-nMesIni) + 1
nMeses := IIF(nMeses>nMaximo,nMaximo,nMeses)
nMeses := IIF(nMeses<0,0,nMeses)
Return(nMeses)

/*

Ŀ
Funo     MA290Filt Autor  Marcos Bregantim       Data  04/07/95 
Ĵ
Descrio  Retorna .t. ou .f. para validacao do filtro                
Ĵ
 Uso       MATA290                                                    
ٱ


*/
Static FUnction MA290Filtro()
Local lRet := .t.

If aOpcoes[8][1] != "**" .and. !(SB1->B1_TIPO$Trim(aOpcoes[8][1]))
	lRet := .f.
EndIf
If aOpcoes[9][1] != "**" .and. !(SB1->B1_GRUPO$Trim(aOpcoes[9][1]))
	lRet := .f.
EndIf

Return (lRet)

/*

Ŀ
Funo    MA290Process   Autor Rodrigo de A Sartorio Data  08/02/96 
Ĵ
Descrio Funcao que chama a regua e executa as funcoes de processamento
Ĵ
 Uso      MATA290                                                       
ٱ


*/
Static Function MA290Process(aOpcoes,lEnd)

ProcRegua(nTotRegs)

If aOpcoes[1][1] == "x"         // Atualiza Consumo do Mes
	A290CalCon()
EndIf

If aOpcoes[2][1] == "x"         // Calcula por Peso
	A290CalNor(aOpcoes[2][2])
Else
	A290CalMin(aOpcoes[3][2])    // Calculo pela Tendencia
EndIf

If aOpcoes[4][1] == "x"         // Calculo Lote Economico
	//Ŀ
	// Os parametros passados na funcao A290CalLot sao :            
	//                                                              
	// aOpcoes[4][2] = Se deve calcular o ponto de pedido           
	// aOpcoes[6][1]-> Periodo Aquisicao A                          
	// aOpcoes[6][2]-> Periodo Aquisicao B                          
	// aOpcoes[6][3]-> Periodo Aquisicao C                          
	// aOpcoes[7][1]-> %Distribuicao     A                          
	// aOpcoes[7][2]-> %Distribuicao     B                          
	// aOpcoes[7][3]-> %Distribuicao     C                          
	//
	A290CalLot(aOpcoes[4][2],aOpcoes[6][1],aOpcoes[6][2],aOpcoes[6][3],;
				  aOpcoes[7][1],aOpcoes[7][2],aOpcoes[7][3])
EndIf
If aOpcoes[5][1] == "x"         // Ajusta Lote Economico
	A290AjuLot(aOpcoes[5][2])    // Disponibilidade
EndIf

Return


/*

Ŀ
Funo    MA290VldPer   Autor Rodrigo de A Sartorio Data  08/02/96 
Ĵ
Descrio Funcao que valida os percentuais do Calculo ABC              
Ĵ
 Uso      MATA290                                                      
ٱ


*/
Static Function A290VldPer(nA2,nB2,nC2)
lRet:=.T.
If nA2+nB2+nC2 != 100
   lRet:=.F.
EndIf
Return lRet

/*


Ŀ
 Funo     CA290TROCA                                                 
Ĵ
 Autor      Rodrigo de Almeida Sartorio               Data  09.01.96 
Ĵ
 Descrio  Troca marcador entre x e branco                            
Ĵ
  Uso       SigaEST - Advanced                                         
ٱ


*/
Function CA290TROCA(nIt,aArray)
aArray[nIt,1] := !aArray[nIt,1]
Return aArray