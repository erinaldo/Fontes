/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³  DATA  ³ BOPS ³Program.³					ALTERACAO				  ³±±
±±ÃÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³03.08.98³17223A³Eduardo ³ Alterado o Calc.do LE para considerar o PE   ³±±
±±³22.09.98³XXXXXX³Rodrigo ³ Alterado o Calc.do LE para desconsiderar o PE³±±
±±³05.10.98³03771A³Rodrigo ³ Incluida chamada da funcao ListBoxAll        ³±±
±±³24.10.98³XXXXXX³Fabio C.³ Incluida tratamento para a Shell com IFDEF   ³±±
±±³06.11.98³15652A³Rodrigo ³ Acerto no calculo com material aprp. indireta³±±
±±³30.06.98³PROTHE³CesarVal³ Acerto no tamanho dos MSGET                  ³±±
±±³24/05/00³XXXXXX³Patricia³ Alt. A290MENU(), da tabela "03" para "SBM".  ³±±
±±³08/05/02³XXXXXX³Felipe R³ Especifico CIEE.                             ³±±
±±ÀÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
#INCLUDE "MATA290.CH"
//#include "fivewin.ch"
/*  Cabecalho padrao.
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ MATA290  ³ Autor ³ Eveli Morasco         ³ Data ³ 31/01/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Calculo do lote economico                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ MATA290(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MATA290A  ºAutor  ³Microsiga           º Data ³  05/08/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ O usuario necessita da inclusao de                         º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Especifico CIEE.                                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MATA290
PRIVATE aOpcoes,nTotRegs:=0,lEnd:=.F.

#IFDEF TOP
	TCInternal(5,"*OFF")   // Desliga Refresh no Lock do Top
#ENDIF
aOpcoes := A290Menu()
IF aOpcoes == NIL
	Return
Endif	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para desenhar cursor                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SB1")
nTotRegs := nTotRegs + RecCount()
nTotRegs := nTotRegs + RecCount()

dbSelectArea("SB3")
nTotRegs := nTotRegs + RecCount()
nTotRegs := nTotRegs + RecCount()
nTotRegs := nTotRegs + RecCount()
nTotRegs := nTotRegs + RecCount()
nTotRegs := nTotRegs + RecCount()

dbSelectArea("SD1")
nTotRegs := nTotRegs + RecCount()

dbSelectArea("SD2")
nTotRegs := nTotRegs + RecCount()
	
dbSelectArea("SD3")
nTotRegs := nTotRegs + RecCount()
	
Processa({|lEnd| MA290Process(aOpcoes,@lEnd)},OemToAnsi(STR0001),OemToAnsi(STR0002),.F.)    //"C lculo do Lote Econ“mico"###"Efetuando C lculo do Lote Econ“mico..."

Return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A290CalCon³ Autor ³ Eveli Morasco         ³ Data ³ 10/02/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Recalcula o consumo medio do mes                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ A290CalCon()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
STATIC Function A290CalCon
Static l290Cons

LOCAL cCod,nTotCod:=0
LOCAL cMes:="B3_Q"+StrZero(Month(dDataBase),2) , cDataIni, cDataFim
LOCAL nCons:=0

l290Cons := If(l290Cons==NIL,ExistBlock("A290CONS"),l290Cons)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Zera o arquivo de consumos antes de recalcular               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SB3")
dbSeek(cFilial)
While !EOF() .And. B3_FILIAL == cFilial
	RecLock("SB3",.F.) // tentar ver replace all para TOPCONN
	Replace &(cMes) With 0
	MsUnlock()
	dbSkip()
	IncProc()
   MsUnlock()
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Processa o arquivo SD2 -> Itens das notas fiscais de entrada ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SD2")           // itens das notas fiscais de saida

#IFDEF TOP
	cDataINI := StrZero(Year(dDataBase),4,0)+StrZero(Month(dDataBase),2,0)+"01"
	cDataFim := Dtos(LastDay(dDataBAse))
	cFilSD2 := 'DTOS(D2_EMISSAO) >= "'+cDataINI+'" .and. DTOS(D2_EMISSAO) <= "'+cDataFIM+'"'
	MsFilter(cFILSD2)
#ENDIF
dbSetOrder(1)
dbSeek(cFilial)
While !EOF() .And. D2_FILIAL == cFilial
	nTotCod := 0
	cCod    := D2_COD
	While !EOF() .And. D2_FILIAL+D2_COD == cFilial+cCod

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Despreza Notas Fiscais Lancadas Pelo Modulo do Livro Fiscal  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If D2_ORIGLAN == "LF" .or. D2_TIPO $ "DB"
			dbSkip()
			Loop
		EndIf

		#IFDEF SHELL
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Despreza Notas Fiscais Canceladas.                           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If SD2->D2_CANCEL == "S"
				dbSkip()
				Loop
			EndIf
		#ENDIF

		If Month(D2_EMISSAO) == Month(dDataBase) .And. Year(D2_EMISSAO) == Year(dDataBase)
			dbSelectArea("SF4")
			dbSeek(cFilial+SD2->D2_TES)
			dbSelectArea("SD2")
			If SF4->F4_ESTOQUE == "S"
				If D2_TES <= "500"
					nTotCod := nTotCod - D2_QUANT
				Else
					nTotCod := nTotCod + D2_QUANT
				EndIf
			EndIf
		EndIf
		dbSkip()
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Movimentacao do Cursor                                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IncProc()
   EndDo
	dbSelectArea("SB3")
	dbSeek(cFilial+cCod)
	If EOF()
		RecLock("SB3",.T.)
		Replace B3_FILIAL With cFilial, B3_COD With cCod
	Else
		RecLock("SB3",.F.)
	EndIf
	Replace &(cMes) With &(cMes) + nTotCod
	
	MsUnlock()
	
	dbSelectArea("SD2")
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Processa o arquivo SD1 -> Itens das notas fiscais de Dev.Vdas³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SD1")           // itens das notas fiscais de entrada
#IFDEF TOP
	cDataINI := StrZero(Year(dDataBase),4,0)+StrZero(Month(dDataBase),2,0)+"01"
	cDataFim := Dtos(LastDay(dDataBAse))
	cFilSD1 := 'DTOS(D1_DTDIGIT) >= "'+cDataINI+'" .and. DTOS(D1_DTDIGIT) <= "'+cDataFIM+'" .and. '
	cFILSD1 += 'D1_TIPO == "D"'
	MsFilter(cFILSD1)
#ENDIF
dbSetOrder(2)
dbSeek(cFilial)
While !EOF() .And. D1_FILIAL == cFilial
	nTotCod := 0
    cCod    := D1_COD
    While !EOF() .And. D1_FILIAL+D1_COD == cFilial+cCod

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Despreza Notas Fiscais Lancadas Pelo Modulo do Livro Fiscal  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If D1_ORIGLAN $ "LF"
			dbSkip()
			Loop
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Especifico para a SHELL                                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		#IFDEF SHELL
			If D1_CANCEL == "S"
				dbSkip()
				Loop
			EndIf
		#ENDIF

		If Month(D1_DTDIGIT) == Month(dDataBase) .and. D1_TIPO="D" .And.;
		   Year(D1_DTDIGIT) == Year(dDataBase)
			dbSelectArea("SF4")
			dbSeek(cFilial+SD1->D1_TES)
			dbSelectArea("SD1")
			If SF4->F4_ESTOQUE == "S"
				If D1_TES <= "500"
					nTotCod := nTotCod - D1_QUANT
				Else
					nTotCod := nTotCod + D1_QUANT
				EndIf
			EndIf
		EndIf
		dbSkip()
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Movimentacao do Cursor                                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IncProc()
	EndDo
	dbSelectArea("SB3")
	dbSeek(cFilial+cCod)
	If EOF()
		RecLock("SB3",.T.)
		Replace B3_FILIAL With cFilial, B3_COD With cCod
	Else
		RecLock("SB3",.F.)
	EndIf
	Replace &(cMes) With &(cMes) + nTotCod
	
	MsUnlock()

	dbSelectArea("SD1")
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Processa o arquivo SD3 -> Movimentacoes internas             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SD3")           // movimentacoes internas

#IFDEF TOP
	cDataINI := StrZero(Year(dDataBase),4,0)+StrZero(Month(dDataBase),2,0)+"01"
	cDataFim := Dtos(LastDay(dDataBAse))
	cFilSD3 := 'DTOS(D3_EMISSAO) >= "'+cDataINI+'" .and. DTOS(D3_EMISSAO) <= "'+cDataFIM+'"'
	cFilSD3 += '.And.!(D3_CF=="DE8".Or.D3_CF=="RE8")'
	SET FILTER TO &cFILSD3
#ENDIF

dbSetOrder(3)       // ordem por codigo de produto
dbSeek(cFilial)
While !EOF() .And. D3_FILIAL == cFilial
	nTotCod := 0
	cCod    := D3_COD
	While !EOF() .And. D3_FILIAL+D3_COD == cFilial+cCod
		If Month(D3_EMISSAO) == Month(dDataBase) .And. Subs(D3_CF,2,1) == "E" .and.;
		   Year(D3_EMISSAO) == Year(dDataBase)
			If !(Substr(D3_CF,3,1) $ "3478")
				If D3_TM <= "500"
					nTotCod := nTotCod - D3_QUANT
				Else
					nTotCod := nTotCod + D3_QUANT
				EndIf
			EndIf
		EndIf
		dbSkip()
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Movimentacao do Cursor                                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IncProc()
   EndDo
	If l290Cons 
		nCons:=ExecBlock("A290CONS",.F.,.F.,{cCod,nTotCod})
		If ValType(nCons) == "N"
			nTotCod:=nCons
		EndIf
	EndIf
	dbSelectArea("SB3")
	dbSeek(cFilial+cCod)
	If EOF()
		RecLock("SB3",.T.)
		Replace B3_FILIAL With cFilial, B3_COD With cCod
	Else
		RecLock("SB3",.F.)
	EndIf
	Replace &(cMes) With &(cMes) + nTotCod
	
	MsUnlock()
	
	dbSelectArea("SD3")
EndDo
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve ordem principal dos arquivos                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RETINDEX("SD1")
Set FIlter to
RETINDEX("SD2")
Set FIlter to
RETINDEX("SD3")
Set FIlter to

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A290CalNor³ Autor ³ Eveli Morasco         ³ Data ³ 10/02/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Calculo normal da media de consumos (utiliza os pesos)     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ A290CalNor()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
STATIC Function A290CalNor(nPorInc)
LOCAL nX,aPesos[12],cPesos,nTPesos:=0,nMeses,aPesoAux,nI,nConsid
LOCAL aTamSX3:={}

AFILL(aPesos,0)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Pega a configuracao de pesos do cliente                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cPesos := GetMv("MV_PESOS")
cPesos := SubStr(cPesos,1,12)
cPesos := cPesos+Space(12-Len(cPesos))

For nX := 1 To 12
	aPesos[nX] := Val(SubStr(cPesos,nX,1))
Next nX

aPesoAux := aClone(aPesos)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Varre o arquivo de demandas e verifica filtro no SB1         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SB3")
dbSeek(cFilial)
While !EOF() .And. B3_FILIAL == cFilial
	dbSelectArea("SB1")
	dbSeek(cFilial+SB3->B3_COD)
	nMeses := CalcMeses(SB1->B1_CONINI)
	aPesos := aClone(aPesoAux)
	nI := nMeses

	If nMeses < 12
		nX := (Month(dDataBase)+1) - nMeses
		If nX <= 0
			nX += 13
		Endif
		If nX = 13
			nX := 1
		Endif
		aPesos := {0,0,0,0,0,0,0,0,0,0,0,0}
		If nMeses > 0
			nConsid := Month(dDataBase)+1
			nConsid := IIF(nConsid >12,1,nConsid)
			While nX != nConsid
				aPesos[nX] := aPesoAux[nX]
				nX++
				nX := IIF(nX>12,1,nX)
			End
		Endif
	Endif
	nTpesos := 0
	For nX := 1 To 12
		nTpesos += aPesos[nX]
	Next nX
	
	dbSelectArea("SB3")
	If MA290Filtro()
		RecLock("SB3",.F.)
		Replace  B3_MEDIA With ((B3_Q01*aPesos[01]+B3_Q02*aPesos[02]+B3_Q03*aPesos[03]+;
										 B3_Q04*aPesos[04]+B3_Q05*aPesos[05]+B3_Q06*aPesos[06]+;
										 B3_Q07*aPesos[07]+B3_Q08*aPesos[08]+B3_Q09*aPesos[09]+;
										 B3_Q10*aPesos[10]+B3_Q11*aPesos[11]+B3_Q12*aPesos[12])/;
										 nTpesos)*(1+nPorInc/100)
		Replace	B3_TOTAL With B3_MEDIA*SB1->B1_CUSTD
		aTamSX3:=TamSX3("B3_CTOTAL")
		Replace	B3_CTOTAL With Transform(B3_TOTAL,PesqPict("SB3","B3_CTOTAL",aTamSX3[1]))
		MsUnlock()
	EndIf
	dbSkip()
   IncProc()
EndDo

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A290CalMin³ Autor ³ Eveli Morasco         ³ Data ³ 11/02/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Calculo da media de consumos que utiliza o conceito dos    ³±±
±±³          ³ minimos quadrados.                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ A290CalMin()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
STATIC Function A290CalMin(nQtdMes)
LOCAL nX,cMesAux2,cMeses:="010203040506070809101112"
LOCAL nTotx:=0,nMpx:=0,nToty:=0,nMpy:=0,nTotXaYa:=0,nTotXaXa:=0,nTotYaYa:=0
LOCAL aX[nQtdMes],aY[nQtdMes],aXa[nQtdMes],aYa[nQtdMes]
LOCAL aXaYa[nQtdMes],aXaXa[nQtdMes],aYaYa[nQtdMes]
LOCAL nMesProj,nSX,nSY,nRXY,nK1,nK2,nYP,nMeses,nMesAux
LOCAL aTamSX3:={}
nMesAux := nQtdMes

For nX := 1 To nQtdMes
	 aX[nX] := nX
	 aY[nX] := 0
	aXa[nX] := 0
	aYa[nX] := 0
 aXaYa[nX] := 0
 aXaXa[nX] := 0
 aYaYa[nX] := 0
Next nX

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Varre o arquivo de demandas e verifica filtro no SB1         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SB3")
dbSeek(cFilial)
STORE nQtdMes+1 TO nMesProj

// DEFINICOES DO X  --> MESES

FOR nX := 1 TO nQtdMes
	nTotx := nTotx + aX[nX]
NEXT
nMpx := nTotx/nQtdMes               // MEDIA PONDERADA DE X

While !EOF() .And. B3_FILIAL == cFilial
	nToty    := 0
	nTotXaYa := 0
	nTotXaXa := 0
	nTotYaYa := 0
	dbSelectArea("SB1")
	dbSeek(cFilial+SB3->B3_COD)
	nQtdMes := CalcMeses(SB1->B1_CONINI,nMesAux)
	dbSelectArea("SB3")
	If MA290Filtro()

		// DEFINICOES DO Y  --> QTDES
		For nX := 1 To nQtdMes
			STORE "B3_Q"+SUBS(cMeses+cMeses,(24+Month(dDataBase)*2)-1-(nQtdMes-nX)*2,2) TO cMesAux2
			aY[nX] := &(cMesAux2)
			nToty  := nToty + aY[nX]
		Next nX
		nMpy := nToty/nQtdMes               // MEDIA PONDERADA DE Y

		For nX := 1 To nQtdMes

			// CALCULO DO XA
			aXa[nX] := aX[nX] - nMpx

			// CALCULO DO YA
			aYa[nX] := aY[nX] - nMpy

			// CALCULO DO XA*YA
			aXaYa[nX] := aXa[nX]  * aYa[nX]
			nTotXaYa  := nTotXaYa + aXaYa[nX]

			// CALCULO DO XA*XA
			aXaXa[nX] := aXa[nX]  * aXa[nX]
			nTotXaXa  := nTotXaXa + aXaXa[nX]

			// CALCULO DO YA*YA
			aYaYa[nX] := aYa[nX]  * aYa[nX]
			nTotYaYa  := nTotYaYa + aYaYa[nX]
		Next nX

		nSX  := Abs( Sqrt( nTotXaXa / nQtdMes ) )
		nSY  := Abs( Sqrt( nTotYaYa / nQtdMes ) )
		nRXY := nTotXaYa / ( nQtdMes * nSX * nSY )
		nK1  := nRXY * ( nSX / nSY )
		nK2  := nRXY * ( nSY / nSX )
		nYP  := ( nK2 * nMesProj ) + ( nMpy - ( nK2 * nMpx ) )

		RecLock("SB3",.F.)
		Replace B3_MEDIA WITH nYP,B3_TOTAL WITH B3_MEDIA*SB1->B1_CUSTD
		aTamSX3:=TamSX3("B3_CTOTAL")
		Replace	B3_CTOTAL With Transform(B3_TOTAL,PesqPict("SB3","B3_CTOTAL",aTamSX3[1]))
		MsUnlock()
	EndIf
	dbSkip()
	IncProc()
EndDo

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A290CalLot³ Autor ³ Eveli Morasco         ³ Data ³ 12/02/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Calcula o lote economico                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ A290CalLot()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
STATIC Function A290CalLot(cCalPon,nPerA,nPerB,nPerC,nPorA,nPorB,nPorC)
LOCAL nTotCon:=0,nTotA:=0,nTotB:=0,nTotC:=0,nLote,cClasse,nPrazo
LOCAL cIndSB3	:= CriaTrab(NIL,.F.), nIndSB3
dbSelectArea("SB3")
nIndSB3 := RetIndex("SB3")
IndRegua("SB3",cIndSB3,"B3_FILIAL+B3_CTOTAL",,".T.", OemToAnsi(STR0003))    //"Selecionando Registros..."

dbSeek(cFilial+"z",.t.)
dbSkip(-1)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Soma o valor total das medias de consumo                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

While !Bof() .And. B3_FILIAL == cFilial
	dbSelectArea("SB1")
	dbSeek(cFilial+SB3->B3_COD)
	dbSelectArea("SB3")
	If MA290Filtro()
		nTotCon := nTotCon + B3_TOTAL
	EndIf
	dbSkip(-1)
	IncProc()
EndDo
nTotA   :=  nPorA * nTotCon / 100
nTotB   := (nPorB * nTotCon / 100) + nTotA
nTotC   := (nPorC * nTotCon / 100) + nTotB
nTotCon := 0

dbSeek(cFilial+"z",.t.)
dbSkip(-1)

While !Bof() .And. B3_FILIAL == cFilial
	dbSelectArea("SB1")
	If	dbSeek(cFilial+SB3->B3_COD)
		dbSelectArea("SB3")
		If MA290Filtro()
			Do Case
				Case nTotCon < nTotA
					nLote   := B3_MEDIA * nPerA
					cClasse := 'A'
				Case nTotCon < nTotB
					nLote   := B3_MEDIA * nPerB
					cClasse := 'B'
				OtherWise
					nLote   := B3_MEDIA * nPerC
					cClasse := 'C'
			EndCase
			nLote := IIF(nLote < SB1->B1_QE,SB1->B1_QE,Round(nLote / IIF(SB1->B1_QE==0,1,SB1->B1_QE), 0) * IIF(SB1->B1_QE==0,1,SB1->B1_QE))
			dbSelectArea("SB1")
			nPrazo := CalcPrazo(B1_COD,B1_LE)
			RecLock("SB1",.F.)
			Replace B1_LE With nLote
			If cCalPon == "x"
				Replace B1_EMIN WITH (SB3->B3_MEDIA * (nPrazo / 30 ))+CalcEstSeg( SB1->B1_ESTFOR )
			EndIf
			MsUnlock()		
			dbSelectArea("SB3")
			RecLock("SB3",.F.)
			Replace B3_CLASSE With cClasse
			MsUnlock()
			nTotCon := nTotCon + B3_TOTAL
		EndIf
	EndIf
	dbSelectArea("SB3")
	dbSkip(-1)
	IncProc()
EndDo

RetIndex("SB3")
Ferase(cIndSB3+OrdBagExt())
dbSetOrder(1)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A290AjuLot³ Autor ³ Eveli Morasco         ³ Data ³ 13/02/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Ajusta o lote economico pelo valor disponivel para compras ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ A290AjuLot()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
STATIC Function A290AjuLot(nValDisp)
LOCAL nQatu:=0,nPedido:=0,nTotVal:=0,nFalta:=0,nDif:=0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Seleciona ordem por produto para o SC1 e o SC7               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SC1")
dbSetOrder(2)

dbSelectArea("SC7")
dbSetOrder(4)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Soma o valor total das compras do mes                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SB1")
dbSeek(cFilial)
While !EOF() .And. B1_FILIAL == cFilial
	If MA290Filtro()
		dbSelectArea("SB2")
		dbSeek(cFilial+SB1->B1_COD)
		nQatu   := 0
		nPedido := 0
		While !EOF() .And. B2_FILIAL+B2_COD == cFilial+SB1->B1_COD
			nQatu := nQatu + B2_QATU
			dbSkip()
		EndDo
		dbSelectArea("SC1")
		dbSeek(cFilial+SB1->B1_COD)
		While !EOF() .And. C1_FILIAL+C1_PRODUTO == cFilial+SB1->B1_COD
			If C1_QUANT > C1_QUJE .And. Month(C1_DATPRF) == Month(dDataBase) .And.;
			   Year(C1_DATPRF) == Year(dDataBase)
				nPedido := nPedido + (C1_QUANT - C1_QUJE)
			EndIf
			dbSkip()
		EndDo
		dbSelectArea("SC7")
		dbSeek(cFilial+SB1->B1_COD)
		While !EOF() .And. C7_FILIAL+C7_PRODUTO == cFilial+SB1->B1_COD
			If C7_QUANT > C7_QUJE .And. Month(C7_DATPRF) == Month(dDataBase) .And. Empty(C7_RESIDUO);
			   .And. Year(C7_DATPRF) == Year(dDataBase)
				nPedido := nPedido + (C7_QUANT-C7_QUJE)
			EndIf
			dbSkip()
		EndDo
		dbSelectArea("SB3")
		dbSeek(cFilial+SB1->B1_COD)
		dbSelectArea("SB1")
		nFalta := (B1_EMIN+SB3->B3_MEDIA) - (nQatu+nPedido)
		If nFalta > 0
			If nFalta < B1_LE
				nFalta := B1_LE
			EndIf
			nTotVal := nTotVal + (nFalta * IIF(B1_UPRC > 0,B1_UPRC,B1_CUSTD))
		EndIf
	EndIf
	dbSkip()
	IncProc()
EndDo
If nTotVal <= 0
	nTotVal := 1
EndIf
nDif := (nValDisp/nTotVal)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Somente ajusta se o valor informado nao conseguir comprar    ³
//³ pelo menos 85% da necessidade calculada.                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nDif < 0.85
	dbSelectArea("SB1")
	dbSeek(cFilial)
	While !EOF() .And. B1_FILIAL == cFilial
		If MA290Filtro()
			RecLock("SB1",.F.)
			Replace B1_LE With B1_LE * nDif
			MsUnlock()
		EndIf
		dbSkip()
		IncProc()
   EndDo
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve a ordem principal dos arquivos                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SC1")
dbSetOrder(1)

dbSelectArea("SC7")
dbSetOrder(1)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ A290Menu ³ Autor ³ Jorge Queiroz         ³ Data ³ 06/01/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Processa a string das opcoes de processamento              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ ExpA1 := A290Menu()                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 := Array devolvido pela escolha                      ³±±
±±³          ³ [1][1]-> Atualiza Consumo do Mes (x ou Branco)             ³±±
±±³          ³ [2][1]-> Calcula por Peso        (x ou Branco)             ³±±
±±³          ³    [2]-> Incremento Percentual   (Percentual N 3)          ³±±
±±³          ³ [3][1]-> Calculo pela Tendencia  (x ou Branco)             ³±±
±±³          ³    [2]-> Numero de Meses         (Meses N 2)               ³±±
±±³          ³ [4][1]-> Calculo Lote Economico  (x ou Branco)             ³±±
±±³          ³    [2]-> Calculo Ponto Pedido    (x ou Branco)             ³±±
±±³          ³ [5][1]-> Ajusta Lote Economico   (x ou Branco)             ³±±
±±³          ³    [2]-> Disponibilidade         (Valor N 11)              ³±±
±±³          ³ [6][1]-> Periodo Aquisicao A     (Valor N  4,1)            ³±±
±±³          ³ [6][2]-> Periodo Aquisicao B     (Valor N  4,1)            ³±±
±±³          ³ [6][3]-> Periodo Aquisicao C     (Valor N  4,1)            ³±±
±±³          ³ [7][1]-> %Distribuicao     A     (Percentual N 5,2)        ³±±
±±³          ³ [7][2]-> %Distribuicao     B     (Percentual N 5,2)        ³±±
±±³          ³ [7][3]-> %Distribuicao     C     (Percentual N 5,2)        ³±±
±±³          ³ [8][1]-> Tipo de Material  (** Todos)                      ³±±
±±³          ³ [9][1]-> Grupos de Material  (** Todos)                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
STATIC FUNCTION A290MENU()
LOCAL oDlg,oQual,oQual2,oUsado,nCalculo:=1,oChk1,oChk2,oChk3,oChk4,lCalc,lLote,;
	lPedido,lDispoF,aTipo:={},aGrupo:={},nIncre:=nMeses:=nDispoF:=0,;
   nA1:=nB1:=nC1:=1,nA2:=nB2:=30,nC2:=40,oGet1,oGet2,nOpca:=2,aOpt[9][3],;
   oChkQual,oChkQual2,lQual:=.T.,lQual2:=.T.
LOCAL oOk := LoadBitmap( GetResources(), "LBOK")
LOCAL oNo := LoadBitmap( GetResources(), "LBNO")
LOCAL cVarQ := cVarQ2:="  "
LOCAL cCapital
Private cCadastro := OemToAnsi(STR0004)   //"Lote Econ“mico"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Monta a Tabela de Tipos                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX5")
dbSeek(cFilial+"02")
While (X5_filial == cFilial) .AND. (X5_tabela == "02")
	cCapital := OemToAnsi(Capital(X5Descri()))
	AADD(aTipo,{.T.,SubStr(X5_chave,1,3)+cCapital})
	dbSkip()
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Monta a Tabela de Grupos                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SBM")
dbSeek(xFilial())
While !EOF() .And. BM_FILIAL == cFilial
	cCapital := OemToAnsi(Capital(BM_DESC))
	AADD(aGrupo,{.T.,SubStr(BM_GRUPO,1,5)+" "+cCapital})
	dbSkip()
EndDo

DEFINE MSDIALOG oDlg TITLE cCadastro From 145,0 To 455,628 OF oMainWnd PIXEL
	@ 02,15 TO 25,120 LABEL "" OF oDlg  PIXEL
	@ 10,20 CHECKBOX oChk1 VAR lCalc PROMPT OemToAnsi(STR0005) SIZE 90, 10 OF oDlg PIXEL ;oChk1:oFont := oDlg:oFont   										//"Atualiza‡„o do Consumo do Mˆs"
	@ 28,15 TO 92,120 LABEL OemToAnsi(STR0006) OF oDlg  PIXEL    //"C lculos"
	@ 38,20 RADIO oUsado VAR nCalculo 3D SIZE 70,10 PROMPT OemToAnsi(STR0007),OemToAnsi(STR0008) OF oDlg PIXEL      											//"Por Peso"###"Pela Tendˆncia"
    oUsado:bChange := { || IIF(nCalculo=1,(oGet1:Enable(),oGet2:Disable()),(oGet1:Disable(),oGet2:Enable())) }
	@ 62,20 Say OemToAnsi(STR0009) SIZE 40,10 OF oDlg PIXEL    																													//"Incremento:"
	@ 62,67 MSGET oGet1 VAR nIncre When IIF(nCalculo=1,.T.,.F.) Picture "999" SIZE 15,10 OF oDlg PIXEL
	@ 78,20 Say OemToAnsi(STR0010) SIZE 60,10 OF oDlg PIXEL     																												//"N£mero de Meses:"
	@ 78,67 MSGET oGet2 VAR nMeses When IIF(nCalculo=2,.T.,.F.) Picture "999" SIZE 15,10 VALID (nMeses < 13 .And. nMeses > 0) OF oDlg PIXEL
	@ 95,15 TO 148,120 LABEL "" OF oDlg  PIXEL
	@ 103,20 CHECKBOX oChk2 VAR lLote PROMPT OemToAnsi(STR0011) SIZE 80, 10 OF oDlg PIXEL ;oChk2:oFont := oDlg:oFont    										//"C lculo do Lote Econ“mico"
	@ 113,20 CHECKBOX oChk3 VAR lPedido When lLote PROMPT OemToAnsi(STR0012) SIZE 80, 10 OF oDlg PIXEL ;oChk3:oFont := oDlg:oFont   	//"C lculo do Ponto de Pedido"
	@ 123,20 CHECKBOX oChk4 VAR lDispoF PROMPT OemToAnsi(STR0013) SIZE 90, 10 OF oDlg PIXEL ;oChk4:oFont := oDlg:oFont    									//"Ajusta Lote Econ“mico pela dispo-"
    @ 133,20 Say OemToAnsi(STR0014) SIZE 80,10 OF oDlg PIXEL    																												//"nibilidade financeira"
	@ 135,70 MSGET nDispoF When lDispoF Picture "@E 99,999,999,999" VALID nDispoF > 0 SIZE 45,10 OF oDlg PIXEL
	@ 02,130 TO 68,300 LABEL OemToAnsi(STR0015) OF oDlg PIXEL    																												//"Classifica‡„o ABC"
	@ 15,182 TO 60,219 LABEL "A" OF oDlg  PIXEL
	@ 15,220 TO 60,257 LABEL "B" OF oDlg  PIXEL
	@ 15,258 TO 60,295 LABEL "C" OF oDlg  PIXEL
	@ 23,135 Say OemToAnsi(STR0016) SIZE 45,10 OF oDlg PIXEL   //"Per¡odo de"
	@ 30,135 Say OemToAnsi(STR0017) SIZE 45,10 OF oDlg PIXEL   //"Aquisi‡„o(meses)"
	@ 43,135 Say OemToAnsi(STR0018) SIZE 45,10 OF oDlg PIXEL   //"Distribui‡„o"
	@ 50,135 Say OemToAnsi(STR0019) SIZE 45,10 OF oDlg PIXEL   //"Percentual (%)"
	@ 25,185 MSGET nA1 Picture "99.9" VALID nA1 >0 SIZE 17,10 OF oDlg PIXEL
	@ 25,223 MSGET nB1 Picture "99.9" VALID nB1 >0 SIZE 17,10 OF oDlg PIXEL
	@ 25,261 MSGET nC1 Picture "99.9" VALID nC1 >0 SIZE 17,10 OF oDlg PIXEL
  	@ 45,185 MSGET nA2 Picture "99.9" VALID nA2 >0 SIZE 17,10 OF oDlg PIXEL
	@ 45,223 MSGET nB2 Picture "99.9" VALID nB2 >0 SIZE 17,10 OF oDlg PIXEL
	@ 45,261 MSGET nC2 Picture "99.9" VALID nC2 >0 SIZE 17,10 OF oDlg PIXEL
	@ 76,130 LISTBOX oQual VAR cVarQ Fields HEADER "",OemToAnsi(STR0020) SIZE 75,50 ON DBLCLICK (aTipo:=ca290troca(oQual:nAt,aTipo),oQual:Refresh()) ON RIGHT CLICK ListBoxAll(nRow,nCol,@oQual,oOk,oNo,@aTipo) NOSCROLL OF oDlg PIXEL  //"Tipos de Material"
	oQual:SetArray(aTipo)
	oQual:bLine := { || {if(aTipo[oQual:nAt,1],oOk,oNo),aTipo[oQual:nAt,2]}}
	@ 76,225 LISTBOX oQual2 VAR cVarQ2 Fields HEADER "",OemToAnsi(STR0021) SIZE 75,50 ON DBLCLICK (aGrupo:=ca290troca(oQual2:nAt,aGrupo),oQual2:Refresh()) ON RIGHT CLICK ListBoxAll(nRow,nCol,@oQual2,oOk,oNo,@aGrupo) NOSCROLL OF oDlg  PIXEL   //"Grupos de Material"
	oQual2:SetArray(aGrupo)
	oQual2:bLine := { || {if(aGrupo[oQual2:nAt,1],oOk,oNo),aGrupo[oQual2:nAt,2]}}
	
	@ 127,130 CHECKBOX oChkQual  VAR lQual  PROMPT OemToAnsi(STR0024) SIZE 66, 10 OF oDlg PIXEL ON CLICK (AEval(aTipo , {|z| z[1] := If(z[1]==.T.,.F.,.T.)}), oQual:Refresh(.F.)) //"Inverter Selecao"
	@ 127,225 CHECKBOX oChkQual2 VAR lQual2 PROMPT OemToAnsi(STR0024) SIZE 66, 10 OF oDlg PIXEL ON CLICK (AEval(aGrupo, {|z| z[1] := If(z[1]==.T.,.F.,.T.)}),oQual2:Refresh(.F.)) //"Inverter Selecao"
	
    DEFINE SBUTTON FROM 140,240 TYPE 1 ACTION IIF(A290VldPer(nA2,nB2,nC2),(nOpca:=1,oDlg:End()),Help(" ",1,"A290PERC")) ENABLE OF oDlg
	DEFINE SBUTTON FROM 140,270 TYPE 2 ACTION oDlg:End() ENABLE OF oDlg
	oChk2:BlClicked :=	{|| oChk2:Refresh()}
ACTIVATE MSDIALOG oDlg CENTERED

If nOpca = 1
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Grava Atualizacao do Consumo do Mes no array³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lCalc
	   aOpt[1,1]:= "x"
	Else
	   aOpt[1,1]:=" "
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Grava Tipo de Calculo e valor p/ formula de calculo no array³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nCalculo=1
	   aOpt[2,1]:="x"
	   aOpt[2,2]:=nIncre
	   aOpt[3,1]:=" "
	   aOpt[3,2]:=0
	Else
	   aOpt[2,1]:=" "
   	aOpt[2,2]:=0
   	aOpt[3,1]:="x"
   	aOpt[3,2]:=nMeses
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Grava Calculo do Lote Economico no array³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lLote
	   aOpt[4,1]:="x"
   	If lPedido
	      aOpt[4,2]:="x"
	   EndIf
	Else
   	aOpt[4,1]:=" "
	   aOpt[4,2]:=" "
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Grava disponibilidade financeira no array³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lDispoF
	   aOpt[5,1]:="x"
	   aOpt[5,2]:=nDispoF
	Else
	   aOpt[5,1]:=" "
	   aOpt[5,2]:=0
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Grava Periodos de Aquisicao no array³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aOpt[6,1]:=nA1
	aOpt[6,2]:=nB1
	aOpt[6,3]:=nC1

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Grava percentuais no array      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aOpt[7,1]:=nA2
	aOpt[7,2]:=nB2
	aOpt[7,3]:=nC2

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Move aTipo p/aOpt               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aOpt[8][1]:=""
	nArr:=0
	FOR i:=1 TO LEN(aTipo)
		IF aTipo[i][1]
			nArr++
			aOpt[8][1] := aOpt[8][1]+SubStr(aTipo[i,2],1,2)+"|"
		ENDIF
	Next i
	IF nArr == Len(aTipo)
		aOpt[8][1]:="**"
	End

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Move aGrupo p/aOpt              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aOpt[9][1]:=""
	nArr:=0
	FOR i:=1 TO LEN(aGrupo)
		IF aGrupo[i][1]
			nArr++
			aOpt[9][1] := aOpt[9][1]+SubStr(aGrupo[i,2],1,4)+"|"
		ENDIF
	Next i
	IF nArr == Len(aGrupo)
		aOpt[9][1]:="**"
	EndIf
Else
	aOpt:=NIL
Endif
DeleteObject(oOk)
DeleteObject(oNo)
Return aOpt

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ CalcMeses³ Autor ³ Wilson Junior         ³ Data ³ 28/03/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna o Numero de Meses para calculo                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function CalcMeses(dInicio,nMaximo)
Local nAnoIni := Year(dInicio),nMesIni:=Month(dInicio)
Local nAnoFim := Year(dDataBase),nMesFim:=Month(dDataBase)
Local nMeses
nMaximo := IIF(nMaximo == Nil,12,nMaximo)
nMeses := ((nAnoFim-nAnoIni)*12) + (nMesFim-nMesIni) + 1
nMeses := IIF(nMeses>nMaximo,nMaximo,nMeses)
nMeses := IIF(nMeses<0,0,nMeses)
Return(nMeses)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ MA290Filt³ Autor ³ Marcos Bregantim      ³ Data ³ 04/07/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna .t. ou .f. para validacao do filtro                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static FUnction MA290Filtro()
Local lRet := .t.

If aOpcoes[8][1] != "**" .and. !(SB1->B1_TIPO$Trim(aOpcoes[8][1]))
	lRet := .f.
EndIf
If aOpcoes[9][1] != "**" .and. !(SB1->B1_GRUPO$Trim(aOpcoes[9][1]))
	lRet := .f.
EndIf

Return (lRet)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MA290Process  ³ Autor ³Rodrigo de A Sartorio³ Data ³ 08/02/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Funcao que chama a regua e executa as funcoes de processamento³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MATA290                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MA290Process(aOpcoes,lEnd)

ProcRegua(nTotRegs)

If aOpcoes[1][1] == "x"         // Atualiza Consumo do Mes
	A290CalCon()
EndIf

If aOpcoes[2][1] == "x"         // Calcula por Peso
	A290CalNor(aOpcoes[2][2])
Else
	A290CalMin(aOpcoes[3][2])    // Calculo pela Tendencia
EndIf

If aOpcoes[4][1] == "x"         // Calculo Lote Economico
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Os parametros passados na funcao A290CalLot sao :            ³
	//³                                                              ³
	//³ aOpcoes[4][2] = Se deve calcular o ponto de pedido           ³
	//³ aOpcoes[6][1]-> Periodo Aquisicao A                          ³
	//³ aOpcoes[6][2]-> Periodo Aquisicao B                          ³
	//³ aOpcoes[6][3]-> Periodo Aquisicao C                          ³
	//³ aOpcoes[7][1]-> %Distribuicao     A                          ³
	//³ aOpcoes[7][2]-> %Distribuicao     B                          ³
	//³ aOpcoes[7][3]-> %Distribuicao     C                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	A290CalLot(aOpcoes[4][2],aOpcoes[6][1],aOpcoes[6][2],aOpcoes[6][3],;
				  aOpcoes[7][1],aOpcoes[7][2],aOpcoes[7][3])
EndIf
If aOpcoes[5][1] == "x"         // Ajusta Lote Economico
	A290AjuLot(aOpcoes[5][2])    // Disponibilidade
EndIf

Return


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MA290VldPer  ³ Autor ³Rodrigo de A Sartorio³ Data ³ 08/02/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Funcao que valida os percentuais do Calculo ABC              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MATA290                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A290VldPer(nA2,nB2,nC2)
lRet:=.T.
If nA2+nB2+nC2 != 100
   lRet:=.F.
EndIf
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡„o    ³ CA290TROCA                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Autor     ³ Rodrigo de Almeida Sartorio              ³ Data ³ 09.01.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡„o ³ Troca marcador entre x e branco                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³  Uso      ³ SigaEST - Advanced                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function CA290TROCA(nIt,aArray)
aArray[nIt,1] := !aArray[nIt,1]
Return aArray