#INCLUDE "LOJA140.CH"
#INCLUDE "PROTHEUS.Ch"
#INCLUDE "TBICONN.CH"
#INCLUDE "FWADAPTEREAI.CH"

#DEFINE TEF_SEMCLIENT_DEDICADO  	"2"     //Utiliza TEF Dedicado Troca de Arquivos
#DEFINE TEF_COMCLIENT_DEDICADO  	"3"		//Utiliza TEF Dedicado com o Client
#DEFINE TEF_DISCADO             	"4"		//Utiliza TEF Discado 
#DEFINE TEF_LOTE                	"5"		//Utiliza TEF em Lote
#DEFINE TEF_CLISITEF				"6"		//Utiliza a DLL CLISITEF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Release 11.5 - Cartao Fidelidade³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
#DEFINE MOVFID_ESTPGTO				"3"		//Codigo do tipo de movimento para cancelamento de venda paga com cartão fidelidade
#DEFINE MOVFID_ESTREC				"4"		//Codigo do tipo de movimento para cancelamento de venda de produto do tipo recarga de cartão fidelidade

STATIC oIntCVenda 	:= Nil									// Objeto da integracao
STATIC oProcOff 	:= Nil									// Objeto de OffLine
Static lMvLjPdvPa   := ExistFunc("LjxBGetPaf") .AND. LjxBGetPaf()[2] //Indica se é pdv  
Static lIsVendaVP 	:= .F.
Static lIsVdRecCP 	:= .F.
Static lExcAuto		:= IsBlind()
Static cNFisCanc		:= "" //nota de cancelamento sat

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ Loja140  ³ Autor ³ Vendas Clientes       ³ Data ³ 02.01.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Exclus„o de Notas Fiscais                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaLoja                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LOJA140()

Local cLabelOpcao   := STR0003  	// "Exclus„o NF/Orc."
Local cLock 		:= cUserName + cEstacao
Local aCores 		:= Lj140DefLeg()  
Local cFiltro	    := NIL									// Filtro da mBrowse (personalizado) 

Local lLJ7069       := ExistFunc("U_LJ7069")				// Verifica se existe o PE LJ7069

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Faz o controle via LockByName para evitar que um usuário acesse       ³
//³ 2 vezes uma rotina que use os periféricos de automação, evitando assim³
//³ a concorrência dos mesmos.                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If SetMDIChild( 0 ) .AND. !LockByName( cLock )
	Return Nil
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Caso for modulo loja e determinados estados, não vai ser possivel     ³
//³ cancelamento de notas e orçamento pelo siga loja                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If LjNfPafEcf(SM0->M0_CGC) .And. nModulo <> 5 .And. lMvLjPdvPa
	MsgAlert(STR0091) //"Nao e permitido exclusao de DAV - PAF-ECF REQUISITO XI "
	Return Nil	
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis 										     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private cCadastro    := STR0001	  	// Exclus„o N.F./Orcam.
Private aTefDados    := {}
Private lVendaRapida := .F.
Private aRotina      := {}

If cPaisLoc <> "BRA"
	Private lNFManual  := .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Carrega as variaveis e perfil de caixa no primeiro acesso³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nModulo == 5
	If !SetMDIChild( 0 )
		FTVDOpenLoja()
	EndIf 
	
	AjustaSX3()
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa 	 ³
//³ ----------- Elementos contidos por dimensao ------------	 ³
//³ 1. Nome a aparecer no cabecalho 							 ³
//³ 2. Nome da Rotina associada									 ³
//³ 3. Usado pela rotina										 ³
//³ 4. Tipo de Transa‡„o a ser efetuada							 ³
//³	 1 - Pesquisa e Posiciona em um Banco de Dados				 ³
//³	 2 - Exclui a nota fiscal/orcamento/pedido                   ³
//³	 3 - Anula nota fiscal (localizacoes)                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//|  Pesquisa / Exclus„o NF/Orc.                                          |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cPaisLoc == "BRA"
	aRotina := MenuDef()
Else
	If cPaisLoc == "SAL"
		cCadastro   := STR0061  // "Exclus„o Doc.Saida/Orcam."
		cLabelOpcao := STR0062  // "Exclus„o Doc/Orc."
	ElseIf cPaisLoc == "GUA"
		cLabelOpcao := STR0065  // "Cancelar NF(NCC)"
	EndIf
	aRotina := MenuDef()
EndIf



DbSelectArea("SL1")
DbSetOrder(1)    

//Ponto de Entrada LJ7069 - que permite a alteração do mBrowser com o filtro retornado
If lLj7069
	xRet := ExecBlock("LJ7069",.F.,.F.)
	If ValType(xRet) == "C"
	    cFiltro  := xRet
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endere‡a a fun‡„o de BROWSE									 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
mBrowse(,,,,"SL1",,,,,, aCores,,,,,,,,cFiltro )

// Zerar as váriaveis referentes ao TEF
// Quando sair e retornar, as variaveis serao reinicializadas
// Ajustando a impressao na impressora nao fiscal	
FTVDFinishLoja()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Recupera a Integridade dos dados						     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
msUnlockAll()
Return Nil


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |MenuDef	³ Autor ³ Vendas Clientes       ³ Data ³28/12/06  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao de definição do aRotina                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ aRotina   retorna a array com lista de aRotina             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef() 

Local cLabelOpcao   := STR0003  	// "Exclus„o NF/Orc."
Local aRotina	:= {}
If cPaisLoc == "BRA"
	aRotina := {	{ STR0002  		,"AxPesqui"   , 0 , 1 , , .F.},;     //pesquisar
				 	{ STR0003  		,"lj140Exc"   , 0 , 5 , , .T.} }     //excluir
Else
	If cPaisLoc == "SAL"
		cLabelOpcao := STR0062  // "Exclus„o Doc/Orc."
	ElseIf cPaisLoc == "GUA"
		cLabelOpcao := STR0065  // "Cancelar NF(NCC)"
	EndIf
	aRotina := { 	{ STR0002     	, "AxPesqui"  , 0 , 1 , , .F. }, ;  //Pesquisa
				 	{ cLabelOpcao 	, "lj140Exc"  , 0 , 5 , , .T. }, ;  //"Exclus„o NF/Orc."
					{ STR0031     	, "lj140Anul" , 0 , 5 , , .T. } }   //"Cancelar"
	
EndIf

AAdd(aRotina,{"Legenda","Lj140Leg",0,8 , , .T. })      //legenda

							
							
Return(ARotina)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ lj140Exc ³ Autor ³ Vendas Clientes       ³ Data ³ 02.01.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de exclusao de notas fiscais                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lj140Exc(ExpC1,ExpN2,ExpN3,ExpA4,ExpL5,ExpC6,ExpC7)        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN2 = Numero do registro                                 ³±±
±±³          ³ ExpN3 = Opcao selecionada                                  ³±±  
±±³          ³ ExpA4 = Reservado		                                  ³±±
±±³          ³ ExpL5 = Rotina Automatica                                  ³±± 
±±³          ³ ExpC6 = Filial do Orcamento                                ³±±
±±³          ³ ExpC7 = Numero do Orcamento                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function lj140Exc( cAlias, nReg, nOpcx, aReserv, lAuto, cFilOrc, cNumOrc, cNotaCanc )
Local nCnt       	:= 0											// Contador
Local nTamL1Cup  	:= TamSX3("L1_NUMCFIS")[1]    					// Tamanho do campo L1_NUMCFIS
Local lVendido   	:= .F.											// Valida o L2_VENDIDO
Local nOpca			:= 0							   				// Variavel com a opcao da tela
Local cNumPdv    	:= Space(10)									// Numero do PDV
Local cNumPdvAtu 	:= Space(4)						   				// Numero do PDV atual
Local oDlg															// Objeto Dialog
Local oCheckNFMan									   				// Objeto CheckBox se gera NF Manual
Local lLj140CanT 	:= (ExistTemplate("LJ140CAN"))					// Valida a existencia do Template
Local lLj140Can  	:= (ExistBlock("LJ140CAN"))		   				// Valida a existencia do Ponto de Entrada
Local lLj140DelT	:= (HasTemplate("DRO") .AND. ExistTemplate("LJ140DEL")) // Valida a existencia da função de delecao do template de drogarias
Local nUsado		:= 0							   				// Contador para o aCols
Local cNumCupFis 	:= Space(nTamL1Cup)				   				// Numero do cupom fiscal
Local nDecs      	:= 0	   										// Casas decimais
Local nPosVlrTot 	:= 0											// Posicao do Valor Total
Local nPosDescPro	:= 0									   		// Posicao do Desconto do produto
Local nPosVlrUni 	:= 0											// Posicao do valor unitario
Local nPosQuant  	:= 0											// Posicao da quantidade
Local nPosQtd  	    := 0											// Posicao da quantidade
Local nPosTesPro	:= 0											// Posicao do Tes
Local nY           	:= 0											// Contador utilizado no For
Local nImpostos  	:= 0											// Impostos
Local nImpsInc   	:= 0											// Valor dos impostos
Local aPropImp   	:= {}											// Array com impostos proporcionais
Local cSerie     	:= AllTrim(ljGetStation("LG_SERIE"))				// Serie da estacao
Local lTitCarteira	:= .T.											// Indica se o titulo esta em carteira (.T.) ou com algum portador (.F.)
Local cDescMot      := ""											// Descricao do motivo
Local xRet															// Retorno da funcao
Local nAbatimentos  := 0											// Abatimentos
Local lUsafd    	:= SuperGetMV("MV_LJUSAFD",,.F.) 				// Utiliza Fidelizacao de cliente ??
Local cTpOper		:= "3"											// Indica que e uma operacao de exclusao de pontos
Local nPosProd   	:= 0                           					// Posicao do codigo do produto 
Local nPosVlItem 	:= 0                          					// Posicao do valor do item            
Local cGrupoProd 	:= ""											// Indica o grupo do produto
Local aProdCri		:= {}											// Array com os produtos a serem excluidos
Local cNumAnt		:= ""											// Numero anterior
Local lGeraNCC   	:= 0											// Numero da Ncc gerada
Local cNumNFDev  	:= ""											// Numero da nota fiscal de devolucao
Local cSerieDev  	:= ""											// Serie de devolucao
Local cMotivo    	:= "" 											// Motivo da devolucao
Local nX			:= 0											// Contador de For
Local lRetorno		:= .F.											// Retorno do ValType(xRet)   
Local aVales		:= {}											// Vetor com os vales            
Local cPdv     		:= AllTrim(LjGetStation("LG_PDV"))				// PDV da estacao
Local cOper		    := ""											// codigo da operacao
Local cNum			:= ""                              				// numero do orçamento
Local cFilSL1		:= ""											// Filial da Reserva
Local aAreMt		:= {}											// Bk do area
Local cChaveCHRe 	:= ''											// Chave de busca para deletar os cheques na venda com reserva
Local oPanelCab														// Objeto do tipo tPanel posicionado no cabecalho da tela         
Local oPanelCen                                         			// Objeto do tipo tPanel posicionado no centro da tela   
Local aSizeAut 	:= MsAdvSize()										// Tamanho da tela. 
Local aObjects 	:={}												// Posicao da Tela
Local aInfo  	:={}												// Posicao da Tela
Local aPosObj 	:={}												// Posicao da Tela
Local nVlrOrc		:= 0											// Soma dos valores dos itens (L2_VLRITEM)
Local aOrcamento	:= {}											// armazena os orcamentos que deverao ser excluidos
Local lSubClosed	:= .F.											// verifica se existe algum suborcamento finalizado
Local aArea			:= {}											// armazena a area do orcamento principal(orcamento pai) em caso de reservas
Local lLastOrc		:= .F.											// verifica se e o ultimo orcamento a ser excluido
Local lIntGC		:= .F.  										// Integracao com Gestao de Concessionarias
Local lAmbPAF		:= LjNfPafEcf(SM0->M0_CGC)						// verifica se o ambiente está em modo PAF
Local lNota			:= .F. 											// Controla se existe nota fiscal
Local cAuxDoc		:= ""
Local cAuxSerie		:= ""
Local lNFCe			:= ExistFunc("LjEmitNFCe") .AND.  LjEmitNFCe() .AND. ColumnPos("L1_KEYNFCE") > 0
Local lCliRecIss	:= .F.											// Cliente Recolhe ISS ?
Local lLstPre	 	:= .F.											// indica contem um item de Lista de Presente
Local lTefCancelado := .F.
Local aAuxRet		:= {.F.,.F.,"",{},"","",0,"",.F.,.F.,.F.,{},{},.F.}

Local nPosNumcFi:= 0									// Posicao do Numero do cartao fidelidade

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Tratamento para e-Commerce      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lECommerce	:= SuperGetMV("MV_LJECOMM",,.F.) .And. ( ((ExistFunc("LJ861ECAuto") .And. LJ861ECAuto() .Or. ExistFunc("LJ862ECAuto") .And. LJ862ECAuto()) )  .OR.  ( SuperGetMv("MV_LJECOMO", .T., .F.) .AND. ( IsInCallStack("Lj907OrEx") .OR. ( SL1->(ColumnPos("L1_ECFLAG")) > 0 .AND. SL1->L1_ECFLAG == "1") ) )  )
Local  lECCia		:= SuperGetMv("MV_LJECOMO", .T., .F.) .AND.  SL1->(ColumnPos("L1_ECFLAG") > 0) .AND. (SL1->L1_ECFLAG == "1")  
Local lXECommerce	:= .F. 
Local aAreaMF5		:= {} 
Local lLjcFid	 	:= SuperGetMv("MV_LJCFID",,.F.) .AND. CrdxInt()				//Indica se a recarga de cartao fidelidade esta ativa
Local lAmbOffLn 	:= SuperGetMv("MV_LJOFFLN", Nil, .F.)						//Identifica se o ambiente esta operando em offline

Private aHeader[0]
Private aCols[0]
Private aCampos		:=	{}

Default cFilOrc		:= ""	   								   		// Filial do orcamento
Default cNumOrc 	:= ""	  										// Numero do orcamento
Default lAuto		:= .F.
Default cNotaCanc	:= ""

If !Empty(cNotaCanc)
	cNfisCanc := cNotaCanc
EndIf

Lj140AtuDic()

//Atualiza Static
lExcAuto	:= lAuto // Rotina automatica

If lExcAuto
	//Se a variavel de retorno do ExecAuto nao foi criada, faz a inicializacao
	If Type("lMsErroAuto") == "U"
		Private lMsErroAuto	:= .F.
	EndIf
EndIf

//Instancia o objeto oProcOff para Loja Offline.(Variavel Static)
If lAmbOffLn
	oProcOff := LJCProcessoOffLine():New("006") 
EndIf

If SL2->(ColumnPos("L2_VALEPRE")) > 0 .And. SuperGetMv("MV_LJVALEP",,.F.) .And. AliasIndic("MDD")

	aArea := GetArea()
	
	DbSelectArea("SL2")
	DbSetOrder(1)
	
	DbSelectArea("MDD")
	DbSetOrder(1)
	
	/* Validação para não execluir a venda caso possua vale presente utilizado */			
	If SL2->(DbSeek( xFilial("SL2") + SL1->L1_NUM ))
		While SL2->(!EOF()) .And. xFilial("SL2") == SL2->L2_FILIAL .AND. SL2->L2_NUM == SL1->L1_NUM
			If !Empty(SL2->L2_VALEPRE)
				If MDD->(DbSeek( xFilial("MDD") + SL2->L2_VALEPRE )) .And. SuperGetMv("MV_LJBXPAR",,.F.)  .And. MDD->(ColumnPos("MDD_SALDO" )) > 0
					If MDD->MDD_SALDO <> MDD->MDD_VALOR
						MsgAlert(STR0139) //Não é possível fazer o cancelamento da venda pois existe um vale presente já utilizado.
						
						If lExcAuto
							lMsErroAuto := .T.
							Help(" ",1,"NO140CANC",,STR0139,3,1)
						EndIf
						
						Return( Nil )
					EndIf
				EndIf
			EndIf
			SL2->(DbSkip())	
		EndDo
	EndIf
	
	RestArea(aArea)
EndIf

//Caso seja Orçamento e-Commerce nao pode excluir via browser.
If !( lECommerce ) .AND.  SL1->(ColumnPos("L1_ECFLAG") > 0) .AND. (SL1->L1_ECFLAG == "1")  
	If !lECCia 
	
		aAreaMF5 := MF5->(GetArea())    
		MF5->( DbSetOrder(1) ) 
		//Pedidos e-commerce, verifica se foi processado o pagamento
		If  MF5->(DbSeek(xFilial("MF5") + "SL1" + SL1->L1_FILIAL+ SL1->L1_NUM)) .AND. (MF5->MF5_ECPAGO = "1" .AND. MF5->MF5_ECFLAG = "1")
			If lExcAuto
				lMsErroAuto := .T.				
			EndIf
			
			Help(" ",1,"NO140CANC",," -> SIGALOJA - e-Commerce!",3,1)
			Return( Nil )
		EndIf
		RestArea(aAreaMF5)          
	Else    
		If !Empty(SL1->L1_DOC) .AND. !Empty(SL1->L1_SERIE)     .AND. !Empty(SL1->L1_EMISNF)
			//Pedidos e-commerce, verifica se foi realizada a venda             
			If lExcAuto
				lMsErroAuto := .T.				
			EndIf
			
			Help(" ",1,"NO140CANC",," -> SIGALOJA - e-Commerce!",3,1)
			Return( Nil )
		EndIf
	EndIf
EndIf	

If lExcAuto  

	//Carrega a variável Inclui, utilizada no inicializador padrão do campo L2_REVLPRE
	If Type("INCLUI") <> "L"
		Private INCLUI := (nOpcx <> 3)
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Carrega as variaveis e perfil de caixa no primeiro acesso³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Type("lFTStartSilenc") == "U" .AND. nModulo == 5
		If !SetMDIChild( 0 )
			FTVDOpenLoja(lExcAuto)
		EndIf 
	EndIf

	If ( !Empty(cFilOrc) .AND. !Empty(cNumOrc) )
		
		DbSelectArea("SL1")
		SL1->( DbSetOrder(1) )
		
		If SL1->( DbSeek(cFilOrc+cNumOrc) )
			//"Orcamento XXXXXX localizado."
			Conout(STR0116 + cNumOrc + STR0100)
		Else
			//"Orcamento XXXXXX nao encontrado."
			lMsErroAuto := .T.
			Help(" ",1,"NO140CANC",,STR0116 + cNumOrc + STR0101,3,1)
			conout(STR0116 + cNumOrc + STR0101)
			Return( .F. )
		EndIf
	Else
		Do Case
			Case ( Empty(cFilOrc) .AND. Empty(cNumOrc) )
				//"Filial e o numero do orcamento nao foram informados."
				Conout(STR0102)
				Help(" ",1,"NO140CANC",,STR0102,3,1)
			Case Empty(cFilOrc)
				//"Filial do orcamento nao foi informado."
				Conout(STR0103)
				Help(" ",1,"NO140CANC",,STR0103,3,1)
			Case Empty(cNumOrc)
				//"Numero do orcamento nao foi informado."
				Conout(STR0104)
				Help(" ",1,"NO140CANC",,STR0104,3,1)
		End Case		
		
		lMsErroAuto := .T.
			
		Return .F.
	EndIf
EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
|aOrcamento:                                                                                                 	|
|	Atraves dos itens do orcamento principal, identificamos quais sao os sub-orcamentos(quando tiver reservas)	|
|	 e adicioanamos a esse array. Quando o orcamento nao possuir reservas, ele mesmo sera adicionado.		  	|
|	Estrutura do array aOrcamento																				|
|	[01] Filial do sub-orcamento L2_FILRES																	  	|
|	[02] Numero do sub-orcamento L2_ORCRES																	  	|
|	[03] indica se o orcamento em questao foi finalizado													  	|
|	[04] indica se o orcamento em questao foi possui algum pedido de venda amarrado								|
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/
If cPaisLoc <> "BRA"
	Private lAnulaSF3  := AtIsRotina("LJ140ANUL")
	If cPaisLoc == "GUA"
		cNumNFDev  := CriaVar("L1_DOC") //Numero da Nota de Credito a ser gerada
		cSerieDev  := AllTrim(SuperGetMV("MV_LJNFTRO"))
		cMotivo    := Space(6)
		lGeraNCC   := !lAnulaSF3
		lAnulaSF3  := .T.
	EndIf
EndIf

//Verifica se eh venda de Vale Presente
lIsVendaVP := If( ExistFunc("Lj7VPNew") .And. Lj7VPNew(), Lj7VdaIsVP(SL1->L1_NUM), .F.)

//-------------------------------------------------------------------------
// Carrega as variaveis estaticas referente ao "Cartao Presente" (Gift Card).
//-------------------------------------------------------------------------
lIsVdRecCP := .F.
If ExistFunc("Lj7CP_OK")
	If Lj7CP_OK()
		Lj7CP_Load()
		lIsVdRecCP := Lj7VdIsRCP(SL1->L1_NUM) //Verifica se eh venda de Recarga de Cartao Presente (Gift Card)
	Else
		lIsVdRecCP := .F.
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Carregamos aOrcamento com todos os orcamentos que serao excluidos.	|
//| A variavel lSubClosed, indica se algum sub-orcamento foi finalizado	|
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aOrcamento := LJ140Fill(@lSubClosed)

//verifica se aOrcamento possui algum orcamento finalizado, seja ele pai ou filho
lVendido := aScan( aOrcamento, {|x| x[3] == .T.} ) > 0

//verifica se ha itens de Lista de Presente LOJA846
If ExistFunc("Lj8EstCC")
	lLstPre := aScan( aOrcamento, {|x| x[5] == .T.} ) > 0
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se a venda pode ser cancelada³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Lj140VlVend(@lFiscal	, @cNumCupFis	, @cNumPdvAtu	, @cSerie	, ;
			    @cAlias		, @nOpcA		, @lGeraNCC		, @lLj140Can, ;
			    nTamL1Cup	, aOrcamento	, lSubClosed	, lECommerce .AND. lECCia)
	Return .F.		
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//|  Ponto de Entrada para uso de Template          |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lLj140CanT	// Executa um ponto de entrada para outras verifica‡”es
	lLj140CanT := ExecTemplate("LJ140CAN",.F.,.F.)
	If ValType(lLj140CanT) <> "L" .OR. !lLj140CanT
		Return Nil
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//|  Ponto de Entrada      							|
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nModulo == 72
	lLj140Can := KEXF360()	
	If ValType(lLj140Can) <> "L" .OR. !lLj140Can
		Return Nil	
	EndIf
EndIf

If lLj140Can	// Executa um ponto de entrada para outras verifica‡”es
	lLj140Can := ExecBlock("LJ140CAN",.F.,.F.)
	If ValType(lLj140Can) <> "L" .OR. !lLj140Can
		Return Nil
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Pontos de Entrada LJ140 utilizado pelo Template de Drogarias ³
//³ para delecao dos registros da tabela LK9-Logs Anvisa  		³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lLj140DelT
	ExecTemplate("LJ140DEL",.F.,.F., {lExcAuto})
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se a venda ja foi devolvida³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Lj140VlDev(@lVendido, @nCnt, aOrcamento, lSubClosed)
	Return .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Avisa o usuario sobre a geracao ou nao da Nota de Credito - Loc. Guatemala  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cPaisLoc == "GUA" .AND. lVendido
	If lGeraNCC
		If !lExcAuto
			If !MsgYesNo( STR0066 )					// "Esta operacao de cancelamento vai gerar uma Nota de Credito ao Cliente. Confirma?"
				Return NIL
			EndIf
		EndIf	
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//|     "Esta operacao de cancelamento nao gera Nota de Credito."                                           |
		//|     "Deve ser utilizada quando o pagamento da Nota Fiscal nao foi efetuado ou "                         |
		//|     "na ocorrencia de erro na impressao do documento. Confirma?"                                        |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lExcAuto
			If !MsgYesNo( STR0067 + chr(13) + STR0068 + chr(13) + STR0069 )
				Return NIL
			EndIf
		EndIf	
	EndIf
EndIf

If !lExcAuto
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se o cupom foi impresso na impressora atual³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Lj140VlImp(	lVendido	, cNumPdv	, lFiscal	, cNumPdvAtu, ;
					cNumAnt		, nTamL1Cup	, aOrcamento, lSubClosed, ;
					lLstPre)
		Return .F.
	EndIf
EndIf

If lVendido	//verifica se trata de um orcamento finalizado
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se tiver algum sub-orcamento finalizado, deve-se posicionar nele para que seja feitas as validacoes|
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lSubClosed
		aArea := SL1->( GetArea() )
		If !Lj140Pos(aOrcamento)
			Return .F.
		EndIf
	Endif

	If !Empty(SL1->L1_DOC)
		If !Empty( SF2->F2_NFCUPOM )
			//se houver NF sobre Cupom, executamos a sua exclusao
			If !LojR130( {{SF2->F2_DOC, SF2->F2_SERIE, SF2->F2_CLIENTE, SF2->F2_LOJA}} )
					Help( 	" ", 1, "NOEXCNFCUP", ,;
							STR0078 + Subs(SF2->F2_NFCUPOM,1,3) + " " + Subs( SF2->F2_NFCUPOM,4 ) + STR0079 + SL1->L1_SERIE + " " + SL1->L1_DOC,;
							1, 0 )
					//"Não foi possível excluir a nota fiscal XXX XXXXXX" #" que foi relacionada ao cupom XXX XXXXXX"

			EndIf				
		EndIf
	EndIf

	//retorno a area do orcamento principal
	If lSubClosed
		SL1->( RestArea(aArea) )
	EndIf

EndIf

LJ140FillGet( 5 )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Salva a integridade dos campos de Bancos de Dados 			 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nX := 1 To Len(aHeader)
	Do Case
		Case AllTrim(aHeader[nX][2])	==	"L2_VRUNIT"
			nPosVlrUni := nX
		Case AllTrim(aHeader[nX][2])	==	"L2_VLRITEM"
			nPosVlrTot := nX
		Case AllTrim(aHeader[nX][2])	==	"L2_QUANT"
			nPosQuant  := nX
		Case AllTrim(aHeader[nX][2])	==	"L2_DESCPRO"
			nPosDescPro:= nX
		Case AllTrim(aHeader[nX][2])	==	"L2_TES"
			nPosTesPro := nX			
	EndCase
Next nX 

While .T.
	nImpostos := 0
	nImpsInc  := 0
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica nivel do desconto.							 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea( "SX3" )
	DbSetOrder( 2 )
	DbSeek( "L1_DESCONT" )
	DbSetOrder( 1 )
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Aceita o cabe‡alho do Orcamento 							³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	M->L1_NUM		:= SPACE( TamSX3( "L1_NUM" )[1] )
	M->L1_DOC		:= SL1->L1_DOC
	M->L1_NROPCLI	:= SPACE( TamSX3( "L1_NROPCLI" )[1] )
	M->L1_VLRTOT	:= 0
	M->L1_CLIENTE	:= SuperGetMv( "MV_CLIPAD" )
	M->L1_DESCONT	:= 0
	M->L1_VEND		:= SuperGetMv( "MV_VENDPAD" )
	M->L1_DTLIM		:= dDataBase
	M->L1_VLRLIQ	:= 0
	M->L2_VALDESC	:= 0
	M->L2_DESC		:= 0
	
	DbSelectArea( "SL1" )
	cNum 			:= SL1->L1_NUM
	cFilSL1			:= SL1->L1_FILIAL
	M->L1_NUM		:= SL1->L1_NUM
	M->L1_NUMORC	:= SL1->L1_NUMORC
	M->L1_NROPCLI	:= SL1->L1_NROPCLI
	M->L1_VLRTOT	:= SL1->L1_VLRTOT
	M->L1_CLIENTE	:= SL1->L1_CLIENTE
	M->L1_DESCONT	:= SL1->L1_DESCONT
	M->L1_VEND		:= SL1->L1_VEND
	M->L1_DTLIM		:= SL1->L1_DTLIM
	M->L1_VLRLIQ	:= SL1->L1_VLRLIQ
	M->L1_LOJA		:= SL1->L1_LOJA
	M->L1_VALISS	:= SL1->L1_VALISS

	If SL1->( ColumnPos( "L1_ABTOPCC" ) ) > 0
		M->L1_ABTOPCC := SL1->L1_ABTOPCC
	EndIf
	
	nDecs	:= MsDecimais(SL1->L1_MOEDA)
	
	For nX := 1 To (Len(aCols) - 3)
		If cPaisLoc <> "BRA"
			If nPosDescPro > 0
				aCols[nX][nPosVlrTot] += aCols[nX][nPosDescPro]
			EndIf
			aCols[nX][nPosVlrUni] := Round( aCols[nX][nPosVlrTot] / aCols[nX][nPosQuant], nDecs )
			aPropImp :=	TesImpInf( aCols[nX][nPosTesPro])

			For nY := 1	To Len( aPropImp )
				If ( aPropImp[nY][3]=="1" )
					nImpostos += SL2->( FieldGet( ColumnPos( "L2_" + Substr( aPropImp[nY][2],4,7 ) ) ) )
				ElseIf ( aPropImp[nY][3]=="3" )
					nImpsInc  += SL2->( FieldGet( ColumnPos( "L2_" + Substr( aPropImp[nY][2],4,7 ) ) ) )
				EndIf
			Next nY
		EndIf
	Next nX
	
	If Len(aCols) == 0 
		If !lExcAuto 
			Help(" ","1","TROCADO")
			SetCursor(0)
			Return( Nil )
		Else
		  	//"Este orcamento contem um ou mais produtos trocados, por isso nao podera ser utilizado."
			Conout(STR0105)
			SetCursor(0)
			Return( Nil ) 		
		EndIf	
	EndIf
	
	nOpca := 0
	If cPaisloc <> "BRA"
		M->L1_VLRLIQ := M->L1_VLRTOT - nImpostos + M->L1_DESCONT
	EndIf	

    
	If !lExcAuto
		
				
		DEFINE MSDIALOG oDlg TITLE cCadastro From 3,0 TO 25,80 OF oMainWnd

			@1,01 Say STR0011		// Or‡amento:
			@1,05 MSGet M->L1_NUM When .F.

			@1,10 Say STR0023		// 'Documento    :'
			@1,15 MSGet M->L1_DOC When .F.

			//"NCC Manual?"
			If cPaisLoc == "GUA" .AND. lGeraNCC
				@5,220 CHECKBOX oCheckNFMan VAR lNFManual PROMPT STR0072 OF oDlg SIZE 60,08
			EndIf

			//Conferencia de caixa
			oGet := MSGetDados():New(30,5,100,315,nOpcx,,,"")

			@ 105, 005 TO 147, 110 OF oDlg PIXEL
			@ 105, 112 TO 147, 225 OF oDlg PIXEL
			@ 105, 227 TO 147, 315 OF oDlg PIXEL
		
			@ 110, 008 SAY xPadL( STR0012, 36 )		 SIZE 30, 07	OF oDlg PIXEL	// Seu Pedido:
			@ 110, 038 MSGET M->L1_NROPCLI When .F.	 SIZE 30, 08	OF oDlg PIXEL

			@ 122, 008 SAY xPadL( STR0013, 24 )		 SIZE 30, 07	OF oDlg PIXEL	// Cliente:
			@ 122, 038 MSGET M->L1_CLIENTE When .F.	 SIZE 30, 08	OF oDlg PIXEL

			@ 134, 008 SAY xPadL( STR0014, 31 )		 SIZE 30, 07	OF oDlg PIXEL	// Vendedor:
			@ 134, 038 MSGET M->L1_VEND When .F.	 SIZE 30, 08	OF oDlg PIXEL

			@ 110, 115 SAY xPadL( STR0015, 66 ) SIZE 66, 7 OF oDlg PIXEL	// Total de Mercadorias:

			@ 122, 115 SAY xPadL( STR0016, 36 ) SIZE 36, 7 OF oDlg PIXEL	// Desconto:
			If cPaisLoc == "BRA"
				@ 122, 170 MSGET M->L1_DESCONT When .F. Picture "@E 999,999.99" SIZE 46, 08 OF oDlg PIXEL
			Else
				@ 122, 170 MSGET M->L1_DESCONT When .F. Picture PesqPict("SL1","L1_DESCONT") SIZE 46, 08 OF oDlg PIXEL
			EndIf

			If cPaisLoc == "BRA"
			
				@ 110, 230 SAY xPadL( STR0017, 25 ) SIZE 25, 7 OF oDlg PIXEL	// L¡quido:
				@ 110, 260 MSGET M->L1_VLRLIQ When .F. Picture "@E 999,999.99"	SIZE 46, 08 OF oDlg PIXEL
				
				@ 122, 230 SAY xPadL( STR0018, 28 ) SIZE 25, 7 OF oDlg PIXEL	// Validade:
				@ 122, 260 MSGET M->L1_DTLIM  When .F.							SIZE 46, 08 OF oDlg PIXEL

				@ 110, 170 MSGET M->L1_VLRTOT When .F. Picture "@E 999,999.99"	SIZE 46, 08 OF oDlg PIXEL

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³bloco responsavel pelo Abatimento³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				@ 134, 115 SAY xPadL( STR0076, 66 ) SIZE 66, 07 OF oDlg PIXEL   // Abatimentos:
				nAbatimentos := 0			
				If SL1->( ColumnPos( "L1_ABTOPCC" ) ) > 0
					M->L1_VLRLIQ -= M->L1_ABTOPCC
					nAbatimentos += M->L1_ABTOPCC
				EndIf				
				// Verifica se esta usando a nova configuracao para confirmar se o cliente recolhera o iss.
				If SuperGetMV("MV_LJRECIS",,.F.) .AND. SL1->(ColumnPos("L1_RECISS")) > 0   
					lCliRecIss := SL1->L1_RECISS == "1" .AND. GetNewPar("MV_DESCISS", .F.)  					
				Else
					lCliRecIss := (Posicione("SA1",1,xFilial("SA1")+M->L1_CLIENTE+M->L1_LOJA,"SA1->A1_RECISS") == "1" .AND.;
									GetNewPar("MV_DESCISS", .F.) )
				EndIf					
					
				If lCliRecIss
					M->L1_VLRLIQ -= M->L1_VALISS
					nAbatimentos += M->L1_VALISS
				EndIf
				@ 134, 170 MSGET nAbatimentos When .F. Picture "@E 999,999.99"	SIZE 46, 08 OF oDlg PIXEL
			
			Else
			
				@ 110, 230 SAY xPadL( STR0018, 28) 			SIZE 28, 7 	OF oDlg PIXEL	// Validade:
				@ 110, 260 MSGET M->L1_DTLIM	When .F.	SIZE 46, 08	OF oDlg PIXEL

				//o valor liquido e valor de mercadoria sao invertidos ?
				@ 110, 170 MSGET M->L1_VLRLIQ	When .F. Picture PesqPict("SL1","L1_VLRLIQ") 	SIZE 46, 08 OF oDlg PIXEL
								
				@ 134, 115 SAY xPadL( STR0017, 25) SIZE 25, 7 OF oDlg PIXEL	// L¡quido:				
				@ 134, 170 MSGET M->L1_VLRTOT	When .F. Picture PesqPict("SL1","L1_VLRTOT") 	SIZE 46, 08 OF oDlg PIXEL
				
				@ 122, 230 SAY xPadL( STR0027, 28 ) SIZE 28, 7 OF oDlg PIXEL	// Imp. Inc.:
				@ 122, 260 MSGET nImpsInc When .F. /*Picture PesqPict("SL1","L1_VALIMP1")*/			SIZE 46, 08 OF oDlg PIXEL
					
				@ 134, 230 SAY xPadL( STR0028, 28 ) SIZE 28, 7 OF oDlg PIXEL 	// Imp. Disc.:
				@ 134, 260 MSGET nImpostos When .F. /*Picture PesqPict("SL1","L1_VALIMP1")*/ 		SIZE 46, 08 OF oDlg PIXEL
				
			EndIf

		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 1,oDlg:End()},{|| nOpca := 2,oDlg:End()}) CENTERED
	
	Else
		//Rotina Automatica
		nOpcA := 1			
	EndIf
	
	If nOpcA == 1
	
		aArea := SL1->(GetArea())

		//se for filho UNICO finalizado, add o orcamento pai ao aOrcamento, pois se somente existir um filho, o orcamento pai nao sera excluido
		If lSubClosed .AND. Len(aOrcamento) == 1
			AADD(aOrcamento, {SL1->L1_FILIAL, SL1->L1_NUM, .F.})
		EndIf

		// NFC-e
		If SL1->L1_TPORC == 'E' .AND. (!Empty(SL1->L1_KEYNFCE) .OR. SL1->L1_STORC == "A") .AND. IIF(SL1->(ColumnPos("L1_SERSAT")) > 0,Empty(AllTrim(SL1->L1_SERSAT)),.T.)
			
		   	If !isBlind()                      
		   		/*
		   			Validação para exclusão da Venda NFC-e quando tiver TEF
		   		*/
		   		/*Array criado para que seja usado ao inves de declarar cada variavel*/
		   		//aAuxRet		:=	{.F.,.F.,"",{},"","",0,"",.F.,.F.,.F.,{},{},.F.}
				aAuxRet[1]	:= .F.
				aAuxRet[3]	:= LJPREFIXO() + SL1->L1_DOC
				aAuxRet[7]	:= TamSx3("F2_DOC")[1]
				aAuxRet[8]	:= SL1->L1_DOC
				aAuxRet[11]	:= .T. //lTefOk
				aAuxRet[12]	:= {}
				aAuxRet[13]	:= {}
				aAuxRet[14] := Lj140Valida( aAuxRet[1]/*lJob*/, @aAuxRet[2]/*lVendTef*/,aAuxRet[3]/*cChave*/,@aAuxRet[4]/*aCheques*/,;
											@aAuxRet[5]/*cSerNfCup*/,@aAuxRet[6]/*cNumNfCup*/,aAuxRet[7]/*nTamDoc*/,aAuxRet[8]/*cNumNota*/,;
											@aAuxRet[9]/*lAchouSF2*/,aAuxRet[10]/*lLstPre*/)
				If aAuxRet[14] .And. aAuxRet[2]
					Lj140TlCTef(aAuxRet[1]		,	@aAuxRet[2]	, Iif( Type("oTef") == "U" , Nil, @oTef ), @aAuxRet[11],;
								@aAuxRet[12]	,	@aAuxRet[13])

					//Se o cancelamento do TEF não teve sucesso , não permito prosseguir com o cancelamento da NFCE
					If !aAuxRet[11]
						If lExcAuto
							lMsErroAuto := .T.
							Help(" ",1,"NO140CANC",,STR0145,3,1)
						Else
							Alert(STR0145) //"Operação de cancelamento do TEF não realizada, portanto NFC-e não poderá ser cancelada"
						EndIf
												
						Return .F.
					EndIf
				EndIf
			EndIf
			
			If !Lj140NFCe(aOrcamento)
				Return .F.
			EndIf

		// SAT
		ElseIf !isBlind() .AND. IIF(SL1->(ColumnPos("L1_SERSAT")) > 0,!Empty(SL1->L1_SERSAT),.T.) .And. !Empty(SL1->L1_KEYNFCE)
			If ExistFunc("LJSatxCanc") .AND. !LJSatxCanc(.F., @cNFisCanc)
				Return .F.
			EndIf	
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ primeiro cancelaremos os filhos finalizados(aVendido[3]) e soh por ultimo o orcamento pai ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		// Inicio de Transacao em Banco
		// Para quando for transaco TEF e ecorrer algum erro, nao cancela os orcamentos filhos
		BeginTran()
		For nCnt := 1 To Len(aOrcamento)
			//verifica se eh um orcamento filho finalizado
			If !Empty(aOrcamento[nCnt][1]) .AND. !Empty(aOrcamento[nCnt][2]) .AND. aOrcamento[nCnt][3]
				lVendido := aOrcamento[nCnt][3]
				If !Lj140Pos(aOrcamento)
					Return .F.
				Endif
			Else
				lLastOrc := .T.				//utilizado no final do FOR para que saia do Loop
				lVendido := aOrcamento[nCnt][3]
				SL1->( RestArea(aArea) )	//posiciona no orcamento pai ou no orcamento unico, caso nao tenha sub-orcamentos 
				Lj140DelNCC()				//estorna NCC somente na exclusao do orcamento pai ou orcamento unico finalizado
				If lLstPre
					Lj140LstP(SL1->L1_FILIAL, SL1->L1_NUM)	//estorna os lancamentos na conta corrente e quantidade da Lista de Presente
				EndIf
			EndIf

	        cNum	:= SL1->L1_NUM
			cFilSL1	:= SL1->L1_FILIAL
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Funcao para exclusao do historico do pontuacao do Cliente.                ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
			If ExistFunc("lj140EXF") //Funcao utilizada no Venda Assistida (CRDXFUNE.PRW)
				LJ140EXF()
			EndIf
			
			If (ExistTemplate("LJ140EXC"))
				ExecTemplate("LJ140EXC",.F.,.F.,{lVendido})
			EndIf
			
			If (ExistBlock("LJ140EXC"))
				ExecBlock("LJ140EXC",.F.,.F.)
			EndIf
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Funcao para zerar os pontos obtidos na venda                              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If CRDXInt() .AND. lUsaFD
				
				If !Empty( SL1->L1_DOC )
					
					nPosProd   	:= aScan(aHeader,{|x| Alltrim(Upper(x[2]))=="L2_PRODUTO"})	// Posicao do codigo do item
					nPosVlItem 	:= aScan(aHeader,{|x| Alltrim(Upper(x[2]))=="L2_VLRITEM"})	// Posicao do Valor Total do Item
	                nPosQtd 	:= aScan(aHeader,{|x| Alltrim(Upper(x[2]))=="L2_QUANT"})	// Posicao do Valor Total do Item				
									
					For nY := 1 to Len( aCols )
						cGrupoProd :=  Posicione("SB1",1,xFilial("SB1") + aCols[nY][nPosProd],"SB1->B1_GRUPO")
							AADD ( aProdCri, { aCols[nY][nPosProd], cGrupoProd , aCols[nY][nPosVlItem] , aCols[nY][nPosQtd] } )
					
					Next nY
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Funcao para debitar os pontos                 ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If !Crd240_002(cTpOper			,;	//01 Exclusao
									M->L1_CLIENTE	,;	//02 Codigo do cliente
									M->L1_LOJA		,;	//03 Loja do clinte
									aProdCri		,;	//04 Array com os produtos para analise dos pontos
									M->L1_VLRLIQ	,;	//05 Total da Venda
									SL1->L1_DOC		,;	//06 Numero do documento
									SL1->L1_SERIE	,;	//07 Serie
									SL1->L1_DOC		,;	//08 Numero da venda original
									SL1->L1_SERIE	,;	//09 Serie da venda original
									Nil				,;	//10 Identifica se e do Loja ou do Televendas
									Nil				,;	//11 Indica se grava MAX ou somente calcula pontos
									Nil				,;	//12 Total de pontos
									Nil				 )	//13 Pagamento com Vale-Compra calcular sobre a dif.
						If !lExcAuto
							Aviso( STR0006,STR0077,{STR0048} )
							Return NIL
						Else 
							//"Falha no cancelamento dos pontos referente a campanha de fidelizacao." 
							Conout(STR0106)
							Return NIL
						EndIf	
					Else
					
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Retorna status do vale compra                ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					 	If AliasIndic( "MB1" )
					 		dbSelectArea("MB1")
					 		dbSetOrder(2) // MB1_FILIAL+MB1_SERIE+MB1_NF+MB1_CODIGO
					 		dbSeek(xFilial("MB1") + SL1->L1_SERIE + SL1->L1_DOC)
					 		
					 		While !EOF() .AND. MB1_FILIAL + MB1_SERIE + MB1_NF == xFilial("MB1") + SL1->L1_SERIE + SL1->L1_DOC
						 		aVales := {{MB1->MB1_CODIGO,Nil,Nil,MB1->MB1_CODIGO,.F.}}
						 		
						 		If MB1->MB1_OPER == 2
						 			cOper := "4"
						 		ElseIf MB1->MB1_OPER == 3 	  
						 			cOper := "2"
						 		EndIf	
						 		
						  		If !Crd240UpdMAV( aVales, cOper )
						  			If !lExcAuto		
										Aviso( STR0006,STR0077,{STR0048} )
									Else 
										//"Falha no cancelamento dos pontos referente a campanha de fidelizacao." 
										Conout(STR0106)
									EndIf	
								Else
							 		aVales := {MB1->MB1_CODIGO,Nil,Nil,MB1->MB1_CODIGO,.F.}
						 			CRDA270Log( aVales, cOper, SL1->L1_DOC, cPdv, SL1->L1_SERIE )			 	
								EndIf	
						 		
						 		dbSelectArea("MB1")
					 			dbSkip()
					 		EndDo
					 	EndIf			
					EndIf
				EndIf
			EndIf
			
			If AliasIndic('MDU')
			
				aAreMt := GetArea()
				
				DbSelectArea('MDU')
				MDU->( DbSetOrder(2) )		
				MDU->( DbSeek(xFilial( "SL2" )+SL1->L1_DOC+SL1->L1_SERIE) )
				
				While !EOF() .AND. (MDU->MDU_DOC == SL1->L1_DOC) .AND. (MDU_SERIE == SL1->L1_SERIE)
					
					RecLock('MDU',.F.)  
						MDU->( DbDelete() )
					MDU->( MsUnLock() )

					MDU->( DbSkip() )
				End	
				
				RestArea(aAreMt)		
			
			EndIf	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Determinar a numeracao e o motivo da NCC - Loc. Guatemala                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cPaisLoc == "GUA" .AND. lVendido
				If lGeraNCC .AND. !Lj140Manual()
					Return NIL
				EndIf
				If !Lj021MotDev(@cDescMot)
					Return NIL
				EndIf
				If lGeraNCC
					If !LJ021Nota(.T.)
						Return NIL
					EndIf
				EndIf
			EndIf
			
			If !lVendido
				cChaveCHRe := LJPREFIXO() + SL1->L1_DOCPED
			EndIf

			If !Lj140ChkRes(lVendido, cChaveCHRe)
				Return Nil
			EndIf

			If lVendido
				lNota := nModulo == 5 .And. !Empty(SL1->L1_DOC) .And. !Empty(SL1->L1_SERIE)
				cAuxDoc := SL1->L1_DOC
				cAuxSerie := SL1->L1_SERIE
				xRet  := Lj140Grava(Nil, cDescMot, lGeraNCC, cNumNFDev, cSerieDev, cMotivo, cNum, lNFCe, lLstPre)
				cNfisCanc := ""
			EndIf
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Avalia se o orcamento contem reservas, nesse caso sera necessario limpar as tabelas SL1 e SL2 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( IIf(!lAmbPAF, Empty(L1_DOC), .T.) .AND. SL1->L1_TIPO == "P" .AND. !Empty(SL1->L1_DOCPED) .AND. SL1->L1_STATUS<>"D" .AND. ( SL1->(ColumnPos("L1_STATUES")) = 0 .OR. Empty(SL1->L1_STATUES) )	) //Pedidos encerrados
				Lj140LpSL2(cNum, @nVlrOrc)
				Lj140LpSL1(CrdxInt(), cNum, nVlrOrc)
			EndIf		
	        
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|    Verifica se o orcamento foi gerado pela integracao com Gestao de Concessionarias  |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If (SuperGetMV("MV_VEICULO",,"N")) == "S" .AND. SL1->(ColumnPos("L1_ORIGEM")) > 0 .AND. SL1->L1_ORIGEM == "V" .and. !SL1->L1_VEICTIP $ "1/2"
		 		lIntGC  := .T.
	   		EndIf     	
	
			If !lVendido .OR. FunName() = "LOJA220" .OR. lIntGC .Or. nModulo == 5 .OR. lECommerce
				If ( (lAmbPAF .AND. nModulo <> 5) .OR. (IIf(LjAnalisaLeg(6)[1], LjEmiteNF(SM0->M0_CGC), .T.) .AND. nModulo <> 5) );
					.OR. FunName() = "LOJA220" .OR. cPaisLoc <> "BRA" ;
					.OR. ( !LjMPreVend(SM0->M0_CGC) .AND. LjNVCC(SM0->M0_CGC) .AND. nModulo <> 5 );
					.OR. ( nModulo == 5 .And. !(lNota) )
					lj140ExcOrc()
				ElseIf (!lFiscal .AND. lIntGC .AND. nModulo == 5) .OR. lECommerce
					lj140ExcOrc()
				ElseIf nModulo <> 5 
					If lFiscal
						If !lExcAuto
							MsgStop(STR0050 + Chr(10)+ Chr(13) + ; 				//"Esse cupom sera cancelado de forma "
							STR0051 + Chr(10)+ Chr(13) + ; 						//"automatica. Conforme lei estadual  "
							STR0052)												//"vigente que regulamenta pre-venda. " 
						Else 
							Conout(STR0050+STR0051+" "+STR0052)
						EndIf	

						If Lj140ICOrc()
							lj140ExcOrc()
						Else
							If !lExcAuto
								MsgStop( 	STR0030 + Chr(10)+ Chr(13) + ; 	//"Existem problemas com a impressora fiscal"
											STR0059, STR0047 ) 					//"Não foi possível imprimir e cancelar o orçamento!" //Atenção
							Else 
								Conout(STR0030)
					    		Conout(STR0059)
							EndIf				
						EndIf

					Else 
						If !lExcAuto
							MsgStop(STR0060 + Chr(10)+ Chr(13) + ;				//"Para cancelar o orcamento o usuario deve ser fiscal"
									STR0063 + Chr(10)+ Chr(13) + ;				//"Por exigencia da Legislação Vigente no Estado, para todo orçamento cancelado, "
									STR0064, STR0047)							//"deve ser impresso um cupom fiscal e seu cancelamento"
						Else 
						  	Conout(STR0060)
				        	Conout(STR0063+STR0064)	
						EndIf			
					EndIf
				EndIf
			EndIf
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|     Verifica se o Siga Loja esta integrado com o Gestão Hospitalar                                      |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If GetNewPar("MV_LOJAHSP",.F.)
				HS_ExclRAt(M->L1_NUM, aOrcamento[nCnt][3])
			EndIf
	
			If ValType(xRet)=="L"
				lRetorno := xRet
			Else
				lRetorno :=	.F.		
			EndIf			
		
		    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	  		//³Verifica se o Integraçao com o Plano de Saude   	 					       ³
	 		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lRetorno .AND. HasTemplate("DRO") .AND. SuperGetMv("MV_PLSATIV",.F.,.F.);
				.AND. !Empty(SL1->L1_MATRIC) .AND. ExistFunc("PLSDELVE")
					PLSDELVE(M->L1_DOC,SL1->L1_MATRIC)
			Endif
		
			If cPaisLoc == "GUA" .AND. lGeraNCC .AND. lRetorno
				Lj021Imprime()
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//| "Aten‡„o"###"Foi gerada a Nota de Credito "###"Ok"  |
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !lExcAuto
					Aviso( STR0006,STR0070 + AllTrim( cSerieDev ) + "-" + cNumNFDev,{STR0048} )
				Else 
					Conout(STR0070 + AllTrim( cSerieDev ) + "-" + cNumNFDev)
				EndIf		
			EndIf
					
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Caso seja o orcamento pai ou orcamento unico, exclui o cartão fidelidade e sai do Loop³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lLastOrc
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Release 11.5 - Cartao Fidelidade³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
				If lLjcFid .AND. xRet
					nPosProd   	:= aScan(aHeader,{|x| Alltrim(Upper(x[2]))=="L2_PRODUTO"})		// Posicao do codigo do item
					nPosNumcFi 	:= aScan(aHeader,{|x| AllTrim(Upper(x[2])) == "L2_NUMCFID"})	// Posicao do Numero do cartao fidelidade
					For nY := 1 to Len( aCols )
						//Cancelamento de venda de recarga de cartao fidelidade
						If LaFunhProd(aCols[nY][nPosProd])
					 		Ca280Exec("CA280ESLD",aCols[nY][nPosNumcFi],,,M->L1_DOC,cSerie,M->L1_LOJA,MOVFID_ESTREC)
					 	Else
					 		//Cancelamento de venda paga com cartao fidelidade
							DbSelectArea("SL4")
							DbSetOrder (1)
							If DbSeek (xFilial("SL4")+M->L1_NUM)
								While SL4->(!Eof()) .AND. SL4->L4_NUM == M->L1_NUM
									If Alltrim(SL4->L4_FORMA) == "FID"
										//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
										//³Se a venda foi paga com cartao fidelidade, entao o ³
										//³movimento de saida sera estornado da tabela MBN    ³
										//³e o valor devolvido ao respectivo saldo.           ³
										//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
										Ca280Exec("CA280ESLD",SL4->L4_NUMCFID,,,M->L1_DOC,cSerie,M->L1_LOJA,MOVFID_ESTPGTO)
					 					Exit
					 				EndIf
					 				SL4->(DbSkip())
								End
							EndIf
		
					 	Endif
					Next nY		
				EndIf
				
				Exit //saida do For nCnt
			EndIf
		
		Next nCnt
	Else
		DbSelectArea("SL1")
	End
	Exit
End
 
EndTran()
              
// Destravar todos os registros que estejam eventualmente locados
MsUnLockAll() 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se existe SC gerada pelo orcamente. se tiver cancela		       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ    
If lRetorno .And. nOpcA == 1 .AND. ( (AllTrim(Str(SuperGetMv("MV_LJGERSC",,1))) $ "2|3")  .OR. lECommerce )
	If  ExistFunc("LA590CncSC")
    	LA590CncSC(cNum,cFilSL1)
    Endif	
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se for orçamento e-commerce realiza a sinalização do cancelanmento para ser enviado para o site³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If lRetorno .And. !( lECommerce ) .AND.  SL1->(ColumnPos("L1_ECFLAG") > 0) .AND. (SL1->L1_ECFLAG == "1")
         
   	aAreaMF5 := MF5->(GetArea())    
	MF5->( DbSetOrder(1) ) 
	//Pedidos e-commerce, verifica se foi processado o pagamento
	If  MF5->(DbSeek(xFilial("MF5") + "SL1" + SL1->L1_FILIAL+ SL1->L1_NUM))

		    If  MF5->( RecLock("MF5") )
		        MF5->MF5_ECFLAG := "2"  //Inativo
		        MF5->MF5_ECDTEX := " "
		    	MF5->MF5_ECSTAT := "5"  // 5-Status e-commerce para cancelado
		    	MF5->( MsUnLock() )
		    EndIf   
	EndIf
	RestArea(aAreaMF5)
EndIf

FTVDFinishLoja()

Return nOpca

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³lj140Grava³ Autor ³ Vendas Clientes       ³ Data ³ 02/01/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Atualiza‡„o de arquivos.                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³lj140Grava( )                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LJ140Grava(lJob	 , 	cDescMot, 	lGeraNCC, cNumNFDev	,;
					cSerieDev, 	cMotivo ,	cNum	, lNFCe		,;
					lLstPre)

Local lVendTef 	   	:= .F.							// Venda com TEF
Local lValido		:= .F.				            // se os dados estao validos	
Local lCancECF		:= .F.							// se foi cancelado na impressora fiscal
Local lDataBase		:= .F.							// se foi cancelado no banco de dados
Local lAchouSF2	   	:= .F.						  	// Variavel de retorno se encontrou o SF2 ou nao
Local lRet			:= .F.							// Variavel de Retorno
Local cChave										// Chave de busca
Local cSerNfCup    	:= ""							// Serie cupom
Local cNumNfCup    	:= ""							// Nf cupom
Local cNumNota     	:= SL1->L1_DOC					// Nota da venda
Local nTamDoc      	:= TamSx3("F2_DOC")[1]         	// Tamanho do campo F2_DOC
Local aCheques 	   	:= {}							// Informacoes do cheque
Local cSupervisor   := "" 							// Supervisor/msg mostrada no cancelamento
Local lUseSAT		:= .F.		//Usa SAT				//Usa SAT
Local lGerInte		:= SuperGetMV("MV_LJGRINT",.F.,.F.)	// Verifica se a integracao esta habilitada
Local lAmbOffLn 	:= SuperGetMv("MV_LJOFFLN", Nil, .F.)	//Identifica se o ambiente esta operando em offline

Default lJob 		:= .F.							// Fala se é via Job ou nao
Default cDescMot	:= ""                                       
Default lGeraNCC	:= .F.
Default cNumNFDev	:= ""
Default cSerieDev	:= ""
Default cMotivo		:= ""
Default cNum		:= ""
Default lNFCe		:= .F.
Default lLstPre		:= .F.

// Verifica se utiliza SAT
lUseSAT 	:= IIF(ExistFunc("LjUseSat"),LjUseSat(),.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Instacia Objeto para faze Offline. (Variavel statica)³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lAmbOffLn
	oProcOff := LJCProcessoOffLine():New("006")
Endif

If Type("LANULASF3")=="U"
	Private lAnulaSF3 := .F.
EndIf

If lLstPre
	cChave := LJPREFIXO() + Iif(!Empty(SL1->L1_DOC),SL1->L1_DOC,SL1->L1_DOCPED)
Else
	cChave := LJPREFIXO() + SL1->L1_DOC
EndIf 

iRet := 0

lValido :=  Lj140Valida(lJob, 			@lVendTef, 		cChave,		@aCheques,; 
						@cSerNfCup, 	@cNumNfCup, 	nTamDoc, 	cNumNota,;
						@lAchouSF2,		lLstPre)

If lValido .AND. !lNFCe .AND. !lUseSAT
	cSupervisor:= " |"+cNumNota
	lCancECF := Lj140CancCupom(lAchouSF2,cSupervisor)
EndIf
				
If lValido .AND. (lNFCe .OR. lCancECF .OR. lUseSAT)

	If lGerInte
		oIntCVenda := LJCAdapXMLEnvVenda():New()
		Lj140AddIn()
	EndIf
	
	
	Begin Transaction
	
		lDataBase := Lj140CancDB(aCheques, 	lVendTef, 	cChave, 	@cSerNfCup,;
							   @cNumNfCup, 	nTamDoc, 	cNumNota, 	lAchouSF2,;
							   lJob, 		cDescMot, 	lGeraNCC, 	cNumNFDev, ;
							   cSerieDev, 	cMotivo, 	cNum	)

	End Transaction 

EndIf		                                 

lRet := lValido .AND. lDataBase .AND. (lNFCe .OR. lUseSAT .OR. lCancECF)

//Envia os dados de cancelamento para integracao
If lRet

	If lGerInte 
		Lj140Integ()
	EndIf 
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Faz integração offline³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lAmbOffLn
		Lj140EvInt()
	Endif
	
	//------------------------------------------------------------------
	// Verifica se a venda foi paga com Vale Presente e estorna o mesmo.
	//------------------------------------------------------------------
	Lj140EstVP( cNum )
EndIf

Return lRet


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ lj140Exc ³ Autor ³ Vendas Clientes       ³ Data ³ 04.04.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de exclusao de Orcamentos                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lj140ExcOrc(ExpC1)                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 - Filial utilizada para exclusao (reservas)          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function lj140ExcOrc( cFilRes, cChaveCHRe )           

Local cFilL2    := SL2->( xFilial( "SL2" ) )
Local cFilE1    := SE1->( xFilial( "SE1" ) )
Local cFilL4    := SL4->( xFilial( "SL4" ) )
Local cFilE5	:= SE5->( xFilial( "SE5" ) )
Local aRetCrd   := {}                       	//Array  de retorno do cancelamento do contrato - integracao SIGACRD
Local lCrdxInt  := CrdxInt()                	//Controla se tem integracao com SIGACRD
Local cParcela  := "" 							//Define tipo da duplicata	
Local aDadosOri	:= {}							//Dados do orcamento original
Local nTamE1Parc:= TamSX3("E1_PARCELA")[1]		//Tamanho do campo
Local aClientes	:= {}
Local cMvLjPref := SuperGetMV("MV_LJPREF")
Local cPrefE1	:= ""							//elemento que sera utilizado como SE1->E1_PREFIXO, na busca do titulo
Local lIntGC	:= .F.
Local cCompFil  := "" 							//Verifica se a tabela SE1 e compartilhada por FILIAL. 
Local lLjChkNFS := ExistFunc("LjChkNFS") .And. LjChkNFS()
Local aPedidos	:= {}
Local nX		:= 0 							//Verifica se a tabela SE1 e compartilhada por FILIAL.
Local lNFCe	    := ExistFunc("LjEmitNFCe") .AND.  LjEmitNFCe() .AND. ColumnPos("L1_KEYNFCE") > 0
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Tratamento para e-Commerce      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lECommerce  :=  (SuperGetMV("MV_LJECOMM",,.F.) .And. ExistFunc("LJ862ECAuto") .And. LJ862ECAuto())
Local lECCia :=  SuperGetMv("MV_LJECOMO", .T., .F.)  .AND. ( IsInCallStack("Lj907OrEx")  .OR. ( SL1->(ColumnPos("L1_ECFLAG")) > 0 .AND. SL1->L1_ECFLAG == "1") ) 
Local cNumReserva := ""                   //Codigo da Reserva no Orcamento    
Local cLogPath :=  SuperGetMV("MV_LOGPATH",,"LOGS")     
Local lLiberOk	:= .T.
Local lResidOk	:= .T.
Local lFaturOk	:= .F.
Local lECCiaRes := lECCia .AND. SuperGetMv("MV_LJECOM0", , .F.)

DEFAULT cFilRes 	:= cFilAnt
DEFAULT cChaveCHRe 	:= ''

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Verifica se o orcamento foi gerado pela integracao com Gestao de Concessionarias  |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
 
If (SuperGetMV("MV_VEICULO",,"N")) == "S" .AND. SL1->(ColumnPos("L1_ORIGEM")) > 0 .AND. SL1->L1_ORIGEM == "V" .and. !SL1->L1_VEICTIP $ "1/2"
	lIntGC  := .T.
EndIf


If SuperGetMV("MV_1DUP")=="1"
	cParcela := "1"
Else
	cParcela := "A"
EndIf

cParcela	:= PadR( cParcela , nTamE1Parc)		// Ajusta de acordo com o tamanho do E1_PARCELA
If !Empty( cFilL2 )
	cFilL2 := cFilRes
EndIf
If !Empty( cFilL4 )
	cFilL4 := cFilRes
EndIf

If lECCia .AND. !Empty(SL1->L1_DOCPED) .and. !Empty(SL1->L1_SERPED)
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ`Ù
	If !Lj140ExR(cLogPath, SL1->L1_DOCPED , SL1->L1_SERPED)
		Return .F.
	EndIf  
EndIf

Begin Transaction    



	If lLjChkNFS  .And. !Empty(SL1->L1_DOCPED) .and. !Empty(SL1->L1_SERPED)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Efetua a exclusão da SD2 e SF2³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	  	aPedidos := {}
		DbSelectArea("SD2")
		SD2->(DbSetOrder(3))
		If SD2->(DbSeek(xFilial("SD2")+SL1->L1_DOCPED+SL1->L1_SERPED))

			While !SD2->(Eof()) .AND.	(xFilial("SD2")	== SD2->D2_FILIAL) 	.AND. ;
								(SD2->D2_DOC 		== SL1->L1_DOCPED 	.AND. ;
								SD2->D2_SERIE 		== SL1->L1_SERPED)
				
	        	nPos := ascan( aPedidos, SD2->D2_PEDIDO )
	        	If nPos == 0
	        		aadd(aPedidos, SD2->D2_PEDIDO )
	        	EndIf
	        	SD2->( dbSkip() )
	        End
		  	Lj140CnSD2(SL1->L1_DOCPED,SL1->L1_SERPED,.T.)
			Lj140CnSF2(SL1->L1_DOCPED,SL1->L1_SERPED)
		EndIf
		
		If lECCia 
			aadd(aPedidos, SL1->L1_PEDRES )
		EndIf
        
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Apaga Pedido de vendas quando for "Entrega"    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
		If Len( aPedidos ) > 0
			For nX := 1 to len(aPedidos)
				DbSelectArea("SC5")
				SC5->(DbSetOrder(1)) //C5_FILIAL+C5_NUM 
			   	If SC5->(DbSeek(xFilial("SC5")+ aPedidos[nX] ))
		    		DbSelectArea("SC6")
					SC6->(DbSetOrder(1)) //C6_FILIAL+C6_NUM
					If SC6->(DbSeek(xFilial("SC6")+SC5->(C5_NUM)))
						While !SC6->(Eof()) .AND. SC6->C6_NUM == SC5->(C5_NUM)
				    		RecLock('SC6',.F.)  
							If lECCia .AND. Empty(SC6->C6_RESERVA)
								MaAvalSC6("SC6",2,"SC5",.T.,.F.,@lLiberOk,@lResidOk,@lFaturOk, .f.)	
							EndIf
							
							SC6->(DbDelete())
							SC6->(MsUnLock())
							SC6->(dbSkip())
						End
					EndIf
					
					//Exclui liberação
					DbSelectArea("SC9")
					SC9->(DbSetOrder(1)) //C9_FILIAL+C9_Pedido
					If SC9->(DbSeek(xFilial("SC9")+ SC5->(C5_NUM) ))
						While !SC9->(Eof()) .AND. SC9->C9_PEDIDO == SC5->(C5_NUM)
				    		RecLock('SC9',.F.)  
							SC9->(DbDelete())
							SC9->(MsUnLock())
							SC9->(dbSkip())
						End
					EndIf	
					//Exclui cabeçalho(SC5)
					RecLock('SC5',.F.)  
					If lECCia .AND. SC5->(ColumnPos("C5_STATUS")) > 0
						SC5->C5_STATUS := "90"
						SC5->C5_VOLTAPS := ""
					EndIf
					SC5->(DbDelete())   
					SC5->(MsUnLock())
			  	EndIf
			Next nX
		EndIf 
		
		DbSelectArea( "SL2" )
		SL2->(DbSetOrder(1)) //L2_FILIAL+L2_NUM+L2_ITEM+L2_PRODUTO
		If SL2->(DbSeek( cFilL2+SL1->L1_NUM ))
			While !SL2->(Eof()) .AND. cFilL2==SL2->L2_FILIAL .AND. SL2->L2_NUM == SL1->L1_NUM
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Excluir a reserva de Estoque                                ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !( Empty(SL2->L2_RESERVA) )

					cNumReserva := SL2->L2_RESERVA

					dbSelectArea("SC0")
					SC0->(dbSetOrder(1))
					SC0->(DbSeek(xFilial("SC0")+cNumReserva))  // Atenção caso altere este trecho, faça a manutenção tbm na função: A430DelMvc
					While ( !SC0->(Eof()) ) .And. (SC0->(C0_FILIAL+C0_NUM) == xFilial("SC0")+cNumReserva)
						SC0->(RecLock("SC0",.F.))  //Ajuste para estornar o B2_RESERVA
						//Item Liberado tem que voltar o SC0->C0_QTDPED para a quantidade do item
						If lECCiaRes .AND. SC0->C0_TIPO  = "LB" .AND. SC0->C0_QTDPED = 0
						
							LjGrvLog( SL1->L1_NUM, "Voltando a quantidade em Pedido")
							SC0->C0_QTDPED := SL2->L2_QUANT
						EndIf
						SC0->C0_QUANT  += SC0->C0_QTDPED
						SC0->C0_QTDPED -= SC0->C0_QUANT
						SC0->(MsUnlock())
					
						a430Reserv({3,C0_TIPO,C0_DOCRES,C0_SOLICIT,C0_FILRES},;
							cNumReserva,;
							SC0->C0_PRODUTO,;
							SC0->C0_LOCAL,;
							SC0->C0_QUANT,;
							{	SC0->C0_NUMLOTE,;
							SC0->C0_LOTECTL,;
							SC0->C0_LOCALIZ,;
							SC0->C0_NUMSERI})
						SC0->(MsUnLock())
						SC0->(dbSkip())
					EndDo
					
					DbSelectArea( "SL2" )
				EndIf
	
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Nao excluo o orcamento quando se tratar de uma venda com reserva, apenas limpo os campos da reserva ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				Reclock( "SL2" ,.F.)
				Replace SL2->L2_FILRES		with	SPACE(TamSx3("L2_FILRES")[1])
				Replace SL2->L2_ORCRES		with	SPACE(TamSx3("L2_ORCRES")[1]) 
				Replace SL2->L2_VENDIDO		with	SPACE(TamSx3("L2_VENDIDO")[1]) 
				Replace SL2->L2_SERIE		with	SPACE(TamSx3("L2_SERIE")[1]) 
				Replace SL2->L2_PDV			with	SPACE(TamSx3("L2_PDV")[1]) 
				Replace SL2->L2_SITUA		with	SPACE(TamSx3("L2_SITUA")[1]) 
				Replace SL2->L2_RESERVA		with	SPACE(TamSx3("L2_RESERVA")[1]) 
				Replace SL2->L2_NUMORIG		with	SPACE(TamSx3("L2_NUMORIG")[1]) 
				Replace SL2->L2_SITTRIB		with	SPACE(TamSx3("L2_SITTRIB")[1]) 
				Replace SL2->L2_VDMOST		with	SPACE(TamSx3("L2_VDMOST")[1]) 

				SL2->(MsUnlock())
				SL2->(dbSkip())
			End
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Recupera os dados do tiulo a partir do orcamento original³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aDadosOri := Lj140OrcOrig()
		
		If Len(aDadosOri) > 0
			
			cCompFil := FWModeAccess("SE1", 3) //Verifica se a filial da SE1 e compartilhada. 
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Filial do titulo³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If Alltrim(cCompFil) <> "C" 
				cFilE1 := aDadosOri[1]
			EndIf
			
			/*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			  ³Prefixo do Titulo - quando o campo SL1->L1_SERPED for diferente do campo SE1->E1_PREFIXO,				|
			  ³devemos montar a chave de busca com o parametro MV_LJPREF. Quando o conteudo do parametro nao for padrao,|                     |
			  |ele devera ser macro executado assim como ocorre na funcao LOJXFUNC (responsavel pela gravacao).			|
			  ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			DbSelectArea("SE1")
			DbSetOrder(1) //E1_FILIAL+E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO		
			If SE1->( DbSeek(cFilE1 + aDadosOri[4] + aDadosOri[5]) ) 	//E1_FILIAL + E1_PREFIXO + E1_NUM
				cPrefE1 := aDadosOri[4]									//SL1->L1_SERPED
			ElseIf !Empty(aDadosOri[4]) .AND. !Empty(aDadosOri[5]) .AND. AllTrim(Upper(cMvLjPref)) <> "SF2->F2_SERIE"
				cPrefE1 := &(cMvLjPref) //como o conteudo do parametro nao e padrao, ele e macro executado.
			EndIf
	        
			aAdd( aClientes , {aDadosOri[2] , aDadosOri[3]} )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Busca se houve pagamentos do tipo que o cliente sao adms  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("SL4")
			DbSetOrder(1) //E1_FILIAL+E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO
			If DbSeek(cFilL4 + SL1->L1_NUM )
				While !Eof() .AND. cFilL4 == SL4->L4_FILIAL .AND. SL4->L4_NUM == SL1->L1_NUM
					If AllTrim(SL4->L4_FORMA) $ "CC|VA|CO|CD|FI"
						If AScan(aClientes,{|x| x[1] == Left(L4_ADMINIS,3)}) == 0
							aAdd( aClientes , {Left(L4_ADMINIS,3) , "01"} )
						EndIf	
					EndIf				
					DbSkip()
				End
			EndIf
			                                                             
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Apaga os cheques quando utilizada reserva
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !Empty(cChaveCHRe) 
				DbSelectArea("SE1")
				DbSetOrder(1) //E1_FILIAL+E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO
				If DbSeek(cFilE1 + cPrefE1 + aDadosOri[5])
					While !Eof() .AND. SE1->E1_FILIAL == cFilE1 .AND. SE1->E1_PREFIXO == cPrefE1 .AND. SE1->E1_NUM == aDadosOri[5]                 
						If AScan(aClientes,{|x| AllTrim(x[1])==AllTrim(SE1->E1_CLIENTE) .AND. AllTrim(x[2])==AllTrim(SE1->E1_LOJA)}) > 0
							Lj140CHDelReserva(cChaveCHRe)				
						EndIf	
						SE1->(DbSkip())
					End
				EndIf
			EndIf
			     
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Apaga os titulos do SE1 relacionados ao orcamento excluido³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("SE1")
			DbSetOrder(1) //E1_FILIAL+E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO
			If DbSeek(cFilE1 + cPrefE1 + aDadosOri[5])
		
				While !Eof() .AND. SE1->E1_FILIAL == cFilE1 .AND.;
					SE1->E1_PREFIXO	== cPrefE1 .AND. ;
					SE1->E1_NUM		== aDadosOri[5]

					If AScan( aClientes,{|x| AllTrim(x[1]) == AllTrim(SE1->E1_CLIENTE) .AND. AllTrim(x[2]) == AllTrim(SE1->E1_LOJA)} ) > 0
						Reclock("SE1",.F.)
						SE1->( dbDelete() )
						SE1->( MsUnlock() )
					EndIf	
					SE1->(DbSkip())
				End
			EndIf

			cCompFil := FWModeAccess("SE5", 3) //Verifica se a filial da SE1 e compartilhada. 
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Filial da Movimentacao³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If Alltrim(cCompFil) <> "C" 
				cFilE5 := aDadosOri[1]
			EndIf
	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Apaga a movimentacao bancaria relacionados ao orcamento excluido³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("SE5")
			DbSetOrder(7) //E5_FILIAL, E5_PREFIXO, E5_NUMERO, E5_PARCELA, E5_TIPO, E5_CLIFOR, E5_LOJA, E5_SEQ
			If DbSeek(cFilE5 + cPrefE1 + aDadosOri[5])
		
				While !Eof() .AND. SE5->E5_FILIAL == cFilE5 .AND.;
					SE5->E5_PREFIXO	== cPrefE1 .AND.;
					SE5->E5_NUMERO	== aDadosOri[5]
					
					// Verifica cliente e loja da movimentacao
					If SE5->E5_CLIFOR + SE5->E5_LOJA <> aDadosOri[2] + aDadosOri[3]
						SE5->(DbSkip())
						Loop
					EndIf
					
					Reclock("SE5",.F.)
					SE5->( dbDelete() )
					SE5->( MsUnlock() )
					SE5->( DbSkip() )
					
				End
			EndIf
	
		EndIf


		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ`¿
		//³limpar os campos do orçamento  para possibilitar sua reutilização se for necessário.³
	
		Reclock( "SL1" ,.F.)
		Replace SL1->L1_PDV		with SPACE(TamSx3("L1_PDV")[1])
		Replace SL1->L1_EMISNF	with CTOD("")
		Replace SL1->L1_TIPO	with SPACE(TamSx3("L1_TIPO")[1])
		Replace SL1->L1_OPERADO	with SPACE(TamSx3("L1_OPERADO")[1])
		Replace SL1->L1_DOCPED	with SPACE(TamSx3("L1_DOCPED")[1])
		Replace SL1->L1_SERPED	with SPACE(TamSx3("L1_SERPED")[1])
		Replace SL1->L1_DOC		with SPACE(TamSx3("L1_DOC")[1])
		Replace SL1->L1_SERIE	with SPACE(TamSx3("L1_SERIE")[1])
		Replace SL1->L1_RESERVA	with SPACE(TamSx3("L1_RESERVA")[1])
		Replace SL1->L1_IMPRIME	with '1N'
		Replace SL1->L1_STATUS	with SPACE(TamSx3("L1_STATUS")[1])
		Replace SL1->L1_OPERACA	with SPACE(TamSx3("L1_OPERACA")[1])
		Replace SL1->L1_SITUA	with SPACE(TamSx3("L1_SITUA")[1])
		Replace SL1->L1_NUMORIG	with SPACE(TamSx3("L1_NUMORIG")[1])
		Replace SL1->L1_TIPODES	with SPACE(TamSx3("L1_TIPODES")[1])

		MsUnlock()

	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Apaga Pedido de vendas quando for "Entrega"    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
		If lECCia .OR. (Empty( SL1->L1_DOCPED ) .AND. !Empty( SL1->L1_FILRES ) .AND. Empty(SL1->L1_DOC))
			DbSelectArea("SC5")
			SC5->(DbSetOrder(1)) //C5_FILIAL+C5_NUM 
		   	If SC5->(DbSeek(xFilial("SC5")+ SL1->(L1_PEDRES)))
	    		DbSelectArea("SC6")
				SC6->(DbSetOrder(1)) //C6_FILIAL+C6_NUM
				If SC6->(DbSeek(xFilial("SC6")+SC5->(C5_NUM)))
					While !SC6->(Eof()) .AND. SC6->C6_NUM == SC5->(C5_NUM)
			    		RecLock('SC6',.F.)  
						If lECCia .AND. Empty(SC6->C6_RESERVA)
							MaAvalSC6("SC6",2,"SC5",.T.,.F.,@lLiberOk,@lResidOk,@lFaturOk, .f.)	
						EndIf

						SC6->(DbDelete())
						SC6->(MsUnLock())
						SC6->(dbSkip())
					End
				EndIf
				
				//Exclui liberação
				DbSelectArea("SC9")
				SC9->(DbSetOrder(1)) //C9_FILIAL+C9_Pedido
				If SC9->(DbSeek(xFilial("SC9")+ SL1->(L1_PEDRES)))
					While !SC9->(Eof()) .AND. SC9->C9_PEDIDO == SC5->(C5_NUM)
			    		RecLock('SC9',.F.)  
						SC9->(DbDelete())
						SC9->(MsUnLock())
						SC9->(dbSkip())
					End
				EndIf	
				//Exclui cabeçalho(SC5)
				RecLock('SC5',.F.)
				If lECCia .AND. SC5->(ColumnPos("C5_STATUS")) > 0
					SC5->C5_STATUS := "90"
					SC5->C5_VOLTAPS := ""
				EndIf				  
				SC5->(DbDelete())
				SC5->(MsUnLock())
		  	
		  		RecLock("SL1", .F.)
		  		SL1->L1_PEDRES := Space( TamSx3("L1_PEDRES")[1] )
		  		SL1->( MsUnlock() )
		  		
		  	EndIf
		EndIf
	
		DbSelectArea( "SL2" )
		SL2->(DbSetOrder(1)) //L2_FILIAL+L2_NUM+L2_ITEM+L2_PRODUTO
		If SL2->(DbSeek( cFilL2+SL1->L1_NUM ))
			While !SL2->(Eof()) .AND. cFilL2==SL2->L2_FILIAL .AND. SL2->L2_NUM == SL1->L1_NUM
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Excluir a reserva de Estoque quando e-commerce.             ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If  lECommerce  .Or. !( Empty(SL2->L2_RESERVA) )
				
					cNumReserva := SL2->L2_RESERVA
				
					dbSelectArea("SC0")
					SC0->(dbSetOrder(1))
					SC0->(DbSeek(xFilial("SC0")+cNumReserva))  // Atenção caso altere este trecho, faça a manutenção tbm na função: A430DelMvc
					While ( !SC0->(Eof()) ) .And. (SC0->(C0_FILIAL+C0_NUM) == xFilial("SC0")+cNumReserva)
						SC0->(RecLock("SC0",.F.))  //Ajuste para estornar o B2_RESERVA
						
						If lECCiaRes .AND. SC0->C0_TIPO  = "LB" .AND. SC0->C0_QTDPED = 0
						
							LjGrvLog( SL1->L1_NUM, "Voltando a quantidade em Pedido")
							SC0->C0_QTDPED := SL2->L2_QUANT
						EndIf						
						
						SC0->C0_QUANT  += SC0->C0_QTDPED
						SC0->C0_QTDPED -= SC0->C0_QUANT
						SC0->(MsUnlock())
					
						a430Reserv({3,C0_TIPO,C0_DOCRES,C0_SOLICIT,C0_FILRES},;
							cNumReserva,;
							SC0->C0_PRODUTO,;
							SC0->C0_LOCAL,;
							SC0->C0_QUANT,;
							{	SC0->C0_NUMLOTE,;
							SC0->C0_LOTECTL,;
							SC0->C0_LOCALIZ,;
							SC0->C0_NUMSERI})
						SC0->(MsUnLock())
						SC0->(dbSkip())
					EndDo
					
					DbSelectArea( "SL2" )
				EndIf
	
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Excluo o orcamento quando se tratar de uma venda com reserva³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				IF Empty( SL1->L1_DOCPED) .OR. lIntGC .OR. ( lECommerce  .OR. lECCia)
					Reclock( "SL2" ,.F.)
					SL2->(dbDelete())
					SL2->(MsUnlock())
				Else
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Nao excluo o orcamento quando se tratar de uma venda com reserva, apenas limpo os campos da reserva ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					Reclock( "SL2" ,.F.)
					Replace SL2->L2_FILRES		with	SPACE(TamSx3("L2_FILRES")[1])
					Replace SL2->L2_ORCRES		with	SPACE(TamSx3("L2_ORCRES")[1])
					SL2->(MsUnlock())
				EndIf
				SL2->(dbSkip())
			End
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Efetua a exclusão da SD2 e SF2³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	  	If Empty(SL1->L1_PEDRES) .And. !Empty(SL1->L1_DOC)
		  	Lj140CnSD2(SL1->L1_DOC,SL1->L1_SERIE,.F.)
			Lj140CnSF2(SL1->L1_DOC,SL1->L1_SERIE)
		EndIf		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Recupera os dados do tiulo a partir do orcamento original³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aDadosOri := Lj140OrcOrig()
		
		If Len(aDadosOri) > 0
			
			cCompFil := FWModeAccess("SE1", 3) //Verifica se a filial da SE1 e compartilhada. 
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Filial do titulo³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If Alltrim(cCompFil) <> "C" 
				cFilE1 := aDadosOri[1]
			EndIf
			
			/*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			  ³Prefixo do Titulo - quando o campo SL1->L1_SERPED for diferente do campo SE1->E1_PREFIXO,				|
			  ³devemos montar a chave de busca com o parametro MV_LJPREF. Quando o conteudo do parametro nao for padrao,|                     |
			  |ele devera ser macro executado assim como ocorre na funcao LOJXFUNC (responsavel pela gravacao).			|
			  ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			DbSelectArea("SE1")
			DbSetOrder(1) //E1_FILIAL+E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO		
			If SE1->( DbSeek(cFilE1 + aDadosOri[4] + aDadosOri[5]) ) 	//E1_FILIAL + E1_PREFIXO + E1_NUM
				cPrefE1 := aDadosOri[4]									//SL1->L1_SERPED
			ElseIf !Empty(aDadosOri[4]) .AND. !Empty(aDadosOri[5]) .AND. AllTrim(Upper(cMvLjPref)) <> "SF2->F2_SERIE"
				cPrefE1 := &(cMvLjPref) //como o conteudo do parametro nao e padrao, ele e macro executado.
			EndIf
	        
			aAdd( aClientes , {aDadosOri[2] , aDadosOri[3]} )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Busca se houve pagamentos do tipo que o cliente sao adms  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("SL4")
			DbSetOrder(1) //E1_FILIAL+E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO
			If DbSeek(cFilL4 + SL1->L1_NUM )
				While !Eof() .AND. cFilL4 == SL4->L4_FILIAL .AND. SL4->L4_NUM == SL1->L1_NUM
					If AllTrim(SL4->L4_FORMA) $ "CC|VA|CO|CD|FI"
						If AScan(aClientes,{|x| x[1] == Left(L4_ADMINIS,3)}) == 0
							aAdd( aClientes , {Left(L4_ADMINIS,3) , "01"} )
						EndIf	
					EndIf				
					DbSkip()
				End
			EndIf
			                                                             
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Apaga os cheques quando utilizada reserva
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !Empty(cChaveCHRe) 
				DbSelectArea("SE1")
				DbSetOrder(1) //E1_FILIAL+E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO
				If DbSeek(cFilE1 + cPrefE1 + aDadosOri[5])
					While !Eof() .AND. SE1->E1_FILIAL == cFilE1 .AND. SE1->E1_PREFIXO == cPrefE1 .AND. SE1->E1_NUM == aDadosOri[5]                 
						If AScan(aClientes,{|x| AllTrim(x[1])==AllTrim(SE1->E1_CLIENTE) .AND. AllTrim(x[2])==AllTrim(SE1->E1_LOJA)}) > 0
							Lj140CHDelReserva(cChaveCHRe)				
						EndIf	
						SE1->(DbSkip())
					End
				EndIf
			EndIf
			     
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Apaga os titulos do SE1 relacionados ao orcamento excluido³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("SE1")
			DbSetOrder(1) //E1_FILIAL+E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO
			If DbSeek(cFilE1 + cPrefE1 + aDadosOri[5])
		
				While !Eof() .AND. SE1->E1_FILIAL == cFilE1 .AND.;
					SE1->E1_PREFIXO	== cPrefE1 .AND.;
					SE1->E1_NUM		== aDadosOri[5]
	                 
					If AScan( aClientes,{|x| AllTrim(x[1]) == AllTrim(SE1->E1_CLIENTE) .AND. AllTrim(x[2]) == AllTrim(SE1->E1_LOJA)} ) > 0
						Reclock("SE1",.F.)
						SE1->( dbDelete() )
						SE1->( MsUnlock() )
					EndIf	
					SE1->(DbSkip())
				End
			EndIf
			
			cCompFil := FWModeAccess("SE5", 3) //Verifica se a filial da SE1 e compartilhada. 
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Filial da Movimentacao³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If Alltrim(cCompFil) <> "C" 
				cFilE5 := aDadosOri[1]
			EndIf
	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Apaga a movimentacao bancaria relacionados ao orcamento excluido³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("SE5")
			DbSetOrder(7) //E5_FILIAL, E5_PREFIXO, E5_NUMERO, E5_PARCELA, E5_TIPO, E5_CLIFOR, E5_LOJA, E5_SEQ
			If DbSeek(cFilE5 + cPrefE1 + aDadosOri[5])
		
				While !Eof() .AND. SE5->E5_FILIAL == cFilE5 .AND.;
					SE5->E5_PREFIXO	== cPrefE1 .AND.;
					SE5->E5_NUMERO	== aDadosOri[5]
					
					// Verifica cliente e loja da movimentacao
					If SE5->E5_CLIFOR + SE5->E5_LOJA <> aDadosOri[2] + aDadosOri[3]
						SE5->(DbSkip())
						Loop
					EndIf
					
					Reclock("SE5",.F.)
					SE5->( dbDelete() )
					SE5->( MsUnlock() )
					SE5->(DbSkip())
					
				End
			EndIf
	
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//|     Se usar os modulos de gestao de concessionarias chama a funcao que grava os relacionamentos.        |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If SuperGetMv("MV_VEICULO") == "S"
			If !Empty(SL1->L1_VEICTIP) .AND. !Empty(SL1->L1_VEIPESQ)
				FG_DEVLOJA( SL1->L1_VEICTIP, SL1->L1_VEIPESQ, "", "", .T. )
			EndIf
		EndIf
		
		If lCrdxInt .AND. SL1->(ColumnPos("L1_CONTRA")) > 0
			If !Empty(SL1->L1_CONTRA)
				aRetCrd      := aClone(CrdxVenda( "3"  ,{"",""}  ,SL1->L1_CONTRA  ,.F.  ,;
				NIL  ,NIL ))
			EndIf
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Nao excluo o orcamento quando se trata de uma venda com reserva                           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IF Empty( SL1->L1_DOCPED) .OR. lIntGC
			DbSelectArea( "SL4" )
			If DbSeek( cFilL4+SL1->L1_NUM )
				While !Eof() .AND. cFilL4==L4_FILIAL .AND. L4_NUM == SL1->L1_NUM
					Reclock("SL4",.F.)
					SL4->( dbDelete() )
					SL4->( MsUnlock() )
					SL4->( dbSkip() )
				End
			EndIf
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Nao excluo o orcamento quando se trata de uma venda com reserva                           ³
		//³ Apenas retorno seus campos ao conteudo original para permitir a reutilizacao do orcamento ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea( "SL1" )
		IF (Empty( SL1->L1_DOCPED) .OR. lIntGC ) .OR. lECCia
			Reclock( "SL1" ,.F.)
			Replace SL1->L1_NUMORC With Space(TamSx3("L1_NUMORC")[1])
			Replace SL1->L1_RESERVA With Space(TamSx3("L1_RESERVA")[1])
			SL1->( dbDelete() )
			SL1->( MsUnlock() )
			If lExcAuto
				//"Orcamento excluido com sucesso."
				Conout(STR0117)
			EndIf
		ElseIf FunName() <> "STBORCCANCEL"
			Reclock( "SL1" ,.F.)
			Replace SL1->L1_PDV		with SPACE(TamSx3("L1_PDV")[1])
			Replace SL1->L1_EMISNF	with CTOD("")
			Replace SL1->L1_TIPO	with SPACE(TamSx3("L1_TIPO")[1])
			Replace SL1->L1_OPERADO	with SPACE(TamSx3("L1_OPERADO")[1])
			Replace SL1->L1_DOCPED	with SPACE(TamSx3("L1_DOCPED")[1])
			Replace SL1->L1_SERPED	with SPACE(TamSx3("L1_SERPED")[1])
			MsUnlock()
		EndIf

	EndIf
End Transaction 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inclusao de chamada de Ponto de Entrada - Template.                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If ExistTemplate("LOJA140B")
	ExecTemplate("LOJA140B",.F.,.F.)
EndIf
       
If nModulo == 72
	KEXF680()
EndIf  

If ExistBlock("LOJA140B")
	ExecBlock("LOJA140B",.F.,.F.)
EndIf

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³lj140chkresºAutor ³ Vendas Clientes    º Data ³  09/04/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³No cancelamento da venda verifica se ha reservas em aberto  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP6                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function lj140chkres(lVendido, cChaveCHRe)
Local nX                                                                               	// Aux do For
Local aReservas 	:= {}                                                              	// Array com dados da Reserva 
Local nPosProduto 	:= aScan(aHeader,{|x| Alltrim(Upper(x[2]))=="L2_PRODUTO"})			// Posicao de cabecalho
Local nPosDescPro 	:= aScan(aHeader,{|x| Alltrim(Upper(x[2]))=="L2_DESCRI"})			// Posicao de cabecalho
Local nPosLocal  	:= aScan(aHeader,{|x| Alltrim(Upper(x[2]))=="L2_LOCAL"})			// Posicao de cabecalho
Local nPosReserva  	:= aScan(aHeader,{|x| Alltrim(Upper(x[2]))=="L2_RESERVA"})			// Posicao de cabecalho		
Local nPosLojaRes  	:= aScan(aHeader,{|x| Alltrim(Upper(x[2]))=="L2_LOJARES"})			// Posicao de cabecalho
Local nPosQtdeDev  	:= aScan(aHeader,{|x| Alltrim(Upper(x[2]))=="L2_QUANT"})			// Posicao de cabecalho
Local cLojaLocal	:= ""																// Loja Local
Local cLojaReserva	:= ""																// Loja Reserva
Local lRet	 		:= .T.																// Retorno da funcao
Local cTexto 		:= If(lVendido,STR0032,STR0033) 									// "do orçamento"###"da venda"
Local nRegs         := Len(aCols)														// Qunantidade de registros
Local cOrcPai       := ""                                                        		// Orcamento Pai
Local nCntRes       := 0																// Contador de reserva
Local lLjPrdRes		:= ExistBlock("LJPRDRES")											// Se existe ponto de entrada
Local lLjPrdResT	:= ExistTemplate("LJPRDRES")										// Template
Local aLjPrdRes		:= {}  																// Retorno do P.E. LJPRDRES
Local nLinRes		:= 0																// Linha do array aReserva
Local nAtualizRes	:= 0																// Atualiza a reserva
Local aAuxRes		:= {}																// Array auxiliar para reserva
Local lPedVen		:= SL1->(ColumnPos("L1_PEDRES")) > 0								// Verifica se o campo L1_PEDRES existe

Default cChaveCHRe := ''
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada para tratamento do código e descricao do produto             ³
//³ Retorno a posicao da aCols que contem o codigo e a descricao do produto       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lLjPrdResT
	aLjPrdRes := ExecTemplate("LJPRDRES",.F.,.F.,{nPosProduto,nPosDescPro} )
	If ValType(aLjPrdRes) == "A" .AND. Len(aLjPrdRes) >= 2
		nPosProduto	:= aLjPrdRes[1]
		nPosDescPro	:= aLjPrdRes[2]
	EndIf
EndIf

If lLjPrdRes
	aLjPrdRes := ExecBlock("LJPRDRES",.F.,.F.,{nPosProduto,nPosDescPro} )
	If ValType(aLjPrdRes) == "A" .AND. Len(aLjPrdRes) >= 2
		nPosProduto	:= aLjPrdRes[1]
		nPosDescPro	:= aLjPrdRes[2]
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Pega o codigo da loja local               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SLJ")
SLJ->( DbSetOrder(3) )
If DbSeek(xFilial("SLJ")+SM0->M0_CODIGO+FWGETCODFILIAL)
	cLojaLocal := SLJ->LJ_CODIGO
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se o foi feito Reserva para este orcamento, e se o mesmo veio pela venda assistida ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If !Empty( SL1->L1_DOCPED )
	
	cOrcPai := SL1->L1_NUM
	SL1->( DbSetOrder( 1 ) )
	
	For nX := 1 to nRegs
		If !Empty( GDFieldGet("L2_ORCRES",nX) )
			nCntRes := aScan(aReservas,{|ExpA1| ExpA1[1]+ExpA1[2] == GDFieldGet("L2_FILRES",nX) + GDFieldGet("L2_ORCRES",nX)})
			If nCntRes == 0
				AAdd( aReservas, {GDFieldGet("L2_FILRES",nX), GDFieldGet("L2_ORCRES",nX)} )
			EndIf
		EndIf
	Next nX
	
	If lRet
		For nX := 1 to Len( aReservas )
			If SL1->( DbSeek( aReservas[nX][1]+aReservas[nX][2] ))
				If lPedVen .AND. !Empty(SL1->L1_PEDRES) .AND. !LjCancPed(aReservas[nX][1], SL1->L1_PEDRES)
					lRet := .F.
					Exit
				EndIf
				lj140ExcOrc( aReservas[nX][1], cChaveCHRe)
			EndIf
		Next nX
	EndIf
	
	SL1->(DbSeek( xFilial( "SL1" ) + cOrcPai ))
Else
	For nX := 1 to nRegs
		If nPosReserva > 0 .AND. nPosLojaRes > 0 .AND. nPosProduto > 0 .AND. nPosLocal > 0 .AND. nPosQtdeDev > 0
			If !Empty(aCols[nX][nPosReserva])
				If Empty(aCols[nX][nPosLojaRes] )
					cLojaReserva := cLojaLocal
				Else
					cLojaReserva := aCols[nX][nPosLojaRes]
				EndIf
				aAdd(aReservas, {aCols[nX][nPosReserva], cLojaReserva, aCols[nX][nPosProduto], aCols[nX][nPosLocal]} )
			EndIf 
		EndIf
	Next nX
	
	If !Empty(aReservas)
		If !(LjCancRes(aReservas))
			lRet := MsgYesNo(STR0034 + cTexto + "?") //"Não foi possível cancelar a reserva. Deseja continuar a exclusão "
		EndIf
	EndIf
EndIf
        
Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ lj140Anul³ Autor ³ Vendas Clientes       ³ Data ³ 03.12.02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de anulacao de notas fiscais                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lj140Anul(ExpC1,ExpN1,ExpN2)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function lj140Anul(cAlias,nReg,nOpcx)

Lj140Exc(cAlias, nReg, nOpcx)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ Lj140Leg  ³ Autor ³ Vendas Clientes       ³ Data ³ 05.03.04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao de legenda da tela								   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Lj140Leg()		                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj140Leg()

Local aLegenda	:= {}
Local lEmitNFCe	:= ExistFunc("LjEmitNFCe") .AND. LjEmitNFCe()	//Indica se esta no modo NFC-e
 
Aadd( aLegenda, {"BR_VERDE"		, STR0039} ) //"Orçamentos em aberto"
Aadd( aLegenda, {"BR_VERMELHO"	, STR0040} ) //"Vendas efetuadas"
Aadd( aLegenda, {"BR_AMARELO"	, STR0041} ) //"Orcamentos com reservas"
Aadd( aLegenda, {"BR_AZUL"		, STR0042} ) //"Pedidos encerrados"
Aadd( aLegenda, {"BR_PRETO"		, STR0043} ) //"Orcamentos em aberto vencidos"
Aadd( aLegenda, {"BR_MARROM"	, STR0044} ) //"Devoluções pendentes"
Aadd( aLegenda, {"BR_LARANJA"	, STR0082} ) //"Orçamentos com Pedidos de Venda"
Aadd( aLegenda, {"BR_CINZA"		, STR0084} ) //"Orçamentos Cancelados"
Aadd( aLegenda, {"BPMSEDT1"		, STR0096} ) //"Vendas estornadas"

If lEmitNFCe
	aAdd( aLegenda,	{"BPMSEDT2"		, STR0140} )	//"Cancelamento com processamento pendente"
	aAdd( aLegenda,	{"BR_PRETO_0"	, STR0141} )	//"Cancelamento não enviado ao TSS"
	aAdd( aLegenda,	{"BR_PRETO_1"	, STR0142} )	//"Cancelamento aguardando autorização do SEFAZ"
	aAdd( aLegenda,	{"BR_PRETO_2"	, STR0143} )	//"Cancelamento autorizado pela SEFAZ"
	aAdd( aLegenda,	{"BR_PRETO_3"	, STR0144} )	//"Cancelamento não autorizado pela SEFAZ"
EndIf

BrwLegenda(STR0045, STR0046, aLegenda) //"Orcamentos"###"Legenda"

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ lj140ICOrc³ Autor ³ Vendas Clientes       ³ Data ³ 05.03.04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de impressao e cancelamento de orçamentos		   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lj140ICOrc()		                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj140ICOrc()
Local cMensagem 	:= ""
Local lPreVenda		:= SuperGetMv("MV_LJPRVEN",,.T.) 	// Define se o cliente trata o orcamento como pre-venda
Local lImprime		:= .T.                         		// Controle para verificar se a impressao foi bem sucedida 
Local lImpOK 		:= .T.								// Status da Impressao	
Local lPafEcf		:= LjNfPafEcf(SM0->M0_CGC)


//³ Mensagem promocional, exigencia do PAF-ECF apresentar o numero da Pré Venda cancelada ³
If lPafEcf .AND. lPreVenda .AND. SL1->(ColumnPos("L1_NUMORC")) > 0
	cMensagem := STR0092 + SL1->L1_NUMORC + Chr(10)+ Chr(13) 		//"PV:"
	cMensagem += STR0050 + Chr(10)+ Chr(13) + ; 					//"Esse cupom sera cancelado de forma "
				 STR0122 + Chr(10)+ Chr(13)  						//"automatica. Requisito V do Ato Cotepe/ICMS 0608(PAF-ECF)"	
Else 
	cMensagem := STR0011 + SL1->L1_NUM + Chr(10)+ Chr(13)  			//"Or‡amento:"
	cMensagem +=	STR0050 + Chr(10)+ Chr(13) + ; 					//"Esse cupom sera cancelado de forma "
					STR0051 + Chr(10)+ Chr(13) + ; 					//"automatica. Conforme lei estadual  "
					STR0052											//"vigente que regulamenta pre-venda. "		
EndIf


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Imprime o cupom fiscal³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lImpOK := Lj140ImpOrc(cMensagem)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza SL1  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RecLock( "SL1" ,.F.)
SL1->L1_STORC	:= "C"
If lPafEcf .And. lPreVenda .And. lImpOK //Atualiza a data da impressão para que apareça no registro R04 do PAF
	SL1->L1_EMISSAO := dDatabase
EndIf
SL1->(MsUnLock())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cancela o cupom conforme previsto no Ato Cotepe 0608(PAF-ECF) ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lImprime := .T.
While lImprime .AND. lImpOK
	If !Lj7CaUlCup(.F.) 
		If Aviso(STR0006,STR0058 + CHR(10) + Chr(13) +; //"Atencao ## "Falha no cancelamento do cupom."
				 STR0087, {STR0088,STR0089}) = 2 	 	//"Executa a impressão novamente? # Sim # Não "
			lImprime 	:= .F.
			lImpOK 	:= .F.
		EndIf
	Else
		lImprime := .F.
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//|     Se usar os modulos de gestao de concessionarias chama a funcao que grava os relacionamentos.        |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If SuperGetMv("MV_VEICULO") == "S"
			If !Empty(SL1->L1_VEICTIP) .AND. !Empty(SL1->L1_VEIPESQ)
				FG_DEVLOJA(SL1->L1_VEICTIP,SL1->L1_VEIPESQ,"","",.t.)
			EndIf
		EndIf
   	EndIf
End

Return(lImpOK)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140GeraNC³ Autor ³ Vendas Clientes       ³ Data ³ 03.05.04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Gerar Nota de Credito para o cancelamento - Loc. Guatemala  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Lj140GeraNCC(ExpA1,ExpA2,ExpN1)                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 = array com os dados da NF cancelada                  ³±±
±±³Parametros³ ExpA2 = array com os dados dos itens da NF cancelada        ³±±
±±³Parametros³ ExpN1 = iteracao                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Lj140GeraNCC(aItensSF2,	aItensSD2,	nI, cNumNFDev, ;
							 cSerieDev, cMotivo)

Local cEstado  := ""							//Variavel que recebe o Estado  	
Local cItemOri := ""							//Variavel que recebe item de origem 
Local cItem    := ""							//Variavel que recebe Item 
Local cCampo   := ""							//Variavel que recebe o campo para validação
Local cTESNCC  := ""							//Variavel que recebe TES da NCC
Local nX, nY									//Variavel contador
Local nItem    := 0								//Variavel numerica que recebe item 
Local nTamItem := TamSX3("D1_ITEM")[1]			//Variavel que recebe tamanho do compo D1_ITEM
Local nTotItens:= 0								//Variavel que recebe total de itens
Local nPosCols := 1								//Variavel que recebe posição da coluna
Local nPosRow  := 0								//Variavel que recebe posição da linha
Local nPosCpo  := 0								//Variavel que recebe posição do campo 
Local nValImps := 0								//Variavel que recebe valor dos impostos 
Local aTesImpInf  := {}							//Array contendo informações da TES
Local aImps    := {}							//Array contendo informações dos impostos
Local aCustoDev:= {}							//Array contendo informações do Custo
Local aGetBook := {}							//Array que recebe informaçõrs sobre o registro do Livro Fiscal 
Local aLivro   := {}							//Array que recebe informações sobre o livro fiscal
Local aImpVarSF1 := {}							//Array contendo informações para gerar os impostos variaveis para gravar no cabecalho da NCC

Private aImpVarSD1 := {0,0,0,0,0,{}}

SA1->(DbSetOrder(1))
If SA1->(DbSeek(xFilial("SA1")+aItensSF2[nI][3]+aItensSF2[nI][4]))
	cEstado  := SA1->A1_EST
EndIf

RecLock("SF1",.T.)
Replace SF1->F1_FILIAL		with xFilial("SF1")
Replace SF1->F1_DOC   		with cNumNFDev
Replace SF1->F1_SERIE		with cSerieDev
Replace SF1->F1_FORNECE 	with aItensSF2[nI][3]
Replace SF1->F1_LOJA	 	with aItensSF2[nI][4]
Replace SF1->F1_EMISSAO 	with dDataBase
Replace SF1->F1_DTDIGIT 	with dDataBase
Replace SF1->F1_TIPO	 	with "D"
Replace SF1->F1_ESPECIE 	with "NCC"
Replace SF1->F1_EST 	 	with cEstado
Replace SF1->F1_FORMUL  	with "S"
Replace SF1->F1_MOEDA  		with aItensSF2[nI][7]
Replace SF1->F1_TXMOEDA 	with aItensSF2[nI][8]
Replace SF1->F1_TIPODOC 	with "04"
Replace SF1->F1_VALMERC 	with aItensSF2[nI][6]
Replace SF1->F1_VALBRUT 	with aItensSF2[nI][5]

If cPaisLoc == "GUA"
	Replace SF1->F1_MOTIVO 	with cMotivo
	If ColumnPos("F1_MANUAL") > 0          
		If lNFManual
    		REPLACE SF1->F1_MANUAL WITH "S"
	    Else                               
	    	REPLACE SF1->F1_MANUAL WITH "N"
		EndIf
	EndIf
EndIf

For nX := 1 to Len(aItensSD2)
	nTotItens  += aItensSD2[nX][7]
Next nX

nItem  := 1
While aItensSD2[nItem][15] == aItensSF2[nI][1] .AND. ;
	aItensSD2[nItem][16] == aItensSF2[nI][2]
	
	cItem  :=  StrZero(nItem,nTamItem)
	
	SB1->(DbSetOrder(1))
	SB1->(DbSeek(xFilial("SB1")+aItensSD2[nItem][2]))
	
	SF4->(DbSetOrder(1))
	SF4->(DbSeek(xFilial("SF4")+aItensSD2[nItem][9]))
	cTESNCC  := IIf(!Empty(SF4->F4_TESNCC),SF4->F4_TESNCC,SuperGetMV("MV_TESTROC"))
	SF4->(DbSeek(xFilial("SF4")+cTESNCC))
	
	aCustoDev := {aItensSD2[nItem][17]/aItensSD2[nItem][5],;
	aItensSD2[nItem][18]/aItensSD2[nItem][5],;
	aItensSD2[nItem][19]/aItensSD2[nItem][5],;
	aItensSD2[nItem][20]/aItensSD2[nItem][5],;
	aItensSD2[nItem][21]/aItensSD2[nItem][5]}
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Calcular os impostos variaveis                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aImpVarSD1 := {0,0,0,0,0,{}}
	aImpVarSD1[1] := aItensSD2[nItem][5]
	aImpVarSD1[2] := aItensSD2[nItem][6]
	aImpVarSD1[3] := aItensSD2[nItem][7]
	nPosRow  := nItem
	CalcTesxIp("E",nTotItens,aItensSD2[nItem][14],aItensSD2[nItem][2],nPosCols,nPosRow,"RAPIDA")
	
	Reclock("SD1",.T.)
	Replace SD1->D1_FILIAL  	with xFilial("SD1")
	Replace SD1->D1_COD 	 	with aItensSD2[nItem][2]
	Replace SD1->D1_UM			with aItensSD2[nItem][3]
	Replace SD1->D1_SEGUM		with aItensSD2[nItem][4]
	Replace SD1->D1_GRUPO	    with aItensSD2[nItem][13]
	Replace SD1->D1_TIPO	 	with "D"
	Replace SD1->D1_ORIGLAN		with "LO"
	Replace SD1->D1_TP			with SB1->B1_TIPO
	Replace SD1->D1_NUMSEQ  	with ProxNum()
	Replace SD1->D1_QUANT	    with aItensSD2[nItem][5]
	Replace SD1->D1_VUNIT	    with aItensSD2[nItem][6]
	Replace SD1->D1_TOTAL	    with aItensSD2[nItem][7]
	Replace SD1->D1_TES 	 	with cTESNCC
	Replace SD1->D1_FORNECE 	with aItensSD2[nItem][11]
	Replace SD1->D1_LOJA	 	with aItensSD2[nItem][12]
	Replace SD1->D1_DOC 	 	with cNumNFDev
	Replace SD1->D1_SERIE	    with cSerieDev
	Replace SD1->D1_EMISSAO 	with dDataBase
	Replace SD1->D1_DTDIGIT 	with dDataBase
	Replace SD1->D1_NFORI	    with aItensSD2[nItem][15]
	Replace SD1->D1_SERIORI 	with aItensSD2[nItem][16]
	Replace SD1->D1_ITEMORI   	with aItensSD2[nItem][1]
	Replace SD1->D1_NUMCQ   	with xNumCaixa()
	Replace SD1->D1_LOCAL	    with aItensSD2[nItem][8]
	Replace SD1->D1_ITEM	 	with cItem
	Replace SD1->D1_CF			with aItensSD2[nItem][10]
	Replace SD1->D1_ESPECIE   	with "NCC"
	Replace SD1->D1_FORMUL  	with "S"
	Replace SD1->D1_TIPODOC 	with "04"
	Replace SD1->D1_CUSTO     	with aCustoDev[1]
	Replace SD1->D1_CUSTO2    	with aCustoDev[2]
	Replace SD1->D1_CUSTO3    	with aCustoDev[3]
	Replace SD1->D1_CUSTO4    	with aCustoDev[4]
	Replace SD1->D1_CUSTO5    	with aCustoDev[5]
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Gravar os impostos variaveis no detalhe da Nota de Credito  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nX := 1 To Len(aImpVarSD1[6])
		If Substr(aImpVarSD1[6,nX,7],3,7) == "_BASIMP"
			cCampo   := "D1"+Substr(aImpVarSD1[6,nX,7],3,8)
			nPosCpo  := ColumnPos(cCampo)
			FieldPut(nPosCpo,aImpVarSD1[6,nX,3])
		EndIf
		
		If Substr(aImpVarSD1[6,nX,6],3,7) == "_VALIMP"
			cCampo   := "D1"+Substr(aImpVarSD1[6,nX,6],3,8)
			nPosCpo  := ColumnPos(cCampo)
			FieldPut(nPosCpo,aImpVarSD1[6,nX,4])
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|    Soma os impostos discriminados                                                                       |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If Substr(aImpVarSD1[6,nX,5],2,1) == "1"
				nValImps  += aImpVarSD1[6,nX,4]
			EndIf
		EndIf
	Next nX
	MsUnlock()
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Gerar os impostos variaveis para gravar no cabecalho da NCC ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nX := 1 To Len( aImpVarSD1[6] )
		If (nY := aScan( aImpVarSF1,{|x| x[1] == aImpVarSD1[6,nX,8] } )) == 0
			AAdd( aImpVarSF1, { aImpVarSD1[6,nX,8], 0.00, aImpVarSD1[6,nX,2], aImpVarSD1[6,nX,1] } )
			nY := Len( aImpVarSF1 )
		EndIf
		aImpVarSF1[ nY,2 ] += aImpVarSD1[6,nX,4]
		
		If (nY := aScan( aImpVarSF1,{|x| x[1] == aImpVarSD1[6,nX,9] } )) == 0
			AAdd( aImpVarSF1, { aImpVarSD1[6,nX,9], 0.00, aImpVarSD1[6,nX,2], aImpVarSD1[6,nX,1] } )
			nY := Len( aImpVarSF1 )
		EndIf
		aImpVarSF1[ nY,2 ] += aImpVarSD1[6,nX,3]
	Next nX
	
	SA1->(DbSetOrder(1))
	SA1->(DbSeek(xFilial("SA1")+SF1->F1_FORNECE+SF1->F1_LOJA))
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Gerar o registro do Livro Fiscal                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aLivro := GetBook(@aGetBook, aImpVarSD1, "V", aItensSF2[nI][8], aLivro, "E" )
	
	nItem++
	If nItem > Len(aItensSD2)
		Exit
	EndIf
End

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gravar os impostos variaveis no cabecalho da Nota de Credito³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nX := 1 To Len(aImpVarSF1)
	aImpVarSF1[nX,1] := "F1"+Substr(aImpVarSF1[nX,1],3,8)
Next nX
RecLock("SF1",.F.)
For nX := 1 To Len(aImpVarSF1)
	If (aImpVarSF1[nX,2] > 0)
		nPosCpo := FieldGet(ColumnPos(aImpVarSF1[nX,1]))+aImpVarSF1[nX,2]
		SF1->( FieldPut( ColumnPos( aImpVarSF1[nX,1] ),nPosCpo ) )
	EndIf
Next nX
SF1->F1_VALBRUT  := nTotItens + nValImps
MsUnLock()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gravar Registro nos Livros Fiscais...			           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nX := 2 To Len( aLivro )
	RecLock( "SF3",.T. )
	For nY := 1 To Len( aLivro[1] )
		SF3->( FieldPut(ColumnPos(aLivro[1,nY]),aLivro[nX,nY]) )
	Next nY
	SF3->F3_FILIAL 		:= xFilial("SF3")
	SF3->F3_FORMUL 		:= "S"
	SF3->F3_ESPECIE		:= "NCC"
	SF3->F3_TIPO   		:= "D"
	SF3->F3_CLIEFOR		:= aItensSF2[nI][3]
	SF3->F3_LOJA		:= aItensSF2[nI][4]
	MsUnLock()
Next nX

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140Manual³ Autor ³ Vendas Clientes       ³ Data ³ 22.06.04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Pede confirmacao para NCC Manual                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Lj140Manual()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj140Manual()

Local lRet   := .T.								//Retorno da funcao
Local nOpcao := 1								//Opcao escolhida

If cPaisLoc == "GUA" .AND. lNFManual
	nOpcao := Aviso(STR0006,STR0073,{STR0074,STR0075}) //"Aten‡„o"###"Sera gravada uma Nota de Credito MANUAL. Confirma a operacao?"###"Confirma"###"Cancela"
	If nOpcao == 2
		lRet  := .F.
	EndIf
EndIf

Return lRet


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140Valida³ Autor ³ Vendas Clientes       ³ Data ³ 21.11.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Verifica se a venda pode ser cancelada                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function Lj140Valida(lJob		, lVendTef	, cChave	, aCheques,; 
							cSerNfCup	, cNumNfCup	, nTamDoc	, cNumNota,;
							lAchouSF2	, lLstPre)

Local lBaixaAut    	:= .F.							// Baixa automatica
Local lMvFisNota   	:= SuperGetMv("MV_FISNOTA")		// Conteudo parametro MV_FISNOTA
Local nTamF2_DOC   	:= TamSx3("F2_DOC")[1]          // Tamanho Nota Fiscal
Local lUsaFD       	:= SuperGetMV("MV_LJUSAFD",,.F.) 		// Conteudo parametro MV_LJUSAFD
Local lValeCompra  	:= .F. 									//Variavel qu controle se o Titulo e Vale compra
Local nHoras 		:= 0                            		// Quantidade de horas da hora atual
Local dDtDigit 		:= dDataBase							// Data da emissao da nota
Local nSpedExc 		:= SuperGetMV("MV_SPEDEXC",,72)			// Indica a quantidade de horas q a NFe pode ser cancelada

Default lLstPre		:= .F.

cNumNota := PadR(cNumNota,nTamF2_DOC)                               

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Nos casos de JOB e EXECAUTO, a variavel publica cEstacao possui um valor default, sendo assim,	|
//| nao podemos usa-la como chave(LG_COD) para atribuir um valor as seguintes variaveis de sistema: |
//| - lFiscal, pois dependendo do campo LG_IMPFISC                                                  |
//|	- lUsaTef, pois depende do valor do campo LG_TIPTEF												|
//|Portanto essas validacoes devem ser feitas antes da chamada dessa funcao							|
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !isBlind() .ANd. !lIsVendaVP .And. !lIsVdRecCP .And. !lLstPre //verifica se ha interface com o usuario e nao eh venda de "Vale Presente" e nem "Recarga de Cartao Presente (Gift Card)"
	If lFiscal
		If Empty( SL1->L1_PDV ) .AND. !lMvFisNota //eh usuario fiscal
			If !IsBlind()
				MsgInfo( STR0035 ) //'Usuário não altorizado a excluir NF'
				Return( .F. )
			Else 
				Conout(STR0035)
				Return( .F. )
			EndIf	
		EndIf
	Else
		If !Empty( SL1->L1_PDV )
			If !lExcAuto
				MsgInfo( STR0036 ) //'Usuário não altorizado a excluir Cupon Fiscal'
				Return( .F. )
			Else 
				Conout( STR0036 )
				Return ( .F. )
			EndIf	
		EndIf
	EndIf
EndIf

If !isBlind() .AND. lUsatef .AND. SL1->L1_VENDTEF == "S" .AND. (SL1->L1_CARTAO > 0 .OR. SL1->L1_VLRDEBI > 0)
	lVendTef := .T.
ElseIf !isBlind() .AND. !lUsatef .AND. SL1->L1_VENDTEF == "S" .AND. (SL1->L1_CARTAO > 0 .OR. SL1->L1_VLRDEBI > 0)
	lVendTef := .F.
	If !lExcAuto
		Help(" ",1,"NOUSERTEF")
		Return .F.
	Else
		//"O usuario atual nao e habilitado a utilizar as rotinas do TEF (Transferencia  Eletronica de Fundos), nao podera excluir esta venda, pois foi realizada via TEF." 
		Conout(STR0107)
	EndIf		
EndIf

If !Lj140CHCarrega(cChave, @aCheques, @lValeCompra, @lBaixaAut, @lUsaFD)
	Return .F.
EndIf

DbSelectArea( "SF2" )
SF2->( DbSetOrder(1) )
lAchouSF2 := SF2->( DbSeek(xFilial("SF2") + cNumNota + SL1->L1_SERIE) )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Verifica se existe NF para o cupom. Caso exista armazena o numero e a serie para futura exclusao...     |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( (lJob .OR. lExcAuto .OR. lFiscal) .AND. (SF2->F2_ECF == "S") ) .AND. !Empty(SF2->F2_NFCUPOM) .AND. lAchouSF2
	cSerNfCup := SubStr(SF2->F2_NFCUPOM,1,TamSx3("F2_SERIE")[1])
	cNumNfCup := SubStr(SF2->F2_NFCUPOM,4,nTamDoc)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se cheque ja' foi depositado                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posicao de Aceitacao de Numero de Cheque³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Substr(SL1->L1_CONFVEN,1,1)  <> "N" 
	If !Lj140CHDepositado(@aCheques)
		Return .F.
	EndIf
EndIf

If lAchouSF2
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se eh uma notafiscal eletronica , pois neste caso deve respeitar o 	³
	//³ parametro MV_SPEDEXC que indica o numero de horas que a Nfe pode ser excluidas 	³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dDtdigit := IIf(SF2->(ColumnPos('F2_DTDIGIT'))>0 .And. !Empty(SF2->F2_DTDIGIT),SF2->F2_DTDIGIT,SF2->F2_EMISSAO)
	If "SPED"$SF2->F2_ESPECIE .AND. (SF2->F2_FIMP$"TS") //verificacao apenas da especie como SPED e notas que foram transmitidas ou impressoo DANFE
		nHoras := SubtHoras( dDtdigit, SF2->F2_HORA, dDataBase, SubStr(Time(),1,2)+":"+SubStr(Time(),4,2) )
		If nHoras > nSpedExc
			If !lExcAuto
				MsgAlert(STR0094+ Alltrim(STR(nSpedExc))+STR0095)//"Não foi possivel excluir a nota, pois o prazo para o cancelamento da NF-e é de "#" horas." 
				Return(.F.)
			Else 
			    Conout(STR0094+ Alltrim(STR(nSpedExc))+STR0095)
				Return( .F. )
			EndIf	
	   	EndIf
	EndIf
EndIf

Return .T.
          
/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o    ³Lj140CancCupom³ Autor ³ Vendas Clientes       ³ Data ³ 21.11.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Cancela o Cupom fiscal                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                         ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Lj140CancCupom(lAchouSF2,cSupervisor)
Local nRet			:= 0			// Retorno do cupom
Local cAntNumCup	:= ""
Local lCancela		:= .T.

DEFAULT cSupervisor := ""
  
If lFiscal .AND. lAchouSF2
    
	//Quando efetuo um cancelamento de uma nota também passa por aqui
	//portanto verifico se a venda se a refere a uma venda de cupom
	IF !Empty(AllTrim(SL1->L1_NUMCFIS))
		nRet := IFPegCupom( nHdlECF,@cAntNumCup )
		
		If AllTrim(cAntNumCup) <> AllTrim(SL1->L1_NUMCFIS) .AND. ExistFunc("IFEstornVinc")
			nRet := IFEstornVinc(	nHdlECF,If(SL1->(ColumnPos("L1_CGCCLI")) > 0 ,SL1->L1_CGCCLI, ""),"","",;
									STR0130 ,cAntNumCup) //"Cancelamento de Comprovante de Crédito e Débito"
	
			If nRet <> 0
				MsgAlert(STR0131)
				//"Não foi possível efetuar o cancelamento do cupom vinculado. Verifique se o último
				//impresso foi um relatório gerencial caso seja não é permitido o cancelamento "
	
				FR271BGeraSL("SL1", {{"L1_SITUA",	"00"}}) // Retira a 'solicitação de cancelamento
	
				// "O Cupom fiscal nº "+cNumCup+", não pode ser cancelado."
				MsgStop(STR0132 + SL1->L1_NUMCFIS+ STR0133)//"O Cupom fiscal nº "  ", não pode ser cancelado."

				lCancela := .F.
			EndIf
		EndIf
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Cancelamento do £ltimo Cupom Fiscal						 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lCancela .And. SF2->F2_ECF == "S" .AND. IFCancCup(nhdlECF,cSupervisor) <> 0
		
		/*  "N„o foi possível efetuar a exclus„o"
		    "Existem problemas com a impressora fiscal" */
		If !lExcAuto
			MsgInfo( ( STR0029 ) + Chr(10) + Chr(13) + STR0030 )
		Else
			Conout( STR0029 )
			Conout( STR0030 )	
		EndIf
		
		lCancela := .F.	
	EndIf
EndIf             
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//|     Se usar os modulos de gestao de concessionarias chama a funcao que grava os relacionamentos.        |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lCancela .And. SuperGetMv("MV_VEICULO") == "S"
	If !Empty(SL1->L1_VEICTIP) .AND. !Empty(SL1->L1_VEIPESQ)
		FG_DEVLOJA(SL1->L1_VEICTIP,SL1->L1_VEIPESQ,SL1->L1_DOC,SL1->L1_SERIE,.T.)
		If IsBlind()
			// Segunda chamada da função do DMS para excluir o orçamento no DMS , isso pq no PAF nao permite excluir da base o orçamento
			// Essa funcao eh chamada novamente para excluir do DMS
			FG_DEVLOJA(SL1->L1_VEICTIP,SL1->L1_VEIPESQ,"","",.T.)
		EndIf	
	EndIf
EndIf

Return lCancela

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o    ³Lj140CancDB   ³ Autor ³ Vendas Clientes       ³ Data ³ 21.11.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Cancela o Cupom na base de dados                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                         ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Lj140CancDB(aCheques	,lVendTef	,cChave		,cSerNfCup,;
							cNumNfCup	,nTamDoc	,cNumNota	,lAchouSF2,;
							lJob		,cDescMot	,lGeraNCC	,cNumNFDev,;
							cSerieDev	,cMotivo	,cNum		)

Local lTefOk       	:= .T.							// Verificacao TEF
Local cSerNota     	:= SL1->L1_SERIE				// Serie da Venda
Local lExisteFT	   	:= AliasInDic("SFT")			// Se existe o SFT

Local lNfCup       	:= .F.                          // Controla se eh NF sobre cupom          
Local lCrdxInt     	:= CrdxInt()                    // Controla se tem integracao com SIGACRD

Local aItensSD2    	:= {}							// Itens SD2
Local aItensSF2    	:= {}							// Itens SF2
Local aRetCrd      	:= {}                          	// Array  de retorno do cancelamento do contrato - integracao SIGACRD

Local cCliNota 	   	:= ""							// Cliente da Nota
Local cCliLoja		:= ""							// Loja da Nota         
Local aRetCartao 	:= {}							//Variavel para guardar as informacoes do array "oTef:aRetCartao" caso estiver configurado a CLISITEF
Local aCartaoSL4	:= {}							//Variavel para guardar as informacoes do cancelamento do cartao para atualizar campos na tabela SL4

If Substr(SL1->L1_CONFVEN,1,1)  <> "N" // Posicao de Aceitacao de Numero de Cheque
	Lj140CHSEFDel(@aCheques)
EndIf

Lj140TlCTef(	@lJob		, @lVendTef		, Iif( Type("oTef") == "U" , Nil, @oTef ), @lTefOk,;
				@aRetCartao	, @aCartaoSL4	)

/*Re-abre o pedido, caso tenha seja nativo de SC5 - DAV */
If SL2->(ColumnPos("L2_PEDSC5")) > 0 .And. SL2->(ColumnPos("L2_ITESC6")) > 0 .And. SL2->(ColumnPos("L2_SEQUEN")) > 0
	If ExistFunc("MT461DvCN")
		MT461DvCN(SL1->L1_DOC, SL1->L1_SERIE, SL1->L1_NUM)
	EndIf
Endif

/* Cancela o TEF na base de dados */
Lj140CnTEF(@lTefOk		, @lJob			, @lVendTef		, @cChave	,;
		   @cNumNota	, @cSerNota		, @cCliNota		, @cCliLoja	,; 
		   @cDescMot	, @lAchouSF2	, @lExisteFT	, @lNfCup	,;	
		   @aItensSD2	, @cNumNfCup	, @cSerNfCup	, @nTamDoc	,;
		   @lCrdxInt	, @aRetCrd		, @lGeraNCC		, @aItensSF2,;
		   @aItensSD2	, cNumNFDev		, cSerieDev		, cMotivo	,;
		    cNum	 	, aRetCartao	, aCartaoSL4	)

Return .T.

 
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140CnSD2    ³ Autor ³ Vendas Clientes       ³ Data ³ 24.11.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Cancela o SD2                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function Lj140CnSD2(cNumNota, cSerNota, lNfCup, aItensSD2)
Local cTESSaida    	:= ""							// TES saida
Local lLjD1TesT    	:= (ExistBlock("LJD1TesT"))		// Verifica a existencia do PE para tratamento de TES
Local cTESTroca	   	:= SuperGetMv("MV_TESTROCA")	// TES padrao para o processo de troca
Local nTamD2_DOC   	:= TamSx3("D2_DOC")[1]         	// Tamanho Nota Fiscal
Local cAuxChave	   	:= ""							// Variavel auxiliar para guardar a chave do SB2
Local lCAT83		:= SD2->(ColumnPos("D2_CODLAN")) > 0 .AND. ExistFunc("FISA023") .AND. SuperGetMV("MV_CAT8309",,.F.)
Local lAmbOffLn 	:= SuperGetMv("MV_LJOFFLN", Nil, .F.)	//Identifica se o ambiente esta operando em offline
Local cD2TESBkp		:=	""								// Backup do valor da SD2 (D2_TES) antes do replace

cNumNota := PadR(cNumNota,nTamD2_DOC)
          
DbSelectArea("SD2")
SD2->(DbSetOrder(3))

If SD2->(DbSeek(xFilial("SD2")+cNumNota+cSerNota))
	While !SD2->(Eof()) .AND.	(xFilial("SD2")	== SD2->D2_FILIAL) 	.AND. ;
								(SD2->D2_DOC 		== cNumNota 	.AND. ;
								 SD2->D2_SERIE 		== cSerNota)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//|    Considerar somente os itens de NF                                                                    |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If cPaisLoc <> "BRA"
			If SD2->D2_TIPODOC <> "01"
				SD2->(DbSkip())
				Loop
			EndIf
		EndIf
		
		If lNfCup .OR. (SD2->D2_PDV == SL1->L1_PDV)
			cTESSaida  := SD2->D2_TES
			If !lNfCup .AND. SF4->(DbSeek(xFilial("SF4") + SD2->D2_TES))
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Atualiza CAT/83						  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lCAT83
					FISA023("SD2",SD2->D2_FILIAL+SD2->D2_DOC+SD2->D2_SERIE+SD2->D2_CLIENTE+SD2->D2_LOJA+SD2->D2_COD+SD2->D2_ITEM,"E")
				EndIf
				If SF4->F4_ESTOQUE == "S"
					If SB2->( DbSeek(cFilial + SD2->D2_COD + SD2->D2_LOCAL ) )
						
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//|     Verifica se o cliente utiliza tratamento específico para o controle de TES         |
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If nModulo == 72
							aRet := KEXF510( cTESTroca , SD2->D2_COD , 0 )
							If ValType(aRet) == "A"
								cTESTroca := aRet[1]
							EndIf
						EndIf
						
						If lLjD1TesT
							aRet := ExecBlock("LJD1TesT",.F.,.F.,{cTESTroca,SD2->D2_COD,0})
							If ValType(aRet) == "A"
								cTESTroca := aRet[1]
							EndIf
						EndIf
						
						RecLock("SD2",.F.)
						cD2TESBkp := SD2->D2_TES
						Replace SD2->D2_TES with cTESTroca
						
						aCMSaida := PegaCusD2()
						
						B2AtuComD2( aCMSaida, 1,,.F.)
						Replace SD2->D2_TES with cD2TESBkp		// Retorna a TES original para o campo D2_TES para não perder o histórico do campo.
						SB2->(MsUnLock())
						
						If !( SD2->D2_TIPO $ "DBCIP" )
							DbSelectArea("SB3")
							SB3->(DbSetOrder(1))
							If SB3->(DbSeek(xFilial("SB3") + SD2->D2_COD))
								RecLock("SB3")
							Else
								RecLock("SB3",.T.)
								Replace SB3->B3_FILIAL with xFilial("SB3")
								Replace SB3->B3_COD    with SD2->D2_COD
							EndIf
							FieldPut(ColumnPos("B3_Q"+StrZero(Month(SD2->D2_EMISSAO),2)),FieldGet(ColumnPos("B3_Q"+StrZero(Month(SD2->D2_EMISSAO),2)))-SD2->D2_QUANT)
							SB3->B3_MES := SD2->D2_EMISSAO
							SB3->(MsUnLock())
						EndIf
						
					EndIf
				EndIf
			EndIf
			
			DbSelectArea( "SD2" )
			
			If cPaisLoc == "GUA"
				Aadd(aItensSD2,{	SD2->D2_ITEM	, SD2->D2_COD	 	, SD2->D2_UM		, SD2->D2_SEGUM , ;
									SD2->D2_QUANT	, SD2->D2_PRCVEN	, SD2->D2_TOTAL		, SD2->D2_LOCAL , ;
									cTESSaida		, SD2->D2_CF		, SD2->D2_CLIENTE	, SD2->D2_LOJA  , ;
									SD2->D2_GRUPO	, SD2->D2_PRUNIT	, SD2->D2_DOC		, SD2->D2_SERIE , ;
									SD2->D2_CUSTO1	, SD2->D2_CUSTO2	, SD2->D2_CUSTO3	, SD2->D2_CUSTO4,;
									SD2->D2_CUSTO5 } )
			EndIf
			
			RecLock("SD2",.F.,.T.)
			SD2->( dbDelete() )
			
			//Adiciona a tabela para exclusao no processo off-line
			If lAmbOffLn
				oProcOff:Inserir("SD2", xFilial("SD2") + SD2->D2_DOC + SD2->D2_SERIE + SD2->D2_CLIENTE + SD2->D2_LOJA + SD2->D2_COD + SD2->D2_ITEM, 3, "DELETE")
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Envia alteração de estoque OffLine³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
			cAuxChave := xFilial("SD2") + SD2->D2_COD + SD2->D2_LOCAL 
			If lAmbOffLn				
				oProcOff:Inserir("SB2", cAuxChave, 1, "UPDATE")						
			EndIf
			SD2->(MsUnlock())
		EndIf
		SD2->(dbSkip())
	EndDo
EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140CnSF2    ³ Autor ³ Vendas Clientes       ³ Data ³ 24.11.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Cancela o SF2                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function Lj140CnSF2(cNumNota, 	cSerNota, 	cNumNfCup, 	cSerNfCup, ;
							 lNfCup, 	aItensSF2, 	nTamDoc)
 
Local lFoundNF     	:= .F.									// Se achou NF
Local lFisLivro		:= (SuperGetMV("MV_LJLVFIS",,1) == 2)	// Utiliza novo conceito para geracao do SF3
Local lAmbOffLn 		:= SuperGetMv("MV_LJOFFLN", Nil, .F.)	//Identifica se o ambiente esta operando em offline
Local lNFCe			:= ExistFunc("LjEmitNFCe") .AND.  LjEmitNFCe() .AND. SL1->(ColumnPos("L1_KEYNFCE")) > 0
Local lIntegHtl		:= SuperGetMv("MV_INTHTL",, .F.) .And. SL1->L1_ORIGEM == "N" .And. FWHasEAI("LOJA140",, .T., .T.) //Integracao via Mensagem Unica - Hotelaria

Default nTamDoc		:= TamSx3("F2_DOC")[1]         			// Tamanho do campo F2_DOC


If lIntegHtl //Validação para saber se é NCFE no caso de vendas da integração hotel
	lNFCe := AllTrim(SL1->L1_ESPECIE) == "NFCE" .And. !Empty(SL1->L1_KEYNFCE) 
EndIf

cNumNota := PadR(cNumNota, nTamDoc)

DbSelectArea("SF2")
SF2->( DbSetOrder(1) )
If SF2->( DbSeek(xFilial("SF2") + cNumNota + cSerNota) )
	If cPaisLoc <> "BRA"
		lFoundNF  := .F.
		While !Eof() .AND. xFilial("SF2") 	== SF2->F2_FILIAL 	.AND. cNumNota == SF2->F2_DOC .AND.  cSerNota 	== SF2->F2_SERIE .AND. ;
							!lFoundNF
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|    Verifica se eh NF                                                                                    |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If SF2->F2_TIPODOC <> "01"
				DbSkip()
				Loop
			EndIf
			
			lFoundNF  := .T.
		End
	EndIf
	cNumNota := SF2->F2_NEXTDOC
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//|    Exclui a Nf gerada para um determinado cupom, caso a mesma exista...                                 |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Empty(cNumNota) .AND. !Empty(cNumNfCup)
		cNumNota := cNumNfCup
		cSerNota := cSerNfCup
		cNumNfCup:= ""
		cSerNfCup:= ""
		lNfCup   := .T.
	EndIf
	If cPaisLoc <> "BRA"
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//|     Armazenar os dados dos itens para gerar a NCC - Loc. Guatemala                                      |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		Aadd(aItensSF2,{ 	SF2->F2_DOC		, SF2->F2_SERIE		, SF2->F2_CLIENTE	, SF2->F2_LOJA	  , ;
							SF2->F2_VALBRUT	, SF2->F2_VALMERC	, SF2->F2_MOEDA		, SF2->F2_TXMOEDA	} )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Cancela a baixa dos titulos com pagamento a vista.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SE4->(DbSetOrder(1))
		SE4->(DbSeek(xFilial("SE4")+SF2->F2_COND))
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//|     Estornar a baixa dos titulos baixados automaticamente                                           |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		CaBxAutSE1(SF2->F2_COND,"SE1")
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Gera SF3 de acordo com a MATXFIS³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    If lFisLivro
		MaFisAtuSF3(2,"S",SF2->(Recno()),"SF2",,,,,,,cNfisCanc)
		MaFisEnd()
	EndIf
	
	//Integracao Hotel, se NFCE altera status SF3 para nao enviar para Sefaz
	If lIntegHtl
		If lNFCe			
			SF3->(DbSetOrder(6)) //F3_FILIAL+F3_NFISCAL+F3_SERIE				
			If SF3->( DbSeek(xFilial("SF3") + SF2->F2_DOC + SF2->F2_SERIE) )
				While !Eof() .And. xFilial("SF3") 	== SF3->F3_FILIAL .And. SF2->F2_DOC == SF3->F3_NFISCAL .And. SF3->F3_SERIE == SF2->F2_SERIE
					SF3->(RecLock("SF3", .F.))
					SF3->F3_CODRSEF := "101"
					SF3->(MsUnlock())
					SF3->(dbSkip())
				ENDDO	
			EndIf				
		EndIf
	EndIf
	
	RecLock("SF2",.F.,.T.)
	SF2->( dbDelete() )
	//Adiciona a tabela para exclusao no processo off-line
	If lAmbOffLn
		oProcOff:Inserir("SF2", xFilial("SF2") + SF2->F2_DOC + SF2->F2_SERIE + SF2->F2_CLIENTE + SF2->F2_LOJA, 1, "DELETE")
	Endif
	MsUnlock()
Else
	cNumNota := Space(nTamDoc)
EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140DtSFT    ³ Autor ³ Vendas Clientes       ³ Data ³ 24.11.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Altera a data da tabela SFT                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function Lj140DtSFT(cSerNota, cNumNota, cCliNota, cCliLoja, lExisteFT)
Local nTamFT_NFISCAL := TamSx3("FT_NFISCAL")[1] //Tamanho Nota Fiscal

cNumNota := PadR(cNumNota,nTamFT_NFISCAL)                  

If (cPaisLoc == "BRA" .AND. lExisteFT)
	DbSelectArea("SFT")
	DbSetOrder(1)
	If DbSeek( xFilial("SFT") + "S" + cSerNota + cNumNota + cCliNota + cCliLoja)
		While !Eof() .AND. xFilial("SFT")+cSerNota+cNumNota+cCliNota+cCliLoja == SFT->FT_FILIAL ;
							+ SFT->FT_SERIE+SFT->FT_NFISCAL+SFT->FT_CLIEFOR+SFT->FT_LOJA
			RecLock("SFT",.F.)
			Replace SFT->FT_DTCANC	With dDatabase
			Replace SFT->FT_OBSERV	With "NF CANCELADA"
			MsUnLock()
			dbSkip()
			Loop
		End
	EndIf
    EndIf
    
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140CancSF3  ³ Autor ³ Vendas Clientes       ³ Data ³ 24.11.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Cancela o SF3                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Lj140CancSF3(cSerNota, 	cNumNota, 	cCliNota, 	cCliLoja, ;
							 cDescMot, 	lAchouSF2, 	lExisteFT)

Local aOtimizacao  		:= {}						  	  		// Array utilizada na MATXFIS
Local lPais				:= .T.							 		// Variavel logica que recebe que testa se o pais e <> de Brasil
Local nTamF3_NFISCAL 	:= TamSx3("F3_NFISCAL")[1]        		// Tamanho Nota Fiscal
Local lFieldA1Mod 		:= SA1->(ColumnPos("A1_MODCFD")) > 0 	// Verifica se existe o campo A1_MODCFD
Local lFieldStatus		:= SF3->(ColumnPos("F3_STATUS")) > 0	// Verifica se existe o campo F3_STATUS
Local aCposZerar		:= {}									// Lista de campos a zerar ao anular NF (bolivia)
Local nX				:= 0									// Auxiliar de loop
Local nCpo				:= 0									// Auxiliar de loop
Local cCampo			:= ""									// Nome do campo F3_VALIMP?
Local aCposImp			:= {}									// Prefixos de campos de impostos a serem zerados
Local cSufixo			:= ""									// Final do nome do campo

cNumNota := PadR(cNumNota,nTamF3_NFISCAL) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Lista de campos a serem zerados ao anular a NF (Bolivia),³
//³para integracao com DaVinci                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cPaisLoc == "BOL"

	aCposZerar := {"F3_VALCONT","F3_DESPESA","F3_VALOBSE","F3_VALMERC"}
	aCposImp   := {"F3_VALIMP","F3_ALQIMP","F3_BASIMP","F3_RETIMP"}

	For nX := 1 to 35

		If nX <= 9
			cSufixo := Str(nX,1,0)
		Else
			cSufixo := Chr(55+nX)
		EndIf
		
		For nCpo := 1 to Len(aCposImp)
			cCampo := aCposImp[nCpo] + cSufixo
			If SF3->(ColumnPos(cCampo)) > 0
				AAdd(aCposZerar,cCampo)
			EndIf
		Next nCpo

	Next nX

EndIf

DbSelectArea("SF3")
DbSetOrder(5)
If DbSeek( xFilial("SF3") + cSerNota + cNumNota )

	While !Eof() .AND. xFilial("SF3") + cSerNota + cNumNota == SF3->F3_FILIAL + SF3->F3_SERIE + SF3->F3_NFISCAL
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Excluir apenas Nota Fiscal de Saida	    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !(Substr(SF3->F3_CFO,1,1) >= "5")
			dbSkip()         
			Loop
		EndIf
		RecLock("SF3",.F.)

		If cPaisLoc<>"BRA"
			lPais := lAnulaSF3
		Else
			lPais := .T.
		EndIf	
		
		If lPais
			Replace SF3->F3_DTCANC with dDatabase
			Replace SF3->F3_OBSERV with "NF CANCELADA"
			If cPaisLoc == "GUA"
				Replace SF3->F3_OBSERV   with cDescMot
			EndIf
			If cPaisLoc == "BOL" .AND. lFieldStatus
				Replace SF3->F3_STATUS with "A"
				For nX := 1 to Len(aCposZerar)
					Replace SF3->&(aCposZerar[nX]) With 0
				Next nX
			EndIf
		Else
			If cPaisLoc == "MEX" .AND. AliasIndic("MDL") .AND. lFieldA1Mod
				CFDExcXML(SF2->F2_ESPECIE,SF2->F2_CLIENTE,SF2->F2_LOJA,SF2->F2_DOC,SF2->F2_SERIE,.T.)		
			EndIf		
			DbDelete()
		EndIf
		MsUnLock()
		dbSkip()
	End
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Preenche a Data de Cancelamento da venda³
	//³no campo FT_DTCANC para a tabela SFT    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Lj140DtSFT(cSerNota, cNumNota, cCliNota, cCliLoja, lExisteFT)
	
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//| Caso nao tenha sido gerado F3, deve registrar o cancelmento desta NF, sempre que o MV_MAPARES == "N"  |
	//| para cupom fiscal  																					  |			
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
 If !Empty(SL1->L1_DOC+SL1->L1_SERIE) .AND. lAchouSF2 .AND. ;
    ( Empty(SL1->L1_PDV) .OR. (!Empty(SL1->L1_PDV) .AND. ( LjNfPafEcf(SM0->M0_CGC) .OR. SuperGetMV("MV_MAPARES") == "N" .OR. cPaisLoc <> "BRA" )))
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Carrega a Nota Fiscal SF3 referente a Notas Fiscais de Entrada³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		MaFisIniNF(2,SF2->(Recno()),@aOtimizacao,"SF2",.T.)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Inicializa a gravacao nas funcoes Fiscais               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		MaFisWrite()
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Efetua a gravacao dos registros referente a Nota no SF3 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("SF2")
		
		MaFisAtuSF3(1,"S",SF2->(Recno()),"SF2")
		
		DbSelectArea("SF3")
		DbSetOrder(5)
		If DbSeek( xFilial("SF3") + cSerNota + cNumNota )
			While !Eof() .AND. xFilial("SF3")+cSerNota+cNumNota == SF3->F3_FILIAL+SF3->F3_SERIE+SF3->F3_NFISCAL
		
				RecLock("SF3",.F.)
				Replace SF3->F3_DTCANC	with dDatabase
				Replace SF3->F3_OBSERV	with "NF CANCELADA"
				SF3->(MsUnLock())
				MaFisEnd()
				MsUnLock()
				SF3->(dbSkip())
			End
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Preenche a Data de Cancelamento da venda³
		//³no campo FT_DTCANC para a tabela SFT    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (cPaisLoc == "BRA" .AND. lExisteFT)
			DbSelectArea("SFT")
			DbSetOrder(1)
			If DbSeek( 	xFilial("SFT") 	+ "S" + cSerNota + cNumNota + ;
						cCliNota 		+ cCliLoja)
				While !Eof() .AND. xFilial("SFT") 	+ cSerNota + cNumNota + ;
									cCliNota 		+ cCliLoja == SFT->FT_FILIAL 	+ SFT->FT_SERIE + ;
									SFT->FT_NFISCAL	+ SFT->FT_CLIEFOR				+ SFT->FT_LOJA
			
					RecLock("SFT",.F.)
					Replace SFT->FT_DTCANC	With dDatabase
					Replace SFT->FT_OBSERV	With "NF CANCELADA"
					MsUnLock()
					dbSkip()
					Loop
				End
			EndIf
	    EndIf
	EndIf
	If cPaisLoc == "MEX" .AND. AliasIndic("MDL") .AND. lFieldA1Mod
		CFDExcXML(SF2->F2_ESPECIE,SF2->F2_CLIENTE,SF2->F2_LOJA,SF2->F2_DOC,SF2->F2_SERIE,.T.)							
	EndIf	
EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140LpSL2    ³ Autor ³ Vendas Clientes       ³ Data ³ 24.11.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ LImpa SL2                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Lj140LpSl2(cNum, nVlrOrc)

Local nMvLjTpDes	:= SuperGetMv( "MV_LJTPDES", , 0 )
Local lAtuMds		:= SuperGetMv("MV_LJIPECF",,.F.) .And. AliasInDic("MDS") .And. IsInCallStack("LJ140CNTEF")
Local cCpoBas		:= ""
Local aAreaMDS		:= MDS->(GetArea())
Local lAmbOffLn 	:= SuperGetMv("MV_LJOFFLN", Nil, .F.)	//Identifica se o ambiente esta operando em offline

DEFAULT nVlrOrc   	:= 0	// Valor Total do Orcamento
DEFAULT cNum	  	:= ""

If FunName() <> "STBORCCANCEL"
	DbSelectArea( "SL2" )
	SL2->( DbSetOrder(1) )
	SL2->( DbSeek( xFilial("SL2")+ cNum ) )
	
	While SL2->(!Eof()) .AND. xFilial("SL2") == SL2->L2_FILIAL .AND. SL2->L2_NUM == cNum

		//Tratamento para cancelamento de cupom fiscal para atualização da tabela MDS
		If lAtuMds .And. !Empty(SL2->L2_DOC) .And. Left(SL2->L2_SITTRIB,1) $ "T|S" //Situacao Tributaria (T=ICMS ou S=ISS)

			cCpoBas	:= "MDS_BA" + Right(AllTrim(SL2->L2_SITTRIB),Len(AllTrim(SL2->L2_SITTRIB))-1)

			If MDS->(ColumnPos(cCpoBas) ) > 0
				If MDS->( DbSeek(xFilial("MDS") + DTOS(SL1->L1_EMISNF) + SL2->L2_PDV ) )
					RecLock("MDS",.F.)
		       	    REPLACE MDS->(&cCpoBas) WITH MDS->(&cCpoBas) - (SL2->L2_VLRITEM + SL2->L2_VALFRE)
				    MDS->(MsUnlock())
			    EndIf
			EndIf

		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se o orcamento possui reserva³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("SC0")
		SC0->( DbSetOrder(1) ) //C0_FILIAL + C0_NUM + C0_PRODUTO + C0_LOCAL
		Reclock( "SL2",.F.,.T.)	
		If !SC0->( DbSeek(SL2->L2_FILIAL + SL2->L2_RESERVA + SL2->L2_PRODUTO + SL2->L2_LOCAL) )		
			Reclock( "SL2",.F.,.T.)	
				REPLACE SL2->L2_RESERVA		WITH ""
			SL2->( MsUnlock() )
		EndIf
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Restaura os valores anteriores ao fechamento da venda,  ³
		//³para que durante a proxima finalizacao, os valores sejam³
		//³recalculados.                                           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("SL2")
		//PAF-ECF: Não deve limpar, movimento deverá constar no Movimento por ECF do menu Fiscal
		If !LjNfPafEcf(SM0->M0_CGC) 
			Reclock( "SL2",.F.,.T.)
			REPLACE SL2->L2_VENDIDO	WITH " "
			REPLACE SL2->L2_DOC		WITH ""
			REPLACE SL2->L2_SERIE	WITH ""
			REPLACE SL2->L2_PDV		WITH ""
			REPLACE SL2->L2_VENDIDO	WITH " "
	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se houve desconto no total da venda, reaplica o valor³
			//³descontado porporcionalmente                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			IF cPaisLoc == 'BRA'
				If SL2->L2_DESCPRO > 0 
					REPLACE SL2->L2_VLRITEM	WITH SL2->(L2_VLRITEM + L2_DESCPRO)
					If  nMvLjTpDes == 2
						REPLACE SL2->L2_VRUNIT	WITH (SL2->L2_PRCTAB - ( SL2->L2_PRCTAB * (SL2->L2_DESC / 100)) )
					Else
						REPLACE SL2->L2_VRUNIT	WITH SL2->(L2_VLRITEM / L2_QUANT)
					End
				EndIf
		
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Se houve acrescimo na venda, retira o valor do acrescimo³
				//³dos itens.                                              ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SL1->L1_JUROS > 0 .OR. AllTrim( SL1->L1_CONDPG ) == "CN"
					REPLACE SL2->L2_VLRITEM WITH SL2->((L2_PRCTAB * L2_QUANT) - L2_VALDESC)
					If  nMvLjTpDes == 2	
						REPLACE SL2->L2_VRUNIT  WITH (SL2->L2_PRCTAB	- ( SL2->L2_PRCTAB * (SL2->L2_DESC / 100)  )	 )
					Else
						REPLACE SL2->L2_VRUNIT  WITH SL2->(L2_VLRITEM/L2_QUANT)
					EndIf	
				EndIf
			
				REPLACE SL2->L2_VALICM	WITH 0
				REPLACE SL2->L2_BASEICM	WITH SL2->(L2_PRCTAB * L2_QUANT)
				REPLACE SL2->L2_DESCPRO WITH 0
			EndIf 
				
			SL2->( MsUnlock() )
		EndIf
	
		If lAmbOffLn
			oProcOff:Inserir("SL2", xFilial("SL2") + SL2->L2_NUM + SL2->L2_ITEM +SL2->L2_PRODUTO, 1, "DELETE")
		Endif
	
		nVlrOrc += SL2->L2_VLRITEM	
		SL2->( DbSkip() )
	End
EndIf

RestArea(aAreaMDS)
Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140LpSL1    ³ Autor ³ Vendas Clientes       ³ Data ³ 24.11.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ LImpa SL1                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpL1 = Controla se tem integracao com SIGACRD                 ³±±
±±³          ³ ExpC2 = Numero do orcamento                                    ³±±
±±³          ³ ExpN2 = Valor liquido do Orcamento                             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function Lj140LpSL1(lCrdxInt, cNum, nVlrOrc)

Local nI			:= 0								// Contador de For
Local cCpoTroco		:= ""								// Campo troco
Local nVlrTot		:= 0								// Valor com desconto sobre o total aplicado
Local nVlrDescFi	:= 0								// Valor com Desconto Financeiro
Local nDescont		:= 0								// Valor do Desconto sobre o Total
Local nValFrete    	:= 0								// Valor do Frete
Local cMvTmkLoj     := SuperGetMV("MV_TMKLOJ",,.F. )   // verIfica se ha integracao com o Callcenter
Local aSL1Area		:= {}
Local lAmbOffLn 	:= SuperGetMv("MV_LJOFFLN", Nil, .F.)	//Identifica se o ambiente esta operando em offline

DEFAULT cNum		:= ""

If FunName() <> "STBORCCANCEL"

	DbSelectArea("SL1")
	SL1->( DbSetOrder(1) )
	SL1->( DbSeek(xFilial("SL1")+ cNum) )
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Aplica novamente o desconto sobre o total.³
	//³O desconto financeiro sera calculado      ³
	//³novamente na finalizacao da Venda.        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nVlrTot 	:= (nVlrOrc - SL1->L1_DESCONT)	//Valor com desconto sobre o total aplicado
	nVlrDescFi	:=	IIF(SL1->( ColumnPos( "L1_DESCFIN" )) > 0,SL1->L1_DESCFIN , 0  ) // Retira o desc Financeiro,será recalculado na finalizacao da venda
	nDescont	:= SL1->L1_DESCONT				//Valor do desconto sobre o total
	nValFrete   := SL1->L1_FRETE				//Valor do Frete
	
	Reclock( "SL1" ,.F.,.T.)
	
	REPLACE SL1->L1_DOC 		WITH ""
	REPLACE SL1->L1_SERIE 		WITH ""
	REPLACE SL1->L1_PDV 		WITH ""
	REPLACE SL1->L1_EMISNF 		WITH Ctod(" ")
	REPLACE SL1->L1_TIPO 		WITH ""
	If SL1->( ColumnPos("L1_DOCRPS") ) > 0
		Replace SL1->L1_DOCRPS	with ""
		Replace SL1->L1_SERRPS	with ""
	EndIf
	
	
	If cPaisLoc == "BRA"
		REPLACE SL1->L1_DESCONT		WITH nDescont
		REPLACE SL1->L1_VLRTOT		WITH nVlrTot
		REPLACE SL1->L1_VLRLIQ		WITH nVlrTot + nValFrete
		REPLACE SL1->L1_VALBRUT  	WITH nVlrTot + nValFrete
		REPLACE SL1->L1_SITUA		WITH ""

		//Limpa chave da NFCe
		If SL1->(ColumnPos("L1_KEYNFCE")) > 0                
			REPLACE SL1->L1_KEYNFCE	WITH ""
			REPLACE SL1->L1_SITUA	WITH ""
		EndIf			
	Else
		REPLACE SL1->L1_VALBRUT		WITH SL1->L1_VLRTOT
	EndIf
	REPLACE SL1->L1_DINHEIR 	With 0
	REPLACE SL1->L1_CHEQUES  	With 0
	REPLACE SL1->L1_CARTAO   	With 0
	REPLACE SL1->L1_CONVENI  	With 0
	REPLACE SL1->L1_VALES    	With 0
	REPLACE SL1->L1_FINANC   	With 0
	REPLACE SL1->L1_OUTROS   	With 0
	REPLACE SL1->L1_ENTRADA		With 0
	REPLACE SL1->L1_VLRDEBI  	With 0
	REPLACE SL1->L1_VENDTEF  	With ""
	REPLACE SL1->L1_NUMCFIS  	With ""
	REPLACE SL1->L1_OPERADO  	With ""
	REPLACE SL1->L1_BLCRED   	With ""
	REPLACE SL1->L1_VALICM		With 0
	
	If lCrdxInt .AND. SL1->(ColumnPos("L1_CONTRA")) > 0
		REPLACE SL1->L1_CONTRA  With ""
	EndIf
	          
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//   Zera os campos de troco...                                                                             |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cPaisLoc <> "BRA"
		For nI := 1 To MoedFin()
			cCpoTroco := "L1_TROCO"+AllTrim(Str(nI))
			If SL1->(ColumnPos(cCpoTroco)) > 0
				SL1->(FieldPut(ColumnPos(cCpoTroco),0))
			EndIf
		Next nI
	EndIf
	
	//PAF-ECF: Não deve limpar todos os dados, apenas sinalizar 
	If !LjNfPafEcf(SM0->M0_CGC) 
		REPLACE SL1->L1_PDV 		WITH ""
		REPLACE SL1->L1_NUMCFIS  	With ""		
		
		If cPaisLoc == "BRA"
			REPLACE SL1->L1_DESCONT		WITH nDescont	- nVlrDescFi
			REPLACE SL1->L1_VLRTOT		WITH nVlrTot	+ nVlrDescFi
			REPLACE SL1->L1_VLRLIQ		WITH nVlrTot + nValFrete
			REPLACE SL1->L1_VALBRUT  	WITH nVlrTot + nValFrete
		Else
			REPLACE SL1->L1_VALBRUT		WITH SL1->L1_VLRTOT	
		EndIf
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//|Verifica se existe o numero da reserva no item, caso nao, limpa a reserva do cabecalho|
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If SL1->L1_RESERVA == 'S'
		aSL1Area := SL1->( GetArea() )
		DbSelectArea("SL2")
		SL2->( DbSetOrder(1) )
		If SL2->( DbSeek(SL1->L1_FILIAL + SL1->L1_NUM) ) .AND. Empty(SL2->L2_RESERVA)
			REPLACE SL1->L1_RESERVA  With ""
		EndIf
		SL1->( RestArea(aSL1Area) )
	EndIf
	
	SL1->( MsUnlock() )
	
	//Adiciona a tabela para exclusao no processo off-line
	If lAmbOffLn
		oProcOff:Inserir("SL1", xFilial("SL1") + SL1->L1_NUM, 1, "DELETE")
	
		
		DbSelectArea("SL4")
			
		If (SL4->(DbSeek(xFilial("SL4") + SL1->L1_NUM)))
		
			While !SL4->(EOF()) .AND. SL4->L4_FILIAL + SL4->L4_NUM == xFilial("SL4") + SL1->L1_NUM
				//Adiciona a tabela para exclusao no processo off-line
				oProcOff:Inserir("SL4", xFilial("SL4") + SL4->L4_NUM + SL4->L4_ITEM, 2, "DELETE")	
				SL4->(DbSkip())
			End	
		
		EndIf
	Endif	
	
	If cMvTmkLoj == "S" .And. !Empty(SL1->L1_NUMATEN)
		DbSelectArea("SUA")
		DbSetOrder(1)
		If DbSeek(xFilial("SUA") + SL1->L1_NUMATEN) 
			RecLock("SUA",.F.) 
			Replace SUA->UA_OPER   WITH "2"	   
			Replace SUA->UA_STATUS WITH "SUP"
			Replace SUA->UA_EMISNF WITH Ctod(" ")
			Replace SUA->UA_SERIE  WITH SPACE(TamSx3("UA_SERIE")[1])
			Replace SUA->UA_DOC    WITH SPACE(TamSx3("UA_DOC")[1])
			SUA->( MsUnlock() )
		EndIF
	Endif
EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140CnFin    ³ Autor ³ Vendas Clientes       ³ Data ³ 24.11.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Cancela venda no financeiro                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Lj140CnFin(cChave, lCrdxInt, aRetCrd)

Local aParc        	:= {}							// Parcelas
Local cChaveSE1    	:= ""							// Chave para SE1
Local lDelSE5      	:= .F. 						  	// Variavel que verificara se o SE5 foi excluido                               
Local lCancelouCRD 	:= .F.                         	// Indica se o contrato ja foi cancelado
Local lParam	   	:= .F.							// Indica se a rotina de troca esta' ativa
Local lAmbOffLn 	:= SuperGetMv("MV_LJOFFLN", Nil, .F.)	//Identifica se o ambiente esta operando em offline
Local lIntegHtl		:= SuperGetMv("MV_INTHTL",, .F.) .And. SL1->L1_ORIGEM == "N" .And. FWHasEAI("LOJA140",, .T., .T.) //Integracao via Mensagem Unica - Hotelaria

If Substr(SL1->L1_CONFVEN,6,1) <> "N"
	DbSelectArea("SE1")
	SE1->( DbSetOrder(1) ) //E1_FILIAL + E1_PREFIXO + E1_NUM + E1_PARCELA + E1_TIPO
	If SE1->( DbSeek(xFilial("SE1") + cChave) ) 
		SAE->( DbSetOrder( 1 ) )
		While !Eof() .AND. xFilial("SE1") == SE1->E1_FILIAL .AND. cChave == SE1->(E1_PREFIXO + E1_NUM)
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se a origem for pelo financeiro, nao efetua a exclusao do titulo    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If "FIN" $ SE1->E1_ORIGEM
				SE1->(DbSkip())
				Loop
			EndIf
			
			//Tratamento Hotelaria - Cancela compensacao dos RAs			
			If lIntegHtl
				If !Empty(SE1->E1_CONHTL)
					Lj140ExRAComp()
				EndIf
			EndIf
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|   Eh necessario armazenar as parcelas para o tratamento do SE5                                          |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cPaisLoc <> "BRA"
				lParam := SuperGetMV("MV_LJTRLOC")  //Rotina de Troco localizada ativa
			Else	
				lParam := SuperGetMV("MV_LJTROCO")  //Rotina de Troco localizada ativa
			EndIf	

			AAdd(aParc,{SE1->E1_PARCELA,SE1->E1_TIPO})

			If lParam  //Rotina de Troco localizada ativa
				cChaveSE1 := SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA+Space(Len(SE5->E5_TIPO))+;
				SE1->E1_CLIENTE+SE1->E1_LOJA
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//|   Excluir os registros de troco correspondentes                                                 |
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
				Lj140ExTr(cChaveSE1, @lDelSE5)

			EndIf
			
			If cPaisLoc == "BRA"
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//|    Excluir os registros de movimentação bancaria - Integridade Referencial                   |
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				lj140ExBan(@lDelSE5)
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//|    Excluir registro do Contas a Pagar (quando houver Taxa de ISS)                                       |
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				Lj140CnCP()
			EndIf 
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Exclui registro do contas a pagar (Quando usar o conceito ³
			//³ de gerar contas a receber com valor bruto e contas a      ³
			//³ pagar com a taxa adminstrativa. parametro MV_LJGERTX = .T.³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If SuperGetMV("MV_LJGERTX",,.F.)				
				Lj140ExCap()	
			Endif
			
			If lCrdxInt .AND. !lCancelouCRD .AND. SE1->(ColumnPos("E1_NUMCRD")) > 0
				If !Empty(SE1->E1_NUMCRD)
					aRetCrd      := aClone(CrdxVenda( "3"  ,{"",""}  ,SE1->E1_NUMCRD  ,.F.  ,;
					NIL  ,NIL ))
					lCancelouCRD := .T.
				EndIf
			EndIf
	
			If oIntCVenda <> Nil
				oIntCVenda:Inserir("SE1", xFilial("SE1") + SE1->E1_PREFIXO + SE1->E1_NUM + SE1->E1_PARCELA + SE1->E1_TIPO, "1", "5")
				oIntCVenda:Gerar()				
			EndIf
	
			Reclock("SE1")
			SE1->(dbDelete())
			SE1->( MsUnLock() )

			//Adiciona a tabela para exclusao no processo off-line
			If lAmbOffLn
				oProcOff:Inserir("SE1", xFilial("SE1") + SE1->E1_PREFIXO + SE1->E1_NUM + SE1->E1_PARCELA + SE1->E1_TIPO, 1, "DELETE")
			Endif
			MSUnLock()
			SE1->(dbSkip())
		End

	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se for Dinheiro e SE5 nao estiver sido excluido acima³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lj140ExDin(lDelSE5, cChave, aParc)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Realiza a exclusao do SE1 apos o SE5 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Lj140CnSE5(cChave)
EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140ExTr     ³ Autor ³ Vendas Clientes       ³ Data ³ 24.11.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Excluir os registros de troco correspondentes                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Lj140ExTr(cChaveSE1, lDelSE5, cSEQ)
                    
Local aAreaAtu := GetArea()
Local aAreaSE5 := GetArea("SE5")  
Local lAmbOffLn:= SuperGetMv("MV_LJOFFLN", Nil, .F.)	//Identifica se o ambiente esta operando em offline

DEFAULT cSEQ := StrZero(1,TamSX3("E5_SEQ")[1])
                    
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//|   Excluir os registros de troco correspondentes                                                 |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SE5")
DbSetOrder(7)
If DbSeek(xFilial("SE5")+cChaveSE1)
	While !Eof() .AND. xFilial("SE5") == SE5->E5_FILIAL .AND. cChaveSE1 == SE5->E5_PREFIXO+;
						SE5->E5_NUMERO + SE5->E5_PARCELA + SE5->E5_TIPO + SE5->E5_CLIFOR + SE5->E5_LOJA
		
		If SE5->E5_MOEDA <> "TC" .OR. SE5->E5_TIPODOC <> "VL" .OR.;
			SE5->E5_RECPAG <> "P" .OR. SE5->E5_SEQ <> cSEQ
			DbSkip()
			Loop
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se for dinheiro devera atualizar o Saldo Bancario³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ								
		If IsMoney(SE1->E1_TIPO) 		
 			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza saldo do BANCO Caixa ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			AtuSalBco(SE5->E5_BANCO, SE5->E5_AGENCIA, SE5->E5_CONTA, dDataBase, SE5->E5_VALOR, "-")
	    	lDelSE5:= .T.
	    EndIf	                                  

		If oIntCVenda <> Nil
		   	oIntCVenda:Inserir("SE5", xFilial("SE5") + SE5->E5_NATUREZ + SE5->E5_PREFIXO + SE5->E5_NUMERO + SE5->E5_PARCELA + SE5->E5_TIPO + DTOS(dDatabase), "4", "5")
			oIntCVenda:Gerar()		   	
		EndIf
	    
		RecLock("SE5",.F.)
		SE5->( DbDelete() )

		//Adiciona a tabela para exclusao no processo off-line
		If lAmbOffLn .And. (oProcOff <> Nil)
			oProcOff:Inserir("SE5", xFilial("SE5") + SE5->E5_NATUREZ + SE5->E5_PREFIXO + SE5->E5_NUMERO + SE5->E5_PARCELA + SE5->E5_TIPO + DTOS(dDatabase), 4, "DELETE")
		Endif

		SE5->( MsUnlock() )
		SE5->( DbSkip() )
	End
EndIf

RestArea(aAreaSE5)
RestArea(aAreaAtu)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³lj140ExBan    ³ Autor ³ Vendas Clientes       ³ Data ³ 24.11.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Excluir os registros de movimentação bancaria                   ³±±
±±³          ³ Integridade Referencial                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function lj140ExBan(lDelSE5)

Local aE5Docum 	:= {}   // Array com os numeros do SE5 das NCC que deverão ser excluidas
Local nI 		:= 0	// Contador de For 
Local nTamChave := (TamSx3("E5_PREFIXO")[1]) + (TamSx3("E5_NUMERO")[1]) + (TamSx3("E5_PARCELA")[1]) + (TamSx3("E5_TIPO")[1]) // 16
Local cChvSe5	:= Space(nTamChave)  
Local lAmbOffLn	:= SuperGetMv("MV_LJOFFLN", Nil, .F.)	//Identifica se o ambiente esta operando em offline

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//|    Excluir os registros de movimentação bancaria - Integridade Referencial                   |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

cChaveSE1 := SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA+SE1->E1_TIPO+SE1->E1_CLIENTE+SE1->E1_LOJA

DbSelectArea("SE5")
DbSetOrder(7)
If DbSeek(xFilial("SE5")+cChaveSE1)
	While !Eof() .AND. xFilial("SE5") == SE5->E5_FILIAL .AND. ;
		cChaveSE1 == 	SE5->E5_PREFIXO	+ SE5->E5_NUMERO + SE5->E5_PARCELA + ;
						SE5->E5_TIPO  	+ SE5->E5_CLIFOR + SE5->E5_LOJA

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se for dinheiro devera atualizar o Saldo Bancario antes de excluir o registro do SE5³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ					
		If IsMoney(SE1->E1_TIPO)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza saldo do BANCO Caixa ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			AtuSalBco(SE5->E5_BANCO, SE5->E5_AGENCIA, SE5->E5_CONTA, dDataBase, SE5->E5_VALOR, "-")
			lDelSE5:= .T.
            EndIf

		If oIntCVenda <> Nil
			oIntCVenda:Inserir("SE5", xFilial("SE5") + SE5->E5_NATUREZ + SE5->E5_PREFIXO + SE5->E5_NUMERO + SE5->E5_PARCELA + SE5->E5_TIPO + DTOS(dDatabase), "4", "5")
			oIntCVenda:Gerar()
		EndIf

		RecLock("SE5",.F.)
		SE5->( DbDelete() )
		//Adiciona a tabela para exclusao no processo off-line
		If lAmbOffLn
			oProcOff:Inserir("SE5", xFilial("SE5") + SE5->E5_NATUREZ + SE5->E5_PREFIXO + SE5->E5_NUMERO + SE5->E5_PARCELA + SE5->E5_TIPO + DTOS(dDatabase), 4, "DELETE")
		Endif
		SE5->( MsUnlock() )

		If !Empty(SE5->E5_DOCUMEN) .AND. "CR" $ SE5->E5_TIPO         // Somente adiciona os CR que foram usados para comp as NCCs
			aAdd(aE5Docum,{ SubStr(SE5->E5_DOCUMEN,1,nTamChave),SE5->E5_DATA, SE5->E5_VALOR } )
		EndIf

		SE5->( DbSkip() )
	End
EndIf

For nI = 1 to Len(aE5Docum)			
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//|    Excluir os Tipos NCC da movimentação bancaria caso existam vinculados ao Tipo CR 		|
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ                 	
	cChvSe5 := aE5Docum[nI][01] 
	If DbSeek(xFilial("SE5")+cChvSe5)  
		While !Eof() .AND. xFilial("SE5") == SE5->E5_FILIAL 
			If SubStr(SE5->E5_DOCUMEN,1,nTamChave) $ cChaveSE1 .AND. SE5->E5_DATA == aE5Docum[nI][02] .AND. SE5->E5_VALOR == aE5Docum[nI][03]		  	
				RecLock("SE5",.F.)
					SE5->(DbDelete())
				SE5->(MsUnlock())
				Exit				
			EndIf
			SE5->(DbSkip())
		End
	EndIf
Next nI

//Tratamento para Troco de NCC:
If Len(aE5Docum) > 0 .And. SuperGetMV("MV_LJCPNCC",,1) == 4
	cChvSe5 := SE1->E1_PREFIXO+SE1->E1_NUM
	SE5->(DbSetOrder(7))
	If SE5->(DbSeek(xFilial("SE5")+cChvSe5))
		While SE5->(!Eof()) .AND. xFilial("SE5") == SE5->E5_FILIAL .AND. cChvSe5 == SE5->E5_PREFIXO+SE5->E5_NUMERO
							
			If SE5->E5_MOEDA <> "TC" .OR. SE5->E5_TIPODOC <> "VL" .OR. SE5->E5_RECPAG <> "P"
				SE5->(DbSkip())
				Loop
			EndIf

			SE5->(RecLock("SE5",.F.))
			SE5->( DbDelete() )
			//Adiciona a tabela para exclusao no processo off-line
			If lAmbOffLn .And. (oProcOff <> Nil)
				oProcOff:Inserir("SE5", xFilial("SE5") + SE5->E5_NATUREZ + SE5->E5_PREFIXO + SE5->E5_NUMERO + SE5->E5_PARCELA + SE5->E5_TIPO + DTOS(dDatabase), 4, "DELETE")
			Endif
			SE5->( MsUnlock() )
			SE5->( DbSkip() )
		End
	EndIf
EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140CnCP     ³ Autor ³ Vendas Clientes       ³ Data ³ 24.11.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Excluir registro do Contas a Pagar (quando houver Taxa de ISS) ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Lj140CnCP()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//|    Excluir registro do Contas a Pagar (quando houver Taxa de ISS)                                       |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SE2")
DbSetOrder(1)
If DbSeek(xFilial("SE2")+SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA+"TX")
	If Alltrim(SE2->E2_NATUREZ) == "ISS"
		RecLock("SE2",.F.)
		SE2->( DbDelete() )
		SE2->( MsUnlock() )
	EndIf
EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³lj140ExDin    ³ Autor ³ Vendas Clientes       ³ Data ³ 24.11.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Se for Dinheiro e SE5 nao estiver sido excluido acima          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function lj140ExDin(lDelSE5, cChave, aParc)

Local nI		:= 0								// Contador de For
Local lAmbOffLn := SuperGetMv("MV_LJOFFLN", Nil, .F.)	//Identifica se o ambiente esta operando em offline

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se for Dinheiro e SE5 nao estiver sido excluido acima³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
If SL1->L1_DINHEIRO <> 0 .AND. !lDelSE5
	DbSelectArea("SE5")
	DbSetOrder(2)
	If cPaisLoc == "BRA"
		If DbSeek(xFilial() + "LJ" + cChave + Space(TamSX3("E5_PARCELA")[1]) + SuperGetMv("MV_SIMB1") + " ")

			If oIntCVenda <> Nil
				oIntCVenda:Inserir("SE5", xFilial("SE5") + SE5->E5_NATUREZ + SE5->E5_PREFIXO + SE5->E5_NUMERO + SE5->E5_PARCELA + SE5->E5_TIPO + DTOS(dDatabase), "4", "5")
				oIntCVenda:Gerar()				
			EndIf

			RecLock("SE5",.F.,.T.)
			SE5->( dbDelete() )
			//Adiciona a tabela para exclusao no processo off-line
			If lAmbOffLn
				oProcOff:Inserir("SE5", xFilial("SE5") + SE5->E5_NATUREZ + SE5->E5_PREFIXO + SE5->E5_NUMERO + SE5->E5_PARCELA + SE5->E5_TIPO + DTOS(dDatabase), 4, "DELETE")
			Endif
			SE5->( MsUnlock() )

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza saldo do BANCO Caixa ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			AtuSalBco(SE5->E5_BANCO, SE5->E5_AGENCIA, SE5->E5_CONTA, dDataBase, SE5->E5_VALOR, "-")
			
		EndIf
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//|Devido ao tratamento de multi-moeda uma venda pode gerar mais de um registro em Mov. Bancario p/ dinheiro|
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		For nI := 1 To Len(aParc)
			If DbSeek(xFilial() + "LJ" + cChave + aParc[nI,1] + aParc[nI,2])
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Atualiza saldo do BANCO Caixa ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				AtuSalBco(SE5->E5_BANCO,SE5->E5_AGENCIA,SE5->E5_CONTA,dDataBase,SE5->E5_VALOR,"-")

				If oIntCVenda <> Nil
					oIntCVenda:Inserir("SE5", xFilial("SE5") + SE5->E5_NATUREZ + SE5->E5_PREFIXO + SE5->E5_NUMERO + SE5->E5_PARCELA + SE5->E5_TIPO + DTOS(dDatabase), "4", "5")
					oIntCVenda:Gerar()
				EndIf

				RecLock("SE5",.F.,.T.)
				dbDelete()
				//Adiciona a tabela para exclusao no processo off-line
				If lAmbOffLn
					oProcOff:Inserir("SE5", xFilial("SE5") + SE5->E5_NATUREZ + SE5->E5_PREFIXO + SE5->E5_NUMERO + SE5->E5_PARCELA + SE5->E5_TIPO + DTOS(dDatabase), 4, "DELETE")
				Endif
				MsUnlock()
			EndIf
		Next nI
	EndIf
	DbSetOrder(1)
EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140CnSE5    ³ Autor ³ Vendas Clientes       ³ Data ³ 24.11.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Realiza a exclusao do SE1 apos o SE5                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Lj140CnSE5(cChave)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Realiza a exclusao do SE1 apos o SE5 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SE1")
If DbSeek(xFilial("SE1")+cChave)
	While !Eof() .AND. xFilial("SE1")==SE1->E1_FILIAL .AND. cChave == SE1->(E1_PREFIXO+E1_NUM)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se a origem for pelo financeiro, nao efetua a exclusao do titulo    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If "FIN"$SE1->E1_ORIGEM
			SE1->(DbSkip())
			Loop
		EndIf

		If oIntCVenda <> Nil
			oIntCVenda:Inserir("SE1", xFilial("SE1") + SE1->E1_PREFIXO + SE1->E1_NUM + SE1->E1_PARCELA + SE1->E1_TIPO, "1", "5")
			oIntCVenda:Gerar()
		EndIf
		
		Reclock("SE1")
		dbDelete()
		MSUnlock()
		dbSkip()
	End
EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140CnTEF    ³ Autor ³ Vendas Clientes       ³ Data ³ 24.11.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Cancela caso o tef Esteja Ok                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function Lj140CnTEF(lTefOk	, lJob		, lVendTef	, cChave	,;
						   cNumNota	, cSerNota	, cCliNota	, cCliLoja	,;
						   cDescMot	, lAchouSF2	, lExisteFT	, lNfCup	,;	
						   aItensSD2, cNumNfCup	, cSerNfCup	, nTamDoc	,;
						   lCrdxInt	, aRetCrd	, lGeraNCC	, aItensSF2	,;
						   aItensSD2, cNumNFDev	, cSerieDev	, cMotivo	,;
						   cNum 	, aRetCartao, aCartaoSL4 )
						   
Local nI		 	:= 0							// Contador de For
Local nVlrOrc	 	:= 0							// Valor original do orcamento com o desconto sobre os itens aplicado
Local nTamL1_DOC	:= TamSx3("L1_DOC")[1]		    // Tamanho Nota Fiscal
Local cPadrao		:= "701"                       	// Codigo Padrao Cabecalho
Local cPadraoIt	:= "730"							// Codigo Padrao Item
Local lPadrao  	:= VerPadrao(cPadrao)			// verifica padrao
Local lPadraoIt	:= VerPadrao(cPadraoIt)			// verifica padrao Item
Local nHdlPrv  		:= 0							// Contabilizacao
Local nTotal   		:= 0							// Contabilizacao
Local cArquivo 		:= ""							// Contabilizacao
Local lAglutina 	:= .T.							// Contabilizacao
Local lMVLJPRDSV   	:= SuperGetMv("MV_LJPRDSV",.F.,.F.)	// Verifica se esta ativa a implementacao de venda com itens de "produto" e itens de "servico" em Notas Separadas
Local lDelNfRPS   	:= .T.
Local lFisLivro		:= (SuperGetMV("MV_LJLVFIS",,1) == 2)	// Utiliza novo conceito para geracao do SF3
Local lIntegHtl		:= SuperGetMv("MV_INTHTL",, .F.) .And. FWHasEAI("LOJA140",, .T., .T.) //Integracao via Mensagem Unica - Hotelaria
					
cNumNota := PadR(cNumNota,nTamL1_DOC)

If lTefOk
	If !lJob .AND. lVendTef
		RecLock("SL1",.F.)
		If cTipTef $ TEF_CLISITEF
			Replace SL1->L1_DOCCANC with aRetCartao[1]:cDocCanRei
			Replace SL1->L1_DATCANC with StrTran(DToC(aRetCartao[1]:dDataCanRei),"/","")
			Replace SL1->L1_HORCANC with aRetCartao[1]:cHoraTrans
		Else
			If Len(aTefDados) > 0
				Replace SL1->L1_DOCCANC with aTefDados[1][6]
				Replace SL1->L1_DATCANC with aTefDados[1][12]
				Replace SL1->L1_HORCANC with aTefDados[1][7]
			EndIf
		EndIf
		MsUnLock()
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Antes de Excluir Comissao - Verifica o Parametro de Geracao de Comissao - 19/12/95³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Substr(SL1->L1_CONFVEN,11,1) <> "N" // Posicao do Parametro de Geracao de Comissao
		
		DbSelectArea("SE3")   
		DbSetOrder(1)
		If DbSeek(xFilial("SE3")+ cChave )
			While !Eof() .AND. !Empty(SL1->L1_VEND) .AND.;
				cChave == SE3->E3_PREFIXO + SE3->E3_NUM
				
				If !Empty( SE3->E3_DATA )
					If !lExcAuto
						Help( " ",1,"A140COMIS")
					Else 
						//"A Comissao referente a esta Nota ja foi PAGA, portanto ela nao sera excluida. As demais rotinas de exclusao desta Nota continuarao normalmente." 
						Conout(STR0108)  
					EndIf
					dbSkip()
					Loop
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Atualiza‡„o de Comiss”es							    	 ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				RecLock("SE3",.F.,.T.)
				dbDelete( )
				MSUnLock()
				dbSkip()
			End
		EndIf
	EndIf
	
	cNumNota := SL1->L1_DOC
	cSerNota := SL1->L1_SERIE
	cCliNota := SL1->L1_CLIENTE
	cCliLoja := SL1->L1_LOJA
	
	While .T.
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualiza‡„o dos Arquivos do Faturamento ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Pontos de Entrada  						³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ExistTemplate("LJ140DEL")
			ExecTemplate("LJ140DEL",.F.,.F., {lJob})
		EndIf
		      
		If nModulo == 72
			KEXF370()
		EndIf
		
		If ExistBlock("LJ140DEL")
			ExecBlock("LJ140DEL",.F.,.F.)
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Atualiza o saldo do cliente                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
		Lj140AtuSald( cChave )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Executa o cancelamento da NF/CF pelo modo antigo,³
		//³caso o parametro MV_LJLVFIS esteja como 1        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lFisLivro
			Lj140CancSF3(@cSerNota, 	@cNumNota, 		@cCliNota, 		@cCliLoja, ;
			             @cDescMot, 	@lAchouSF2, 	@lExisteFT)
		EndIf
							 		
		If oIntCVenda <> Nil
			
			DbSelectArea("SF2")
			DbSetOrder(1)
			
			If DbSeek(xFilial("SF2")+cNumNota+cSerNota)
				oIntCVenda:Inserir("SF2", xFilial("SF2") + SF2->F2_DOC + SF2->F2_SERIE + SF2->F2_CLIENTE + SF2->F2_LOJA, "1", "5")
				oIntCVenda:Gerar()
			EndIf		
		EndIf

		Lj140CnSD2(@cNumNota	,@cSerNota	,@lNfCup	,@aItensSD2	)
		
		Lj140CnSF2(@cNumNota	,@cSerNota	,@cNumNfCup	,@cSerNfCup	, ;
					 @lNfCup	,@aItensSF2	,@nTamDoc	)
					 
		If lPadrao .AND. (cPaisLoc == "MEX" .OR. lIntegHtl) //Mexico ou Integracao Hotelaria verifica Lancamento Padrao
			
			nHdlPrv:=HeadProva("","LOJA140",Substr(cUsername,1,6),@cArquivo)
				
			If  nHdlPrv > 0
				nTotal+=DetProva(nHdlPrv,cPadrao,"LOJA140","")
			EndIf
						
			If  nTotal > 0
				RodaProva(nHdlPrv,nTotal)
				cA100Incl(cArquivo,nHdlPrv,3,"",.F.,lAglutina)
			EndIf
				
		EndIf
		
		//Contabilizacao por Item - Hotelaria
		If lPadraoIt .AND. lIntegHtl
			SD2->(dbSetOrder(3))
			
			If SD2->(dbSeek(xFilial("SD2") + SF2->F2_DOC + SF2->F2_SERIE + SF2->F2_CLIENTE + SF2->F2_LOJA))
				While !SD2->(EOF()) .AND. SD2->D2_FILIAL == SF2->F2_FILIAL .AND. SD2->D2_DOC == SF2->F2_DOC .AND.;
					SD2->D2_SERIE == SF2->F2_SERIE .AND. SD2->D2_CLIENTE == SF2->F2_CLIENTE .AND.; 
					SD2->D2_LOJA == SF2->F2_LOJA 				
					
					//Inicializa variaveis
					cArquivo := ""
					nTotal	  := 0
					nHdlPrv  := 0
					
					//Inicia contabilizacao										
					nHdlPrv := HeadProva("", "LOJA140", Substr(cUsername,1,6), @cArquivo)
					
					If nHdlPrv > 0
						nTotal := DetProva(nHdlPrv, cPadraoIt, "LOJA140","")
					EndIf
					
					If nTotal > 0
						RodaProva(nHdlPrv, nTotal)
						cA100Incl(cArquivo, nHdlPrv, 3, "", .F., lAglutina)
					EndIf
					
					SD2->(dbSkip())
				EndDo
			EndIf
		EndIf
		
		If lMVLJPRDSV .And. Empty(cNumNota) .And. !Empty(SL1->L1_DOCRPS) .And. SL1->L1_DOC <> SL1->L1_DOCRPS .And. lDelNfRPS
			cNumNota := SL1->L1_DOCRPS
			cSerNota := SL1->L1_SERRPS
			lDelNfRPS := .F.
		EndIf
		If !Empty(cNumNota)
			Loop
		EndIf
		Exit
	End
	
	Lj140CnFin(cChave, lCrdxInt, @aRetCrd)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se existe amarracao com lay-away e faz a reabertura se necessario³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cPaisLoc $ "EUA|POR|MEX"
		DbSelectArea("SL2")
		DbSetOrder(1)
		DbSeek( xFilial("SL2")+SL1->L1_NUM )
		
		DbSelectArea("SLP")
		DbSetOrder(4)
		
		While xFilial("SL2")+SL1->L1_NUM == SL2->L2_FILIAL+SL2->L2_NUM
			If DbSeek(xFilial("SLP")+SL2->L2_NUM+SL2->L2_ITEM)
				a800AbreLW(SL2->L2_NUM,SL2->L2_ITEM)
			EndIf
			SL2->(dbSkip())
		End
	EndIf
	
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Gerar Nota de Credito ao Cliente - Loc. Guatemala                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cPaisLoc == "GUA" .AND. lGeraNCC
		For nI := 1 to Len(aItensSF2)
			Lj140GeraNCC(aItensSF2, aItensSD2, nI, cNumNFDev, ;
						 cSerieDev, cMotivo)
		Next nI
	EndIf

	Lj140LpSl2(cNum, @nVlrOrc)

	Lj140LpSL1(lCrdxInt, cNum, nVlrOrc)
	
	
	SEF->(DbSetOrder(1))
	SE1->(DbSetOrder(1))
	dbCommitAll()
	                 
	If nModulo == 72
		KEXF670()
	EndIf
	
	If ExistBlock("LOJA140")
		ExecBlock("LOJA140",.F.,.F.)
	EndIf
EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140CnAdm    ³ Autor ³ Vendas Clientes       ³ Data ³ 24.11.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Cancela caso o tef na Adm                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj140CnAdm(lJob, lVendTef, oTEF, lTefOk, aRetCartao, aCartaoSL4)

Local cTipTef      := LjGetStation("LG_TIPTEF")		// Tipo do TEF selecionado no cadastro de estacao
Local aPagtoCart   := {}
Local aDocTef	   := {}	
Local nCountCanc   := 0
Local lContinua    := .T.
Local nPos   	   := 0
Local cNumTef	   := ""
Local dDtTef		

Default aRetCartao := {}
Default aCartaoSL4 := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//|  Se usar o Tef ira excluir a venda na administradora antes                                              |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lJob .AND. lVendTef
	If cTipTef $ TEF_SEMCLIENT_DEDICADO + ";" + TEF_COMCLIENT_DEDICADO + ";" + TEF_DISCADO
		lTefOk := LOJA010T( "X" )
	EndIf
	If cTipTef $ TEF_CLISITEF
		aPagtoCart := Lj140Cart() //Verifica a qtde de cartoes utilizados no pagamento da venda
		While lContinua
			oTEF:Operacoes("CANCEL_VENDA", {})
			lTefOk := oTef:lTefOk

			If lTefOk
				
				//Guarda informacoes do cancelamento do cartao em questao para atualizar campos (L4_DOCCANC, L4_DATCANC, L4_HORCANC)
				nPos := Ascan( aPagtoCart, {|x| x[1] == AllTrim(oTef:aRetCartao[1]:cDocCanRei)} )
				If nPos > 0
					//				   RECNO da tabela SL4 ,    Doc. Canc.             ,      Data Canc.   		   ,       Hora Canc.
					aAdd( aCartaoSL4, { aPagtoCart[nPos][2], oTef:aRetCartao[1]:cDocCanRei, oTef:aRetCartao[1]:dDataCanRei, oTef:aRetCartao[1]:cHoraTrans } )
				EndIf

				//Faz uma copia o array "oTef:aRetCartao" para utilizar as informacoes mais adiante, 
				//pois o array "oTef:aRetCartao" sera limpado dentro do metodo "oTEF:ImpCupTef()"
				aRetCartao := aClone(oTef:aRetCartao)
				
				If nModulo <> 12 .Or. !LjNfUsaTef(SM0->M0_CGC) //12=SIGALOJA
					oTEF:ImpCupTef()
				Else 
					oTEF:FinalTrn(1, .T.)  //Confirma transações pendentes
				EndIf
				
				// Aqui lTefOk eh forcado VERDADEIRO, pois a confirmacao ja foi enviada ao SITEF
				lTefOk := .T. 
				
				If lTefOk .And. nPos > 0
					//Atualiza campos (L4_DOCCANC, L4_DATCANC, L4_HORCANC) com informacoes do cancelamento.
					dbSelectArea("SL4")
					dbSetOrder(3) 	//L4_FILIAL + L4_DATATEF + L4_NSUTEF
					SL4->( dbGoTo( aPagtoCart[nPos][2] ) )
					dDtTef	:= SL4->L4_DATATEF
					cNumTef := SL4->L4_NSUTEF
														
 					While SL4->(!Eof()) .And. SL4->L4_DATATEF == dDtTef .And. SL4->L4_NSUTEF == cNumTef
						RecLock("SL4",.F.)
							Replace SL4->L4_DOCCANC with aRetCartao[1]:cDocCanRei
							Replace SL4->L4_DATCANC with StrTran(DToC(aRetCartao[1]:dDataCanRei),"/","")
							Replace SL4->L4_HORCANC with aRetCartao[1]:cHoraTrans
						SL4->( MsUnLock() )
						
						If aScan( aDocTef,{|x| Alltrim(x[1]) == Alltrim(SL4->L4_DOCTEF)}) == 0 //Tratamento para mais de uma parcela do mesmo cartao. 
							aAdd( aDocTef , { SL4->L4_DOCTEF, SL4->(Recno()) } )
					   	EndIf

						SL4->(dbSkip())
					EndDo

				EndIf			
				lContinua := Len(aDocTef) < Len(aPagtoCart)
			Else
				lContinua := .F.
			EndIf
		End
	EndIf
	If !Valtype(lTefOK) == "L"
		lTefOK := .F.
	EndIf
	
	If !lTefOk
		If IsInCallStack("Lj7CaUlCup") .OR. IsInCallStack("FR271FCancCup") .OR. !lExcAuto
	   		Help(" ",1,"TEFOK") 
	   	Else 
	   		//"O sistema nao conseguiu comunicacao com a Rede TEF, as alteracoes nao serao gravadas." 
			Conout(STR0109) 
	   	EndIf	
		If cTipTef $ TEF_CLISITEF
			oTef:FinalTrn(1)
		Else
			LOJA010T( "F", "N" )
		EndIf
		Return NIL
	EndIf
EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140VlVend   ³ Autor ³ Vendas Clientes       ³ Data ³ 24.11.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Verifica se a venda pode ser cancelada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Lj140VlVend(lFiscal		, cNumCupFis, cNumPdvAtu	, cSerie	,;
							cAlias		, nOpcA		, lGeraNCC 		, lLj140Can	,;
							nTamL1Cup	, aOrcamento, lSubClosed	, lECCia) 

Local cRet  	:= Space(10)	// Retorno utilizado status do cupom
Local nRet		:= 0			// Retorno do cupom
Local aAreaSL1	:= {}			// armazena a area do orcamento principal(orcamento pai) em caso de reservas
Local aAreaSF2	:= {}			// armazena a area do documento fiscal vinculado ao orcamento principal (orcamento pai)
Local nI		:= 1			// contador
Local cL1Doc	:= ""			// armazena o numero do cupom fiscal
Local lVendaFutura := SuperGetMv("MV_LJVFNFS",Nil, .F.) //Venda Futura {Nota de simples faturamento)

Default aOrcamento	:= {}
Default lSubClosed	:= .F. 
Default lECCia := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Tratamento para orcamentos com filho										³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty( SL1->L1_FILRES ) .AND. !Empty( SL1->L1_ORCRES )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//|  "Este orcamento não poderá ser excluido porque se trata de um orçamento com reserva."|
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !lExcAuto
		MsgStop( STR0037 , STR0006 )
		Return .F.
	Else 		
		lMsErroAuto := .T.
		Help(" ",1,"NO140CANC",,STR0037,3,1)				
		Conout(STR0037)
		Return .F.
	EndIf	
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se foi chamada pela venda rapida³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If FunName()='LOJA220'

	If !lFiscal
		Return .F.
	EndIf

	nRet := IFStatus( nHdlECF, '5', @cRet )				// Verifica se o cupom está aberto
	If nRet == 7
		If !lExcAuto 
			MsgInfo( STR0024 )								//'Cupom nao foi finalizado'
			Return .F.
		Else 
			Conout( STR0024 )
			Return ( .F. )	
		EndIf	
	EndIf

	nRet := IFPegCupom( nHdlECF, @cNumCupFis)
	nRet := IFPegPDV( nHdlECF, @cNumPdvAtu )
	If Empty(cNumPdvatu) .OR. Empty(cNumCupFis)
		If !lExcAuto
	   		MsgInfo( STR0025 )								//'Erro de comunicacao com a impressora fiscal'
	   		Return .F.
	   	Else 
	   		Conout( STR0025 )
			Return ( .F. )
	   	EndIf	
	EndIf

	cNumCupFis := Repl('0',nTamL1Cup - Len( Alltrim( cNumCupFis ) ) ) + Alltrim( cNumCupFis )
	cSerie     := cSerie + Space( TamSX3("L1_SERIE")[1] - Len( cSerie ) )
	DbSelectArea("SL1")
	DbSetOrder(2)
	If !DbSeek( xFilial("SL1") + cSerie + cNumCupFis + cNumPdvAtu )
		If !lExcAuto 
			MsgInfo( STR0026 + cNumCupFis )					//Nao foi encontrado registro do cupom :
			Return .F.
		Else
			Conout( STR0026 + cNumCupFis )
			Return ( .F. )
		EndIf		
	EndIf

EndIf

If !lExcAuto
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ A ocorrencia 42 (ACS), verifica se o usu rio poder  ou n„o  ³
	//³ efetuar a exclusÆo da nota fiscal. 						    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !ChkPsw( 42 )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Restaura a integridade da janela ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea(cAlias)
		Return .F.
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Confere a configuracao do caixa, verificando se o mesmo     ³
	//³ possui permissao para o cancelamento do cupom.              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !LJProFile(8,,,,, .T. )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Restaura a integridade da janela ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea(cAlias)
		Return .F.
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se Caixa esta Aberto  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lFiscal .AND. !ljCxAberto()
		Return .F.
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³verifica se o orcamento possui algum Pedido de Venda e se ele esta faturado|
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !LJ140VldPV(aOrcamento)
	Return .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³validacao: verifica se existem sub-orcamentos finalizados com cupons fiscais diferentes|
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lSubClosed
	aAreaSL1 := SL1->( GetArea() )

	For nI := 1 To Len(aOrcamento)

		If !Empty(aOrcamento[nI][1]) .AND. !Empty(aOrcamento[nI][2]) .AND. aOrcamento[nI][3] 	//verifica se eh um sub-orcamento finalizado
			If SL1->( DbSeek(aOrcamento[nI][1] + aOrcamento[nI][2]) ) 							//posiciona no sub-orcamento
				If Empty(cL1Doc)
					cL1Doc := AllTrim(SL1->L1_DOC)												//guardo o numero do cupom fiscal do sub-orcamento
					SL1->( RestArea(aAreaSL1) )
				ElseIf !Empty(SL1->L1_DOC) .AND. cL1Doc <> AllTrim(SL1->L1_DOC)  				//comparacao de cupons fiscais
					lRet := .F.
					MsgStop( STR0120, STR0006 )
					//"Nao e permitido excluir um orcamento aglutinador quando mais de um sub-orcamento estiver finalizado." | "Atencao"
					Exit
				Else				
					SL1->( RestArea(aAreaSL1) )
				EndIf
			Else
				lRet := .F.
				MsgStop(STR0119 + AllTrim(aOrcamento[nI][2]), STR0006)
				//"Nao foi possivel localizar o sub-orcamento:" | "Atencao"
				Exit
			EndIf
		EndIf

	Next
EndIf

nOpcA := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se tiver algum sub-orcamento finalizado, deve-se posicionar nele para que seja feitas as validacoes|
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lSubClosed
	aAreaSL1 := SL1->( GetArea() )
	aAreaSF2 := SF2->( GetArea() )
	If !Lj140Pos(aOrcamento)
		Return .F.
	EndIf
EndIf

//---
// Verifica se a data da nota é menor que a database, caso seja, deve-se fazer a devolucao da nota
//---
If !Empty(SL1->L1_DOC) .AND. !lIsVendaVP .AND. !lIsVdRecCP
	SF2->( DbSetOrder(1) )
	If SF2->( DbSeek( xFilial("SF2") + SL1->L1_DOC + SL1->L1_SERIE ) )
		If (SF2->F2_EMISSAO) < (dDataBase) .AND. Alltrim(SF2->F2_ECF) == "S" .AND. nModulo <> 5 .AND. FunName() <> "STBORCCANCEL"
			//"Esta nota fiscal nao pode ser excluida. A data de emissao da nota fiscal nao pode ser menor que a database." 
			If lExcAuto
				lMsErroAuto := .T.
			EndIf
			
			Help(" ", 1, "LJ140EXC", Nil, STR0110)
			Return .F.
		Else
		EndIf
	Else
		//Avisa quando venda nao foi processada no GrvBatch
		If AllTrim(SL1->L1_SITUA) == "RX"
 			//#"Cancelamento não poderá ser realizado." #"Processo de Integração ERP(Job GravaBatch) não foi realizado para essa venda."
			If lExcAuto
				lMsErroAuto := .T.
			EndIf
			
			conout( (STR0125 + STR0126) )
			Help( ,, "NO140CANC",, STR0125 + STR0126, 1, 0 )
		Else
			//#"Não foi possível localizar registro SF2 para o Doc.:"  #" - Serie:"
			If lExcAuto
				lMsErroAuto := .T.
			EndIf
			
			conout( (STR0123 + SL1->L1_DOC + STR0124 + SL1->L1_SERIE) )
			Help( ,, "NO140CANC",, (STR0123+SL1->L1_DOC+STR0124+SL1->L1_SERIE), 1, 0 )
		EndIf
		
		Return .F.
	EndIf

ElseIf cPaisLoc == "GUA" .AND. lGeraNCC
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//|    "Aten‡„o"###"Nao e permitido gerar Nota de Credito para cancelamento de orcamento."###"OK"           |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !lExcAuto
		Aviso(STR0006, STR0071, {STR0048} )
		Return .F.
	Else 
		Conout( STR0071 )
		Return ( .F. )
	EndIf	
EndIf

If Empty(SL1->L1_NUM)	// Verifica se Nao ‚ Devolucao
	If !lExcAuto
		Help(" ",1,"NFDEVOL")
		Return .F.
	Else
		//"Este registro refere-se a devolucao de produtos para fornecedores, portanto somente podera ser excluido na rotina de Devolucao de Compras."
		lMsErroAuto := .T.
		Conout(STR0111)
		Return ( .F. )
	EndIf		
Else
	If !lIsVendaVP .And. !lIsVdRecCP
		If !Empty(SL1->L1_DOC) .And. AllTrim(SF2->F2_ECF) == "S"
			If Month( SF2->F2_EMISSAO ) <> Month( dDataBase ) .OR. Year( SF2->F2_EMISSAO ) <> Year( dDataBase )
				If !lExcAuto
					Help(" ",1,"FORA_MES")
					Return .F.
				Else
					//"Nao se pode excluir uma nota fiscal quando o mes ou ano de sua emissao for diferente da database do sistema."  
					lMsErroAuto := .T.
					Help(" ",1,"FORA_MES") 
					Conout(STR0112)
					Return ( .F. )
				EndIf	
			EndIf
		EndIf
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o caixa pode excluir a Nota Fiscal ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty( SL1->L1_DOC )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Ponto de Entrada para n„o considerar tais condi‡oes³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !lLj140Can
		If __cUserID <> "000000" 		 //Se Administrador, pode excluir a nota
			If xNumCaixa() <> SL1->L1_OPERADO
				If !ChkPsw(42)
					If !lExcAuto
						Help(" ",1,"OUTROCAIXA")
						Return .F.
					Else
						//"Esta operacao foi realizada por outro caixa, por isso so podera ser desfeita pelo caixa de origem, Administrador ou Usuario com autorizacao definida na senha." 
						lMsErroAuto := .T.
						Help(" ",1,"OUTROCAIXA")
						Conout(STR0113)
					EndIf
				EndIf
			EndIf
		EndIf
	EndIf
EndIf

If LjAnalisaLeg(6)[1]
	If SL1->(ColumnPos("L1_STORC")) > 0
		If SL1->L1_STORC == 'C' .AND. Alltrim(SL1->L1_SITUA) <> "OK"
			If !lExcAuto
				MsgStop(STR0085, STR0086) //"Este orçamento já foi Cancelado" ### "Atenção"
				Return(.F.)
			Else 
				lMsErroAuto := .T.
				Conout( STR0085 )
				Return( .F. )
			EndIf
		EndIf
	EndIf
EndIf

IF SL1->(ColumnPos("L1_STATUES")) > 0    //Estorno
   If !Empty(SL1->L1_STATUES)
	  If !lExcAuto
		  MsgStop(STR0097, STR0006) //"Esta venda já foi estornada." ### "Atenção"
		  Return.F.
	  	Else	
	      lMsErroAuto := .T.
	      Conout( STR0097 )
	      Help(" ",1,"NO140CANC",,STR0097,3,1)
	      Return( .F. )
	 	EndIf
   Endif
Endif

If cPaisLoc $ "MEX" .AND.  !Empty(SF2->F2_NFORI)
	MsgAlert(STR0090 , STR0086)			// #"Nao foi possivel Cancelar pois ja existe uma Fatura Global para esta venda!"	# "Atencion"
	Return .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Release 11.5 - Chile - F2CHI                                ³
//³Nao se cancela venda no Chile.Para qualquer venda que       ³
//³precise ser cancelada e necessario criar uma nota de credito³
//³atraves do processo de devolucao.                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cPaisLoc == "CHI"
	If !Empty(SL1->L1_DOC) .AND. !Empty(SL1->L1_SERIE)
		MsgAlert(STR0099 , STR0086)// #"Cancelamento de venda não permitido.Você deve gerar uma nota de crédito através da rotina de Troca/Devolução."# "Atencion"	
		Return .F.
	EndIf
EndIf

//Verificação rferente a venda futura:
If lVendaFutura .AND. !lECCia
	If !Empty(SL1->L1_DOC) .And. !Empty(SL1->L1_DOCPED) .And. SL1->L1_DOC == SL1->L1_DOCPED
		MsgAlert(STR0129 , STR0086)// #Venda ja finalizada no PDV." # "Atencion"
		Return .F.
	EndIf
EndIf

If lECCia   
	If !Lj140VlEC()
		Return .F.
	EndIf
EndIf

//retorno a area do orcamento principal
If lSubClosed
	SL1->( RestArea(aAreaSL1) )
	SF2->( RestArea(aAreaSF2) )	
EndIf


Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140VlDev    ³ Autor ³ Vendas Clientes       ³ Data ³ 24.11.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Verifica se a venda ja foi devolvida                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Lj140VlDev(lVendido, nCnt, aOrcamento, lSubClosed)
        
Local lRet		:= .T.	//variavel de retorno
Local aArea		:= {}	// armazena a area do orcamento principal(orcamento pai) em caso de reservas

Default lSubClosed	:= .F.
Default aOrcamento	:= {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se tiver algum sub-orcamento finalizado, deve-se posicionar nele para que sejao feitas as validacoes|
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lSubClosed
	aArea := SL1->( GetArea() )
	If !Lj140Pos(aOrcamento)
		Return .F.
	EndIf
EndIf

If !Empty( SL1->L1_DOC ) .AND. lRet
	SD2->( dbSetOrder( 3 ) )
	SD2->( dbSeek( xFilial( "SD2" ) + SL1->L1_DOC + SL1->L1_SERIE ) )
	While SD2->(!EoF() .AND. SD2->D2_DOC == SL1->L1_DOC .AND. SD2->D2_SERIE == SL1->L1_SERIE )
		If SD2->D2_QTDEDEV > 0
			If !lExcAuto
				Help(" ","1","JATROCADO")
				lRet  := .F.
				Exit
			Else 
				Conout(STR0114)
				lRet := .F.
				Exit
			EndIf
		Endif

		SD2->( dbSkip() )
	End
EndIf
             
//retorno a area do orcamento principal
If lSubClosed
	SL1->( RestArea(aArea) )
EndIf

Return (lRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140VlImp    ³ Autor ³ Vendas Clientes       ³ Data ³ 24.11.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Verifica se a venda foi cancelada na impressora atual          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Lj140VlImp( lVendido, cNumPdv	, lFiscal	, cNumPdvAtu,;
							cNumAnt	, nTamL1Cup	, aOrcamento, lSubClosed,;
							lLstPre )

Local iRet 			:= 0	//Retorno da impressora
Local cL1Numcfis	:= ""   //Recebe o Valor do L1_NUMCFIS
Local aArea			:= {}	//armazena a area do orcamento principal(orcamento pai) em caso de reservas
Local lMVLJPRDSV   	:= SuperGetMv("MV_LJPRDSV",.F.,.F.)	// Verifica se esta ativa a implementacao de venda com itens de "produto" e itens de "servico" em Notas Separadas (RPS)
Local lLiberaCan 	:= .T. 	//Controla se Libera o cancelamento da venda
Local lEmitNfce		:= ExistFunc("LjEmitNFCe") .AND.  LjEmitNFCe() // Sinaliza se utiliza NFC-e   
Local lVendaNfce	:= .F. //Sinaliza venda com emissao de NFCe
Local nLenAnt		:= 0
Local  lECCia :=   SuperGetMv("MV_LJECOMO", .T., .F.)  .AND.  SL1->(ColumnPos("L1_ECFLAG") > 0) .AND. (SL1->L1_ECFLAG == "1")  


Default aOrcamento 	:= {}
Default lSubClosed	:= .F.
Default lLstPre		:= .F.

//Sinaliza que venda emitiu NFC-e para nao validar venda via ECF 
lVendaNfce := IIF(lEmitNfce .AND. SL1->(ColumnPos("L1_KEYNFCE")) > 0 .AND. !Empty(SL1->L1_KEYNFCE), .T., .F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se foi cupom fiscal e se ‚ a impressora que o emitiu ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty( SL1->L1_PDV ) .AND. lVendido .AND. FunName()<>'LOJA220' .AND. !lVendaNfce .And. !lIsVendaVP .And. !lIsVdRecCP .And. !lLstPre
	cNumPdv := SL1->L1_PDV

	If !lFiscal  .AND. !lECCia
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//|    Esta venda s¢ poder  ser exclu¡da pela impressora fiscal                                             |
		//|    que efetuou a mesma. Numero da impressora que registrou a venda:                                     |
		//|    Aten‡„o                                                                                              |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lExcAuto
			MsgStop( STR0004 + STR0005 + cNumPdv, STR0006 )
			Return .F.
		Else 
			Conout( STR0004 + STR0005 + cNumPdv )
			Return ( .F. )
		EndIf	
	Else
		
		cNumPdvAtu := Space(10)
		iRet := IFPegPDV( nHdlECF, @cNumPdvAtu )
		
		
		If AllTrim( cNumPdv ) <> AllTrim( cNumPdvAtu )	.AND. !lECCia		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|   Esta venda foi efetuada por outro caixa                                                               |
			//|   O PDV que efetuou a venda foi:                                                                        |
			//|   Aten‡„o                                                                                               |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !lExcAuto 
		   		MsgStop( STR0007 + STR0008 + cNumPdv, STR0006 )
		   		Return .F.
		   	Else
		   		Conout ( STR0007 + STR0008 + cNumPdv )
				Return ( .F. )
			EndIf		
		EndIf

	EndIf
	
	If lFiscal
	
		cNumAnt := Space(10)
		If cPaisLoc == "ARG"
			If !lExcAuto
				Aviso( STR0047,STR0049,{ STR0048 } )	//"Atencao"###"Para cancelar um cupom fiscal gerar uma Nota de Credito."###"OK"
				Return .F.
			Else 
				Conout ( STR0049 )
			    Return ( .F. )
			EndIf	
		Else
			iRet := IFPegCupom( nHdlECF, @cNumAnt, "T" )
			nLenAnt	 := Len(AllTrim(cNumAnt))
			cNumAnt := StrZero(Val(cNumAnt),nTamL1Cup)
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se tiver algum sub-orcamento finalizado, deve-se posicionar nele para que seja feitas as validacoes|
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lSubClosed
			aArea := SL1->( GetArea() )
			If !Lj140Pos(aOrcamento)
				Return .F.
			EndIf
		EndIf

		cL1Numcfis  := StrZero(Val(SL1->L1_NUMCFIS),nTamL1Cup) 

		//Caso nao seja o ultimo cupom, verifica se eh uma venda apenas de "Servico" (RPS)
		If cL1Numcfis <> cNumAnt
			If !Empty(SL1->L1_DOCRPS) .And. SL1->L1_DOC+SL1->L1_SERIE == SL1->L1_DOCRPS+SL1->L1_SERRPS
				//Verifica se pode excluir
				lLiberaCan := Lj140LbRPS()
			EndIf
		EndIf
		
		//Valida se e' o ultimo cupom fiscal, antes de prosseguir com a exclusao
		If cL1Numcfis  <> cNumAnt  .AND. Lj140LibCanc() .And. !lLiberaCan .AND. FunName() <> "STBORCCANCEL" 
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|   Por se tratar de impressora fiscal, s¢ poder  ser excluido o £ltimo                                   |
			//|   Cupom vendido. (Vide convˆnio ICMS - 156/96)                                                          |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			SL1->(DbSetOrder(1))
			If !lExcAuto
				MsgStop( STR0009 + STR0010 )
				Return .F.
			Else 
				Conout ( STR0009 + STR0010 )
				Return ( .F. )
			EndIf	
		EndIf

		//retorno a area do orcamento principal
		If lSubClosed
			SL1->( RestArea(aArea) )
		EndIf

	EndIf
EndIf

Return .T.

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±º Programa ³ LJ110FillGetº Autor ³ Vendas Clientes   º Data ³ 09/01/2007 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Desc.    ³ Montagem do aHeader e aCols pelo Protheus 9.                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºSintaxe   ³ Void LJ110FillGet(ExpN1,ExpC2)                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ ExpN1 = Operacao                                    		   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ Nil                                                 		   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA110                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function LJ140FillGet( nOpc )

Local cSeek		:= 	NIL								// Variavel de busca
Local cWhile	:= 	NIL								// Enquanto
Local aNoFields	:= {}								// Campos que não seram mostrados
Local bCond		:= {|| SL2->L2_STATUS <> "D" }		// Condicao para While
Local bAction1	:= {|| .T. }						// Condicao para While
Local bAction2	:= {|| .F. }						// Condicao para While
Local lInclusao	:= .T. 									// Se eh inclusao

If nOpc <> 3
	lInclusao := .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se for inclusao, nao alimenta as variaveis³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lInclusao
	cSeek := xFilial("SL2") + SL1->L1_NUM
	cWhile := "SL2->L2_FILIAL + SL2->L2_NUM"
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Campo que não sera apresentado³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aAdd(aNoFields,"L2_NUM")
If cPaisLoc <> "BRA"
	aAdd(aNoFields,"L2_ICMSRET")
	aAdd(aNoFields,"L2_BRICMS")
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Montagem aHeader, aCols³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len(aHeader) == 0 .AND. Len(aCols) == 0
	FillGetDados(	nOpc			,	"SL2"							,	1				,	cSeek			,;
					{|| &cWhile}	,	{{bCond,bAction1,bAction2}}		,	aNoFields		,	/*aYesFields*/	,; 
					/*lOnlyYes*/	,	/*cQuery*/						,	/*BuildAcols*/	, 	lInclusao 		,;
					/*aHeaderAux*/	,	/*aColsAux*/					,	/*bAfterCols*/	, 	/*bBeforCols*/ 	)
EndIf
 
Return Nil


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj140OrcOrigº Autor ³ Vendas Clientes  º Data ³  25/10/07   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna os dados do orcamento que originou o titulo no SE1  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Loja140                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Lj140OrcOrig()

Local aArea		:= GetArea()			// Armazena posicionamento atual
Local aAreaSL1	:= SL1->(GetArea())	// Armazena o posicionamento da tabela SL1
Local aDados	:= {}					// Array de retorno

DbSelectArea("SL1")
DbSetOrder(1)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Busca os dados para localizacao do titulo no orcamento³
//³original somente se os campos estiviverem preeenchidos³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty( SL1->L1_FILRES + SL1->L1_ORCRES )
	If DbSeek( SL1->L1_FILRES + SL1->L1_ORCRES )
	
		aDados	:=	{	SL1->L1_FILIAL	,;
						SL1->L1_CLIENTE	,;
						SL1->L1_LOJA	,;
						SL1->L1_SERPED	,;
						SL1->L1_DOCPED	}
	EndIf
EndIf

RestArea(aAreaSL1)
RestArea(aArea)

Return aDados

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³LOJA140   ºAutor  ³Vendas Clientes     º Data ³  28/01/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Efetua o Cancelamento do Pedido de Venda gerado atraves da º±±
±±º          ³ rotina de Reservas / Entrega.                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Loja140                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function LjCancPed(cFilRes, cPedVen)
Local aCabPed	:= {}					//Cabecalho do Pedido
Local aItensPed	:= {}					//Itens do Pedido
Local aLinhaPed	:= {}					//Itens do Pedido
Local cFilBkp	:= cFilAnt				//Guarda a Filial atual
Local lRet		:= .T.					//Variavel de Retorno da Funcao

DEFAULT	cFilRes	:= cFilAnt				//Atribui a Filial Atual
DEFAULT cPedVen	:= ""					//Pedido de Venda

PRIVATE lMsErroAuto	:= .F.				//Determina se houve algum tipo de erro durante a execucao do ExecAuto

If Empty(cPedVen)
	lRet := .F.
EndIf

If lRet
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Altera para a Filial do Pedido.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cFilAnt	:= cFilRes

	DbSelectArea("SC5")
	DbSetOrder(1)	//Filial + Pedido
	If DbSeek(cFilRes + cPedVen)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Guarda o Cabecalho do Pedido³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Aadd(aCabPed,{ "C5_FILIAL",		SC5->C5_FILIAL			,NIL })
		Aadd(aCabPed,{ "C5_NUM",		SC5->C5_NUM				,NIL })

		DbSelectArea("SC6")
		DbSetOrder(1)	//Filial + Pedido
		If DbSeek(SC5->C5_FILIAL + SC5->C5_NUM)

			While !SC6->(Eof()) .AND. SC5->C5_FILIAL + SC5->C5_NUM == SC6->C6_FILIAL + SC6->C6_NUM
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Estorno da Liberacao do Pedido de Venda.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				MaAvalSC6(	NIL,		4,			NIL,		.F.,;
							.F.,		.F.,		.F.,		.F.,;
							.F.,		0,			NIL,		NIL,;
							NIL)

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Guarda os Itens do Pedido   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				Aadd(aLinhaPed,{ "C6_FILIAL",	SC6->C6_FILIAL	 				,NIL })
				Aadd(aLinhaPed,{ "C6_ITEM",		SC6->C6_ITEM					,NIL })
				Aadd(aLinhaPed,{ "C6_PRODUTO",	SC6->C6_PRODUTO		  			,NIL })
				Aadd(aItensPed, aLinhaPed)
				aLinhaPed := {}

				SC6->(DbSkip())
			End
		EndIf
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Apos o Estorno da Liberacao,³
	//³exclui o Pedido de Venda.   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("SC5")
	DbSetOrder(1)	//Filial + Pedido
	If DbSeek(cFilRes + cPedVen)
		MSExecAuto( {|x,y,z| Mata410(x,y,z)} , aCabPed, aItensPed, 5)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se houveram erros durante a exclusao do Pedido.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lMsErroAuto
			lRet := !lMsErroAuto
		EndIf
		If lMsErroAuto
			DisarmTransaction()
			MostraErro()
		EndIf
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Restaura a Filial Original³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cFilAnt	:= cFilBkp
EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj7DefLeg ºAutor  ³Vendas Clientes     º Data ³  24/01/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Define as cores das Legendas                               º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA701                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Lj140DefLeg()

Local cFiltro	:= ""
Local aCores	:= {}
Local lLjPedVe	:= SuperGetMV("MV_LJPEDVE", NIL, .F.)
Local lPosPed	:= SL1->(ColumnPos("L1_PEDRES") ) > 0
Local lEmitNFCe	:= ExistFunc("LjEmitNFCe") .AND.  LjEmitNFCe()

If lEmitNFCe
	cFiltro := '!Empty(L1_DOC) .AND. !Empty(L1_SERIE) .AND. L1_STORC <> "A" .AND. !(L1_SITUA $ "X0|X1|X2|X3") .AND. !(L1_STATUS $ "D|F") .AND. (ColumnPos("L1_STATUES")=0 .OR. Empty(L1_STATUES))'
	Aadd( aCores, {cFiltro, "BR_VERMELHO"} )	//"Vendas efetuadas"
	
	cFiltro := '!Empty(L1_DOC) .AND. L1_TPORC == "E" .AND. L1_SITUA == "RX" .AND. L1_STORC == "A"'
	Aadd( aCores, {cFiltro, "BPMSEDT2"	} )		//"Cancelamento com processamento pendente"
	
	cFiltro := '!Empty(L1_DOC) .AND. L1_TPORC == "E" .AND. L1_SITUA $ "X0|RY"'
	Aadd( aCores, {cFiltro, "BR_PRETO_0"} )		//"Cancelamento não enviado ao TSS"
	
	cFiltro := '!Empty(L1_DOC) .AND. L1_TPORC == "E" .AND. L1_SITUA == "X1"'
	Aadd( aCores, {cFiltro, "BR_PRETO_1"} )		//"Cancelamento aguardando autorização do SEFAZ"
	
	cFiltro := '!Empty(L1_DOC) .AND. L1_TPORC == "E" .AND. L1_SITUA == "X2"'	
	Aadd( aCores, {cFiltro, "BR_PRETO_2"} )		//"Cancelamento autorizado pela SEFAZ"
	
	cFiltro := '!Empty(L1_DOC) .AND. L1_TPORC == "E" .AND. L1_SITUA == "X3"'
	Aadd( aCores, {cFiltro, "BR_PRETO_3"} )		//"Cancelamento não autorizado pela SEFAZ"
Else
	cFiltro := '!Empty(L1_DOC) .AND. !Empty(L1_SERIE) .AND. L1_STATUS <> "D" .AND. (ColumnPos("L1_STATUES")=0 .OR. Empty(L1_STATUES))'	
	Aadd( aCores, {cFiltro, "BR_VERMELHO"})		// "Vendas efetuadas"
EndIf

cFiltro := '!Empty(L1_DOC) .AND. !Empty(L1_SERIE) .AND. L1_STATUS == "T" .AND. (ColumnPos("L1_STATUES")=0 .OR. Empty(L1_STATUES))'
Aadd( aCores, {cFiltro, "BR_BRANCO"		} ) 	// "Transação TEF desfeita"

cFiltro := 'ColumnPos("L1_STATUES")>0 .AND. !Empty(L1_STATUES)'
Aadd( aCores, {cFiltro, "BPMSEDT1"		} )		// "Vendas estornadas" - estorno

cFiltro := 'Empty(L1_DOC) .AND. Empty(L1_RESERVA) .AND. DDATABASE <= L1_DTLIM .AND. L1_STATUS <> "D" .AND. IIf(ColumnPos("L1_STORC")>0, L1_STORC <> "C", .T.) .AND. (ColumnPos("L1_STATUES")=0 .OR. Empty(L1_STATUES))'
Aadd( aCores, {cFiltro, "BR_VERDE"		} )		// "Orçamentos em aberto"

If lLjPedVe .AND. lPosPed
	cFiltro := 'Empty(L1_DOC) .AND. !Empty(L1_RESERVA) .AND. Empty(L1_DOCPED) .AND. L1_STATUS <> "D" .AND. Empty(L1_PEDRES) .AND. (ColumnPos("L1_STATUES")=0 .OR. Empty(L1_STATUES))'
	Aadd( aCores, {cFiltro, "BR_AMARELO"} )		// "Orcamentos com reservas"
	
	cFiltro := 'Empty(L1_DOC) .AND. !Empty(L1_RESERVA) .AND. Empty(L1_DOCPED) .AND. L1_STATUS <> "D" .AND. !Empty(L1_PEDRES) .AND. (ColumnPos("L1_STATUES")=0 .OR. Empty(L1_STATUES))'
	Aadd( aCores, {cFiltro, "BR_LARANJA"} ) 	// "Orcamentos com Pedidos de Venda"
Else
	cFiltro := 'Empty(L1_DOC) .AND. !Empty(L1_RESERVA) .AND. Empty(L1_DOCPED) .AND. L1_STATUS <> "D" .AND. (ColumnPos("L1_STATUES")=0 .OR. Empty(L1_STATUES))'
	Aadd( aCores, {cFiltro, "BR_AMARELO"} ) 	// "Orcamentos com reservas"
EndIf

cFiltro := 'Empty(L1_DOC) .AND. L1_TIPO == "P" .AND. !Empty(L1_DOCPED) .AND. L1_STATUS <> "D" .AND. (ColumnPos("L1_STATUES")=0 .OR. Empty(L1_STATUES))'
Aadd( aCores, {cFiltro, "BR_AZUL"	} )		//"Pedidos encerrados"

cFiltro := 'Empty(L1_DOC) .AND.  Empty(L1_RESERVA) .AND. DDATABASE > L1_DTLIM .AND. L1_STATUS <> "D" .AND. (ColumnPos("L1_STATUES")=0 .OR. Empty(L1_STATUES))'
Aadd( aCores, {cFiltro, "BR_PRETO"	} )		//"Orcamentos em aberto vencidos"

cFiltro := 'L1_STATUS == "D" .AND. (ColumnPos("L1_STATUES")=0 .OR. Empty(L1_STATUES))'
Aadd( aCores, {cFiltro, "BR_MARROM"	} ) 	//"Devoluções pendentes"

cFiltro := 'Empty(L1_DOC) .AND. IIf(ColumnPos("L1_STORC")>0, L1_STORC == "C", .T.)'
Aadd( aCores, {cFiltro, "BR_CINZA"	} )		//"Orçamentos Cancelados"

Return aCores

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj140AtuSaldºAutor  ³ Vendas Clientes  º Data ³  16/07/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Atualiza o saldo do cliente.                                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Loja140                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Lj140AtuSald( cChave )
Local lCartao      := .T.							// Se utiliza cartao

If Substr(SL1->L1_CONFVEN,6,1) <> "N"
	DbSelectArea("SE1")
	If DbSeek(xFilial("SE1")+cChave)
		SAE->( DbSetOrder( 1 ) )
		While !Eof() .AND. xFilial("SE1") == SE1->E1_FILIAL .AND. cChave == SE1->(E1_PREFIXO + E1_NUM)
			
			lCartao := .F.
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se a origem for pelo financeiro, nao efetua a exclusao do titulo    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If "FIN"$SE1->E1_ORIGEM
				SE1->(DbSkip())
				Loop
			EndIf
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|    Se o título não for do cliente verifica se é de alguma administradora financeira,                    |
			//|    se for não roda a rotina de atualização do calso do cliente e pula para o próximo registro.          |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If SL1->L1_CLIENTE+SL1->L1_LOJA <> SE1->E1_CLIENTE + SE1->E1_LOJA
				If SAE->( DbSeek( xFilial( "SAE" ) + SubStr( SE1->E1_CLIENTE,1,3 ) ) )
					lCartao := .T.
				EndIf
			EndIf
			
			If !IsMoney(E1_TIPO) .AND. !lCartao .AND. !(SuperGetMv( "MV_CLIPAD" )+SuperGetMv( "MV_LOJAPAD" ) == SL1->L1_CLIENTE+SL1->L1_LOJA)
				SA1->(DbSetOrder(1))
				SA1->(DbSeek(xFilial("SA1")+SL1->L1_CLIENTE+SL1->L1_LOJA))
				AtuSaldup("-", SE1->E1_VALOR, SE1->E1_MOEDA, SE1->E1_TIPO , , SE1->E1_EMISSAO)
			EndIf
			
			SE1->( DbSkip() )
        End
   EndIf
EndIf
Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj140IntegºAutor  ³Vendas Clientes     º Data ³  27/03/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Responsavel em enviar os dados do cancelamento a integracao º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³LOJA140                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Lj140Integ()
	
//Finaliza - Envia EAI
oIntCVenda:Finalizar()	

oIntCVenda := Nil

Return Nil

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³LjAdicInt   ³ Autor ³ Vendas Clientes       ³ Data ³ 28/07/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Buscar os itens cancelados da venda (SLX) para integracao   	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³LOJA140	                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Lj140AddIn()
	
Local aArea		:= {}							//Guarda a area atual
Local aAreaSLX 	:= {}							//Guarda a area do SLX
Local cChave 	:= ""							//Chave

aArea 		:= GetArea()
aAreaSLX 	:= SLX->(GetArea())	

//Itens cancelados na venda
cChave := xFilial("SLX") + SL1->L1_PDV + SL1->L1_DOC + SL1->L1_SERIE

DbSelectArea("SLX")

DbSetOrder(1)	//LX_FILIAL + LX_PDV + LX_CUPOM + LX_SERIE + LX_ITEM + LX_HORA

If DbSeek(cChave)
	
	While !Eof() .AND. (cChave) == (SLX->LX_FILIAL + SLX->LX_PDV + SLX->LX_CUPOM + SLX->LX_SERIE)
		If AllTrim(SLX->LX_TPCANC) == "I"
			oIntCVenda:Inserir("SLX", xFilial("SLX") + SLX->LX_PDV + SLX->LX_CUPOM + SLX->LX_SERIE + SLX->LX_ITEM + SLX->LX_HORA, "1", "3")		
		EndIf
		SLX->(DbSkip())
	End   	
EndIf

RestArea(aArea)	
RestArea(aAreaSLX)

Return(NIL)       


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj140EvIntºAutor  ³Vendas Clientes     º Data ³  27/03/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Responsavel em enviar os dados do cancelamento a integracao º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA140                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Lj140EvInt()
	                
	//Considera os registros deletados
	SET DELETED OFF
	
	//Processa os dados 
	oProcOff:Processar()	
	
	//Desconsidera os registros deletados
	SET DELETED ON
		
Return Nil


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj140LibCancºAutor  ³ Vendas Clientes  º Data ³  18/05/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Faz a liberacao do Cancelamento da venda Fiscal.            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Loja140                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/      

Static Function Lj140LibCanc()
Local lRet := .T.

If cPaisLoc == "MEX" .AND. DtoS(SL1->L1_EMISNF) == DtoS(dDataBase )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//| Pela Legislacao do Mexico pode-se cancelar qualquer cupom do mesmo dia   |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lRet := .F.	
EndIf
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj140ExCap  º Autor ³ Vendas Clientes  º Data ³  20/04/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Exclui registro do contas a pagar (Quando usar o conceito   º±±
±±º          ³de gerar contas a receber com valor bruto e contas a        º±±
±±º          ³pagar com a taxa adminstrativa. parametro MV_LJGERTX = .T.  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºSintaxe   ³ Lj140ExCap()					                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ Nil			                                    	      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ Nil                                                 	      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Loja140                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß 
*/
Function Lj140ExCap()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Excluir registro do Contas a Pagar    |
//| vinculado ao Contas a Receber         |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
DbSelectArea("SE2")
DbSetOrder(1) //E2_FILIAL+E2_PREFIXO
If DbSeek(xFilial("SE2")+SE1->E1_PREFIXO,.T.)
	While (!Eof() .AND. SE2->E2_PREFIXO == SE1->E1_PREFIXO)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Busca titulos em abertos relacionados atraves do campo ³
		//³historico(E2_HIST) com a mesma sequencia de parcela    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If AllTrim(SE2->E2_HIST) == AllTrim(SE1->E1_NUM) .AND. AllTrim(SE2->E2_PARCELA) == AllTrim(SE1->E1_PARCELA)   
			RecLock("SE2",.F.)
			DbDelete()
			MsUnlock()
		Endif			
		DbSkip()
	End 
EndIf

Return (Nil)  

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj140CHCarreºAutor  ³ Vendas Clientes  º Data ³  14/04/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Carrega o array aCheques	              				      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Loja140                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/      
Function Lj140CHCarrega(cChave, aCheques, lValeCompra, lBaixaAut, lUsaFD)
Local cCheq		   	:= ""	// Variavel que recebe parametro MV_CHEQUE ou MV_CHEQUES
Local cCondPag		:= ""	// Condicao de pagamento
Default cChave 		:= ""	// Chave para dbSeek na SE1
Default aCheques 	:= {}	// Array de cheques
Default lValeCompra := .F.	// Vale compra?                                       
Default lBaixaAut 	:= .F.	// Baixa automatica
Default lUsaFD		:= .F.	// Usa fidelizacao

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza‡„o de Contas a Receber e Saldo de Caixa			 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posicao de Geracao Titulo Financeiro³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Substr(SL1->L1_CONFVEN,6,1) <> "N"  

	cCondPag  := AllTrim(SL1->L1_CONDPG)
	If cPaisLoc <> "BRA" .AND. cCondPag <> "CN"
		SE4->(DbSetOrder(1))
		If SE4->(DbSeek(xFilial("SE4")+cCondPag))
			If SE4->(ColumnPos( "E4_BXTITAV" )) > 0
				lBaixaAut := SE4->E4_BXTITAV == "1"
			EndIf
		EndIf
	EndIf

	DbSelectArea("SE1")
	DbSetOrder(1)
	If DbSeek(xFilial("SE1")+ cChave )
		DbSelectArea("SE1")
		While !Eof() .AND. xFilial("SE1")==SE1->E1_FILIAL .AND. cChave == E1_PREFIXO+E1_NUM
			If AllTrim(Upper(SE1->E1_ORIGEM))$"LOJA010/LOJA220/LOJA701/RPC/LOJA720/FATA701"
			  	lValeCompra := .F.
				If lUsaFD .AND. ALLTRIM(SE1->E1_TIPO) == "VA"
					lValeCompra := .T.
				EndIf
				
				If Round(NoRound(SE1->E1_VALOR,3),2) <> Round(NoRound(SE1->E1_SALDO,3),2) .AND. ;
					!IsMoney(SE1->E1_TIPO) .AND. !lBaixaAut .AND. !(SE1->E1_TIPO $ MVABATIM) 
					If !( SL1->L1_CREDITO > 0 .and. (Alltrim(Upper(SE1->E1_TIPO)) == "CR") ) //Pagamento nçao realizado em nota de Crédito     				
						If Alltrim(Upper(SE1->E1_TIPO)) == "CR"
							If !lExcAuto
						  		MsgStop(STR0081)  // "Para esta nota utilize o processo de Troca/Devolução" 
						  	Else 
						  		Conout(STR0081)
						  	EndIf	
						Else
							If !lExcAuto
						   		Help(" ",1,"TITULOBAIXADO")
						    EndIf	
						EndIf 
						Return .F.
					EndIf
				EndIf

				If Type('MVCHEQUES')=='C'
					cCheq := MVCHEQUES
				Else
					cCheq := MVCHEQUE
				EndIf
		
				If SE1->E1_TIPO $ cCheq
					Aadd( aCheques , SE1->E1_PREFIXO+SE1->E1_NUM )
				EndIf
			EndIf
			dbSkip( )
		End
	EndIf
EndIf

Return (.T.)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj140CHDeposºAutor  ³ Vendas Clientes  º Data ³  14/04/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Verifica se os cheques foram depositado      				  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Loja140                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/      
Function Lj140CHDepositado(aCheques)
Local lRet := .T.    	// Retorno da funcao
Local nContador := 0	// Contator
Default aCheques := {}	// Array de cheques
                      
DbSelectArea("SEF")
SEF->( DbSetOrder(3) ) //EF_FILIAL + EF_PREFIXO + EF_TITULO + EF_PARCELA + EF_TIPO + EF_NUM + EF_SEQUENC
If Len(aCheques) > 0  
	If !Empty(SL1->L1_CHEQUES) .AND. DbSeek(xFilial("SL1")+aCheques[1])
		nContador := 1
		While !Eof() .AND. xFilial("SEF")==SEF->EF_FILIAL .AND. nContador <= Len(aCheques)
			If !Empty(SEF->EF_DEPOSIT)
			   	If !lExcAuto
			   		Help(" ",1,"CHEQUEMOV")
					Return ( .F. )
				Else
					//"Esta Nota Fiscal nao podera ser excluida pois foi paga por um cheque que ja foi depositado."
					Conout(STR0115)
					Return ( .F. )  
				EndIf	
			EndIf
			nContador++
			SEF->( DbSkip() )
		End
	EndIf
EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj140CHDelReºAutor  ³ Vendas Clientes  º Data ³  14/04/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Rotina de deleção de cheques no cancelamento de vendas com º±±
±±º          ³ reserva      				  							  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Loja140                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Lj140CHDelReserva(cChaveCHRe)
Local aCheques := {}		// Array de cheques
Default cChaveCHRe := ''	// Chave de busca para deletar os cheques na venda com reserva

Lj140CHCarrega(cChaveCHRe, @aCheques)
If Len(aCheques) > 0
	If Lj140CHDepositado(@aCheques)
		Lj140CHSEFDel(@aCheques)
	EndIf
EndIf

Return Nil


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj140CHSEFDeºAutor  ³ Vendas Clientes  º Data ³  14/04/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Faz a deleção da SEF de acordo com o aCheques	              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Loja140                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/      
Function Lj140CHSEFDel(aCheques)
Local nContador	   	:= 0	// Contador
Local lAmbOffLn 	:= SuperGetMv("MV_LJOFFLN", Nil, .F.)	//Identifica se o ambiente esta operando em offline

Default aCheques := {}		// Array de cheques

If Len(aCheques) > 0
	DbSelectArea("SEF")
	DbSetOrder(3)          		
	If (!Empty(SL1->L1_CHEQUES) .OR. ProcName(1) == 'LJ140CHDELRESERVA') .AND. DbSeek(xFilial("SEF")+aCheques[1])
		nContador := 1
		While !Eof() .AND. xFilial("SEF")==SEF->EF_FILIAL .AND. nContador <= len(aCheques)
			If AllTrim(SEF->EF_CART) == "R"

				If oIntCVenda <> Nil
					oIntCVenda:Inserir("SEF", xFilial("SEF") + SEF->EF_BANCO + SEF->EF_AGENCIA + SEF->EF_CONTA + SEF->EF_NUM, "1", "5")
					oIntCVenda:Gerar()
				EndIf

				Reclock( "SEF",.F.,.T.)
				dbDelete()

				//Adiciona a tabela para exclusao no processo off-line
				If lAmbOffLn
					Conout("Cancelamento - oProcOff:Inserir SEF")
					oProcOff:Inserir("SEF", xFilial("SEF") + SEF->EF_BANCO + SEF->EF_AGENCIA + SEF->EF_CONTA + SEF->EF_NUM, 1, "DELETE")
				EndIf
				
				MSUnLock()
				dbSkip( )
			EndIf
			nContador++
		End
	EndIf
EndIf

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ AjustaSX3  ºAutor  ³Vendas CRM          º Data ³  08/11/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Faz ajustes no dicionário de dados.			              	º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ 																º±±
±±º          ³ 		   													    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Loja140                                                     	º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AjustaSX3()          

Local aArea := SX3->(GetArea())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Altera o X3_USADO dos campos L2_RESERVA e L2_LOJARES, para ser utilizado |
//| também pelo módulo SIGAFAT.												 |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX3")
dbSetOrder(2)
If dbSeek("L2_RESERVA") .And. SX3->X3_USADO <> "„„€€€€€€€€€€€€€"
	RecLock("SX3",.F.)
	Replace SX3->X3_USADO With "„„€€€€€€€€€€€€€"
	MsUnlock()			
EndIf

If dbSeek("L2_LOJARES") .And. SX3->X3_USADO <> "„„€€€€€€€€€€€€€"
	RecLock("SX3",.F.)
	Replace SX3->X3_USADO With "„„€€€€€€€€€€€€€"
	MsUnlock()			
EndIf

RestArea(aArea)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj140Fill	 ºAutor  ³Vendas & CRM		 º Data ³  11/21/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Valida os itens do orcamento e alimenta o array aOrcam     º±±
±±º          ³ que contera os orcamentos que serao excluidos			  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Lj140Exc                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Lj140Fill(lSubClosed)

Local lRet		:= .T.													//verifica se o item ja foi devolvido
Local lAmbPaf	:= LjNfPafEcf(SM0->M0_CGC)								//verifica se esta em ambiente PAF-ECF
Local lPedido	:= .F.													//verifica se o orcamento possui pedido de venda amarrado
Local lIsOrcam	:= Empty(SL1->L1_TIPO) .AND. Empty(SL1->L1_OPERADO)		//indica se eh um orcamento aberto
Local aArea		:= {}													//armazena a area do orcamento principal(orcamento pai) em caso de reservas
Local aOrcamento:= {}													//array com todos os orcamentos a serem excluidos
Local lUpd78Ok	:= IIf(ExistFunc("LjUpd78Ok"),LjUpd78Ok(),.F.)		//indica que o sistema está preparado para utilizar a Lista de Presentes
Local lLstPre 	:= .F.													//indica contem um item de Lista de Presente

Default lSubClosed	:= .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criado a variavel lIsOrcam para especificar que eh apenas um orcamento nao  ³
//³ finalizado pois nao se pode contar somente com o campo L2_VENDIDO.			³
//³ Isto porque para quando existir venda com Retira e Reserva juntos, somente  ³
//³ eh preenchido L2_VENDIDO com "S" na Venda Filho faturado.  					³
//³ Jah quando ocorre importacao para o PDV como em caso de PAF-ECF, todos os 	³
//³ campos sao preenchido com "S" (Tanto Pai quanto Filhos).					³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

SL2->( DbSetOrder( 1 ) )
SL2->( DbSeek( xFilial( "SL2" ) + SL1->L1_NUM ) )

While !SL2->( Eof() ) .AND. xFilial("SL2") + SL2->L2_NUM == xFilial("SL1") + SL1->L1_NUM

	lPedido := (!Empty(SL2->L2_RESERVA) .AND. ( SL2->L2_ENTREGA == '3' .OR. SL2->L2_ENTREGA == '1'))
	
	If lUpd78Ok .AND. !Empty(SL2->L2_CODLPRE)
		lLstPre := .T.
	EndIf

	If (  lAmbPaf .AND. SL2->L2_VENDIDO == "S" .AND. !lPedido ) .OR. ;
	   ( !lAmbPaf .AND. !lPedido .AND. !lIsOrcam .AND. IIf(SL2->(ColumnPos("L2_DOCPED")) > 0,Empty(SL2->L2_DOCPED),.T.) )

		//Verificamos se o orcamento em questao possui sub-orcamentos reservados (filhos)
		If lRet .AND. !Empty( SL2->L2_FILRES ) .AND. !Empty( SL2->L2_ORCRES )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se for Retira Posterior, posiciono no orcamento "filho" para verificar se o mesmo ja foi finalizado|
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If SL2->L2_ENTREGA == "1"
				aArea := SL1->( GetArea() )
				If SL1->( DbSeek(SL2->L2_FILRES + SL2->L2_ORCRES) ) .AND. Empty(SL1->L1_DOC)
					//se nao foi finalizado, ele e adicionado como orcamento
					Aadd( aOrcamento, {SL2->L2_FILRES, SL2->L2_ORCRES, .F., lPedido, lLstPre} )
					SL1->( RestArea(aArea) )
					SL2->( DbSkip() )
					Loop    
				EndIf
				SL1->( RestArea(aArea) )
			EndIf
			
			lSubClosed := .T. //indica que algum sub-orcamento esta finalizado
		EndIf

		//Adicionamos o orcamento ao array aOrcamento, mesmo que os campos	|
		//	L2_FILRES e L2_ORCRES estejam vazios, ou seja, nao ha reservas	|
		If ( aScan(aOrcamento, {|x| x[1] + x[2] == SL2->L2_FILRES + SL2->L2_ORCRES}) ) == 0
			Aadd( aOrcamento, {SL2->L2_FILRES, SL2->L2_ORCRES, .T., lPedido, lLstPre} )
		EndIf

	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Adicionamos o orcamento ao array aOrcamento, mesmo que os campos	|
		//³	L2_FILRES e L2_ORCRES estejam vazios, ou seja, nao ha reservas	|
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( aScan(aOrcamento, {|x| x[1] + x[2] == SL2->L2_FILRES + SL2->L2_ORCRES}) ) == 0				
			Aadd( aOrcamento, {SL2->L2_FILRES, SL2->L2_ORCRES, .F., lPedido, lLstPre} )
		EndIf
	EndIf
	
	SL2->( dbSkip() )
End

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//|aOrcamento eh ordenado em ordem descrescente(TRUE(1), FALSE(0)),          |
//| para que no momento do loop ele exclua primeiro os orcamentos finalizados|
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aSort( aOrcamento, /*nInicio*/, /*nItens*/, { |x,y| x[3] > y[3] } )

Return aOrcamento


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj140Pos  ºAutor  ³Vendas & CRM        º Data ³  11/25/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Posiciona no sub-orcamento( filho) finalizado			  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Lj140Exc / Lj140VlVend / Lj140VlDev / Lj140VlImp			  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Lj140Pos(aOrcamento)

Local nPos			:= 0					//posicao do orcamento finalizado no array aOrcam
Local aSL1Area		:= SL1->( GetArea() )	//armazeno a area do orcamento pai
Local lRet			:= .F.

Default aOrcamento	:= {}

nPos := aScan(aOrcamento, {|x|x[3] == .T.})	//procura o orcamento finalizado no array aOrcam

DbSelectArea("SL1")
SL1->( DbSetOrder(1) )
If nPos > 0 .AND. SL1->( DbSeek(aOrcamento[nPos][1] + aOrcamento[nPos][2]) )
	lRet := .T.	
Else
	If !lExcAuto
		MsgStop(STR0119 + AllTrim(aOrcamento[nPos][2]), STR0006) // "Nao foi possivel localizar o sub-orcamento:" | "Atencao"
	Else 
		Conout(STR0119 + AllTrim(aOrcamento[nPos][2]))
	EndIf	
	SL1->( RestArea(aSL1Area) )
EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj140DelNcc ºAutor  ³Vendas & CRM        º Data ³  11/28/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Cancela a NCC na Base de Dados                                º±±
±±º          ³                                                            	º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Lj140Exc														º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Lj140DelNcc

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona no SE1 antes de executar o FINA330 ,		  ³
//³Ajusta o tamanho dos campos L1_DOC e E1_PARCELA		  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ  

Local cBxDoc		:= ""					// Documento da NCC
Local cBxSerie      := ""					// Série da NCC           
Local cParcela 		:= ""
Local nTamL1Doc	    := 0					//Tamanho do campo
Local nTamE1Parc    := 0					//Tamanho do campo
Local nTamE1Tipo    := 0					//Tamanho do campo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona no SE1 antes de executar o FINA330 ,		  ³
//³Ajusta o tamanho dos campos L1_DOC e E1_PARCELA		  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ  
cParcela	:= SuperGetMV("MV_1DUP")
nTamL1Doc	:= TamSX3("L1_DOC")[1]			//Tamanho do campo
nTamE1Parc	:= TamSX3("E1_PARCELA")[1]		//Tamanho do campo
nTamE1Tipo	:= TamSX3("E1_TIPO")[1]			//Tamanho do campo
cBxDoc		:= PadR( cBxDoc  	, nTamL1Doc )
cParcela	:= PadR( cParcela	, nTamE1Parc)
cTipoSE1	:= PadR( 'CR '		, nTamE1Tipo)

If SL1->L1_CREDITO > 0

	If Empty(SL1->L1_DOC)
		cBxDoc := SL1->L1_DOCPED
	Else 
		cBxDoc := SL1->L1_DOC
	Endif

	If Empty(SL1->L1_SERIE)
		cBxSerie := SL1->L1_SERPED
	Else 
		cBxSerie := SL1->L1_SERIE
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Posiciona no SE1 antes de executar o FINA330³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("SE1")
	DbSetOrder(1)
	If DbSeek(xFilial("SE1") + cBxSerie + cBxDoc + cParcela + cTipoSE1)

	    If SE1->E1_SALDO <> SE1->E1_VALOR
	   		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	   		//³Fina330        ³
	   		//³ 5  - Estorno  ³
			//³.T.- Automatico³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			Fina330(5,.T.)
	    EndIf

	EndIf    

EndIf

Return Nil


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ Lj140Cart  ºAutor  ³ Vendas Clientes  º Data ³ 12/12/2011  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retorna array com informacoes de pagamentos realizados com º±±
±±º          ³ cartoes (Debito e Credito).                                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Loja140                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Lj140Cart()
Local aRet 		:= {}
Local aArea 	:= GetArea()
Local cFormas 	:= "CC|CD"

DbSelectArea("SL4")
SL4->( DbSetOrder(1) )	//L4_FILIAL, L4_NUM, L4_ORIGEM
If SL4->( DbSeek(xFilial("SL4") + SL1->L1_NUM ) )
	/*	Alteramos o campo obtido (L4_NSUTEF ao invés de L4_DOCTEF), pois em um Sitef de produção,
		esses campos são gravados com informações distintas, já no Sitef de homologação, a informação é igual.
		E no cancelamento, utiliza-se o valor do L4_NSUTEF (NSU Sitef) */	
	While !SL4->( Eof() ) .AND. xFilial("SL4") + SL1->L1_NUM == SL4->L4_FILIAL + SL4->L4_NUM
		If AllTrim(SL4->L4_FORMA) $ cFormas .And. Empty(SL4->L4_DATCANC)
			//Tratamento para mais de uma parcela do mesmo cartao
			If aScan( aRet, { |x| x[1] == Alltrim(SL4->L4_NSUTEF)}) == 0
				aAdd( aRet, { AllTrim(SL4->L4_NSUTEF), SL4->(Recno()) } )
			EndIf
		EndIf				
		SL4->( DbSkip() )
	End
EndIf

RestArea(aArea)

Return aRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj140LbRPSºAutor  ³Vendas & CRM        º Data ³ 15/Ago/2012 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Verifica se libera a exclusao quando for venda apenas de   º±±
±±ºDesc.     ³ servico (RPS).                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA140 - Lj140VlImp	 									  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Lj140LbRPS()
Local aArea			:= SL1->( GetArea() )	//armazeno a area do orcamento pai
Local lRet			:= .T.
Local cFilialSL1	:= SL1->L1_FILIAL
Local cNumSL1 		:= SL1->L1_NUM

DbSelectArea("SL1")
SL1->( DbSetOrder(1) ) //Ordem: L1_FILIAL + L1_NUM
While SL1->(!EoF()) .And. SL1->L1_FILIAL == cFilialSL1
	If cNumSL1 <> SL1->L1_NUM .And. !Empty(SL1->L1_DOC)
		lRet := .F. //Nao permite excluir, pois a ultima nota ou cupom gerado nao esta relacionado ao orcamento que deseja excluir
		Exit
	EndIf
	SL1->( DbSkip() )
End

SL1->( RestArea(aArea) )

Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ Lj140VldPV ºAutor³Vendas Clientes ºData³ 14/08/2012  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Verifica se o orcamento possui algum Pedido de venda.º±±
±±º          ³ Se possuir, verifica se o mesmo ja foi faturado		º±±
±±º			 | Observacao: validacao retirada da funcao LJ140Fill	º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Lj140VlVend                                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function LJ140VldPV(aOrcamento)

Local lRet	:= .T.	//retorno da funcao
Local aArea := {}	//armazena a area
Local nPos	:= 1	//elemento do array aOrcamento que possui um pedido de venda

Default aOrcamento := {}

aArea	:= SL1->( GetArea() )
nPos	:= AScan(aOrcamento, {|x| x[4] == .T.} )

DbSelectArea("SL1")
SL1->( DbSetOrder(1) ) //L1_FILIAL + L1_NUM

While nPos > 0

	If SL1->( DbSeek(aOrcamento[nPos][1] + aOrcamento[nPos][2]) )
		If !Empty(SL1->L1_PEDRES)
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³verificamos na tabela SC5, se o pedido de vendas ja foi faturado³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("SC5")
			SC5->( DbSetOrder(1) ) //C5_FILIAL + C5_NUM
			If SC5->( DbSeek(aOrcamento[nPos][1] + SL1->L1_PEDRES) )
				If !Empty(SC5->C5_NOTA) .AND. !Empty(SC5->C5_SERIE)
					If !lExcAuto
				   		MsgStop(STR0083, STR0047)
				   		//"Este Pedido possui Pedidos de Venda finalizados, nao sera possivel efetuar o cancelamento."###"Atencao"
					Else 
						lMsErroAuto := .T.
						Help(" ",1,"NO140CANC",,STR0083,3,1)						
						Conout(STR0083)
					EndIf
					lRet := .F.
					Exit
				EndIf
			EndIf
		ElseIf !Empty(SL1->L1_DOC) .AND. !Empty(SL1->L1_ORCRES) .AND. AllTrim(L1_TIPO) = "P"
			If !lExcAuto
		   		MsgStop(STR0083, STR0047)
		   		//"Este Pedido possui Pedidos de Venda finalizados, nao sera possivel efetuar o cancelamento."###"Atencao"
			Else 
				lMsErroAuto := .T.
				Help(" ",1,"NO140CANC",,STR0083,3,1)
				Conout(STR0083)
			EndIf
			lRet := .F.
			Exit
		EndIf		
	EndIf

	//procuramos por um novo elemento a partir do (ultimo encontrado + 1), para que nao haja repeticao
	nPos := AScan(aOrcamento, {|x| x[4] == .T.}, nPos + 1 )
End

SL1->( RestArea(aArea) )

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140ImpOrc³ Autor ³ Vendas Clientes      ³ Data ³15/Out/2012³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de impressao de orcamentos						   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Lj140ImpOrc()		                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj140ImpOrc(cMensagem, cCliente, cLoja, cCliCGC)
Local cCodProd										//Variavel que recebe codigo do produto	                                              
Local cDescriProd									//Variavel que recebe descrição do produto
Local cQuant										//Variavel que recebe quantidade 
Local cVrUnit										//Variavel que recebe Valor unitario
Local cDesconto										//Variavel que recebe desconto
Local cVlrItem										//Variavel que recebe valor do item 
Local cSitTrib										//Variavel que recebe Situação tributaria 
Local cFormaPgtos									//Variavel que recebe formas de pagamento
Local cNomeForma									//Variavel que recebe nome das formas de pagamento
Local nAliquota										//Variavel que recebe aliquotas
Local nRet											//Variavel que recebe retorno 
Local cTpSolCf 		:= SuperGetMV("MV_TPSOLCF")		//Parametro para validacao do tipo de cliente para o calculo do solidario
Local cUnidMed 		:= " "							//Variavel que recebe unidade de medida
Local nAliqIss 		:= 0							//Recebe o campo B1_ALIQISS ou o parametro MV_ALIQISS	
Local aAreaSA1 		:= SA1->(GetArea())             //Guarda a area do SA1
Local lImprime		:= .T.                         	// Controle de para verificar se a impressao foi bem sucedida 
Local lStatusImp	:= .T.							// Status da Impressao	
Local aLjExcecao	:= {}

Local cNumCupom     := Space(TamSx3("L1_DOC")[1])	// Numero do cupom fiscal
Local cNumPdv 		:= Space(TamSx3("L1_PDV")[1])	// Numero do PDV  
Local cSerie 		:= ""							// Serie do documento

Local cOrcRes	  	:= "" 							// Codigo do orcamento da reserva
Local cPedRes	  	:= "" 							// Codigo do pedido da reserva
Local lCFRetPos 	:= .F.    						// Verifica se eh orcamento (com itens de RETIRA POSTERIOR) apenas para finalizacao (impressao do Cupom Fiscal)
Local aArrayFormas  := {}
Local nI			:= 0
Local nPosForm		:= 0
Local lFormaRetira	:= .F.							//Considera a Forma de pagamento para itens do tipo retira posterior
Local cFormaRetira	:= SuperGetMv("MV_LJFORRET",,"")//Forma de pagamento para itens do tipo retira posterior


Default cMensagem	:= ""							//Mensagem promocional do cupom
Default cCliente	:= Nil
Default cLoja 		:= Nil

DbSelectArea("SA1")
DbSetOrder(1)
DbSeek(xFilial("SA1")+SL1->L1_CLIENTE+SL1->L1_LOJA)

//Pega numero do cupom
While lImprime
	nRet := IfPegCupom( nHdlECF, @cNumCupom )
	If nRet == 1
		If Aviso(STR0006,STR0054 + CHR(10) + Chr(13) +; //"Atencao ## "Problemas na abertura do cupom."
					STR0087, {STR0088,STR0089}) = 2 	//"Executa a impressão novamente? # Sim # Não "
			lImprime 	:= .F.
			lStatusImp	:= .F.
   		EndIf
	Else
		lImprime := .F.
		cNumCupom := PadR( StrZero(Val(cNumCupom)+1,Len(cNumCupom)) , TamSx3("L1_DOC")[1] )
    EndIf   
End

//Pega numero do PDV   
lImprime := .T.
While lImprime
	nRet := IfPegPDV( nHdlECF, @cNumPdv )
	If nRet == 1
		If Aviso(STR0006,STR0054 + CHR(10) + Chr(13) +; //"Atencao ## "Problemas na abertura do cupom."
					STR0087, {STR0088,STR0089}) = 2 	//"Executa a impressão novamente? # Sim # Não "
			lImprime 	:= .F.
			lStatusImp	:= .F.
   		EndIf
	Else
		lImprime := .F.
    EndIf   
End

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Abre o cupom fiscal                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lImprime := .T.
While lImprime
	nRet :=IFAbreCup( nHdlEcf, @cCliCGC, cCliente, cLoja )
	If nRet == 1
		If Aviso(STR0006,STR0054 + CHR(10) + Chr(13) +; //"Atencao ## "Problemas na abertura do cupom."
					STR0087, {STR0088,STR0089}) = 2 	//"Executa a impressão novamente? # Sim # Não "
			lImprime 	:= .F.
			lStatusImp	:= .F.
   		EndIf
	Else
		lImprime := .F.
    EndIf
End
   
//Serie
cSerie   := LjGetStation("LG_SERIE")
/* Atualiza SL1 com informações do Cupom Fiscal */
RECLOCK( "SL1" ,.F.)
SL1->L1_DOC		:= cNumCupom
SL1->L1_PDV		:= cNumPdv
SL1->L1_SERIE	:= cSerie
SL1->(MSUNLOCK())

cOrcRes	  :=  SL1->L1_ORCRES 	// Codigo do orcamento da reserva
cPedRes	  :=  SL1->L1_PEDRES 	// Codigo do pedido da reserva
lCFRetPos :=  Empty(cPedRes) .And. !Empty(cOrcRes) .And. ExistFunc("Lj140ImpOrc") //Verifica se eh orcamento (com itens de RETIRA POSTERIOR) apenas para finalizacao (impressao do Cupom Fiscal)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Imprime os itens   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nRet == 0
	DbSelectArea("SL2")
	DbSetOrder(1)
	DbSeek(xFilial("SL2")+SL1->L1_NUM)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Varre SL2 para imprimir os itens ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	While (SL2->(!EOF())).AND. ((xFilial("SL2") + SL2->L2_NUM) == (xFilial("SL2")+SL1->L1_NUM))
		
		If nModulo == 12	 	// SIGALOJA	
			SB1->(DbSeek(xFilial("SB1")+SL2->L2_PRODUTO))
		ElseIf nModulo == 23	// FRONTLOJA
			SBI->(DbSeek(xFilial("SBI")+SL2->L2_PRODUTO))					                                                            
		Endif                                                       
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Posiciona o SF4   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SF4->(DbSeek(xFilial("SF4")+SL2->L2_TES))
		
		aLjExcecao := LjxExcecao(.T.)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica a situacao tributaria do item                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If SF4->F4_ISS == "S"					// Servico
		
		    If nModulo == 12 .AND. SB1->B1_ALIQISS > 0
				nAliqIss:= SB1->B1_ALIQISS  
			ElseIf nModulo == 23 .AND. SBI->BI_ALIQISS > 0 
				nAliqIss:= SBI->BI_ALIQISS  				
		    Else
				nAliqIss:= SuperGetMv("MV_ALIQISS")
		    EndIf
		
			cSitTrib := "S" + AllTrim(Str(nAliqIss,5,2))
		
		ElseIf nModulo == 12 .AND. If(SF4->(ColumnPos("F4_MKPSOL"))>0,SF4->F4_MKPSOL<>"1",.T.) .And. (SB1->B1_PICMRET > 0 .OR. (Len(aLjExcecao) > 0 .AND. (aLjExcecao[3] > 0 .OR. aLjExcecao[16] > 0 )) );
				.AND. SA1->A1_TIPO $ cTpSolCf .AND. SF4->F4_BSICMST <> 100
			cSitTrib := "F"						// Substituicao tributaria (Icms Solidario)
			                                                   
		ElseIf nModulo == 23 .AND. If(SF4->(ColumnPos("F4_MKPSOL"))>0,SF4->F4_MKPSOL<>"1",.T.) .And. (SBI->BI_PICMRET > 0 .OR. (Len(aLjExcecao) > 0 .AND. (aLjExcecao[3] > 0 .OR. aLjExcecao[16] > 0 )) );
				.AND. SA1->A1_TIPO $ cTpSolCf .AND. SF4->F4_BSICMST <> 100
			cSitTrib := "F"						// Substituicao tributaria (Icms Solidario)
			
		ElseIf SF4->F4_BASEICM > 0 .AND. SF4->F4_BASEICM < 100 
			cSitTrib := "T" + Alltrim(Str( SB0->B0_ALIQRED,5,2 ))		  // com reducao de Icms na Base
			
		ElseIf SF4->F4_LFICM == "I"			// Isento
			cSitTrib := "I"
			
		ElseIf SF4->F4_LFICM == "N"			// N„o sujeito a ICMS
			cSitTrib := "N"
			
		Else									// Com ICMS
			nAliquota := AliqIcms("N","S",SA1->A1_TIPO,"I")
			cSitTrib := "T" + Alltrim(Str(nAliquota,5,2))
			
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Trata as variaveis para o registro do item                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cCodProd 		:= SL2->L2_PRODUTO
		cDescriProd		:= SubStr(SL2->L2_DESCRI + Space(30),1,30)
		cQuant 			:= StrZero(SL2->L2_QUANT,7,2)
		cVrUnit			:= Str(SL2->L2_VRUNIT,10,4)
		cDesconto		:= Iif(!lCFRetPos,Str(SL2->L2_VALDESC,8,2),"0")  // Neste ponte se for cupom fiscal de "Retira Posterior", nao aplica desconto, pois ja foi aplicado
		cVlrItem		:= Str(Val(cVrUnit) * Val(cQuant),11,2)
		If nModulo == 12
			cUnidMed        := SB1->B1_UM
		Elseif nModulo == 23	
			cUnidMed        := SBI->BI_UM
		Endif
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Imprime o item                                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        lImprime := .T.
		While lImprime
			nRet := IFRegItem( 	nHdlECF, cCodProd	, cDescriProd	, cQuant	, ;
								cVrUnit, cDesconto 	, cSitTrib		, cVlrItem	, ;
								cUnidMed )
			If nRet == 1
				If Aviso(STR0006,STR0053 + CHR(10) + Chr(13) +; //"Atencao ## "Falha em registrar item."
						 STR0087, {STR0088,STR0089}) = 2 	 	//"Executa a impressão novamente? # Sim # Não "
					lImprime 	:= .F.
					lStatusImp 	:= .F.
   				EndIf
			Else
				lImprime := .F.
    		EndIf
		End

		DbSelectArea("SL2")
		dbSkip()
	End
EndIf

If SL1->L1_DESCONT > 0 .AND. lStatusImp
	lImprime := .T.
	While lImprime
		nRet := IfDescTot( nHdlECF, Alltrim(Str(SL1->L1_DESCONT,10,2)))
		If nRet == 1
			If Aviso(STR0006,STR0055 + CHR(10) + Chr(13) +; //"Atencao ## "Falha no registro do desconto no total."
					 STR0087, {STR0088,STR0089}) = 2 	 	//"Executa a impressão novamente? # Sim # Não "
				lImprime 	:= .F.
				lStatusImp 	:= .F.
			EndIf
		Else
			lImprime := .F.
		EndIf
	End
EndIf


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona o SL4   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SL4->(DbSeek(xFilial("SL4")+SL1->L1_NUM))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Monta a string cFormaPgtos para enviar para o ECF                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cFormaPgtos := ''
nTotalPgtos := 0
lFormaRetira := !Empty(cFormaRetira) .And. AllTrim(SL1->L1_TIPO) == "P" .And. !Empty(SL1->L1_FILRES) .And. (!Empty(SL1->L1_ORCRES) .Or. !Empty(SL1->L1_DOCPED))
While (nTotalPgtos < SL1->L1_VLRLIQ) .AND. (SL4->L4_NUM == SL1->L1_NUM)

	//Para que ao emitir o cupom fiscal o mesmo nao apresente a forma de pagamento mas sim o definido no parametro
	If lFormaRetira
		cNomeForma := cFormaRetira

	ElseIf Trim(SL4->l4_FORMA) == ''
		cNomeForma := Alltrim(Tabela("24",SuperGetMV("MV_SIMB1"),.F.))
	ElseIf Trim(SL4->L4_FORMA) == 'R$'
		cNomeForma := Alltrim(Tabela("24",AllTrim(SL4->L4_FORMA),.F.))
		
	ElseIf Trim(SL4->L4_FORMA) == 'CH'
		cNomeForma := Alltrim(Tabela("24",AllTrim(SL4->L4_FORMA),.F.))
		
	ElseIf SuperGetMv("MV_LJPAGTO") == 1									//Busca da Tabela 24 do SX5
		cNomeForma := Alltrim(Tabela("24",AllTrim(SL4->L4_FORMA),.F.))
		
	Else
		cNomeForma := Trim(Substr(SL4->L4_ADMINIS,7,14))
		
	EndIf
	                              
	// Verifica se a forma ja esta adicionada no array
	nPosForm := Ascan( aArrayFormas, { |x| Trim(x[1]) == Trim(cNomeForma) } )
	
	If nPosForm == 0           	   
		Aadd(aArrayFormas,{ Trim(cNomeForma), SL4->L4_VALOR } )
	Else                       
		// Soma o valor a forma ja existente no array
		aArrayFormas[nPosForm][2] += SL4->L4_VALOR 
	Endif	
	
	SL4->(dbSkip())
End Do

For nI := 1 to Len(aArrayFormas)
	// Monta string com formas e valores
	cFormaPgtos +=  aArrayFormas[nI][1] + "|" + Alltrim(Str(aArrayFormas[nI][2])) + "|"

Next nI

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Envia as formas de pagamento para o ECF                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lImprime := .T.
While lImprime .AND. lStatusImp 
	nRet := IFPagto( nHdlECF, cFormaPgtos  )
	If nRet == 1
		If Aviso(STR0006,STR0056 + CHR(10) + Chr(13) +; //"Atencao ## "Falha no registro de pagamento."
				 STR0087, {STR0088,STR0089}) = 2 	 	//"Executa a impressão novamente? # Sim # Não "
			lImprime 	:= .F.
			lStatusImp 	:= .F.
		EndIf
	Else
		lImprime := .F.
	EndIf
End

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Fecha o cupom e imprime a mensagem promocional ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ  
lImprime := .T.
While lImprime .AND. lStatusImp
	nRet := IFFechaCup( nHdlECF, cMensagem )
	If nRet == 1
		If Aviso(STR0006,STR0057 + CHR(10) + Chr(13) +; //"Atencao ## "Falha no fechamento do cupom."
				 STR0087, {STR0088,STR0089}) = 2 	 	//"Executa a impressão novamente? # Sim # Não "
			lImprime 	:= .F.
			lStatusImp 	:= .F.
		EndIf
	Else
		lImprime := .F.
	EndIf
End

RestArea(aAreaSA1)

Return lStatusImp

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ Lj140EstVP ºAutor³Vendas Clientes ºData³ 18/06/2013  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Verifica se a venda foi paga com Vale Presente e     º±±
±±º          ³ estorna o mesmo.                              		º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA140                                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Lj140EstVP( cNumOrc )
Local aArea 		:= GetArea()
Local aAreaSL4 		:= SL4->( GetArea() )
Local aAreaSL2 		:= SL2->( GetArea() )
Local aValePre 		:= {}
Local lL2VALEPRE 	:= SL2->(ColumnPos("L2_VALEPRE")) > 0

If ExistFunc("LjVpAtiva")
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se foi utilizado VP na venda como forma de pagamento ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("SL4")
	SL4->( DbSetOrder(1) )
	If SL4->( DbSeek(xFilial("SL4")+cNumOrc) )
		While !SL4->(Eof()) .AND. SL4->L4_FILIAL+SL4->L4_NUM == xFilial("SL4")+cNumOrc
			If AllTrim(SL4->L4_FORMA) == "VP"
	   			aAdd(aValePre,{AllTrim(SL4->L4_NUMCART),"2"}) //Atualizar Vale Presente para "2=Vendido"
			EndIf
			SL4->( DbSkip() )
		End
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se foi vendido algum VP na venda ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lL2VALEPRE
		DbSelectArea("SL2")
		SL2->( DbSetOrder(1) )
		If SL2->( DbSeek(xFilial("SL2")+cNumOrc) )
			While !SL2->(Eof()) .AND. SL2->L2_FILIAL+SL2->L2_NUM == xFilial("SL2")+cNumOrc
				If !Empty(SL2->L2_VALEPRE)
		   			aAdd(aValePre,{AllTrim(SL2->L2_VALEPRE),"1"}) //Atualizar Vale Presente para "1=Ativo"
				EndIf
				SL2->( DbSkip() )
			End
		EndIf
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Estorna os Vales Presentes ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Len(aValePre) > 0
		//Executa funcao para voltar o status do Vale Presente 
		LjVpAtiva(aValePre)
	EndIf
EndIf

RestArea( aAreaSL2 )
RestArea( aAreaSL4 )
RestArea( aArea )

Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ Lj140NFCe  ºAutor³ VAREJO		 ºData³ 28/01/2014  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Envia o cancelamento da NFC-e ao SEFAZ				º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA140                                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Lj140NFCe(aOrcamento)

Local lRet	:= .T.	//indica se o cancelamento foi recebido pelo SEFAZ
Local nI	:= 0	//contador
Local aArea	:= {}

Default aOrcamento := {}

//armazena a area do orcamento pai/principal
aArea := GetArea("SL1")

For nI := 1 To Len(aOrcamento)
	
	//verifica se algum orcamento filho foi finalizado
	If !Empty(aOrcamento[nI][1]) .AND. !Empty(aOrcamento[nI][2]) .AND. aOrcamento[nI][3]
		If !SL1->( MsSeek(aOrcamento[nI][1] + aOrcamento[nI][2]) )
			lRet := .F.
		EndIf		
	Else
		//posiciona no orcamento principal
		RestArea(aArea)
	EndIf

	//se for exclusao de uma venda, marcamos ela para ser cancelada, preenchendo o campo L1_SITUA = 'X0'
	If SL1->L1_SITUA == 'OK' .AND. SL1->L1_STORC <> 'A'
		RecLock( "SL1" )
		Replace SL1->L1_SITUA with "X0"
		SL1->( MsUnlock() )
		
		lRet := .F.
	
	//se o cancelamento foi autorizado pelo SEFAZ, entao permitimos o cancelamento prosseguir no ERP
	ElseIf SL1->L1_SITUA == 'X2'
		lRet := .T.
	
	//se o status da nota for (cancelamento nao enviado para o TSS, cancelamento pendente no SEFAZ ou 
	// cancelamento nao autorizado entao a nota devera ter sua SITUAcao avaliada novamente 
	ElseIf SL1->L1_SITUA $ "X0|X1|X3"				
		Lj140AtuSit()
		lRet := .F.

	Else
		lRet := .F.
		LjGrvLog( SL1->L1_NUM, "SITUA invalido para enviar cancelamento: " + SL1->L1_SITUA )
	EndIf
	
	If lRet
		RecLock("SL1", .F.)
		Replace SL1->L1_TPORC with ""
		Replace SL1->L1_STORC with "C"
		SL1->( MsUnlock() )		
	EndIf

Next

RestArea(aArea)

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140VlEC  ³ Autor ³ Vendas Clientes      ³ Data ³28/jan/2015³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de validação da exclusão de orçamento de venda EC  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Lj140VlEC()   		                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Lj140VlEC()	    
Local lRet 		:= .T.  //Retorno da rotina
Local cFormaPg 	:= ""	//Forma de Pagamento
Local lBaixa 	:= .F. //Exite baixa para o título
Local cParcela 	:= ""	//Parcela do Título
Local cPrefixo 	:= ""	//Prefixo do Título
Local cNum 		:= ""	//Numero do Título
Local cTipo 	:= ""	//Tipo do Título
Local cFornece 	:= ""	//Fornecedor
Local cLojaForn	:= ""	//Loja do Fornecedor
Local lGeraTaxa := SuperGetMV("MV_LJGERTX",,.F.) .AND. ExistFunc("L070IncSA2") //Gera Taxa   
Local cDocPed 	:= "" 	//Numero do Título associado no orçamento
Local cSerPed 	:= ""	//Serie do Pedido
Local aArea 	:= GetArea()  //WorkArea Ativa
Local aAreaSL1 	:= SL1->(GetArea()) //WorkArea do orçamento
Local aAreaSE2	:= SE2->(GetArea()) //WorkArea do Contas a Pagar
Local aAreaSE1	:= SE1->(GetArea()) //WorkArea do Contas a Receber

cFormaPg := SL1->L1_FORMPG
cDocPed := SL1->L1_DOCPED
cSerPed := SL1->L1_SERPED
cFilRes := SL1->L1_FILRES
cPedRes := SL1->L1_PEDRES  

Lj140AdmSA2(@cFornece, @cLojaForn)
	
If !Empty(cDocPed+cSerPed) .AND. Empty(SL1->L1_DOC) .AND. Empty(SL1->L1_SERIE)
	//Exclui o título
	//verificar baixas	
		lBaixa := .F.

		SE1->(DbSetOrder(1) )	//E1_FILIAL+E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO
		
		SE1->(DbSeek(xFilial("SE1")+cSerPed +cDocPed))
	
		Do While !lBaixa .And. SE1->(!Eof() .And. (E1_FILIAL+E1_PREFIXO+E1_NUM == xFilial("SE1")+cSerPed +cDocPed)  .And. AllTrim(E1_TIPO) ==  AllTrim(cFormaPg) )
			cPrefixo 	:= SE1->E1_PREFIXO
			cNum 		:= SE1->E1_NUM
			cParcela 	:= SE1->E1_PARCELA
	 		cTipo		:= SE1->E1_TIPO //tipo  

			lBaixa := SE1->(Round(E1_SALDO,2)) == 0  .Or. ;
					SE1->(ROUND(E1_SALDO,2) # ROUND(E1_VALOR,2) .And. !FXAtuTitCo())   //
			SE1->(DbSkip())                                                            //Baixa do título parcial?
	    EndDo                                                                            
	    
	    If lBaixa
    		//Já foi realizado baixa para uma das parcelas!
	   		If !lExcAuto
				MsgStop( STR0134 , STR0006 ) //"Já foi realizado baixa para uma das parcelas de contas a receber! " #"Atenção"
				lRet := .f.
			Else 
				LjGrvLog( SL1->L1_NUM , "Lj140VlEC " + STR0134 + cSerPed + "/" + cDocPed ) //"Já foi realizado baixa para uma das parcelas de contas a receber! "
				lRet := .F.
			EndIf 
		EndIf
		
	 	If lRet
			//Verifica baixa do SE2, caso exista
			If lGeraTaxa .AND.  RTrim(cTipo) $ "CC/CD"  .AND. !Empty(cFornece) .AND. !Empty(cLojaForn)
				SE2->(DbSetOrder(1))  //E2_FILIAL+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA                                                                                               
				lBaixa := .F.
				If SE2->(DbSeek(xFilial("SE2")+ cPrefixo + cNum + cParcela + cTipo + cFornece + cLojaForn))   
				
					Do While !lBaixa .And. SE2->(!Eof() .And. ;
										E2_FILIAL+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA  == xFilial("SE2")+ cPrefixo + cNum + cParcela + cTipo + cFornece + cLojaForn)
						lBaixa := SE2->(Round(E2_SALDO,2)) == 0  .Or. ;
								SE2->(ROUND(E2_SALDO,2) # ROUND(E2_VALOR,2) )  //Baixa do título parcial?
						SE2->(DbSkip())                                                            
				    EndDo
				EndIf
			    If lBaixa
		    		//Já foi realizado baixa para uma das parcelas!
			   		If !lExcAuto
						MsgStop( STR0135 , STR0006 ) //"Já foi realizado baixa para uma das parcelas de contas a pagar da Taxa Administrativa! "
						lRet :=  .f.
					Else 
						LjGrvLog( SL1->L1_NUM ,"Lj140VlEC " + STR0135 + cSerPed + "/" + cDocPed)    //"Já foi realizado baixa para uma das parcelas de contas a pagar da Taxa Administrativa! "
						lRet := .F.
					EndIf 
				EndIf
							
			EndIf
	 	EndIf
				
EndIf                  

RestArea(aAreaSL1)       
RestArea(aAreaSE1)
RestArea(aAreaSE2)    
RestArea(aArea)

Return lRet                

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140ExR ³ Autor ³ Vendas Clientes      ³ Data ³28/jan/2015  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de realização da exclusão de registros da enda EC  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Lj140ExR (cLogPath, cDocPed , cSerPed)                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Lj140ExR(cLogPath, cDocPed , cSerPed)  
Local lRet 		:= .T.  //Retorno da rotina
Local aTitulo	:= {}   //Array do Título
Local cParcela 	:= ""  	//Parcela do Título
Local lGeraTaxa := SuperGetMV("MV_LJGERTX",,.F.) .AND. ExistFunc("L070IncSA2")  					// Verifica se ira gerar um Contas a Pagar quando existir taxa na admistradora do cartao.
Local cPrefixo 	:= ""	//Prefixo do Título
Local cNum 		:= "" 	//Numero do Título
Local cTipo 	:= "" 	//Tipo do Título
Local cFornece 	:= "" 	//Codigo do Fornecedor
Local cLojaForn	:= ""	//Loja do Fornecedor
Local cAdmFin 	:= "" //Campo Administradora Financeira  
Local aAreaSE1 := SE1->(GetArea())   //WorkAreaSE1
Local aAreaSE2 := SE2->(GetArea())	//WorkAreaSe2 

Default cLogPath :=  SuperGetMV("MV_LOGPATH",,"LOGS")     
Default cSerPed := ""
Default cDocPed := ""   

//Busca o fornecedor
Lj140AdmSA2(@cFornece, @cLojaForn) 

SE1->(DbSetOrder(1)) // E1_FILIAL+E1_PREFIXO+E1_NUM


If  !Empty(cDocPed) .and. !Empty(cSerPed)
		//Exclui o título
		//verificar baixas
	 	
	 	SE1->(DbSeek(xFilial("SE1")+cSerPed +cDocPed))
	 	Do While lRet .AND.  SE1->(!Eof()  .And. E1_FILIAL+E1_PREFIXO+E1_NUM == xFilial("SE1")+cSerPed +cDocPed)
 
			cPrefixo 	:= SE1->E1_PREFIXO
			cNum 		:= SE1->E1_NUM
			cParcela 	:= SE1->E1_PARCELA
	 		cTipo		:= SE1->E1_TIPO   
	 			 	
			lMsErroAuto  := .F.
				
	 		aTitulo := {}
	 		
			aAdd(aTitulo, {"E1_PREFIXO"	,SE1->E1_PREFIXO							,Nil})
			aAdd(aTitulo, {"E1_NUM"		,SE1->E1_NUM								,Nil})
			aAdd(aTitulo, {"E1_PARCELA"	,SE1->E1_PARCELA							,Nil})
			aAdd(aTitulo, {"E1_TIPO"	,SE1->E1_TIPO							,Nil})
			
	    	lECLJAUTO := .T.  //Seta para execauto do ecommerce
		    
			MSExecAuto({|x,y| Fina040(x,y) },aTitulo,5)
			
			If  lMsErroAuto
 			   	If !lExcAuto
					MsgStop( STR0136 + cSerPed + " - " + cDocPed + " - " + cParcela + CRLF + MostraErro(cLogPath)  , STR0006 ) //""Problema na exclusão do Título de Contas a Receber: "#"Atenção"
					lRet :=  .f.
				Else 
					LjGrvLog(SL1->L1_NUM,  STR0136 + cSerPed + " - " + cDocPed + " - " + cParcela + CRLF + MostraErro(cLogPath) )  //"Problema na exclusão do Título de Contas a Receber: "
					lRet := .F.
				EndIf 
	    	
				DisarmTransaction()
				RollBackSX8()
				Exit
				
			Else
				ConfirmSX8()
				
				//Deleta o Contas a pagar, caso exista
				//Verifica baixa do SE2, caso exista
				If lGeraTaxa .AND.  RTrim(cTipo) $ "CC/CD"  .AND. !Empty(cFornece) .AND. !Empty(cLojaForn)
					SE2->(DbSetOrder(1))  //E2_FILIAL+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA                                                                                               
					If SE2->(DbSeek(xFilial("SE2")+ cPrefixo + cNum + cParcela + cTipo + cFornece + cLojaForn))   
  	 	
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Busca titulos em abertos relacionados atraves do campo ³
							//³historico(E2_HIST) com a mesma sequencia de parcela    ³ 
							// VIDE Lj140ExCap 										  ³ 
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If AllTrim(SE2->E2_HIST) == AllTrim(cNum) .AND. AllTrim(SE2->E2_PARCELA) == AllTrim(cParcela)   
								RecLock("SE2",.F.)
								SE2->(DbDelete() )
								SE2->(MsUnlock() )
							Endif			
					EndIf
				EndIf 

			EndIf                           
			SE1->(DbSeek(xFilial("SE1")+cSerPed +cDocPed)) //Posiciona na proxima parcela			
		EndDo	

    EndIf

Return lRet

RestArea(aAreaSE1)
RestArea(aAreaSE2)  

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140AdmSA2³ Autor ³ Vendas Clientes    ³ Data ³28/jan/2015  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa que inclui/retorna o fornecedor associado a Adm Fin³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Lj140AdmSA2(cFornece, cLojaForn)                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj140AdmSA2(cFornece, cLojaForn)     
Local lGeraTaxa 	:= SuperGetMV("MV_LJGERTX",,.F.) .AND. ExistFunc("L070IncSA2")  					// Verifica se ira gerar um Contas a Pagar quando existir taxa na admistradora do cartao.
Local cAdmFin := "" //Campo Administradora Financeira 
Local aAreaSL4 := SL4->(GetArea())
Local aAreaSA2 := SA2->(GetArea()) 

Default cFornece := "" 
Default cLojaForn := "" 

If lGeraTaxa
	SL4->(DbSetOrder(1)) //L4_FILIAL+L4_NUM+L4_ORIGEM   
	SL4->(DbSeek(SL1->L1_FILIAL + SL1->L1_NUM ))
	Do While SL4->(!Eof() .AND. L4_FILIAL+L4_NUM ==   SL1->L1_FILIAL + SL1->L1_NUM )   
	
		If Rtrim(SL4->L4_FORMA) $ "CC/CD"
			cAdmFin := Left( SL4->L4_ADMINIS, TamSx3("AE_COD")[1])				
			Exit
		EndIf
		SL4->(DbSkip(1))
	EndDo 
	If !Empty(cAdmFin)
			SAE->( DbSetOrder(1) ) //AE_FILIAL + AE_COD
			SAE->( Dbseek(xFilial("SAE") + cAdmFin) )

			LjGrvLog( SL1->L1_NUM , "Lj140VlEC - " + STR0137)	//"antes da funcao L070IncSA2"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Inclui o Fornecedor caso nao tenha sido cadastrado,³
			//³e retorna com o codigo para gerar SE2              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cFornece := L070IncSA2()
			If RTrim(SA2->A2_COD) == RTrim(cFornece) 
				cLojaForn := SA2->A2_LOJA
			Else 	
				cLojaForn := "01"	 
			EndIf

			LjGrvLog( SL1->L1_NUM , "Lj140VlEC - " + STR0138)	//"depois da funcao L070IncSA2"
	EndIf

EndIf

RestArea(aAreaSL4)
RestArea(aAreaSA2)

Return

//----------------------------------------------------------------
/*/{Protheus.doc} Lj140LstP
Realiza os estornos de Conta Corrente e Itens da Lista de Presente
@param	 cFilOrc - Filial do orçamento
@param	 cNumOrc - numero do orçamento
@author  Varejo
@version P11.8
@since   02/03/2015
@return  lRet - indica se os estornos foram realizados com sucesso
/*/
//----------------------------------------------------------------
Static Function Lj140LstP(cFilOrc, cNumOrc)

Local aSubOrc	:= {}
Local lRet		:= .F.
Local aSL1Area	:= SL1->( GetArea() )
Local aSL2Area	:= SL2->( GetArea() )
Local aDocs		:= {}
Local aL2Num	:= {}

DbSelectArea("SL1")
SL1->( DbSetOrder(1) )
If SL1->( DbSeek(cFilOrc + cNumOrc) )

	If !Empty(SL1->L1_DOC)
		aAdd(aDocs	,{SL1->L1_DOC,SL1->L1_SERIE}  )
	EndIf

	If !Empty(SL1->L1_DOCPED)
		aAdd(aDocs	,{SL1->L1_DOCPED,SL1->L1_SERPED}  )
	EndIf

	DbSelectArea("SL2")
	SL2->( DbSetOrder(1) )
	If SL2->( DbSeek(cFilOrc + cNumOrc) )
	
		While SL2->( !Eof() ) .AND. SL2->L2_FILIAL + SL2->L2_NUM == cFilOrc + cNumOrc	
			If !Empty(SL2->L2_FILRES) .AND. !Empty(SL2->L2_ORCRES)
				Aadd( aSubOrc, SL2->L2_ORCRES )
			EndIf

			If !Empty(SL2->L2_NUM)
				aAdd(aL2Num, SL2->L2_NUM)
			EndIf

			//estorna as quantidades dos itens da Lista de Presente
			If !Empty(SL2->L2_CODLPRE) .AND. !Empty( SL2->L2_ITLPRE )
				lRet := Lj8EstQtd( SL2->L2_CODLPRE, SL2->L2_ITLPRE, SL2->L2_QUANT )
			EndIf
			
			SL2->( DbSkip() )			
		EndDo
		
		//lança as contrapartidas da Conta Corrente da Lista de Presente
		If Len(aSubOrc) > 0
			lRet := Lj8EstCC( SL1->L1_FILIAL, aSubOrc, SL1->L1_CLIENTE, SL1->L1_LOJA )
		Else
			lRet := Lj8EstCC( SL1->L1_FILIAL, {SL1->L1_NUM}, SL1->L1_CLIENTE, SL1->L1_LOJA )
		EndIf

		//Informativo da operação de cancelamento no obs ou exclusao dos registros.
		//isso dependen do parametros na função.
		If ExistFunc("Lj8SetObs")
			lRet := Lj8SetObs(	SL1->L1_FILIAL	, aL2Num		, aDocs,;
								SL1->L1_CLIENTE	, SL1->L1_LOJA	)
		EndIf

	Else
		lRet := .F.
	EndIf    
Else
	lRet := .F.
EndIf

RestArea(aSL1Area)
RestArea(aSL2Area)

Return lRet


//----------------------------------------------------------------
/*/{Protheus.doc} LJ140PosSE1
Posiciona no registro correto da SE1, pois pode acontecer da SE1 ser 
compartilhada e ter dois registros com E1_NUM + E1_PREFIXO iguais, 
porém originados de filiais diferentes
@param	 cChaveSE1 - chave de busca
@author  Varejo
@version P11.8
@since   13/04/2015
@return  lRet - indica se posicionou no registro correto
/*/
//----------------------------------------------------------------
Static Function LJ140PosSE1(cChaveSE1)

Local lRet := .F.

Default cChaveSE1 := ""

DbSelectArea("SE1")
SE1->( DbSetOrder(1) ) //E1_FILIAL + E1_PREFIXO + E1_NUM + E1_PARCELA + E1_TIPO

If SE1->( MsSeek(cChaveSE1) )
	While SE1->(!EoF()) .AND. SE1->E1_FILIAL + SE1->E1_PREFIXO + SE1->E1_NUM == cChaveSE1
		If SE1->E1_FILORIG == cFilAnt
			lRet := .T.
			Exit
		Else
			SE1->( DbSkip() )
			Loop
		EndIf
	EndDo
EndIf

Return lRet


//----------------------------------------------------------------
/*/{Protheus.doc} LJCANCNFCE
Funcao que devera ser executada em Job.
Faz o monitoramento das NFC-e que estao com cancelamento pendente,
sendo que se o cancelamento for autorizado, ela fará a exclusão da nota no ERP. 
@param	 cEmp - Grupo de Empresa
@param	 cFiliais - Filiais (se for mais de uma, deve-se separar por ;
@param	 cSleep - intervalo entre cada execução
@author  Varejo
@version P11.8
@since   08/09/2015
/*/
//----------------------------------------------------------------
Function LJCancNFCe( cEmp, cFiliais , cSleep )

Local nSleep	:= 0	//intervalo de execucao em milisegundos
Local nFilial	:= 1	//posicao do array que indica qual filial esta sendo processada
Local nTotFil	:= 0	//total de filiais a serem processadas
Local nI		:= 0
Local lTrocaFil	:= .T.	//indica se deve haver a troca de filial a cada execucao do job
Local lAmbOk	:= .F.	//indica se o ambiente esta aberto 
Local aFiliais	:= {}	//array com as filiais que serao processadas
Local aRecnoNFCe:= {}	//arra com os R_E_C_N_O_ das vendas que serao canceladas
Local xRet140

Default cEmp	:= ""
Default cFiliais:= ""
Default cSleep	:= "180"	//em segundos, pois sera convertido em milisegundos

/*
	Intervalo de processamento entre cada filial (também se aplica a uma unica filial)
	Multiplicamos por 1000, pois o parametro da funcao Sleep é em milisegundos
*/
nSleep := Val(cSleep) * 1000	
LjGrvLog( Nil, "Intervalo de execução: " + cSleep + "s" )


/*
	Obtem as filiais a serem processadas
*/
aFiliais := StrTokArr ( cFiliais, "," )
nTotFil	:= Len(aFiliais)

While !KillApp()

	/*
		Nao faz consumo de licença
	*/
	RPCSetType(3)

	While nFilial <= nTotFil

		LjGrvLog( Nil, "Processando a FILIAL: ",  aFiliais[nFilial] )

		/*
			Realiza a ABERTURA do ambiente
		*/
		If lTrocaFil
			//SE o ambiente nao estiver aberto, fazemos a abertura atraves da funcao RPCSetEnv
			If !lAmbOk

				//realizamos a abertura do ambiente
				lAmbOk := RPCSetEnv(cEmp, aFiliais[nFilial], Nil, Nil, "LOJ")

				If !lAmbOk
					LjGrvLog( Nil, "Falha ao abrir o ambiente" )
					Exit
				EndIf

			//SENAO, somente trocamos a variavel publica CFILANT, ja que nao havera a troca do Grupo de Empresa
			Else				
				CFILANT := aFiliais[nFilial]
			EndIf
		EndIf

		DbSelectArea("SL1")
		SL1->( DbSetOrder(9) )	//L1_FILIAL + L1_SITUA + L1_PDV + L1_DOC
		If SL1->( DbSeek(xFilial("SL1") + "X") )
			/*
				Obtemos os dados das vendas que estao com cancelamento pendente
			*/		
			While SL1->(!Eof()) .AND. SL1->L1_FILIAL + SubStr(SL1->L1_SITUA,1,1) == xFilial("SL1") + "X"
				
				/*
				TODO: criar um parametro para verificar se quando o cancelamento for rejeitado,
				qual sera a ação a ser tomada: reenviar o cancelamento automaticamente ou esperar uma ação do usuario
				*/
				Aadd( aRecnoNFCe, SL1->(Recno()) )

				SL1->( DbSkip() )			
			EndDo

			/*
				chama a funcao padrao de exclusao, onde ela fara a validacao se a venda deve ser cancelada
			*/
			If Len(aRecnoNFCe) > 0
				For nI := 1 to Len(	aRecnoNFCe )
					SL1->( DbGoTo(aRecnoNFCe[nI]) )
					
					xRet140 := Lj140Exc( "SL1", aRecnoNFCe[nI], 2, Nil, .T., SL1->L1_FILIAL, SL1->L1_NUM )
					
					If ValType(xRet140) == "N"
						If xRet140 == 1
							LjGrvLog(SL1->L1_NUM, "Cancelamento efetuado com sucesso" ) 
						Else 
							LjGrvLog(SL1->L1_NUM, "Falha ao efetuar o cancelamento" )
						EndIf
					Else
						LjGrvLog( SL1->L1_NUM, "Retorno do cancelamento: ", xRet140 )
					EndIf
				Next
			EndIf
			
		Else
			LjGrvLog( Nil, "Nenhum orcamento para ser cancelado" )
		EndIf

		// limpamos o vetor para melhor gerenciamento de memoria		
		aSize(aRecnoNFCe, 0)

		/*
			Se FILIAL unica, nao e necessario abrir o ambiente a cada execucao
		*/
		If nTotFil == 1
			lTrocaFil := .F.
		Else
			lTrocaFil := .T.
			If nFilial < nTotFil
				nFilial += 1
			Else
				nFilial := 1
			EndIf
		EndIf

		/*
			EXECUTA O INTERVALO A CADA PROCESSAMENTO
		*/
		If nSleep > 0			
			Sleep(nSleep)
		EndIf	
	EndDo

EndDo

If lAmbOk
	//fecha o ambiente antes de sair da funcao
	RPCClearEnv()
EndIf

Return Nil


//----------------------------------------------------------------
/*/{Protheus.doc} LJ140ATUSIT
Função responsavel em atualizar a situação de uma nota que deverá 
ser cancelada. Para NFC-e, nos baseamos no status da venda no TSS,
onde usamos os campos STATUS e STATUSCANC da tabela SPED050. 
@param	 cNum - numero do orcamento
@author  Varejo
@version P11.8
@since   08/09/2015
/*/
//----------------------------------------------------------------
Static Function Lj140AtuSit( cNum )

Local cNovoSitua:= ""	//novo valor do campo L1_SITUA, baseado no STATUSCANC do TSS
Local lAtuSitua	:= .T.	//indica se o L1_SITUA devera ser atualizado
Local lContinua := .T.	//indica se deve continuar com o processo de atualizacao do status da venda
Local aNFCeID	:= {}	//vetor de NFEID (L1_SERIE+L1_DOC) que serao consultados no TSS
Local aNFCeDados:= {}	//vetor retornado pelo TSS com o status de cada nota que foi consultado

Default cNum	:= ""

//
// Se nao receber os parametros, usamos os dados do registro posicionado
//
If !Empty(cNum)
	DbSelectArea("SL1")
	SL1->( DbSetOrder(1) )	
	If !SL1->( DbSeek(xFilial("SL1") + cNum) )		
		lContinua := .F.
	EndIf
EndIf

If lContinua

	If SL1->L1_SITUA == "X0"
		
		If IsBlind()
			lAtuSitua := LjNFCeCanc( SL1->L1_SERIE, SL1->L1_DOC )
		Else
			LjMsgRun( STR0127 + SL1->L1_NUM + STR0128 + SL1->L1_DOC,, {|| LjNFCeCanc(SL1->L1_SERIE, SL1->L1_DOC)} )
			//"Aguarde... Cancelamento da NFC-e sendo processado via TSS. Orcamento:"  #" - Doc.:"				
		EndIf

		If lAtuSitua
			If ExistFunc("LjNFCeGtID")
				cNovoSitua := "X1"
			/*
			Se a funcao LJNFCEGTID nao estiver compilada, mantemos o legado, ou seja,
			basta o TSS aceitar o pedido de cancelameto para cancelarmos a nota no ERP
			*/
			Else				
				cNovoSitua := "X2"
				
				LjGrvLog(SL1->L1_NUM, "Atualize o fonte LOJNFCE.PRW para que o cancelamento por etapas seja realizado.")
			EndIf		
		Else
			LjGrvLog(SL1->L1_NUM, "TSS nao aceitou a solicitação de cancelamento")			
		EndIf
	
	ElseIf SL1->L1_SITUA == "X1"
		
		//obtemos somente as NFEID (L1_SERIE+L1_DOC)
		Aadd( aNFCeID, SL1->L1_SERIE + SL1->L1_DOC )
	
		// obtemos os status da nota no TSS
		aNFCeDados := LjNFCeGtID(aNFCeID)

		LjGrvLog(SL1->L1_NUM, "Dados da NFC-e: ", aNFCeDados)
	
		If Len(aNFCeDados) > 0
		
			Do Case
				// CANCELAMENTO PENDENTE - mantemos o valor do L1_SITUA
				Case aNFCeDados[1][4] == "7" .AND. aNFCeDados[1][5] == "1"
					lAtuSitua := .F.
	
					LjGrvLog( SL1->L1_NUM, "Cancelamento Pendente: ", aNFCeDados[1][4] + " - " + aNFCeDados[1][5] )
				
				// CANCELAMENTO PROCESSADO - atualizamos o valor do L1_SITUA
				Case aNFCeDados[1][4] == "7" .AND. aNFCeDados[1][5] == "2"
					cNovoSitua := "X2"
				
				// CANCELAMENTO COM FALHA - atualizamos o valor do L1_SITUA
				Case aNFCeDados[1][4] == "7" .AND. aNFCeDados[1][5] == "3"
					cNovoSitua := "X3"
				
				// retornou STATUS e/ou STATUSCANC invalidos para cancelamento
				Otherwise
					lAtuSitua := .F.
					
					LjGrvLog( SL1->L1_NUM, "STATUS e STATUSCANC invalidos para cancelamento", aNFCeDados[1][4] + " - " + aNFCeDados[1][5] )
			End Case
	
		Else
			lAtuSitua := .F.	
			
			LjGrvLog( SL1->L1_NUM, "Os dados da NFC-e nao foram retornados: ", SL1->L1_SERIE + SL1->L1_DOC )
		EndIf

	ElseIf SL1->L1_SITUA == "X3"
		cNovoSitua := "X0"

	Else
		LjGrvLog( SL1->L1_NUM, "SITUA invalido para cancelamento: ", SL1->L1_SITUA )
	EndIf
	
	// atualiza o valor do campo L1_SITUA
	If lAtuSitua
		RecLock("SL1", .F.) 
		Replace SL1->L1_SITUA with cNovoSitua
		SL1->( MsUnlock() )
	EndIf

Else
	LjGrvLog( Nil, "Orcamento nao localizado", cNum )
EndIf

Return Nil


//----------------------------------------------------------------
/*/{Protheus.doc} Lj140TlCTef
Função responsavel por abrir a tela de cancelamento do TEF

@param1	 lJob		- é job?
@param2	 lVendtef	- pago em TEF
@param3  oTef		- objeto do TEF
@Param4  lTefOk		- retorna status do cancelamento TEF
@param5  aRetCartao - array com o retorno do cartão
@param6  aCartaoSL4 - array com o conteudo do SL4
@author  Varejo
@version P11.8
@since   21/12/2015
/*/
//----------------------------------------------------------------
Function Lj140TlCTef(	lJob		, lVendtef	, oTef	, lTefOk,;
						aRetCartao	, aCartaoSL4 )

/*Cancela o TEF na Administradora*/
If lUsaTEF
	/*Se estiver configurado a CLISITEF, passa o objeto oTEF.*/
	If cTipTEF == TEF_CLISITEF
		Lj140CnAdm(@lJob, @lVendTef, @oTEF		, @lTefOk, @aRetCartao		, @aCartaoSL4		)
	Else
		Lj140CnAdm(@lJob, @lVendTef, /*@oTEF*/	, @lTefOk, /*@aRetCartao*/	, /*@aCartaoSL4*/	)
	EndIf
EndIf

Return Nil						

//----------------------------------------------------------------
/*/{Protheus.doc} Lj140AtuDic
Função responsável por ajustar dicionario

@author  Varejo
@version P12
@since   04/03/2016
/*/
//----------------------------------------------------------------
Static Function Lj140AtuDic()
Local aAliasOrd := GetArea()
Local aSx2Ord	:= SX2->(GetArea())
Local cSufixo	:= "" 
//Ajusta do nome da nome do arquivo da tabela MDU
DBSelectArea("SX2")
SX2->(DBSeek("SL1"))
cSufixo := SubStr(AllTrim(SX2->X2_ARQUIVO),4)
If SX2->(DBSeek("MDU")) .And. AllTrim(SX2->X2_ARQUIVO) == "0" .And. RecLock("SX2", .F.)
	SX2->X2_ARQUIVO := AllTrim(SX2->X2_CHAVE)+cSufixo
	SX2->(MsUnlock())
EndIf

If AllTrim(SX2->X2_ARQUIVO) == "0"
	Conout("Não foi possivel corrigir o X2_ARQUIVO da tabela 'MDU'. Favor alterar via configurador para o sufix correto.")
EndIf

RestArea(aSx2Ord)
RestArea(aAliasOrd)

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Lj140ExRAComp³ Autor ³ Vendas Cliente    ³ Data ³ 23/07/15 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Rotina para Excluir a campensacao de RAs Hotelaria.        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Lj140ExRAComp()                          		           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno	  ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ LOJA140 															³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function Lj140ExRAComp()

Local aArea 	 		:= GetArea()
Local cWhere 	 		:= "" //Condicional da query
Local cAliasTmp 		:= GetNextAlias()
Local aEstorno 	 	:= {} //Array temporario de RAs para estorno
Local aEstornoTmp 	:= {} //Array temporario de RAs para estorno
Local aRecSE1	 		:= {SE1->(Recno())} //Recno do titulo a receber

//Condicional para a query		
cWhere := "%"
cWhere += " E5_FILIAL = '" + xFilial("SE5") + "'"
cWhere += " AND E5_PREFIXO = '" + SE1->E1_PREFIXO + "'"
cWhere += " AND E5_NUMERO = '" + SE1->E1_NUM + "'"
cWhere += " AND E5_PARCELA = '" + SE1->E1_PARCELA + "'"
cWhere += " AND E5_TIPO = '" + SE1->E1_TIPO + "'"
cWhere += " AND E5_CLIFOR = '" + SE1->E1_CLIENTE + "'"
cWhere += " AND E5_LOJA = '" + SE1->E1_LOJA + "'"
cWhere += " AND SE5.D_E_L_E_T_ = ''"   		   			
cWhere += "%"              											
			                                          
//Executa a query
BeginSql alias cAliasTmp
	SELECT 
		E5_DOCUMEN		 
	FROM %table:SE5% SE5		 							
	WHERE %exp:cWhere% 			
EndSql

//Posiciona no inicio do arquivo
(cAliasTmp)->(dbGoTop())	

//Armazena RAs		
While (cAliasTmp)->(!Eof())
	aAdd(aEstornoTmp, (cAliasTmp)->E5_DOCUMEN)
										
	(cAliasTmp)->(dbSkip())
EndDo

If Len(aEstornoTmp) > 0
	aAdd(aEstorno, aEstornoTmp)

	//Estorna compensacao de RAs
	MaIntBxCR(3, aRecSE1,,,, {.F., .F., .F., .F., .F., .F.},, aEstorno)
EndIf

//Fecha Arquivos temporarios
If (Select(cAliasTmp) > 0)
	(cAliasTmp)->(dbCloseArea())
EndIf

//Restaura areas
RestArea(aArea)

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ IntegDef  ³ Autor ³ Vendas Cliente       ³ Data ³ 12/02/15 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Rotina para Definicao de Integracao via Mensagem Unica.    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ IntegDef(cXml, cTypeTran, cTypeMsg)			              ³±±
±±³          ³ aExp1 - Conteudo da mensagem (XML)    					  	³±±
±±³          ³ aExp2 - Tipo da transacao                             	  	³±±
±±³          ³ aExp3 - Tipo da mensagem          		            	   	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno	  ³ Aray com resultado da execucao e a mensagem Xml de retorno.³±±
±±³          ³ aRet[1]-(boolean) Indica o resultado da execução da função³±±
±±³          ³ aRet[2]-(caracter) Mensagem Xml para envio                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ LOJA140 															³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function IntegDef(cXml, nType, cTypeMsg)

Local aRet := {}

aRet := LOJI140(cXml, nType, cTypeMsg)

Return aRet
