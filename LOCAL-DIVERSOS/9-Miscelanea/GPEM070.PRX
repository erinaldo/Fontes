#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPEM070.CH"
#INCLUDE "TBICODE.CH"
#INCLUDE "TBICONN.CH"

Static lGpa070mn 
Static lGp070Fim 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ GPEM070  ³ Autor ³ Emerson Rosa de Souza          | Data ³ 21.06.00  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±ÃDescricao ³ Calculo da Provisao de Ferias                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPEM070(void)                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.		              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data     ³CHAMADO/REQUISITO³  MOTIVO DA ALTERACAO                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³LTrombini   ³22/01/2014³M12RH01/RQ1021   ³UNIFICACAO DA FOLHA V12                ³±±
±±³Mohanad                                                                          ³±±
±±³Christiane V³17/04/2014³M12RH01/RQ1021   ³UNIFICACAO DA FOLHA V12                ³±±
±±³Christiane V³02/05/2014³M12RH01/RQ1021   ³Zerar nTDiasAfa a cada funcionário     ³±±
±±³Cecilia C.  ³14/05/2014³RHU210_01_12     ³Ajuste na chamada da funcao fCarrTab ao³±±
±±³            ³          ³                 ³carregar a tabela S033 (fPercEmp).     ³±±
±±³Claudinei S.³03/06/2014³           TPQVD1³Remocao do excesso de aspas  fQryDetSRT³±±
±±³            ³          ³                 ³e ajustada a declaracao do aCabProv qdo³±±
±±³            ³          ³                 ³utilizar Item Contabil/Classe de Valor.³±±
±±³Flavio C.   ³09/06/2014³TPUOTR     	   ³REPLICA V11 Ajuste p/ tratar nova opcao ³±±
±±³            ³          ³           	   ³preenchimento do campo X14_RECFAT.		³±±
±±³Allyson M   ³15/07/2014³TPWOS3     	    ³Ajuste na impressao do relatorio de    ³±±
±±³            ³          ³           	    ³rateio de provisao.		            ³±±
±±³            ³          ³           	    ³Ajustes diversos referente congelamento³±±
±±³            ³          ³           	    ³Conceito do congelamento e' restrito p/³±±
±±³            ³          ³           	    ³p periodo aquisitivo e nao deve ocorrer³±±
±±³            ³          ³           	    ³p/ o dias proporcionais.               ³±±
±±³Allyson M   ³24/07/2014³TPZKBZ	        ³Ajuste p/ ordenacao por CC, Item e     ³±±
±±³            ³          ³           	    ³Classe.               					³±±
±±³Allyson M   ³25/07/2014³TQCPCK    	    ³Ajustes p/ gravar o calculo com flag   ³±±
±±³            ³          ³           	    ³de congelamento quando ha situacao do  ³±±
±±³            ³          ³           	    ³congelamento do periodo aquisitivo. 	³±±
±±³Allyson M   ³08/08/2014³TQEYJ2     		³Ajuste no calculo de ferias e para 	³±±
±±³            ³          ³           		³considerar a data de demissao caso     ³±±
±±³            ³          ³           		³funcionario esteja demitido.  			³±±
±±³Allyson M   ³28/10/2014³TQNPFS   	    ³-Ajuste na msg exibida quando calculo  ³±±
±±³            ³          ³           	    ³e' para mes que ja foi fechado. 		³±±
±±³            ³          ³           	    ³-Ajuste p/ calculo em competencia      ³±±
±±³            ³          ³           	    ³anterior a transferencia p/ considerar ³±±
±±³            ³          ³           	    ³o afastamento e situacao folha da  	³±±
±±³            ³          ³           	    ³competencia selecionada. 				³±±
±±³            ³          ³           	    ³-Ajuste na verificacao dos dados da 	³±±
±±³            ³          ³           	    ³transf. anterior com os dados da atual.³±±
±±³            ³          ³           	    ³-Ajuste na validacao de transferencia  ³±±
±±³            ³          ³           	    ³de funcionario demitido.			    ³±±
±±³Allyson M   ³23/01/2015³TRDH62  	    	³-Ajuste p/ sempre calcular provisao    ³±±
±±³            ³          ³           	    ³quando funcionario afastado pelos  	³±±
±±³            ³          ³           	    ³motivos que nao sao tratados ao inves  ³±±
±±³            ³          ³           	    ³de buscar os dados do mes anterior	    ³±±
±±³            ³          ³           	    ³-Ajuste na montagem do temporario TPR  ³±±
±±³            ³          ³           	    ³p/ validar corretamente o item e classe³±±
±±³            ³          ³           	    ³da competencia de calculo.				³±±
±±³Renan Borges³24/02/2015³TRMUM7  	       ³Ajuste p/ carregar filiais da empresa   ³±±
±±³            ³          ³           	   ³corrente sem desposicionar de empresa e ³±±
±±³            ³          ³           	   ³assim o tamanho do cFilAnt continua     ³±±
±±³            ³          ³           	   ³igual ao da empresa logada, pois quando ³±±
±±³            ³          ³           	   ³posicionava em uma empresa cujo tamanho ³±±
±±³            ³          ³           	   ³da filial fosse diferente da empresa cor³±±
±±³            ³          ³           	   ³rente, o cFilAnt tinha seu tamanho alte-³±±
±±³            ³          ³           	   ³rado.                                   ³±±
±±³Allyson M   ³04/03/2015³TRRK24   	   ³Ajuste no calculo de INSS da provisao de³±±
±±³            ³          ³           	   ³13 quando ha diferenca de 13o na Folha. ³±±
±±³            ³          ³           	   ³Ajuste no calculo de INSS da provisao de³±±
±±³            ³          ³           	   ³13 quando ha diferenca de 13o na Folha e³±±
±±³            ³          ³           	   ³desoneracao teve inicio durante o ano.	³±±
±±³Allyson M   ³04/06/2015³TSHGSB     	   ³Ajuste p/ zerar os dias de ferias       ³±±
±±³            ³          ³           	   ³vencidas no cabecalho quando nao tem    ³±±
±±³            ³          ³           	   ³dias vencidas. 							³±±
±±³            ³          ³TSLWFQ   	   ³Ajuste no congelamento de dias de ferias³±±
±±³            ³          ³           	   ³de tipos de afastamentos nao tratados   ³±±
±±³            ³          ³           	   ³para pegar os dias do calculo anterior  ³±±
±±³            ³          ³TSL439   	   ³Ajuste no congelamento em situacao em   ³±±
±±³            ³          ³           	   ³que o afastamento ocorre entre dois     ³±±
±±³            ³          ³           	   ³periodos aquisitivos.  				    ³±±
±±³M. Silveira ³07/06/2015³TSJNSS 		   ³Ajuste na fPercEmp p/ corrigir os campos³±±
±±³            ³          ³				   ³da S033 usados na apuracao da aliquota. ³±±
±±³Allyson M   ³24/06/2015³TSODJL 		   ³Ajuste na fGrvArrPrv() para arredondar o³±±
±±³            ³          ³				   ³valor de baixa na rescisao, para nao    ³±±
±±³            ³          ³				   ³gerar divergencia entre os valores 		³±±
±±³            ³          ³				   ³provisionados							³±±
±±³M. Silveira ³30/06/2015³TSQWF2 		   ³Ajuste para retornar os valores dos     ³±±
±±³            ³          ³				   ³adicionais apos a execucao da fSalInc.  ³±±
±±³Allyson M   ³12/08/2015³TSXNVT   	   ³Ajuste p/ proporcionalizacao das medias ³±±
±±³            ³          ³           	   ³de 13o conforme os avos de direito a 13o³±±
±±³            ³          ³TT7633   	   ³Ajuste p/ montagem correta do TPR quando³±±
±±³            ³          ³           	   ³houve mais do que 1 transferencia e e'  ³±±
±±³            ³          ³           	   ³feito busca dos dados da 1a. transf. 	³±±
±±³Allyson M   ³14/10/2015³TTLNPT     	   ³Ajuste p/ gravar a receita bruta na S033³±±
±±³            ³          ³           	   ³e gravar a diferenca entre a receita    ³±±
±±³            ³          ³           	   ³bruta e liquida no novo campo da S033.	³±±
±±³            ³          ³           	   ³Ajuste p/ gerar a baixa de antecipacao  ³±±
±±³            ³          ³           	   ³de 14o. salario. 						³±±
±±³Matheus M.  ³34/12/2015³TTYIF0 		   ³Ajuste para que seja possível calcular  ³±±
±±³            ³          ³				   ³a provisão em grid sem gerar errorlog.  ³±±
±±³Renan Borges³11/12/2015³TTTRL1 		   ³Ajuste para obter o valor dos adicionais³±±
±±³            ³          ³				   ³pela fSalInc quando a pesquisa do valor ³±±
±±³            ³          ³				   ³nos acumulados nao encontrar os valores.³±±
±±³            ³          ³      		   ³Ajuste na fGeraSRT para nao gerar somen-³±±
±±³            ³          ³				   ³te cabecalho sem valores de lancamentos.³±±
±±³            ³          ³        	       ³Ajuste na verificacao dos dias de ferias³±±
±±³            ³          ³           	   ³antecipadas no SRF   				    ³±±
±±³Flavio Corre³16/02/2016³TUN466   	   ³Ajuste p/ sempre calcular provisao      ³±±
±±³            ³          ³           	   ³quando funcionario afastado ao inves de ³±±
±±³            ³          ³           	   ³buscar os dados do mes anterior.		³±±
±±³Renan Borges³06/05/2016³TVBMO5   	   ³Ajuste p/ sempre gerar o cabeçalho de   ³±±
±±³            ³          ³           	   ³calculo, mesmo quando nao possuir lanca-³±±
±±³            ³          ³           	   ³mentos p/ nao causar divergencia na con-³±±
±±³            ³          ³           	   ³ferencia.                               ³±±
±±³Renan Borges³09/05/2016³TVARIT   	   ³Ajuste p/ calcular adicionais de 13°    ³±±
±±³            ³          ³           	   ³corretamente quando MV_MEDDIRE está com ³±±
±±³            ³          ³           	   ³"S" em seu conteúdo.                    ³±±
±±³Renan Borges³07/06/2016³TVGMHH   	   ³Ajuste para calcular provisão corretamen³±±
±±³            ³          ³           	   ³te quando funcionário possuir parte das ³±±
±±³            ³          ³           	   ³férias calculadas no mes.               ³±±
±±³            ³          ³           	   ³Ajuste para calcular provisão corretamen³±±
±±³            ³          ³           	   ³te quando funcionário possuir parte das ³±±
±±³            ³          ³           	   ³férias pagas anteriormente e estiver pa-³±±
±±³            ³          ³           	   ³gando o restante em outro mes.          ³±±
±±³Allyson M   ³14/06/2016³TVJDFU     	   ³Ajuste p/ nao verificar incorporacao dos³±±
±±³            ³          ³           	   ³adicionais pois nao existe esse conceito³±±
±±³            ³          ³           	   ³na 12, uma vez que os adicionais sempre	³±±
±±³            ³          ³           	   ³serão gerados separados das verbas de   ³±±
±±³            ³          ³           	   ³ferias e de 13o salario   				³±±
±±³Renan Borges|21/06/2016|TVIPFA 	       |Ajuste para ao gerar a Contabilização da³±±
±±³            |          |                |provisão seja gerada separadamente os va³±±
±±³            |          |                |lores para os tipos de contratos.       ³±±
±±ºRenan Borges³24/06/2016³TVIRBJ          ³Ajuste para ao incluir um funcionário   º±±
±±º            ³          ³                ³seja possível calcular sua provisão.    º±±
±±º            ³          ³                ³Ajuste para calcular na provisão adicio-º±±
±±º            ³          ³                ³nais de confiança e transferência.      º±±
±±ºRenan Borges³29/06/2016³TVMFAN          ³Ajuste para calcular provisão com o paraº±±
±±º            ³          ³                ³metro MV_TPBXFER com 2 corretamente.    º±±
±±ºClaudinei S.³29/06/2016³TUTSTS          ³Disponibilizado o calculo da provisao deº±±
±±º            ³          ³                ³ferias para os estagiarios, (Recesso)   º±±
±±º            ³          ³                ³baixa, baixa por transferencia e baixa  º±±
±±º            ³          ³                ³por rescisão.                           º±±
±±ºClaudinei S.³06/07/2016³TVMEN0          ³Incluída verificação de existência para º±±
±±º            ³          ³                ³os novos identificadores de cálculo de  º±±
±±º            ³          ³                ³provisao de recesso.                    º±±
±±³Allyson M   ³14/07/2016³TVKSI3     	   ³Ajuste p/ MV_CSALINC ativo quando baixa ³±±
±±³            ³          ³           	   ³de transferencia p/ considerar o id 318 ³±±
±±³            ³          ³           	   ³do cálculo original e não o atual		³±±
±±³Allyson M   ³15/07/2016³TVLLNS     	   ³Ajustes p/ calculo de provisao em Grid  ³±±
±±³Allyson M   ³26/07/2016³TVRNJ8     	   ³Ajuste p/ efetuar a baixa corretamente  ³±±
±±³            ³          ³           	   ³de ferias quando há ferias partidas e   ³±±
±±³            ³          ³           	   ³abono (parâmetro MV_TPBXFER com 2)		³±±
±±ºRenan Borges³29/08/2016³TVWT57          ³Ajuste para realizar calculo de provisãoº±±
±±º            ³          ³                ³com GRID corretamente quando há férias  º±±
±±º            ³          ³                ³antecipadas no periodo.                 º±±
±±ºClaudinei S.³27/09/2016³TVQQXK          ³Ajuste na provisão para Jornada Variavelº±±
±±º            ³          ³                ³para buscar as médias de 13º e Ferias   º±±
±±º            ³          ³                ³vencidas e proporcionais.               º±±
±±ºCícero Alves³04/10/2016³TWCGW8          ³Ajuste para considerar a data informada º±±
±±º            ³          ³                ³nos prâmetros							º±±
±±ºCícero Alves³04/10/2016³TWEPBC          ³Ajuste para permitir executar a rotina  º±±
±±º            ³          ³                ³sem interface (testes automatizados)	º±±
±±ºRenan Borges³04/10/2016³TWFOJV          ³Ajuste p/ contabilização da provisão    º±±
±±³Allyson M   ³11/11/2016³TWEHXD     	   ³-Ajuste p/ não passar data de referência³±±
±±º            ³          ³                ³p/ fSalInc() p/ cálculo correto em  	º±±
±±º            ³          ³                ³períodos diferentes do aberto na folha 	º±±
±±º            ³          ³                ³períodos diferentes do aberto na folha 	º±±
±±º            ³          ³                ³-Ajuste p/ validar fechamento do roteiroº±±
±±º            ³          ³                ³131 p/ a geração da antecipação do 13º 	º±±
±±º            ³          ³                ³-Retirada mensagem de período encerrado º±±
±±º            ³          ³                ³pois pode haver processos com o período	º±±
±±º            ³          ³                ³fechado e outros em aberto, não sendo	º±±
±±º            ³          ³                ³possível a exibição da mensagem de formaº±±
±±º            ³          ³                ³genérica								º±±
±±ºEduardo K.  ³29/11/2016³TWKZWS          ³Ajuste para não duplicar registros na   º±±
±±º            ³          ³                ³impressão da provisão					º±±
±±ºGabriel A.  ³23/12/2016³MRH-3521        ³Ajuste no cálcudo da provisão de 13º do º±±
±±º            ³          ³                ³mês 12 para que as baixas sejam         º±±
±±º            ³          ³                ³calculadas corretamente;                º±±
±±º            ³          ³                ³Ajuste no cálculo de provisão de férias º±±
±±º            ³          ³                ³proporcionais.                          º±±
±±ºGabriel A.  ³29/12/2016³MRH-3785        ³Ajuste no cálcudo de provisão retroativaº±±
±±º            ³          ³                ³de férias quando há mais de dois        º±±
±±º            ³          ³                ³períodos aquisitivos na SRF.            º±±
±±ºRenan Borges³02/01/2016³MRH-3268        ³Ajuste para calcular provisão corretamenº±±
±±º            ³          ³                ³te quando MV_CSALINC for branco, e fun- º±±
±±º            ³          ³                ³cionário for menor aprendiz com RA_CATEGº±±
±±º            ³          ³                ³ = '07' e MV_MAPREND igual a .T..       º±±
±±³            ³          ³MRH-2818        ³Ajuste p/ não considerar os períodos em ³±±
±±³            ³          ³           	    ³aberto em que a data do período seja   ³±±
±±³            ³          ³           	    ³posterior ao do cálculo	da provisão	³±±
±±ºRenan Borges³03/01/2017³MRH-3280        ³Ajuste para imprimir relatorio Mensal   º±±
±±º            ³          ³                ³com rateio com os valores na linha certaº±±
±±ºGabriel A.  ³03/01/2017³MRH-4221        ³Ajuste na busca de médias do movimento. º±±
±±³Renan Borges³04/01/2017³MRH-4116	 	   ³Ajuste para quando calcular a prov. em  ³±±
±±³            ³          ³                ³um mes que teve 13° fechado seja descon-³±±
±±³            ³          ³                ³tado o que já foi pago corretamente.    ³±±
±±ºGabriel A.  ³10/01/2017³MRH-3785        ³Ajuste no cálcudo de provisão de 13º e  º±±
±±º            ³          ³                ³de férias vencidas quando a folha já foiº±±
±±º            ³          ³                ³fechada.                                º±±
±±³Renan Borges³12/01/2017³MRH-4608	 	   ³Ajuste para ao calcular provisão com o  ³±±
±±³            ³          ³                ³mes fechado, o sistema calcule correta- ³±±
±±³            ³          ³                ³mente os dias que tinha na época.       ³±±
±±ºGabriel A.  ³16/01/2017³MRH-5055        ³Ajuste na busca de períodos quando a RCHº±±
±±º            ³          ³                ³é exclusiva.                            º±±
±±³Renan Borges³27/01/2017³MRH-5615	 	   ³Ajuste para no calculo da provisão quan-³±±
±±³            ³          ³                ³do houver férias e periculosidade paga  ³±±
±±³            ³          ³                ³nas ferias e na folha, seja gerado a bai³±±
±±³            ³          ³                ³xa de adicionais corretamente.          ³±±
±±|Renan Borges|27/01/2017|MRH-5827        |Ajuste para no calculo da provisão quan-|±±
±±|            |          |	               |do houver funcionário for demitido e hou|±±
±±|            |          |	               |ver avo de aviso de férias, seja calcula|±±
±±|            |          |	               |do corretamente nas férias também e não |±±
±±|            |          |	               |somente na rescisão.                    |±±
±±ºGabriel A.  ³31/01/2017³MRH-5954        ³Ajuste na busca da data base quando há  º±±
±±º            ³          ³                ³rescisão posterior.                     º±±
±±|Renan Borges|01/02/2017|MRH-4822        |Ajuste para que ao utilizar o MV_CSALINC|±±
±±|            |          |	               | o sistema não incorpora ao salário a   |±±
±±|            |          |	               |provisão na os adicionais que já estão  |±±
±±|            |          |	               |sendo pagos nela e para nao calc. INSS  |±±
±±|            |          |	               |com verbas que nao incidem.             |±±
±±|Renan Borges|14/02/2017|MRH-6546        |Ajuste parar posicionar na filial corre-|±±
±±|            |          |	               |ta no calculo da provisão mesmo quando  |±±
±±|            |          |	               |for executado o calculo para uma filial |±±
±±|            |          |	               |diferente da posicionada na tela.       |±±
±±|Renan Borges|23/02/2017|MRH-7334        |Ajuste para calcular medias corretamente|±±
±±|            |          |	               | de quando funcionário possuir menos de |±±
±±|            |          |	               |um ano trabalhado.                      |±±
±±|Gabriel A.  |17/03/2017|MRH-7902        |Ajuste para preencher a data base       |±±
±±|            |          |                |corretamente no cálculo de 13º.         |±±
±±|Renan Borges|21/03/2017|MRH-8053        |Ajuste para no cálculo da provisão calcu|±±
±±|            |          |	               |lar a baixa de Inss e FGTS corretamente |±±
±±|            |          |	               |quando houver abono nas férias.         |±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GPEM070()
Local aSays      	:= {}
Local aButtons   	:= {}
Local aParametros	:= Array(16)
Local nOpca      	:= 0
Local nSvRcSRA		:= SRA->(Recno())
Local cTextoProv 	:= ""
Local cSRAFilter	:= SRA->(dbFilter())
Local lRetPE		:= .F.
Local cDescricao
Local cCadastro
Local cGPEGrid		:= ""
Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjCoords	:= {}
Local aObjSize		:= {}
Local nx
Private lAbortPrint := .F.
Private aLogFile 	:= {}
Private aLogTitle 	:= {}
Private aIdProvis   := {}
Private lDissidio	:= .F.
Private lFechou13  	:= .F.
Private lFechouMes  := .f.
Private lItemClVl   := SuperGetMv("MV_ITMCLVL", .F., "2") $ "13"
Private nFec131   	:= 0                                         
Private nVerFatFin 	:= 0
//PERIODOS ABERTOS / FECHADOS
Private cMes          := ""
Private cAno          := ""
Private aPerAberto    := {}
Private aPerAb13      := {}
Private aPrAb131      := {}
Private aPrAb132      := {}
Private aPerFechado   := {}
Private aPerFe13      := {}
Private aPrFe131      := {}
Private aPrFe132      := {}
Private lTemVenc	  := .F.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
							PROCESSAMENTO EM GRID
--> Quando declarar variaveis PRIVATES nas Funcoes ate o Nivel Gpm070Processa e 
necessitar utiliza-los no roteiro de calculo, devera enviar no array aPARAMB  e 
redeclarar na Preparacao do Ambiente com a funcao _SetOwnerPrvt. 
--> Para as variaveis PRIVATES declaradas para o funcionario, enviar atraves  do ar-
ray aCall e redeclarar na GPM070GRD. 
---> Vide Exemplos de ambas as situacoes
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ*/

// Tratar GRID Somente para TOP Connect
Private lGrid		:= SuperGetMv("MV_GRID",, .F.)// Se o parametro esta configurado para utilizacao do GRID

DEFAULT lGpa070mn	:= ExistBlock("GPA070MN") 
DEFAULT lGp070Fim 	:= ExistBlock("GP070FIM")


If lGrid
	// Habilita GRID para rotinas selecionadas em MV_GPEGRID (1=Provisao 2=Calculo Folha/13o. em branco=Todas)
	cGPEGrid := SuperGetMv("MV_GPEGRID",, " ")
	lGrid	 := If(Empty(cGPEGrid) .Or. "1"$cGPEGrid, .T., .F.)
EndIf

// Ponto de entrada para substituir
If (lGpa070mn)
	Execblock("GPA070MN",.F.,.F.)
	Return(Nil)
Endif

// Define o titulo do LOG de Ocorrencias
aLogTitle := { STR0011 } //"PROVISAO DE FERIAS/13o SALARIO"

// Converte os valores do arquivo SRF para o SRT
dbSelectArea("SRT")
dbGoTop()
If SRT->(Eof())
	dbSelectArea("SRF")
	dbGoTop()
	If !Eof()
		GPEProvisao(STR0001,,,,5) // "Importacao do saldo anterior"
		// Apresenta Tela com Log de erros
		fMakeLog(aLogFile, aLogTitle,, .T.)
		If lAbortPrint
			Return
		EndIf
	EndIf
EndIf

cCadastro := OemToAnsi(STR0002) // "Calculo das Provisoes de Ferias e 13o Salario"

// Carregar os Mnemonicos
SetMnemonicos(NIL,NIL,.T.)


Pergunte("GPM070",.F.)

//Monta as Dimensoes dos Objetos
aAdvSize		:= MsAdvSize(,.T.,280)
aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 5 }
aAdd(aObjCoords , { 000 , 000 , .T. , .T. })
aObjSize := MsObjSize(aInfoAdvSize , aObjCoords)

If ! IsBlind()
	DEFINE FONT oFont NAME "Arial" SIZE 7,-11 BOLD
	DEFINE MSDIALOG oDlg TITLE cCadastro From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] PIXEL
		@ aObjSize[1,1] , aObjSize[1,2] GROUP oGroup TO aObjSize[1,3], aObjSize[1,4] LABEL OemToAnsi("") OF oDlg PIXEL
		oGroup:oFont:= oFont
		@ aObjSize[1,1]+15	,aObjSize[1,2]+10 SAY   OemToAnsi(STR0003) Size  500,008 OF oDlg PIXEL FONT oFont
		@aObjSize[1,1]+25	,aObjSize[1,2]+10 SAY   OemToAnsi(STR0004) Size  500,008 OF oDlg PIXEL FONT oFont
		@aObjSize[1,1]+35	,aObjSize[1,2]+10 SAY   OemToAnsi(STR0005) Size  500,008 OF oDlg PIXEL FONT oFont
		@aObjSize[1,1]+45	,aObjSize[1,2]+10 SAY   OemToAnsi(STR0006) Size  500,008 OF oDlg PIXEL FONT oFont
	
		bParam := {|| Pergunte("GPM070",.T.) }
		AAdd(aButtons, {OemToAnsi(STR0079), bParam 				, OemToAnsi(STR0079), OemToAnsi(STR0079) }  )	//"Parametros"
	
		bSet15	:= {|o| nOpca := 1,If(GPM070OK(),oDlg:End(),nOpca:=0)}
		bSet24	:= {|o| oDlg:End() }
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg, bSet15 , bSet24 , Nil , aButtons)  CENTERED
Else
	nOpca := 1
EndIf

// Carregando variaveis mv_par?? para Variaveis do Sistema.
aParametros[01]	:=	lFerias     := (mv_par01==1.Or.mv_par01==3)		//  Tipo de Provisao 1 - Ferias 2 - 13o  3 - Ambas
aParametros[02]	:=	l13oSal     := Iif(cPaisLoc <> "CHI",(mv_par01==2.Or.mv_par01==3),.F.)		//  Tipo de Provisao 1 - Ferias 2 - 13o  3 - Ambas
aParametros[03]	:=	dDataRef   	:= mv_par02							    //  Data de Referencia para Calculo
aParametros[04]	:=	nOrdem  	:= mv_par03								//  Ordem de Calculo 1 - Matricula  2 - Centro de Custo
aParametros[05]	:=	cFilDe	   	:= mv_par04								//	Filial De
aParametros[06]	:=	cFilAte    	:= mv_par05								//	Filial Ate
aParametros[07]	:=	cCcDe	   	:= mv_par06								//	Centro de Custo De
aParametros[08]	:=	cCcAte	   	:= mv_par07								//	Centro de Custo Ate
aParametros[09]	:=	cMatDe	   	:= mv_par08								//	Matricula De
aParametros[10]	:=	cMatAte     := mv_par09								//	Matricula Ate
aParametros[11]	:=	nCorrecao   := (mv_par10 / 100)					    //	Indice de Correcao
aParametros[12]	:=	lDesc1parc  := If(mv_par11 == 1,.T.,.F.)          //  Desc. 1a Parc. 13o
aParametros[13]	:=	lIncluiDem  := If(mv_par12 == 1 .Or. lFerias,.T.,.F.) //  Incluir Demit. 13o
aParametros[14]	:=	n14Salario  := (mv_par13 / 100)                    //  Indice de 14o Sal.
aParametros[15]	:=	cCateg	    := mv_par14								//	Categorias
aParametros[16]	:=	nVerFatFin  := mv_par15 			    			//  Vericar Faturamento-Financeiro (1-Nao; 2-Faturamento; 3-Financeiro; 4-Ambos)

cMes        := StrZero(Month(dDataRef),2)
cAno        := StrZero(Year(dDataRef),4)


//Define se o texto da provisao sera de ferias, 13o ou ambos
cMesAnoProv := StrZero(Month(dDataRef),2)+StrZero(Year(dDataRef),4)
cTextoProv  := If(lFerias .And. l13oSal, STR0011, If(lFerias, STR0009, STR0010)) + " -  " + Transform(cMesAnoProv,"@R 99/9999") //"PROVISŽO DE FERIAS "##"PROVISŽO DE 13o SALARIO"##"PROVISŽO DE FERIAS/13o SALARIO"

If nOpca == 1	
	// Elimina filtro do SRA antes do calculo
	dbSelectArea("SRA")
	Set Filter To
	
	// Carrega variaveis privates comuns a GPEA070,GPER070 e GPEM070|
	GPEProvisao(cTextoProv,,,,4,aParametros)
EndIf


// Restaura filtro do SRA apos o calculos
dbSelectArea("SRA")
Set Filter To &(cSRAFilter)
dbGoTo(nSvRcSRA)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ GP070Proc³ Autor ³ R.H.                ³ Data ³ 26.06.00   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Provisao de Ferias                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPM070Processa()                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GPM070Processa(aPar)
//PROVISAO DE FERIAS
Local lPriVez    := .T.
Local cChaveSRF
Local nX
//PROVISAO DE 13o SALARIO
Local cArqBxa13,cFilBxa13,cMatBxa13,cDatBxa13,cPdBxa13,cValBxa13
//PROVISAO DE FERIAS E 13o SALARIO
Local aAreaSRA     := SRA->(GetArea())
Local nTipoMovMes  := 0
Local nPercFgC     := 0
Local aCabProv     := {}
Local aPerFerAux   := {}
Local cTPRDbf,cTPRNtx
Local cArqDbf,cArqNtx
Local cNomeCpo,cArqBxaPr,cFilBxaPr,cMatBxaPr,cDatBxaPr,cPdBxaPr,cSeqBxaPr,cValBxaPr,cHrsBxaPr
Local lTemCab		:= .F.
Local dDtCalIni		:= Dtoc(MsDate())
Local nDiasFv,nRecnoSRF
// FUNCAO CALC_FER()
Local lDiasAfAcum := .T.
//- GRID
Local aCall			:= {}
Local nLoteGrid 	:= SuperGetMv("MV_REGGRID",,20)
Local nCountReg		:= 0
Local nRegGrid  	:= 0
Local nTrf			:= 0
Local nEleTrf		:= 0
Local nAvosAf		:= 0
Local nDiasAf		:= 0
Local cDtHrProc
Local cMsgLog
Local cProcMed		:= ""           
Local dRefMed		:= dDataRef // Data Referencia para calculo da media
Local dBkpData		:= dDataRef
Local dDataAux		:= CtoD("") 
Local dFerOpen		:= CtoD("") 
Local aTabBPC       := {} // Tabela BPC
Local nValBpc       := 0  // Valor BPC
Local lDepen        := .F. 
Local nPosAux		:= 0
Local lRCHExcl	:= !( Empty( xFilial("RCH") ) )
Local cPdAdic		:= ""
Local cPdsAts		:= ""
Local cPdsPer		:= ""
Local cPdsIns		:= ""
Local cPdsAdC		:= ""
Local cPdsAdT		:= ""
Local cFolAts		:= ""
Local cFolPer		:= ""
Local cFolIns		:= ""
Local cFolAdC		:= ""
Local cFolAdT		:= ""
Local _cFilAnt		:= cFilAnt

Private dIniAfa	:= cTod("")
Private cCongFER   := "1" // Indica se congela por Afastamento Acidente
Private cCong13    := "1" // Indica se congela por Afastamento Acidente 13 SALARIO

Private aFerVenc     := Array(_Linhas,_Colunas)
Private aFerProp     := Array(_Linhas,_Colunas)
Private aRecVenc     := Array(_Linhas,_Colunas)
Private aRecProp     := Array(_Linhas,_Colunas)
Private aTabFer      := {} 	// Tabela para calculo dos dias de ferias
Private aPerFerias   := {}  // Periodos aquisitivos de ferias
Private nV_DFalFer   := 0
Private nP_DFalFer   := 0
Private nTFaltaV	 := 0
Private nTFaltaP	 := 0
Private nDFerAnt     := 0
Private nPerc1T      := 0
Private cCodFer	  	 := cCodAdFer   := cCodUmTer  := cCodFerMs := ""
Private cCodAdicMs   := cCodUmTerMs := cCodAbono  := cCodAboMs := ""
Private cCodSalV     := cCodSalVMs := ""
Private dDtBasFer    := CTOD("")
Private dDtBasCal    := CTOD("")
Private cTpBxFer     := GetMv("MV_TPBXFER",, "1")			//-- Baixa Ferias Total ou Mes e Mes Seguinte
Private cTpCongAf    := "2"									//-- Trata congelamento por periodo Aquisitivo ou Por afastamento
Private cTrfAMES     := (GetMv("MV_TRFAMES",, Space(6))) 	//-- Ano/Mes para inicio das demonstracoes de entrada e saida de transferencias no conceito 1 (transfere saldo origem p/ destino)
Private lTrfAMES     := (!Empty(cTrfAMES) .And. MesAno(dDataRef) >= cTrfAMES)
//PROVISAO DE 13o SALARIO
Private a13Salar     := Array(_Linhas,_Colunas)
Private a14Salar     := Array(_Linhas,_Colunas)
Private aRec13Sl     := Array(_Linhas,_Colunas)
Private aRec14Sl     := Array(_Linhas,_Colunas)
Private lDif13Neg    := (GetMv("MV_DF13NEG",,"S")=="S") // Gerar diferenca de 13o. negativa S/N
Private cCod13o	  	 := cCodAd13o := ""
Private cInss13o	 := ""
Private lBx13Pgt     := .F.								    //-- Baixa 13o Salario pelo pagamento
Private lDesAtiv     := .F.
Private nAvosAnt     := 0
Private nPercAnt     := 0
Private nSalario	 := nSalMes  := nSalDia     := nSalHora  := 0
Private nPercEmp	 := nPercTer := nPercAcTrab := nPercFgts := 0
Private nPerEmp13	 := 0
Private nPercPis	 := 0
Private aInfo	     := {}
Private aVerba       := {}
Private aTransf      := {}
Private aGPSPer      := {}
Private lDemitido    := .F.
Private lINSSAut   	 := .F.
Private lTransfSai   := .F.
Private lSalInc    	 := .F.
Private lTrataTrf    := .F.
Private c__Roteiro   := "   "
Private cPerFeAc     := GetMv("MV_FERPAC",,"N")  				//-- Usada na Funcao FCalcFimAq-(GpexMed).
Private lProvResc    := (GetMv("MV_PROVRES",,"N") == "S") 	//-- Indica se devera provisionar no mes da rescisao
//Private cAfastProv   := "O*P*Q*R*V*W*X*8*B" 				    	//-- Afastamentos tratados pela provisao	//?-
Private aInssEmp[23][2]
Private aVerbasFunc := {}
// POSICAO DOS CAMPOS NO ARRAY aVerbasFunc
Private nPosFil     := 01
Private nPosMat		:= 02
Private nPosPd		:= 03
Private nPosTipo	:= 04
Private nPosQtdSem	:= 05
Private nPosHoras	:= 06
Private nPosValor	:= 07
Private nPosSemana	:= 08
Private nPosParcela := 09
Private nPosPeriodo	:= 10
Private nPosRoteiro	:= 11
Private nPosCc		:= 12
Private nPosData	:= 13
Private nPosDtRef	:= 14
Private nPosSeq		:= 15
Private nPosTpo2	:= 17
Private cCalcSalInc  := SuperGetMv("MV_CSALINC",,Space(6))		//-- Ano/Mes para inicio da busca dos salarios e adicionais no acumulado, sem a utilizacao da fSalInc.
Private lCalcSalInc  := If(Empty(cCalcSalInc) ,.T. , cCalcSalInc > MesAno(dDataRef)  ) 
Private Val_SalMin   := 0.00
Private lTrfSld		 := .F.
Private dDataDem1	 := Ctod("//")
Private lSabDom		:= If(GetMv("MV_SABDOM")=="S", .T., .F.) //Se pagara o sab e domingo qdo demissao na sexta
//Funcao fSalInc
Private aCodFol  	:= {}
Private aRoteiro 	:= {} 
Private nAdtServ 	:= nPeric	:= nInsal := nAdcConf := nAdcTransf := nSalhInc := 0.00	  // Valores dos Adicionais
Private nOutros 	:= 0.00	  // Valores de outras verbas que incorporam
Private cCodAdt  	:= cCodIns  := Space(3) 		  // Codigos que Foram Gerados
Private nSalMin  	:= 0
//FUNCAO fMonta_TPR()
Private cNomeDe  := Replic("A", 30)
Private cNomeAte := Replic("Z", 30)
//FUNCAO Calc_Fer()
Private nDiasProp := nDiasVenc := 0      
Private nTDiasAfa := 0                                           
Private cTipAfa := " "
// QUANDO VIER DE OUTRO LOCAL DIFERENTE DO CALCULO DE PROVISAO NAO DEVERA UTILIZAR O GRID
If Type("lGrid") == "U"
	Private lGrid	:= .F.
EndIf
Private nTimeIni	:= nTimeFim		:=	Seconds()	// Tempo de calculo para o GRID
Private cRecFatEmp	:= ""   
Private cTipoPesq	:= "PRFE"
Private lSemSRF		:= .F.
Private l13Estag	:= .F.
//BUSCA O PERCENTUAL DA CONTRIBUICAO DO FGTS (APOS 01/10/2001
nPercFgC := 0
If MesAno(dDataRef) >= "200110" .And. MesAno(dDataRef) <= "200612"
	nPercFgC := GetMv("MV_PERCFGC",, 0) / 100  //Contribuicao do FGTS
EndIf

If !lGrid
	//Cria Arquivo de Medias Temporario
	Cria_TRP(@cArqDbf,@cArqNtx)
EndIf

//Monta o arquivo temporario "TPR" a partir do SRA e SRE
fMonta_TPR(@cTPRDbf,@cTPRNtx,nOrdem,dDataRef,@lSalInc,@lTrataTrf,@aTransf,lIncluiDem)
dbSelectArea("TPR")

//Arquivo para Controle de LOG - Transacao
If lGrid
	Private cNameFile   := "M070"+FWGrpCompany("SRC")				// Tabela temporaria de log
	Private cFileTrf	:= Subst(CriaTrab(NIL,.f.),1,7) + "T"	// Tabela temporaria de transferencias
	Private cDbfLog 	:= "M070LOG"							// Alias de log
	Private cTrfDbf		:= "M070TBL"							// Alias de transferencia
	Private cChaveLog	:= DTOC(MsDate()) + "-" + Time()
	Private nTamTrf		:= Len(aTransf)						// No. de elementos total de aTransf

	Private nRetCalc	:= 0

	If !fOpenLog(@cDbfLog)   	
		 Aviso(STR0015, STR0027, { STR0016 }) // "Atencao" ## "Nao foi possivel abrir o arquivo de registro de ocorrencias." ## "Ok"
		 Return(.F.)
	EndIf
	
	IF Select(cDbfLog) > 0
		DbSelectArea(cDbfLog)
		If !Eof()
			cDtHrProc := AllTrim(Substr((cDbfLog)->CHAVE, 1)) + " " + OemToAnsi(STR0028) // "Horas"
			cMsgLog := OemToAnsi(STR0029) + " " + cDtHrProc + " " + OemToAnsi(STR0030) + CRLF // "O processamento realizado em:" ## "não foi concluído com Sucesso."
			cMsgLog += OemToAnsi(STR0031) + CRLF + CRLF // "Os últimos funcionários processados foram: "
	
			cMsgLog += Substr(OemToAnsi(STR0032)	+ Space(5),1,16) // "Requisicao"
			cMsgLog += Substr(OemToAnsi(STR0033)	+ Space(5),1,12) // "Filial"
			cMsgLog += Substr(OemToAnsi(STR0034)	+ Space(5),1,15) // "Matricula"
			cMsgLog += OemToAnsi(STR0035)	+ CRLF // "Nome"
			cMsgLog += Replicate("-", 75) + CRLF 
			While (cDbfLog)->(!Eof())
				If (cDbfLog)->CONTROL = '1'
					cMsgLog += (cDbfLog)->REQUIS + Space(8)
					If !Empty((cDbfLog)->MAT)
						cMsgLog += (cDbfLog)->FILIAL + Space(10)
						cMsgLog += (cDbfLog)->MAT + Space(9)
						cMsgLog += Substr((cDbfLog)->NOME,1,36) + CRLF
					Else
						cMsgLog += OemToAnsi(STR0036) + CRLF //"Nenhum Funcionário Processado nessa Requisição"
					EndIf
				EndIf
				(cDbfLog)->(Dbskip())
			EndDo
			cMsgLog += CRLF + CRLF + OemToAnsi(STR0037) //"Deseja Continuar com o Calculo?"
			If MsgYesNo(cMsgLog, OemToAnsi(STR0015))			
				cQuery := " DELETE FROM "
				cQuery += " "+ cNameFile
				TcSqlExec(cQuery)
			Else
			 	Return
			EndIf
		EndIf
	EndIf
	
	// CRIACAO DA TABELA TEMPOARARIA PARA ARMAZENAMENTO DO ARRAY  ³
	// ATRANSF. ISSO FAZ-SE NECESSARIO DEVIDO A GRANDE QUANTIDADE ³
	// DE INFORMACOES QUE ESSE ARRAY PODE CONTER E QUE ASSIM NAO  ³
	// HA COMO PASSA-LO VIA APARAMB NA PREPARACAO DO GRID, POIS   ³
	// PODE ESTOURAR O LIMITE MAXIMO DE TAMANHO DE 1MB DA STRING  ³
	// ENVIADA VIA RPC PARA PREPARACAO DO AMBIENTE.               ³
			
	If !CriaTransf()
		 Aviso(STR0015, STR0063, { STR0016 }) // "Atencao" ## "Não foi possível abrir a tabela temporária de transferências." ## "Ok"
		 Return(.F.)
	EndIf
	
	IF Select(cTrfDbf) > 0
		DbSelectArea(cTrfDbf)
		For nTrf := 1 To nTamTrf
			For nEleTrf := 1 To 3
				Reclock((cTrfDbf), .T.)
				(cTrfDbf)->TRFPOS		:= nTrf	// Posicao no aTransf original
				(cTrfDbf)->TRFELE		:= nEleTrf	// Posicao no aTransf original
				(cTrfDbf)->TRFEMPP	:= aTransf[nTrf, nEleTrf, 1]
				(cTrfDbf)->TRFFILIALP	:= aTransf[nTrf, nEleTrf, 2]
				(cTrfDbf)->TRFCCP		:= aTransf[nTrf, nEleTrf, 3]
				(cTrfDbf)->TRFMATP	:= aTransf[nTrf, nEleTrf, 4]
				(cTrfDbf)->TRFDATE	:= aTransf[nTrf, nEleTrf, 5]
				(cTrfDbf)->TRFNLIDO	:= aTransf[nTrf, nEleTrf, 6]
				(cTrfDbf)->(MsUnlock())
			Next nEleTrf
		Next nTrf
	EndIf
EndIf

//GARANTE ORDEM 1 PARA BUSCA DOS LANCAMENTOS NO MOVIMENTO
dbSelectArea("SRC")
dbSetOrder(1)

//GARANTE ORDEM 1 PARA BUSCA DOS FUNCIONARIOS
dbSelectArea("SRA")
dbSetOrder(1)

//POSICIONA NO INICIO DO ARQUIVO
dbSelectArea("TPR")
dbGoTop()

If !lGrid
	//CARREGA REGUA DE PROCESSAMENTO
	ProcRegua(RecCount())
EndIf

cFilialAnt := Replicate("!", FWGETTAMFILIAL)
cCcAnt	   := "!!!!!!!!!"

While TPR->(!Eof())
	If lAbortPrint
		Exit
	Endif
	If lGrid
		If nCountReg == 0 .Or. nCountReg >= nLoteGrid
			Aadd(aCall, {})
			MsProcTxt(OemToAnsi(STR0044) + " " + Strzero(Len(aCall),6)) // "Numero de Requisições Geradas: "
			nRegGrid++
			nCountReg := 0
		EndIf
		nCountReg++
		Aadd(aCall[nRegGrid], {nRegGrid, TPR->PR_FILIAL, TPR->PR_MAT, TPR->PR_CC, TPR->PR_TIPMOVI })
	Else
		// Apresenta o Funcionario que esta sendo calculado
		IncProc(TPR->PR_FILIAL+" - "+TPR->PR_MAT+" - "+TPR->PR_NOME)
		// Inicializa a gravacao dos lancamentos do SIGAPCO
		PcoIniLan("000091")
		// Garante o Posicionamento do Funcionario no SRA
		dbSelectArea("SRA")
		dbSeek(TPR->PR_FILIAL + TPR->PR_MAT)
		dbSelectArea("TPR")
		
		cProcesso := SRA->RA_PROCES
		lHojorva := SRA->RA_CATFUNC == "H" .And. SRA->(ColumnPos( "RA_HOJORVA" )) > 0 .And. SRA->RA_HOJORVA == "1"
		
		// Quebra filial para buscar as tabelas
		If TPR->PR_FILIAL # cFilialAnt
			If lRCHExcl .Or. (!lRCHExcl .And. cFilialAnt == Replicate("!", FWGETTAMFILIAL) )
				// Carregar os periodos abertos (aPerAberto) e/ou fechados (aPerFechado), de acordo com a competencia de calculo
				fRetPerComp(cMes, cAno, xFilial("RCH",TPR->PR_FILIAL), Nil, fGetCalcRot("5"), @aPrAb131, @aPrFe131, Nil, Nil, Nil, Nil, .F.)//131
				fRetPerComp(cMes, cAno, xFilial("RCH",TPR->PR_FILIAL), Nil, fGetCalcRot("6"), @aPrAb132, @aPrFe132, Nil, Nil, Nil, Nil, .F.)//132
				aPerFe13 := aClone(aPrFe131)
				For nX := 1 to Len(aPrFe132)
					Aadd(aPerFe13,aClone(aPrFe132[nX]))
				Next nX
				
				aPerAb13 := aClone(aPrAb131)
				For nX := 1 to Len(aPrAb132)
					Aadd(aPerAb13,aClone(aPrAb132[nX]))
				Next nX
				
				fRetPerComp(cMes, cAno, xFilial("RCH",TPR->PR_FILIAL), Nil, fGetRotOrdinar(), @aPerAberto, @aPerFechado)
			EndIf
			
			If !Fp_CodFol(@aCodFol,TPR->PR_FILIAL)		 .Or.;
			   !fInfo(@aInfo,TPR->PR_FILIAL)			 .Or.;
	           !fInssEmp(TPR->PR_FILIAL,@aInssEmp,.F.,MesAno(dDataRef))
				Exit
			Endif
			//-- Volta o conteúdo dDataRef que foi informado nos parâmetros
			dDataRef := dBkpData
			//RESGATA OS PERCENTUAIS DE TERCEIROS ARMAZENADOS NA TABELA AUXILIAR S035	
			fGPSVal(TPR->PR_FILIAL,"999999",@aGPSPer,"1")
			// Limpa array de identificadores utilizados na cadastrados
			aIdProvis := {}
			// Carrega Codigos de Ferias/13o Salario Para Baixa da Provisao
			fBusCodBx()
			// Carrega os identificadores da Provisao
			fIdentProv(@aVerba,aCodFol)
			// Verifica a existencia dos identificadores da provisao
			If TPR->PR_CATFUNC $ "E|G"
				fChkIdent(aVerba,_RecVenc,{_Atual,_BxFer,_BxTrf},.T.)
			Else
				fChkIdent(aVerba,_FerVenc,{_Atual,_BxFer,_BxTrf},.T.)
			Endif
			fChkIdent(aVerba,_13Salar,{_Atual,_BxTrf},.T.)
			// Verifica a existencia dos identificadores da provisao Mes	 
			If lGeraPMes
				If TPR->PR_CATFUNC $ "E|G"
					fChkIdent(aVerba,_RecVenc,{_Atual},.T.)
				Else
					fChkIdent(aVerba,_FerVMes,{_Atual},.T.)
				Endif			
				fChkIdent(aVerba,_13SVMes,{_Atual},.T.)
			Endif
			// Verifica a existencia dos identificadores de baixa de 13o.
			lBx13Pgt := fChkIdent(aVerba,_13Salar,{_Bx13O},.T.)
			// Verifica a existencia dos identificadores de correcao
			If nCorrecao > 0
				If TPR->PR_CATFUNC $ "E|G"
					fChkIdent(aVerba,_RecVenc,{_Corre},.T.)
				Else
					fChkIdent(aVerba,_FerVenc,{_Corre},.T.)
				Endif
				fChkIdent(aVerba,_13Salar,{_Corre},.T.)
			EndIf
			// Verifica a existencia dos identificadores da provisao 14 Sal
			If n14Salario > 0
				fChkIdent(aVerba,_14Salar,{_Atual},.T.)
			EndIf
			// Busca percentual do identificador de 1/3 de ferias
			dbSelectArea("TPR")
			cFilAnt := cFilialAnt := TPR->PR_FILIAL
			nPerc1T := PosSrv(aCodFol[77,1],TPR->PR_FILIAL,"RV_PERC")
			// Busca o percentual do identificador do PIS
			nPercPis := PosSrv(aCodFol[229,1],TPR->PR_FILIAL,"RV_PERC") / 100
			// Se gerou Ocorrencias aborta para apresentacao do LOG
			If fAdicLog(aIdProvis,TPR->PR_FILIAL)
				Exit
			EndIf
			
			cProcMed := ""
		EndIf
		
		If cProcMed <> cProcesso
			cProcMed   := cProcesso
			lFechou13  := .F.
			lFechouMes := .F.
			For nX := 1 To Len(aPerFe13)
				If aPerFe13[nX,3] + aPerFe13[nX,4] == cMes + cAno .and. aPerFe13[nX,7] == cProcMed
					lFechou13 := .T.
				EndIf	 	
			Next nX
			For nX := 1 To Len(aPerFechado)
				If aPerFechado[nX,3] + aPerFechado[nX,4] == cMes + cAno .and. aPerFechado[nX,7] == cProcMed
					lFechouMes := .T.
				EndIf	 	
			Next nX
		EndIf
		
		//| Carrega tabela para apuracao dos dias de ferias - aTabFer    |
		//| 1-Meses Periodo    2-Nro Periodos   3-Dias do Mes    4-Fator |
		fTab_Fer(@aTabFer)
		// Carrega variaveis para calculo dos encargos
		fEncargEmp(@nPercEmp, @nPercTer, @nPercAcTrab, @nPercFgts, nPercFgC, @cRecFatEmp, @nPerEmp13)
		nRecnoSRF := 0
		nDFerAnt  := 0								  // Dias de ferias antecipadas
		dDtBasFer := TPR->PR_ADMISSA			      // Data Base Ferias Funcionario
		dDtBasCal := dDtBasFer                        // Data Base utilizada no calculo (de acordo com data de referencia)
		dbSelectArea("SRF")
		SRF->(dbSetOrder(2))
		If !SRF->(dbSeek(TPR->(PR_FILIAL + PR_MAT + FGETCODFOL("0072")))) .Or. (SRA->RA_CATFUNC $ "E*G" .And. ((!SRF->(dbSeek(TPR->(PR_FILIAL + PR_MAT + FGETCODFOL("0891"))))) .Or. ( (SRF->(dbSeek(TPR->(PR_FILIAL + PR_MAT + FGETCODFOL("0891"))))) .And. mv_par01==2)))
			//SE NÃO ENCONTRAR AS VERBAS DE FERIAS OU RECESSO DE ESTAGIARIO EMITE LOG
			If !SRF->(dbSeek(TPR->(PR_FILIAL + PR_MAT + FGETCODFOL("0072")))) .Or. (SRA->RA_CATFUNC $ "E*G" .And. ((!SRF->(dbSeek(TPR->(PR_FILIAL + PR_MAT + FGETCODFOL("0891")))))))
				lSemSRF := .T.
			//SE FOR ESTAGIARIO E SELECIONOU PROVISAO DE 13º SALARIO EMITE LOG
			ElseIf SRA->RA_CATFUNC $ "E*G" .And. ( (SRF->(dbSeek(TPR->(PR_FILIAL + PR_MAT + FGETCODFOL("0891")))) .And. mv_par01==2) )
				l13Estag := .T. 
			Endif
			// Adiciona aviso informando funcionario nao calculado
			If lPriVez
				Aadd(aLogFile, { OemToAnsi(STR0080), "" } ) //"FUNCIONARIOS NAO CALCULADOS!"
				If lSemSRF 
					Aadd(aLogFile[1], OemToAnsi(STR0081)) //"Nao foi encontrado registro de periodo aquisitivo no Controle de Dias de Direito ( SRF )"
					lSemSRF := .F.
				Endif
				If l13Estag
					Aadd(aLogFile[1], OemToAnsi(STR0104)) //"Foi solicitada a Provisão de 13º Salário para Estagiário, o cálculo não será efetuado."
					l13Estag := .F.
				Endif
				Aadd(aLogFile[1], "" )
				Aadd(aLogFile[1], OemToAnsi(STR0082)) // "Filial  Matricula   Nome                               Admissao"
				Aadd(aLogFile[1], "------  ---------   ---------------------------------  ----------" )
				lPriVez := .F.
			EndIf			
			Aadd(aLogFile[1], TPR->(PR_FILIAL + " " + PR_MAT + "      " + PR_NOME + "     " + DTOC(TPR->PR_ADMISSA)))
			DbSelectArea("TPR")
			TPR->(dbSkip())
			Loop
		Else
			// CARREGA PERIODOS AQUISITIVOS PARA CALCULO.
			dDataAux := dDtBasFer
			dFerOpen := CtoD("")
			aPerFerAux := {}
			cChaveSRF := TPR->PR_FILIAL + TPR->PR_MAT + If(TPR->PR_CATFUNC $ "E|G" ,FGETCODFOL("0891"),FGETCODFOL("0072"))
				
			SRH->(DbSetOrder(1))
			If lFechouMes .and. SRH->(DbSeek(SRA->RA_FILIAL + SRA->RA_MAT))
				While SRH->(!Eof() .and. RH_FILIAL + RH_MAT == SRA->RA_FILIAL + SRA->RA_MAT)
					If AnoMes(SRH->RH_DATAINI) >= AnoMes(dDataRef)
						If AnoMes(SRH->RH_DATAINI) > AnoMes(dDataRef)
							dFerOpen := SRH->RH_DATABAS //Grava a data do primeiro periodo aquisitivo pago após a data de referencia
						EndIf
						Exit
					EndIf
					SRH->(DbSkip())
				EndDo
			EndIf
				
			If lFechouMes .and. Empty(dFerOpen) .and. !(Empty(TPR->PR_DEMISSA))
				DbSelectArea("SRG")
				SRG->( DbSetOrder(3) )
				If SRG->( MsSeek(TPR->PR_FILIAL + TPR->PR_MAT + DToS(TPR->PR_DEMISSA) ) )
					DbSelectArea("SRR")
					SRR->( DbSetOrder(8) )
					If SRR->( MsSeek(TPR->PR_FILIAL + TPR->PR_MAT + "R" + DToS(SRG->RG_DTGERAR) + aCodFol[0086,1] ) )
						dFerOpen := SToD( SubStr(SRR->RR_NUMID,1,8) )
					ElseIf SRR->( MsSeek(TPR->PR_FILIAL + TPR->PR_MAT + "R" + DToS(SRG->RG_DTGERAR) + aCodFol[0087,1] ) )
						dFerOpen := SToD( SubStr(SRR->RR_NUMID,1,8) )
					EndIf
				EndIf
			EndIf
				
			lPriSRF := .T.
			SRH->(DbSetOrder(RetOrder("SRH","RH_FILIAL+RH_MAT+DTOS(RH_DATAINI)")))

			While (cChaveSRF) == (SRF->RF_FILIAL + SRF->RF_MAT + SRF->RF_PD) .And. !SRF->(EOF())
				
				If ( ( SRF->RF_STATUS $ " 1" .and. SRF->RF_DATABAS <= dDataRef .and. (lPriSRF .Or.( SRF->RF_DFERVAT > 0 .or. SRF->RF_DFERAAT > 0  .or. SRF->RF_DVENPEN > 0 )) ) .or. ;//Carrega o primeiro periodo aquisitivo com dias vencidos ou a vencer
					(!SRF->RF_STATUS $ " 1" .and. !Empty(dFerOpen) .and. dFerOpen <= SRF->RF_DATABAS))
					If lPriSRF .and. !lFechouMes
						lPriSRF := .F.
						//Se o mês esta aberto, e houve cálculo de férias no mês, 
						//não utiliza o primeiro período aquisitivo que continua aberto,
						//porém deverá ser fechado no fechamneto mensal.
						If SRH->(DbSeek(SRA->RA_FILIAL + SRA->RA_MAT + AnoMes(dDataRef)))
							If SRF->RF_DATABAS < dDataRef .And. (SRH->RH_DFERVEN <= SRH->RH_DFERIAS + SRH->RH_DABONPE)
								SRF->(dbSkip())
								Loop
							EndIf
						EndIf
					EndIf
						
					aAdd(aPerFerAux,{	SRF->RF_DATABAS	,;										  		 			// 01 - Inicio Database de Ferias
										If(Empty(SRF->RF_DATAFIM),fCalcFimAq(SRF->RF_DATABAS),SRF->RF_DATAFIM),;    // 02 - Final Database de Ferias
										SRF->RF_DFERVAT	,;															// 03 - Dias de ferias vencidas
										SRF->RF_DFERAAT	,;															// 04 - Dias de ferias a vencer
										0.00			,;															// 05 - Dias totais de afastamento por periodo
										SRF->RF_OBSERVA	,;															// 06 - Descricao do tipo de afastamento do periodo
										CtoD("")		,;															// 07 - Data de original de termino do p.aquisitivo quando houver prorrogacao do mesmo RWX
										If(Empty(SRF->RF_STATUS),"1",SRF->RF_STATUS),;								// 08 - Status do periodo de ferias:  1-Ativo (Vencidos/A vencer)/2-Prescrito (Perdido)/3-Pago
										CtoD("")		,;															// 09 - Data de Inicio do Proximo periodo caso seja um periodo perdido.
										0				,;															// 10 - Quantidade dias de deducao para o direito apurado no periodo
										SRF->RF_DVENPEN ,;     														// 11 - Dias Vencidos Pendentes
										SRF->RF_IVENPEN ,;     														// 12 - Data Inicia Vencido Pendente
										SRF->RF_FVENPEN ,;															// 13 - Data Inicia Vencido Pendente
										SRF->RF_DFERANT ,;     														// 14 - Dias de Ferias Antecipadas
										SRF->RF_DFALVAT ,;     														// 15 - Dias de Faltas Vencidas
										SRF->RF_DFALAAT ,; 				    										// 16 - Dias de Faltas a Vencer
										If(cPaisLoc$"VEN|EQU",SRF->RF_DBONVAT,NIL),;				 				// 17 - Dias de bono vencido
										If(cPaisLoc$"VEN|EQU",SRF->RF_DBONAAT,NIL),; 								// 18 - Dias de bono a Vencer
										0				,;															// 19 - Total de dias de ferias
										0				,;															// 20 - Total de dias de bonificacao
										0				,;															// 21 - Dias de Faltas vencidas bonificacao
										0				,;															// 22 - Dias de ¦Faltas a Vencer bonificacao		
										0				,;															// 23 - Dias de ausencia convertidos em ferias
										0				,;      													// 24 - Total de Dias de Ferias do Periodo
										SRF->RF_DIASANT ,;					      									// 25 - Dias Gozados Vencidos
										SRF->RF_DIASANT	,;	    													// 26 - Dias Gozados a Vencer 
										0               ,;      													// 27 - Dias Subsid. Vencidos
										0               ,;   														// 28 - Dias Subsid. a Vencer
										0				,; 															// 29 - Dias de Pagto. Minimo na Adm/Dem (cpo. RF_PAGOFER desabilitado 08/2012)
										SRF->( RECNO() ),;															// 30 - Recno do aquivo
										Iif(Type("SRF->RF_FERPAGA")<>"U",  SRF->RF_FERPAGA, 0) ,;					// 31 - Dias pagos em R$ na folha
										SRF->RF_DATAATU	;															
										})
					dDtBasFer := SRF->RF_DATABAS
					If !(MesAno(SRH->RH_DATAINI) == MesAno(dDataRef)) .and. ( Empty(dFerOpen) .or. (dFerOpen > SRF->RF_DATABAS))
						nDFerAnt  += SRF->RF_DFERANT
					EndIf
					//Quando o período esta pago mas as férias ocorreram após a data de referencia da provisão, não grava os dias pagos e deixa o período como ativo. 
					If (!SRF->RF_STATUS $ " 1" .and. !Empty(dFerOpen) .and. dFerOpen <= SRF->RF_DATABAS)
						aPerFerAux[Len(aPerFerAux),8] := "1"
					EndIf
						
					If !SRF->RF_STATUS $ " 1" .and. !Empty(dFerOpen) .and. dFerOpen == SRF->RF_DATABAS .and. Len(aPerFerAux) > 0
						Exit
					EndIf							
				Else
					If SRF->RF_DATAFIM <= dDataRef
						dDtBasFer := SRF->RF_DATAFIM + 1
					EndIf
					nDFerAnt  := 0
				EndIf
				SRF->(dbSkip())
			EndDo
				
			aPerFerias := {}
				
			If Len(aPerFerAux) > 0
				aAdd(aPerFerias,aPerFerAux[Len(aPerFerAux)]) //Recalcula apenas do último periodo aquisitivo aberto em diante
				dDtBasFer := aPerFerias[1][1]
			ElseIf AnoMes(TPR->PR_ADMISSA) == AnoMes(dDataRef) .or. !(Empty(TPR->PR_DEMISSA))//Se for no mes de admissao e não tiver dias de direito, força calculo dos dias de direito desde o inicio do periodo aquisitivo.
				dDtBasFer := dDataAux
			EndIf
				
			nTDiasAfa := 0 
	  		Calc_Fer(aPerFerias,If(lProvResc .And. TPR->PR_TIPMOVI == _Demitido,TPR->PR_DEMISSA,dDataRef),@nDiasVenc,@nDiasProp,@nTDiasAfa,,lDiasAfAcum,.F.,dDtBasFer)
	  		    
			If Len(aPerFerias) == 0 .And. !l13oSal
				TPR->(dbSkip())
				Loop
			EndIf
				
			If Len(aPerFerAux) > 0
				dDtBasFer := aPerFerAux[1][1]
				For nPosAux := 1 to Len(aPerFerias)
					If nPosAux == 1
						aPerFerAux[Len(aPerFerAux)] := aClone(aPerFerias[nPosAux])
					Else
						aAdd(aPerFerAux, aPerFerias[nPosAux])
					EndIf
				Next nPosAux
				aPerFerias := aPerFerAux
			EndIf
			If lFerias				
				//Recalcula Dias Vencidos e proporcionais de incluindo os periodos que não foram enviados para calc_fer
				nDiasVenc := 0
				nDiasProp := 0
				Aeval(aPerFerias,{|X| If(X[8] == "1",(nDiasVenc += X[3], nDiasProp += X[4]),nil)})

				SRF->(dbSetOrder(1))
				For nX := 1 To Len(aPerFerias)
//					If aPerFerias[nX,8] <> "4"
//						If !SRF->(dbSeek(TPR->(PR_FILIAL + PR_MAT + DToS(aPerFerias[nX,1]) + FGETCODFOL("0072"))))
//							SRF->(RecLock("SRF",.T.))
//							SRF->RF_FILIAL  := TPR->PR_FILIAL
//							SRF->RF_MAT     := TPR->PR_MAT
//							SRF->RF_PD      := FGETCODFOL("0072")
//							SRF->RF_DATABAS := aPerFerias[nX,1]
//							SRF->RF_DATAFIM := aPerFerias[nX,2]
//							SRF->RF_DFERVAT := aPerFerias[nX,3]
//							SRF->RF_DIASDIR := aTabFer[3]
//							SRF->RF_DFERAAT := aPerFerias[nX,4]
//							SRF->RF_STATUS  := aPerFerias[nX,8]
//							SRF->RF_DVENPEN := aPerFerias[nX,11]
//							SRF->RF_IVENPEN := aPerFerias[nX,12]
//							SRF->RF_FVENPEN := aPerFerias[nX,13]
//							SRF->RF_DFERANT := aPerFerias[nX,14]
//							SRF->RF_DFALVAT := aPerFerias[nX,15]
//							SRF->RF_DFALAAT := aPerFerias[nX,16]
//						EndIf
//					Endif
					If (dDataRef >= SRF->RF_DATABAS .And. dDataRef <= SRF->RF_DATAFIM)
						nRecnoSRF := SRF->(Recno()) // Preserva o Recno da Tabela SRF p/posterior gravacao 
					EndIf
					If dDataRef >= SRF->RF_DATABAS					
						//Verificar as ausências no período
						fRetAfas(CTOD("01/01/"+Str(Year(dDataRef),4), "DDMMYY"),dDataRef,"",@nAvosAf,@nDiasAf,,@aAfast,,.F.)
						If Len(aAfast)> 0
 							dIniAfa := aAfast[1,3]
 							cCongFeR := aAfast[1,9]
							cCong13  := aAfast[1,10]
						Endif
					Endif
					
					dbSelectArea("SRF")
					//SRF->(MsUnlock()) 
				Next nX
				SRF->(DbGoTo(nRecnoSRF))
			EndIf
		Endif
		SRF->(dbSetOrder(1))

		dbSelectArea("TPR")
		// VERIFICA O TIPO DE MOVIMENTACAO DO FUNCIONARIO
		nTipoMovMes := TPR->PR_TIPMOVI
		lTransfSai  := (nTipoMovMes == _Trfe_Sai)
		lDemitido   := (nTipoMovMes == _Demitido)
		// 1o ELEMENTO DO ARRAY - A N T E R I O R
		fQryDetSRT(aVerba,aTransf,dDataRef,lTrataTrf,.F.,lFerias,l13oSal) 
		// 2o ELEMENTO DO ARRAY - C O R R E C A O
		If nCorrecao > 0
			If lFerias
				fGrvArrPrv(aFerVenc, _Corre, _Anter, nCorrecao, { _Dias })
				fGrvArrPrv(aFerProp, _Corre, _Anter, nCorrecao, { _Dias })
			EndIf
			If l13oSal .And. !TPR->PR_CATFUNC $ "E|G"
				fGrvArrPrv(a13Salar, _Corre, _Anter, nCorrecao, { _Avos,_1Par })
				If n14Salario > 0
					fGrvArrPrv(a14Salar, _Corre, _Anter, nCorrecao, { _Avos,_1Par })
				EndIf
			Endif
		Endif
		// Zera variaveis para uma nova busca de salario e ferias
	    nV_DFalFer := nP_DFalFer := nTFaltaV := nTFaltaP := 0
	    nSalario   := nSalMes    := nSalDia  := nSalHora := 0
	    nAdtServ   := nPeric     := nInsal := nAdcConf := nAdcTransf   := 0.00
		nOutros    := 0.00
		// Se mes estiver fechado, calcular com valores do SRT

		If lFechouMes .And. fBusCabSRT(dDataRef,@aCabProv)
			dDtBasFer  := aCabProv[_DBsProv]
			nDFerAnt   := aCabProv[_DFerAnt]
		    nSalario   := aCabProv[_SalProv]
		    nSalMes    := aCabProv[_SalProv]
		    nSalDia    := aCabProv[_SalProv] / 30
		    nSalHora   := aCabProv[_SalProv] / TPR->PR_HRSMES
		    lTemCab	:= .T.
		EndIf
	
		// 6o ELEMENTO DO ARRAY - B A I X A  D E  F E R I A S / 1 3 o   ³
		If (!lDemitido .Or. lProvResc) .And. !lTransfSai	//?-
			If lFerias
				fBxaFerProv(@dDtBasFer,@nDFerAnt)//,cArqBxaPr,cFilBxaPr,cMatBxaPr,cDatBxaPr,cPdBxaPr,cSeqBxaPr,cValBxaPr,cHrsBxaPr,lTemCab)
			EndIf
			If l13oSal .And. lBx13Pgt .And. (Month(dDataRef) == 12) .And. !TPR->PR_CATFUNC $ "E|G" 
				fBxa13oProv() //cArqBxa13,cFilBxa13,cMatBxa13,cDatBxa13,cPdBxa13,cValBxa13)
			EndIf
		EndIf

		// 4o ELEMENTO DO ARRAY - ATUAL
		If lTransfSai .And. lCalcSalInc
			If nSalMes == 0
				// CALCULA SALARIO MES , DIA , HORA DO FUNCIONARIO
				fSalario(@nSalario,@nSalHora,@nSalDia,@nSalMes,"A",MesAno(dDataRef))
			EndIf	
		Else
			// CALCULA SALARIO INCORPORADO MES , DIA , HORA DO FUNCIONARIO
			If !lCalcSalInc
				cAliasPROC	:= "QPROC"
				fQrySRD(TPR->PR_FILIAL , TPR->PR_MAT , MesAno(dDataRef))
				//BUSCA O SALARIO MES, ADIC.TPO.SERVICO, INSALUBRIDADE E PERICULOSIDADE
				cPdsAts	:=	aCodFol[1297,1] + "|" + aCodFol[1299,1] + "|" + aCodFol[1313,1] + "|" + aCodFol[1315,1] + "|" + ;
							aCodFol[1296,1] + "|" + aCodFol[1298,1] + "|" + aCodFol[1312,1] + "|" + aCodFol[1314,1]	
				cPdsPer	:=	aCodFol[1301,1] + "|" + aCodFol[1303,1] + "|" + aCodFol[1317,1] + "|" + aCodFol[1319,1] + "|" + ;
							aCodFol[1300,1] + "|" + aCodFol[1302,1] + "|" + aCodFol[1316,1] + "|" + aCodFol[1318,1]
				cPdsIns	:=	aCodFol[1304,1] + "|" + aCodFol[1320,1] + "|" + aCodFol[1322,1] + "|" + aCodFol[1306,1] + "|" + ;
							aCodFol[1305,1] + "|" + aCodFol[1307,1] + "|" + aCodFol[1321,1] + "|" + aCodFol[1323,1]
				cPdsAdC	:=	aCodFol[1308,1] + "|" + aCodFol[1324,1] + "|" + aCodFol[1309,1] + "|" + aCodFol[1325,1]
				cPdsAdT	:=	aCodFol[1310,1] + "|" + aCodFol[1326,1] + "|" + aCodFol[1311,1] + "|" + aCodFol[1327,1]
				
				cFolAts		:= aCodFol[001,1] + "|" + aCodFol[002,1] + "|" + aCodFol[003,1] + "|" + aCodFol[004,1] + "|" + aCodFol[005,1]
				cFolPer		:= aCodFol[36,1]
				cFolIns		:= aCodFol[37,1] + "|" + aCodFol[38,1] + "|" + aCodFol[39,1]
				cFolAdC		:= aCodFol[984,1]
				cFolAdT		:= aCodFol[988,1]
				
				cCodAdFol	:= aCodFol[318,1]+ "|" + cFolAts + "|" + cFolPer + "|" + cFolIns + "|" + cFolAdC + "|" + cFolAdT
				cPdAdic := cCodAdFol + "|" +  cPdsAts + "|" + cPdsPer + "|" + cPdsIns + "|" + cPdsAdC + "|" + cPdsAdT
				While !(cAliasPROC)->(eof())
					If lFechouMes
						If !lTemCab .And. (cAliasPROC)->RD_PD == aCodFol[318,1]
							nSalMes += (cAliasPROC)->RD_VALOR
						ElseIf (cAliasPROC)->RD_PD == aCodFol[671,1]
							nAdtServ += (cAliasPROC)->RD_VALOR
						ElseIf (cAliasPROC)->RD_PD == aCodFol[672,1]
							nInsal += (cAliasPROC)->RD_VALOR
						ElseIf (cAliasPROC)->RD_PD == aCodFol[673,1]
							nPeric += (cAliasPROC)->RD_VALOR
						ElseIf (cAliasPROC)->RD_PD $ cPdAdic
							If (cAliasPROC)->RD_PD $ cPdsAts
								If PosSrv(aCodFol[001,1],(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S" .OR.;
									PosSrv(aCodFol[002,1],(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S" .OR.;
									PosSrv(aCodFol[003,1],(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S" .OR.;
									PosSrv(aCodFol[004,1],(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S" .OR.;
									PosSrv(aCodFol[005,1],(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S"
										nSalMes -= (cAliasPROC)->RD_VALOR
								EndIf
							ElseIf (cAliasPROC)->RD_PD $ cPdsPer
								If PosSrv(cFolPer,(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S"
										nSalMes -= (cAliasPROC)->RD_VALOR
								EndIf
							ElseIf (cAliasPROC)->RD_PD $ cPdsIns
								If PosSrv(aCodFol[37,1],(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S" .OR.;
									PosSrv(aCodFol[38,1],(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S" .OR.;
									PosSrv(aCodFol[39,1],(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S"
										nSalMes -= (cAliasPROC)->RD_VALOR
								EndIf
							ElseIf (cAliasPROC)->RD_PD $ cPdsAdC
								If PosSrv(cFolAdC,(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S"
									nSalMes -= (cAliasPROC)->RD_VALOR
								EndIf
							ElseIf (cAliasPROC)->RD_PD $ cPdsAdT
								If PosSrv(cFolAdT,(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S"
									nSalMes -= (cAliasPROC)->RD_VALOR
								EndIf
							Else
								If PosSrv((cAliasPROC)->RD_PD,(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S"
									nSalMes -= (cAliasPROC)->RD_VALOR
								EndIf
							EndIf
						Endif
					Else
						If !lTemCab .And. (cAliasPROC)->RC_PD == aCodFol[318,1]
							nSalMes += (cAliasPROC)->RC_VALOR
						ElseIf (cAliasPROC)->RC_PD == aCodFol[671,1]
							nAdtServ += (cAliasPROC)->RC_VALOR
						ElseIf (cAliasPROC)->RC_PD == aCodFol[672,1]
							nInsal += (cAliasPROC)->RC_VALOR
						ElseIf (cAliasPROC)->RC_PD == aCodFol[673,1]
							nPeric += (cAliasPROC)->RC_VALOR
						ElseIf (cAliasPROC)->RC_PD $ cPdAdic
							If (cAliasPROC)->RC_PD $ cPdsAts
								If PosSrv(aCodFol[001,1],(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S" .OR.;
									PosSrv(aCodFol[002,1],(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S" .OR.;
									PosSrv(aCodFol[003,1],(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S" .OR.;
									PosSrv(aCodFol[004,1],(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S" .OR.;
									PosSrv(aCodFol[005,1],(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S"
										nSalMes -= (cAliasPROC)->RC_VALOR
								EndIf
							ElseIf (cAliasPROC)->RC_PD $ cPdsPer
								If PosSrv(cFolPer,(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S"
										nSalMes -= (cAliasPROC)->RC_VALOR
								EndIf
							ElseIf (cAliasPROC)->RC_PD $ cPdsIns
								If PosSrv(aCodFol[37,1],(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S" .OR.;
									PosSrv(aCodFol[38,1],(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S" .OR.;
									PosSrv(aCodFol[39,1],(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S"
										nSalMes -= (cAliasPROC)->RC_VALOR
								EndIf
							ElseIf (cAliasPROC)->RC_PD $ cPdsAdC
								If PosSrv(cFolAdC,(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S"
									nSalMes -= (cAliasPROC)->RC_VALOR
								EndIf
							ElseIf (cAliasPROC)->RC_PD $ cPdsAdT
								If PosSrv(cFolAdT,(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S"
									nSalMes -= (cAliasPROC)->RC_VALOR
								EndIf
							Else
								If PosSrv((cAliasPROC)->RC_PD,(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S"
									nSalMes -= (cAliasPROC)->RC_VALOR
								EndIf
							EndIf
						Endif
					Endif
					(cAliasPROC)->(dbskip())
				Enddo
				(cAliasPROC)->(dbCloseArea())
				If !lTemCab
					nSalario:= nSalMes
					nSalDia	:= nSalMes / 30
					nSalHora:= nSalMes / TPR->PR_HRSMES
				Endif
				//-- Carrega os codigos dos adicionais de tempo de servico que serao utilizados
				//-- nos calculos de provisoes de ferias e 13o salario.
				fCarAdics()
			Endif

			If !lTransfSai
				If lCalcSalInc .And. ( (SRA->RA_ADCINS $ "2|3|4" .And. Empty(cCodIns)) .Or. (SRA->RA_ADTPOSER # '***N**' .And. Empty(cCodAdt)) )
					fCarAdics()
				EndIf
		
				If nSalMes == 0 .Or. !lTemCab
					If (lCalcSalInc .Or. nSalMes == 0)// .And. !TPR->PR_CATFUNC $ "E|G"  
						fSalInc(@nSalario,@nSalMes,@nSalHora,@nSalDia,.T.,,,,dDataRef,,@nPeric,@nInsal,@nAdtServ,@nAdcConf,@nAdcTransf,@nOutros,@nSalhInc)
					Endif
					//Utiliza o Sal.Incorp. do Cadastro
					If lSalInc .And. TPR->PR_SALINCO > 0
						nSalMes  := TPR->PR_SALINCO
						nSalDia  := TPR->PR_SALINCO / 30
						nSalHora := TPR->PR_SALINCO / TPR->PR_HRSMES
					Endif
				Else
					//Chama funcao para carregar adicionais - AdtServ/Peric/Insal/Adic.Cargo Confianca/Transferencia
					If lCalcSalInc .Or. (nAdtServ+nInsal+nPeric+nAdcConf+nAdcTransf) == 0
						fSalInc(,,,,.T.,,,,dDataRef,,@nPeric,@nInsal,@nAdtServ,@nAdcConf,@nAdcTransf,@nOutros,@nSalhInc)
					Endif
		    	EndIf
		    	
				If lProvResc .And. lDemitido
					//VERIFICA DEMISSAO E SEXTA FEIRA OU SABADO E CONS. PARAMETRO PARA PAGAR SABADO E DOMINGO
					dDataDem1	:= TPR->PR_DEMISSA
					dDataDem1	:= fDtSabDom()
				EndIf
				
				// CARREGANDO ARQUIVO TRP COM MONTA MEDIA
				If (!lDemitido .Or. lProvResc)
					dbSelectArea("TRP")
					Zap    
					dRefMed	:= If(lProvResc .And. lDemitido,dDataDem1,dDataRef)
					GpexMed(dDtBasFer,,dRefMed,,dRefMed,nSalHora,nSalMin,aCodFol,.T.,!lFechouMes,,,,,,,nSalhInc,!lFechou13)
				Endif
				dbSelectArea("TPR")
				// CALCULA FERIAS VENCIDAS E PROPORCIONAIS
				If lFerias
					fProvFer(cCongFeR,@nTipoMovMes,@dDtBasFer)
				EndIf
				// CALCULA O 13o SALARIO
				If l13oSal .And. (!lDemitido .Or. lProvResc) .And. !TPR->PR_CATFUNC $ "E|G"
					fProv13o(cCong13,@nTipoMovMes)
				EndIf
			EndIf
		EndIf

		// 5o ELEMENTO DO ARRAY - B A I X A  T R A N S F E R E N C I A
		// 7o ELEMENTO DO ARRAY - B A I X A  R E S C I S A O
		If lTransfSai .Or. lDemitido
			If lFerias
				If lTransfSai
					fGrvArrPrv(@aFerVenc, _BxTrf, _Anter, 1, {})
					fGrvArrPrv(@aFerProp, _BxTrf, _Anter, 1, {})
				Else
					fGrvArrPrv(@aFerVenc, _BxRes, If(lProvResc,_Atual,_Anter), 1, {})
					fGrvArrPrv(@aFerProp, _BxRes, If(lProvResc,_Atual,_Anter), 1, {})
				EndIf
			EndIf
			If l13oSal  .And. !TPR->PR_CATFUNC $ "E|G"
				If lTransfSai
					fGrvArrPrv(@a13Salar, _BxTrf, _Anter, 1, {})
					fGrvArrPrv(@a14Salar, _BxTrf, _Anter, 1, {})
				Else
					fGrvArrPrv(@a13Salar, _BxRes, If(lProvResc,_Atual,_Anter), 1, {})
					fGrvArrPrv(@a14Salar, _BxRes, If(lProvResc,_Atual,_Anter), 1, {})
				EndIf
			EndIf
		EndIf

		Begin Transaction
			
			// Atualiza o arquivo de cabecalho - SRF
			If (!lDemitido .Or. lProvResc) .And. !lTransfSai
				If lFerias
					nDiasFv := aFerVenc[_Atual,_Dias] + nV_DFalFer
					nDiasFv += If(aFerVenc[_Atual,_Dias] > 0, nDFerAnt, 0)
					nDiasFv -= SRF->RF_DVENPEN
					If nDiasFv = 0 .And. nDFerAnt > 0
						nDiasFv := nDFerAnt * (-1)
					Endif
					aFerVenc[_Atual,_Dias] := If (nDiasFv < 0 .Or. lTrfSld, 0 , nDiasFv) + SRF->RF_DVENPEN
				EndIf
		    EndIf
	
			// PARA O NOVO CALCULO DE RATEIO, CHAMA NOVAMENTE A FUNCAO QUE APURA OS VALORES MENSAIS PARA POSTERIOR GERACAO NA TAB. SRT		
		   	If lGeraPMes 
		    	fQryDetSRT(aVerba,aTransf,dDataRef,lTrataTrf,.T.,lFerias,l13oSal,.T.) 
		    Endif 
			// Grava as ferias vencidas e proporcinais no arquivo detalhe.  |
			If lFerias
				If TPR->PR_CATFUNC $ "E|G"
					fGeraSRT(@aFerVenc,aRecVenc,aVerba,dDataRef,nTipoMovMes,nSalMes,dDtBasFer,nDFerAnt,_RecVenc)
					fGeraSRT(@aFerProp,aRecProp,aVerba,dDataRef,nTipoMovMes,nSalMes,dDtBasFer,nDFerAnt,_RecProp)
				Else
					fGeraSRT(@aFerVenc,aRecVenc,aVerba,dDataRef,nTipoMovMes,nSalMes,dDtBasFer,nDFerAnt,_FerVenc)
					fGeraSRT(@aFerProp,aRecProp,aVerba,dDataRef,nTipoMovMes,nSalMes,dDtBasFer,nDFerAnt,_FerProp)
				Endif
			EndIf
			// GRAVA O 13o E 14o SALARIO NO ARQUIVO DETALHE
			If l13oSal  .And. !TPR->PR_CATFUNC $ "E|G"
				fGeraSRT(@a13Salar,aRec13Sl,aVerba,dDataRef,nTipoMovMes,nSalMes,dDtBasFer,nDFerAnt,_13Salar)
				fGeraSRT(@a14Salar,aRec14Sl,aVerba,dDataRef,nTipoMovMes,nSalMes,dDtBasFer,nDFerAnt,_14Salar)
			EndIf
			
		   	If lGeraPMes 
				fGeraMes()
			Endif  
			// SE FOR RECALCULO, DELETAR O REGISTRO DE "OK", INDICANDO QUE DEVERA SER GERADO UM NOVO SRZ NO MOMENTO DA CONTABILIZACAO
			fDelRegSRZ(3,"1",dDataRef)
			
		End Transaction

		dbSelectArea("TPR")
		// Finaliza a gravacao dos lancamentos do SIGAPCO
		PcoFinLan("000091")
	EndIf
	
	dbSkip()
Enddo

If (lGp070Fim)
	EXECBLOCK("GP070FIM",.F.,.F.)
Endif         

If lGrid
	// EXECUCAO DO CALCULO DA PROVISAO ATRAVES DO GRID
	CalcGrid(aCall,aPar,{cTPRDbf,cTPRNtx},aAreaSRA)
Else
	TRP->(dbCloseArea())
	fErase(cArqNtx + OrdBagExt())
	fErase(cArqDbf + ".DBF")

	TPR->(dbCloseArea())
	fErase(cTPRNtx + OrdBagExt())
	fErase(cTPRDbf + ".DBF")

	nTimeFim	:= Seconds()	// TEMPO DE CALCULO PARA O GRID	

	// APRESENTA TELA COM LOG DE ERROS
	aAdd(aLogFile, { "" })
	aLogFile[Len(aLogFile),1] += STR0057 + " " + SecsToTime(nTimeIni) + " " + STR0046 + " " + dDtCalIni 			+ CRLF // "Inicio do Processo de Calculo:" ## "de"
	aLogFile[Len(aLogFile),1] += STR0045 + " " + SecsToTime(nTimeFim) + " " + STR0046 + " " + Dtoc(MsDate())	+ CRLF // "Final do Processo de Calculo:" ## "de"
	aLogFile[Len(aLogFile),1] += STR0047 + " " + SecsToTime(nTimeFim - nTimeIni) + CRLF // "Duracao do Processo de Calculo:"
	fMakeLog(aLogFile, aLogTitle, "GPM070", .T.)

	// RETORNA AREA ORIGINAL DO CADASTRO DE FUNCIONARIOS
	RestArea(aAreaSRA)
EndIf
cFilAnt := _cFilAnt 

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fProvFer  ³ Autor ³ Emerson Rosa de Souza	³ Data ³ 26.06.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Calcula provisao de ferias vencidas                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fProvFer(cCongAci,cCongDoe,nTipoMovMes,dDtBasFer)          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fProvFer(cCongFeR,nTipoMovMes,dDtBasFer)
Local nAdtDia     := nInsDia   := nPerDia   := nNroFaltas := nPeriVenc := nAdcConDia := nAdcTrfDia := 0
Local nOutDia     := 0
Local nV_TotFer := 0
Local lCongelaFer := .F.
Local aDados      := {}         
Local dRetFimOP   := Ctod("") 
Local k			  := 0
Local nDsrHrsAtiv := 0
Local lEstagio	  := TPR->PR_CATFUNC $ "E|G"
Local nValFerAviso:= 0

//UTILIZADAS NOS PONTOS DE ENTRADA
Private nV_DSaldo := nV_Valor  := nV_Adic := nV_UmTer := nV_Inss := nV_Fgts := nV_Pis := 0
Private nP_DSaldo := nP_Valor  := nP_Adic := nP_UmTer := nP_Inss := nP_Fgts := nP_Pis := 0
Private nV_CSocial := nV_Bse := nP_CSocial := nP_Bse	:= 0
Private nV_ObraSoc := nV_Ley := nV_Jubil := nP_ObraSoc := nP_Ley  := nP_Jubil := 0
Private cProce := ""

// PONTO DE ENTRADA P/ ALTERAR DATABASE FERIAS E DIAS FERIAS ANTECIPADAS
If ExistBlock("GP070ANT")
	aDados	:=	ExecBLock('GP070ANT',.F.,.F.,{dDtBasFer,dDataRef,nDFerAnt})
	If ValType(aDados) == "A" .And. !Empty(aDados)
		If ValType(aDados[1]) == "N" .And. ValType(aDados[2]) == "D"
			nDFerAnt  := aDados[1]
			dDtBasFer := aDados[2]
		EndIf		
	EndIf
Endif   

If lHojorva
	cProce := "1" // Indicador de ferias vencidas, será usado na fMediaHora() do gpexcal3
	fSalHvar()
Endif

nDiasVenc += SRF->RF_DVENPEN // Soma os dias de ferias pendentes

// Verifica se houve afastamento e se deve congelar
If TPR->PR_SITFOLH == "A" .And. cCongFeR  = "2"  
	//congela apos primeiro mes
	If !empty(dIniAfa) .And. (dDataRef - dIniAfa ) >= 30 
		// Congela e mantem os dias de vencidas do mes anterior
		lCongelaFer := .T.
		// Congela e mantem os dias de proporcionais do mes anterior
		nDiasProp := aFerProp[_Anter,_Dias]
	EndIf
Elseif (cCongFeR  == "3" )  .and. ((nTDiasAfa > 0) .and. (Empty(dRetFimOP) .or. (dRetFimOP >=dDataRef))) //-   array
	
	//Somente congelar se houver mais de 180 dias afastado
	If nTDiasAfa >= 180
		nDiasProp	:= 0
		nTipoMovMes	:= _Cong_Fer
	EndIf
Elseif (cCongFeR  == "1" .And. TPR->PR_SITFOLH == "A" .And. TPR->PR_AFASFGT $ "O") .Or.;
	   (cCongFeR  == "1" .And. TPR->PR_SITFOLH == "A" .And. TPR->PR_AFASFGT $ "P")
	// Congela e mantem os dias de vencidas do mes anterior
	lCongelaFer := .T.
Endif

// VERIFICA TIPO DE MOVIMENTACAO DO FUNCIONARIO
nTipoMovMes := If(lCongelaFer .And. !lDemitido, _Cong_Fer, nTipoMovMes) // Alterar Movimento p/2 (congela), qdo "lCongelaFer" for .T. e nao for demitido.

// VERIFICA SE DEVE PAGAR OS ADICIONAIS
nAdtDia 	:= (nAdtServ / 30)
nInsDia 	:= (nInsal / 30)
nPerDia 	:= (nPeric / 30)
nAdcConDia 	:= (nAdcConf / 30)
nAdcTrfDia 	:= (nAdcTransf / 30)
nOutDia 	:= (nOutros / 30)

// CALCULO DAS FERIAS VENCIDAS
If (!lDemitido .Or. lProvResc)
	If  nDiasVenc == 0
		aFerVenc[_Atual,_Dias] := 0	
	Else	
		If lHojorva
			If (!lDemitido .Or. lProvResc)// para jornada variavel a media deve ser feita neste momento 
				dbSelectArea("TRP")
				Zap                                                                   
				dRefMed	:= If(lProvResc .And. lDemitido,dDataDem1,dDataRef)
				GpexMed(dDtBasFer,,dRefMed,,dRefMed,nSalHora,nSalMin,aCodFol,.T.,(!lFechouMes),,,,,,,nSalhInc)
			Endif
		Endif
		dbSelectArea("TRP")
		// VERIFICA NUMERO DE FALTAS EM CADA PERIODO DE FERIAS VENCIDAS
		For k := 1 To Len(aPerFerias)
			nNroFaltas := aPerFerias[k,15]
			TabFaltas(@nNroFaltas)
			nV_DFalFer += nNroFaltas
			nTFaltaV   := nV_DFalFer
		Next k
		nV_DSaldo  := nDiasVenc - nDFerAnt - nV_DFalFer
		nV_Valor   := NoRound(nSalDia * nV_DSaldo)
		nV_Adic    := (nInsDia+nPerdia+nAdtDia+nAdcConDia+nAdcTrfDia+nOutDia) * nV_DSaldo		
		// CALCULA ADICIONAIS PARA CADA PERIODO DE FERIAS VENCIDAS
		nPeriVenc  := Max(Int(nDiasVenc/aTabFer[3]), 1)
		For k := 1 To 6
			cTipMed	:= Str(If(k ==1, 1,k+3), 1)
			nDsrHrsAtiv := fDsrHrsAtiv(cTipMed,aCodFol) //Calculo do DSR / Horas Atividade de professores
			If dbSeek(TPR->PR_FILIAL + TPR->PR_MAT + cTipMed + "999" + "99MD")
				If nV_DSaldo > 0
					nV_Adic += (((TRP->RP_VALATU + nDsrHrsAtiv)/(aTabFer[3]*nPeriVenc)) * nV_DSaldo)
				Endif
				//CALCULA PERIC./INSALUB VERBA DE MEDIAS QUE TEM INCIDENCIA
				nMedPer := nMedIns := 0.00
				FMedPerIns(@nMedPer,@nMedIns,cTipMed,nSalHora,nSalMin,aCodFol)
				nV_Adic += If(nV_DSaldo > 0 ,((nMedPer+nMedIns)/(aTabFer[3]*nPeriVenc))*nV_DSaldo ,0)
			Endif
		Next k
		// PONTO DE ENTRADA PARA ALTERAR VALORES DOS ADICIONAL VENCIDAS
		If ExistBlock("GP070VEN")
			EXECBLOCK("GP070VEN",.F.,.F.)
		Endif
	
		// SO IRA CALCULAR 1/3 DE FERIAS SE NÃO FOR ESTAGIARIO
		If !lEstagio
			nV_UmTer   := NoRound((nV_Valor+nV_Adic) * If (nPerc1T=0.00 .Or. nPerc1T=100.00, 1/3 ,nPerc1T / 100))
		Endif
		
		// PONTO DE ENTRADA PARA ALTERAR VALORES DE 1/3 FER.VENCIDAS
		If ExistBlock("GP070V13")
			EXECBLOCK("GP070V13",.F.,.F.)
		Endif
		nV_TotFer  := nV_Valor + nV_Adic + nV_UmTer
		
		// SO IRA CALCULAR OS ADICIONAIS SE NÃO FOR ESTAGIARIO
		If !lEstagio
			nV_Inss    := NoRound(nV_TotFer * (nPercEmp+nPercTer+nPercAcTrab))
			nV_Fgts    := NoRound(nV_TotFer * nPercFgts)
			nV_Pis	   := NoRound(nV_TotFer * nPercPis)
		Endif
		
		// PONTO DE ENTRADA PARA ALTERAR VALORES DE INSS E FGTS VENCIDAS
		If ExistBlock("GP070VIF")
			EXECBLOCK("GP070VIF",.F.,.F.)
		Endif
	
		// GRAVA OS VALORES CALCULADOS NO ARRAY DE FERIAS VENCIDAS

		aFerVenc[_Atual,_Dias] := nV_DSaldo
		aFerVenc[_Atual,_Prov] := nV_Valor
		aFerVenc[_Atual,_Adic] := nV_Adic
		aFerVenc[_Atual,_1Ter] := nV_UmTer
		aFerVenc[_Atual,_INSS] := nV_Inss
		aFerVenc[_Atual,_FGTS] := nV_Fgts
		aFerVenc[_Atual,_PIS]  := nV_Pis
	EndIf
EndIf

// CALCULO DE FERIAS PROPORCIONAIS
If (!lDemitido .Or. lProvResc) 
	If lDemitido
		DbSelectArea("SRR")
		DbSetOrder(1)
		If DbSeek(TPR->PR_FILIAL + TPR->PR_MAT + 'R' + Dtos(SRG->RG_DATADEM) + aCodFol[230,1])
			nValFerAviso := SRR->RR_VALOR
		EndIf
	EndIf
	If lHojorva
		cProce := "2"	// Indicador de ferias proporcionais, será usado na fMediaHora() do gpexcal3
		fSalHvar()
	Endif		
	For k := 1 To Len(aPerFerias)
		nNroFaltas := aPerFerias[k,16] 
		TabFaltas(@nNroFaltas)
		nP_DFalFer += nNroFaltas
	Next k	
	nP_DSaldo  := Max(nDiasProp - nP_DFalFer - If (nDiasVenc > 0 , 0,nDFerAnt),0)
	nP_Valor   := Round(nSalDia * nP_DSaldo,2) + nValFerAviso
	nP_Adic    := Round((nInsDia+nPerdia+nAdtDia+nAdcConDia+nAdcTrfDia+nOutDia) * nP_DSaldo,2)
	If lHojorva
		If (!lDemitido .Or. lProvResc)// para jornada variavel a media deve ser feita neste momento 
			dbSelectArea("TRP")
			Zap                                                                   
			dRefMed	:= If(lProvResc .And. lDemitido,dDataDem1,dDataRef)
			GpexMed(dDtBasFer,,dRefMed,,dRefMed,nSalHora,nSalMin,aCodFol,.T.,(!lFechouMes),,,,,,,nSalhInc)
		Endif
	Endif
	dbSelectArea("TRP")
	If dbSeek(TPR->PR_FILIAL + TPR->PR_MAT + "2" + "999" + "99MD")
		If nDiasProp > 0
			nP_Adic += Round((TRP->RP_VALATU/nDiasProp) * nP_DSaldo,2)
		Endif

		nDsrHrsAtiv := fDsrHrsAtiv("2",aCodFol) //Calculo do DSR / Horas Atividade de professores
		
		// CALCULA PERIC./INSALUB VERBA DE MEDIAS QUE TEM INCIDENCIA
		nMedPer := nMedIns := 0.00
		FMedPerIns(@nMedPer,@nMedIns,'2',nSalHora,nSalMin,aCodFol)
		If nDiasProp > 0
			nP_Adic += (nMedPer+nMedIns+nDsrHrsAtiv) / nDiasProp * nP_DSaldo
		EndIf
	Endif

	// PONTO DE ENTRADA PARA ALTERAR VALORES DOS ADICIONAIS PROPORCIONAIS
	If ExistBlock("GP070PRO")
		EXECBLOCK("GP070PRO",.F.,.F.)
	Endif
	
	// SO IRA CALCULAR 1/3 DE FERIAS SE NÃO FOR ESTAGIARIO
	If !lEstagio
		nP_UmTer   := Round((nP_Valor+nP_Adic) * If (nPerc1T=0.00 .Or. nPerc1T=100.00, 1/3 ,nPerc1T / 100),2)
	Endif
	
	// PONTO DE ENTRADA PARA ALTERAR VALORES DE 1/3 PROPORCIONAIS
	If ExistBlock("GP070P13")
		EXECBLOCK("GP070P13",.F.,.F.)
	Endif

	nP_TotFer  := nP_Valor + nP_Adic + nP_UmTer
	
	// SO IRA CALCULAR 1/3 DE FERIAS SE NÃO FOR ESTAGIARIO
	If !lEstagio
		nP_Inss    := NoRound(nP_TotFer * (nPercEmp+nPercTer+nPercAcTrab))
		nP_Fgts    := NoRound(nP_TotFer * nPercFgts)
		nP_Pis	   := NoRound(nP_TotFer * nPercPis)
	Endif

	// PONTO DE ENTRADA PARA ALTERAR VALORES DE INSS E FGTS PROPORCIONAIS
	If ExistBlock("GP070PIF")
		EXECBLOCK("GP070PIF",.F.,.F.)
	Endif

	If nDiasVenc == 0 .And. !lTemVenc .And. aFerVenc[_BxFer, _Prov] > 0
		aFerProp[_BxFer] := aClone( aFerVenc[_BxFer] )
		aFill( aFerVenc[_BxFer], 0 )
	EndIf
	
	// GRAVA OS VALORES CALCULADOS NO ARRAY DE FERIAS PROPORCIONAIS
	aFerProp[_Atual,_Dias] := nP_DSaldo
	aFerProp[_Atual,_Prov] := nP_Valor
	aFerProp[_Atual,_Adic] := nP_Adic
	aFerProp[_Atual,_1Ter] := nP_UmTer
	aFerProp[_Atual,_INSS] := nP_Inss
	aFerProp[_Atual,_FGTS] := nP_Fgts
	aFerProp[_Atual,_PIS]  := nP_Pis


Endif

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fBxaFerPro³ Autor ³ Emerson Rosa de Souza	³ Data ³ 26.06.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Busca a Baixa de Ferias                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fBxaFerProv(dDtBasFer,nDFerAnt,cArqBxaPr,cFilBxaPr,...)    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fBxaFerProv(dDtBasFer,nDFerAnt,cArqBxaPr,cFilBxaPr,cMatBxaPr,cDatBxaPr,cPdBxaPr,cSeqBxaPr,cValBxaPr,cHrsBxaPr,lTemCab)
Local nValABoInss := nValABoFGTS := nValAboPis := 0
Local ddTpg0      := CTOD("")
Local lAchou		:= .F.
Local lFerMes		:= .F.
Local lPrimeira	:= .T.
Local lTrocouPer	:= .F.
Local cDataBusca	:= MesAno(dDataRef)
Local nCnt1
// VARIAVEIS UTILIZADAS NOS PONTOS DE ENTRADA
Private nBx_Valor  := nBx_Adic := nBx_UmTer := nBx_Inss := nBx_Fgts := nBx_Pis := 0
Private nBx_SalVa := 0
Private __nDiasFer := 0 // vaiaval com o Total de Dias de Ferias do Mes para ser utilizada no Ponto de Entrada GP070BIF

// VARIAVEIS PARA PROVISIONAR FERIAS QUEBRADAS(MES/MES SEGUINTE)
Private nBx_ValMs := nBx_AdiMs := nBx_UmTMs := nBx_InsMs := nBx_InsAbMs := nBx_FgtMs := nBx_FgtAbMs := nBx_PisMs := nBx_PisAbMs := 0
Private nBx_SalVMs := 0

// VERIFICA SE ESTA DE FERIAS NO MES DA PROVISAO
dbSelectArea("SRH")
If dbSeek(TPR->PR_FILIAL + TPR->PR_MAT)
	While ! Eof() .And. TPR->PR_FILIAL+TPR->PR_MAT == SRH->RH_FILIAL+SRH->RH_MAT
		If MesAno(SRH->RH_DATAINI) == cDataBusca
			If !lTrocouPer
				If SRH->RH_DFERIAS + SRH->RH_DABONPE + SRH->RH_DFALTAS + nDFerAnt >= aTabFer[3] 
					dDtBasFer := SRH->RH_DBASEAT + 1
					If cTpBxFer <> "2"
						If nDiasVenc > 0 .and. !lFechouMes
							nDiasVenc := Max(nDiasVenc - (SRH->RH_DFERIAS + SRH->RH_DABONPE),0)
						Else
							nDFerAnt  := 0
						EndIf
						lTemVenc  := .T.
					Else
						nDFerAnt  := SRH->RH_DFERIAS + SRH->RH_DABONPE + nDFerAnt
					EndIf
				Else
					nDFerAnt  += SRH->RH_DABONPE + SRH->RH_DFERIAS
				EndIf
			EndIf
			If lPrimeira
				lAchou := .T.
				lPrimeira := .F.
				lTrocouPer:= SRH->RH_DATABAS <> dDtBasFer
				If lTrocouPer
					If cTpBxFer <> "2" .OR. !(MesAno(SRH->RH_DATAINI) <> MesAno(SRH->RH_DATAFIM)) 
						nDFerAnt  := 0
						lTemVenc  := .T.
					Else
						nDFerAnt  -= (f_Ultdia(SRH->RH_DATAINI) - Day(SRH->RH_DATAINI)) + 1
					EndIf
				EndIf
			Endif
			__nDiasFer += SRH->RH_DABONPE + SRH->RH_DFERIAS
			lFerMes := (If(!lFerMes, Month(SRH->RH_DATAINI) == Month(dDataRef), .T.))
			// SE TIPO DE BAIXA MES E MES SEGUINTE, ATUALIZA VARIAVEL LACHOU PARA FORCAR A BUSCA DAS FERIAS NO SEGUNDO MES
		ElseIf (cTpBxFer == "2" .And. (MesAno(SRH->RH_DATAFIM) == cDataBusca .Or. MesAno(SRH->RH_DATAFIM+SRH->RH_DABONPE) == cDataBusca))
			If lPrimeira
				lAchou := .T.
				lPrimeira := .F.
			Endif
			lFerMes := (If(!lFerMes, Month(SRH->RH_DATAINI) == Month(dDataRef), .T.))
		Elseif MesAno(SRH->RH_DATAFIM) == cDataBusca
			dDtPg0 := SRH->RH_DTRECIB
		Endif
		dbSkip()
	Enddo
Endif

If lAchou .Or. TPR->PR_CATFUNC $ "E|G"
	// RETORNA AS VERBAS DO FUNCIONARIO A PARTIR DO SRC E SRD
	aVerbasFunc	:= RetornaVerbasFunc(	TPR->PR_FILIAL					,; // Filial do funcionario corrente
										TPR->PR_MAT	  					,; // Matricula do funcionario corrente
										NIL								,; // 
										fGetCalcRot('1')				,; // Roteiro para busca das verbas
										NIL			  					,; // Array com as verbas que deverão ser listadas. Se NIL retorna todas as verbas.
										aPerAberto	  					,; // Array com os Periodos e Numero de pagamento abertos
										aPerFechado	 	 				) // Array com os Periodos e Numero de pagamento fechados

	// BAIXA PELO VALOR PAGO NO MES ATUAL
	For nCnt1 := 1 To Len(aVerbasFunc)
		If Empty(dDtPg0) .Or. (!Empty(dDtPg0) .And. !Empty(aVerbasFunc[nCnt1,nPosSeq]))
		    If cTpBxFer == "2" .And. (aVerbasFunc[nCnt1,nPosPd] == aCodFol[073,1] .Or. aVerbasFunc[nCnt1,nPosPd] == aCodFol[205,1])
				nDFerAnt -= aVerbasFunc[nCnt1,nPosHoras]
		    EndIf
			If aVerbasFunc[nCnt1,nPosPd] $ cCodFer .Or. aVerbasFunc[nCnt1,nPosPd] $ cCodFerMs
				nBx_Valor += aVerbasFunc[nCnt1,nPosValor]     //-- Baixa de Valor Ferias
				If aVerbasFunc[nCnt1,nPosPd] $ cCodFerMs
					nBx_ValMs += aVerbasFunc[nCnt1,nPosValor] //-- Ferias do Proximo Mes
				EndIf
			Endif
			If aVerbasFunc[nCnt1,nPosPd] $ cCodAdFer .Or. aVerbasFunc[nCnt1,nPosPd] $ cCodAdicMs
				nBx_Adic += aVerbasFunc[nCnt1,nPosValor]       //-- Baixa de Adicionais
				If aVerbasFunc[nCnt1,nPosPd] $ cCodAdicMs
					nBx_AdiMs += aVerbasFunc[nCnt1,nPosValor]  //-- Adicionais do Proximo Mes
				EndIf
			Endif
			If aVerbasFunc[nCnt1,nPosPd] $ cCodUmTer .Or. aVerbasFunc[nCnt1,nPosPd] $ cCodUmTerMs
				nBx_UmTer += aVerbasFunc[nCnt1,nPosValor]       //-- Baixa de Um Terco (1/3)
				If aVerbasFunc[nCnt1,nPosPd] $ cCodUmTerMs
					nBx_UmTMs += aVerbasFunc[nCnt1,nPosValor]   //-- Um Terco do Proximo Mes
				EndIf
			Endif
			If (aVerbasFunc[nCnt1,nPosPd] $ cCodFer .Or. aVerbasFunc[nCnt1,nPosPd] $ cCodAdFer .Or. If(cPaisLoc <> "URU",aVerbasFunc[nCnt1,nPosPd] $ cCodUmTer,.F.) .Or.;
   	           aVerbasFunc[nCnt1,nPosPd] $ cCodFerMs .Or. aVerbasFunc[nCnt1,nPosPd] $ cCodAdicMs .Or. aVerbasFunc[nCnt1,nPosPd] $ cCodUmTerMs) .And. !TPR->PR_CATFUNC $ "E|G"
				If aVerbasFunc[nCnt1,nPosPd] $ cCodAbono .OR. PosSrv(aVerbasFunc[nCnt1,nPosPd],TPR->PR_FILIAL,"RV_INSS") == "S"
					nBx_Inss += aVerbasFunc[nCnt1,nPosValor]      // I.N.S.S.
				EndIf
				If aVerbasFunc[nCnt1,nPosPd] $ cCodAbono .OR. PosSrv(aVerbasFunc[nCnt1,nPosPd],TPR->PR_FILIAL,"RV_FGTS") == "S"
					nBx_Fgts += aVerbasFunc[nCnt1,nPosValor]      // F.G.T.S.
				EndIf
				If aVerbasFunc[nCnt1,nPosPd] $ cCodAbono .OR. PosSrv(aVerbasFunc[nCnt1,nPosPd],TPR->PR_FILIAL,"RV_PIS") == "S"
					nBx_Pis  += aVerbasFunc[nCnt1,nPosValor]      // P.I.S.
				EndIf
				If aVerbasFunc[nCnt1,nPosPd] $ cCodFerMs .Or. aVerbasFunc[nCnt1,nPosPd] $ cCodAdicMs .Or. aVerbasFunc[nCnt1,nPosPd] $ cCodUmTerMs
					If aVerbasFunc[nCnt1,nPosPd] $ cCodAboMs .OR. PosSrv(aVerbasFunc[nCnt1,nPosPd],TPR->PR_FILIAL,"RV_INSS") == "S"
						nBx_InsMs += aVerbasFunc[nCnt1,nPosValor]      // I.N.S.S. do Proximo Mes
					EndIf
					If aVerbasFunc[nCnt1,nPosPd] $ cCodAboMs .OR. PosSrv(aVerbasFunc[nCnt1,nPosPd],TPR->PR_FILIAL,"RV_FGTS") == "S"
						nBx_FgtMs += aVerbasFunc[nCnt1,nPosValor]      // F.G.T.S. do Proximo Mes
					EndIf
					If aVerbasFunc[nCnt1,nPosPd] $ cCodAboMs .OR. PosSrv(aVerbasFunc[nCnt1,nPosPd],TPR->PR_FILIAL,"RV_PIS") == "S"
						nBx_PisMs  += aVerbasFunc[nCnt1,nPosValor]      // P.I.S.   do Proximo Mes
					EndIf
				EndIf
			Endif
			If aVerbasFunc[nCnt1,nPosPd] $ cCodSalV .Or. aVerbasFunc[nCnt1,nPosPd] $ cCodSalVMs
				nBx_SalVa += aVerbasFunc[nCnt1,nPosValor]       //-- Baixa Media Salario Vacacional
				If aVerbasFunc[nCnt1,nPosPd] $ cCodSalVMs
					nBx_SalVMs += aVerbasFunc[nCnt1,nPosValor]   //-- Media Sal. Vacac.Prox.Mes
				EndIf
			Endif
			// ABONO PECUNIARIO
			If aVerbasFunc[nCnt1,nPosPd] $ cCodAbono  .Or. aVerbasFunc[nCnt1,nPosPd] $ cCodAboMs
				If PosSrv(aVerbasFunc[nCnt1,nPosPd],TPR->PR_FILIAL,"RV_INSS") == "N"
					If aVerbasFunc[nCnt1,nPosPd] $ cCodAboMs
					    If cTpBxFer != "2"
							nBx_InsMs -= aVerbasFunc[nCnt1,nPosValor] // I.N.S.S. do Proximo Mes
						Else
							nValAboInss += aVerbasFunc[nCnt1,nPosValor]
							nBx_InsAbMs += aVerbasFunc[nCnt1,nPosValor] // I.N.S.S. de Abono do Proximo Mes
						EndIf
					Else
						nValAboInss += aVerbasFunc[nCnt1,nPosValor]
					EndIf
				Endif
				If PosSrv(aVerbasFunc[nCnt1,nPosPd],TPR->PR_FILIAL,"RV_FGTS") == "N"
					If aVerbasFunc[nCnt1,nPosPd] $ cCodAboMs
					    If cTpBxFer != "2"
							nBx_FgtMs -= aVerbasFunc[nCnt1,nPosValor] // F.G.T.S. do Proximo Mes
						Else
							nValAboFgts += aVerbasFunc[nCnt1,nPosValor]
							nBx_FgtAbMs += aVerbasFunc[nCnt1,nPosValor] // F.G.T.S. de Abono do Proximo Mes
						Endif
					Else
						nValAboFgts += aVerbasFunc[nCnt1,nPosValor]
					EndIf
				Endif
				If PosSrv(aVerbasFunc[nCnt1,nPosPd],TPR->PR_FILIAL,"RV_PIS") == "N"
					If aVerbasFunc[nCnt1,nPosPd] $ cCodAboMs
					    If cTpBxFer != "2"
							nBx_PisMs -= aVerbasFunc[nCnt1,nPosValor] // P.I.S. do Proximo Mes
						Else
							nValAboPis += aVerbasFunc[nCnt1,nPosValor]
							nBx_PisAbMs += aVerbasFunc[nCnt1,nPosValor] // P.I.S. de Abono do Proximo Mes
						Endif
					Else
						nValAboPis += aVerbasFunc[nCnt1,nPosValor]
					EndIf
				Endif
			Endif
			//Outras verbas que incorporam
			If aVerbasFunc[nCnt1,nPosTpo2] == 'K' .AND. ( !(aVerbasFunc[nCnt1,nPosPd] $ cCodFer .Or. aVerbasFunc[nCnt1,nPosPd] $ cCodFerMs) .And.;
				!(aVerbasFunc[nCnt1,nPosPd] $ cCodAdFer .Or. aVerbasFunc[nCnt1,nPosPd] $ cCodAdicMs) .And.;
				!(aVerbasFunc[nCnt1,nPosPd] $ cCodUmTer .Or. aVerbasFunc[nCnt1,nPosPd] $ cCodUmTerMs) .And.;
				!(aVerbasFunc[nCnt1,nPosPd] $ cCodAbono  .Or. aVerbasFunc[nCnt1,nPosPd] $ cCodAboMs) .And.;
				!(aVerbasFunc[nCnt1,nPosPd] $ cCodSalV .Or. aVerbasFunc[nCnt1,nPosPd] $ cCodSalVMs) .And.;
				PosSrv(aVerbasFunc[nCnt1,nPosPd],TPR->PR_FILIAL,"RV_INCORP") == "S" )
				nBx_Adic += aVerbasFunc[nCnt1,nPosValor]       //-- Baixa de Adicionais
				If PosSrv(aVerbasFunc[nCnt1,nPosPd],TPR->PR_FILIAL,"RV_INSS") == "S"
					nBx_Inss += aVerbasFunc[nCnt1,nPosValor]      // I.N.S.S.
				EndIf
				If PosSrv(aVerbasFunc[nCnt1,nPosPd],TPR->PR_FILIAL,"RV_FGTS") == "S"
					nBx_Fgts += aVerbasFunc[nCnt1,nPosValor]      // F.G.T.S.
				EndIf
				If PosSrv(aVerbasFunc[nCnt1,nPosPd],TPR->PR_FILIAL,"RV_PIS") == "S"
					nBx_Pis  += aVerbasFunc[nCnt1,nPosValor]      // P.I.S.
				EndIf
			EndIf
		Endif
	Next nCnt1
	nBx_Inss := NoRound((nBx_Inss - nValAboInss) * (nPercEmp+nPercTer+nPercAcTrab))
	nBx_Fgts := NoRound((nBx_Fgts - nValAboFgts) * nPercFgts)
	nBx_Pis  := NoRound((nBx_Pis  - nValAboPis)  * nPercPis)
	
	// VERIFICA SE DEVE BAIXAR PELAS FERIAS DO MES E MES SEGUINTE
	If cTpBxFer == "2"
		// CALCULO DO INSS E FGTS DO PROXIMO MES
		nBx_InsMs := NoRound(nBx_InsMs * (nPercEmp+nPercTer+nPercAcTrab))
		nBx_FgtMs := NoRound(nBx_FgtMs * nPercFgts)
		nBx_PisMs := NoRound(nBx_PisMs * nPercPis)
		nBx_InsAbMs:= NoRound(nBx_InsAbMs * (nPercEmp+nPercTer+nPercAcTrab))
		nBx_FgtAbMs:= NoRound(nBx_FgtAbMs * nPercFgts)
		nBx_PisAbMs:= NoRound(nBx_PisAbMs * nPercPis)
		// SUBTRAI OS VALORES DO PROXIMO MES DO TOTAL DO MES
		If lFerMes
			nBx_Valor -= nBx_ValMs
			nBx_Adic  -= nBx_AdiMs
			nBx_UmTer -= nBx_UmTMs
			nBx_Inss  -= nBx_InsMs
			nBx_Inss  += nBx_InsAbMs
			nBx_Fgts  -= nBx_FgtMs
			nBx_Fgts  += nBx_FgtAbMs
			nBx_Pis   -= nBx_PisMs
			nBx_Pis   += nBx_PisAbMs
			nBx_SalVa -= nBx_SalVMs
			// GRAVA A DIFERENCA QUE SERA BAIXADA NO PROXIMO MES
			aFerVenc[_Atual,_Prov] := nBx_ValMs
			aFerVenc[_Atual,_Adic] := nBx_AdiMs
			aFerVenc[_Atual,_1Ter] := nBx_UmTMs
			aFerVenc[_Atual,_INSS] := nBx_InsMs
			aFerVenc[_Atual,_FGTS] := nBx_FgtMs
			aFerVenc[_Atual,_PIS]  := nBx_PisMs
		EndIf
	EndIf
	
	// PONTO DE ENTRADA PARA ALTERAR VALORES DE INSS E FGTS BAIXAS
	If ExistBlock("GP070BIF")
		EXECBLOCK("GP070BIF",.F.,.F.)
	Endif

	// GRAVA NO ARRAY OS VALORES DE BAIXA
	aFerVenc[_BxFer,_Prov] := nBx_Valor
	aFerVenc[_BxFer,_Adic] := nBx_Adic
	aFerVenc[_BxFer,_1Ter] := nBx_UmTer
	aFerVenc[_BxFer,_INSS] := nBx_Inss
	aFerVenc[_BxFer,_FGTS] := nBx_Fgts
	aFerVenc[_BxFer,_PIS]  := nBx_Pis
Endif

Return (nBx_Valor+nBx_Adic+nBx_UmTer > 0)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao   ³fProv13o  ³ Autor ³ Emerson Rosa de Souza	³ Data ³ 26.06.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Calcula provisao de 13 Salario                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe  ³ fProv13o(cAbatAci,cAbatDoe,nTipoMovMes,cAbatAdo)            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso      ³ Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fProv13o(cCong13,nTipoMovMes)
Local lCongela13 := .F.
Local nAvosDed    := nAvosAf := nV_DFal13o := 0
Local dDtI13	  := CTOD("")
Local cTiposAfa   := ""        
Local cTipoPesq   := "PR13"
Local nB_FGTS	  := 0
Local nB_PIS 	  := 0
Local aAfast      := {}
Local nX		  := 0
Local nAvos_OQ
Local nAvos_B
Local nFaltas13	  := 0
Local nVal13Aviso := 0
// VARIAVEIS UTILIZADAS NOS PONTOS DE ENTRADA
Private nV_Valor := nV_Adic := nV_1Par13 := nV_Inss := nV_Fgts := nV_Pis := nTotMes := 0
Private nV_CSocial := nV_Bse := 0
Private nV_ObraSoc := nV_Ley := nV_Jubil := nP_ObraSoc := nP_Ley  := nP_Jubil := 0
Private cProce := ""

// VERIFICA SE HOUVE AFASTAMENTO E SE DEVE CONGELAR
If (TPR->PR_SITFOLH == "A" .And. cCong13  == "1")
	lCongela13 := .T.
EndIf
   // Indicador de 13º Salário, será usado na fMediaHora() do gpexcal3
	If lHojorva
		cProce := "3"
		fSalHvar ()
	Endif

	cTiposAfa := ""
	ndiasaf := 0
	nAvosAf := 0
	dDtI13  := CTOD("01/01/"+Str(Year(dDataRef),4), "DDMMYY")
	dDtDem	:= TPR->PR_DEMISSA
	If lHojorva
		If (!lDemitido .Or. lProvResc)// para jornada variavel a media deve ser feita neste momento 
			dbSelectArea("TRP")
			Zap                                                                   
			dRefMed	:= If(lProvResc .And. lDemitido,dDataDem1,dDataRef)
			GpexMed(dDtBasFer,,dRefMed,,dRefMed,nSalHora,nSalMin,aCodFol,.T.,(!lFechouMes),,,,,,,nSalhInc)
		Endif
	Endif
	dbSelectArea("TRP")
	TRP->(dbGoTop())
	If !Empty(dDtDem) .And. dbSeek(TPR->PR_FILIAL + TPR->PR_MAT + "3" + "998" + Alltrim(Str(Year(dDtDem), 4)) + Alltrim(Strzero(Month(dDtDem), 2)))
		nFaltas13 := TRP->RP_HORAS
	EndIf
	fRetAfas(dDtI13,  If(lDemitido .And. lProvResc,dDtDem,dDataRef),cTipoPesq,@nAvosAf,@nDiasaf,,@aAfast,,,,,lDemitido .And. lProvResc,nFaltas13)
	nAvosDed += nAvosAf
	For nX := 1 to Len(aAfast)
		If aAfast[nX,5] == "F" //Ferias
			nAvosDed -= aAfast[nX,14] //Avos de férais não devem ser descontados.
		EndIf
	Next nX
//EndIf

// VERIFICA TIPO DE MOVIMENTACAO DO FUNCIONARIO
nTipoMovMes := If(lCongela13, If(nTipoMovMes==_Cong_Fer,_Cong_F13,_Cong_13s), nTipoMovMes)

If ( !lDemitido .Or. lProvResc ) 
	dbSelectArea("TRP")
	// BUSCA O ADICIONAL MEDIAS
	If dbSeek(TPR->PR_FILIAL + TPR->PR_MAT + "3" + "999" + "99MD")
		nV_Adic := TRP->RP_VALATU + fDsrHrsAtiv("3",aCodFol)
		// CALCULA PERIC./INSAL VERBA DE MEDIAS QUE TEM INCIDENCIA
		nMedPer := nMedIns := 0.00
		FMedPerIns(@nMedPer,@nMedIns,'3',nSalHora,nSalMin,aCodFol)
		nV_Adic += (nMedPer+nMedIns)
	Endif
	
	// BUSCA A 1o PARCELA ANTECIPADA
	If lDesc1Parc
		// PESQUISA ADIANTAMENTO DA 1¦ PARCELA 13o SALARIO NO ACUMULADO
		If dbSeek(TPR->PR_FILIAL + TPR->PR_MAT + "3" + "997" + "9598")
			nV_1Par13 := TRP->RP_VALATU
		Endif
		// PESQUISA ADIANTAMENTO DA 1¦ PARCELA 13o SALARIO NO MOVIMENTO
		If !lFechou13
			dbSelectArea("SRC")
			If dbSeek(TPR->PR_FILIAL + TPR->PR_MAT + aCodFol[022,1])
				nV_1Par13 += SRC->RC_VALOR
			EndIf
			// PESQUISA A DIFERENCA DO ADIANTAMENTO DA 1¦ PARCELA 13o SALARIO
			If dbSeek(TPR->PR_FILIAL + TPR->PR_MAT + aCodFol[163,1])
				nV_1Par13 += SRC->RC_VALOR
			EndIf
			dbSelectArea("TRP")
		Endif
	Endif
	// BUSCA AS FALTAS EM AVOS
	If dbSeek(TPR->PR_FILIAL + TPR->PR_MAT + "3" + "998" + "9998")
		nV_DFal13o := TRP->RP_HORAS
	Endif
	// CALCULA OS AVOS DO FUNCIONARIO
	fAvos13(@nTotMes,If(lProvResc .And. lDemitido,dDataDem1,dDataRef),nV_DFal13o,nFaltas13,,dDataRef)
	
	If lDemitido .and. PosSrv(aCodFol[115,1],TPR->PR_FILIAL,"RV_REF13") == "S"
		DbSelectArea("SRR")
		DbSetOrder(1)
		If DbSeek(TPR->PR_FILIAL + TPR->PR_MAT + 'R' + Dtos(SRG->RG_DATADEM) + aCodFol[115,1])
			nVal13Aviso := SRR->RR_VALOR
		EndIf
	EndIf
	
	// ABATER AVOS AFASTADOS NO ANO
	nTotMes := If (nTotMes - nAvosDed > 0 , nTotMes - nAvosDed , 0)
	
	
	// CALCULO DO MES
	nV_Valor  := Round(((nSalMes * nTotMes) / 12) + nVal13Aviso , 2)
	nV_Adic   += Round(((nPeric+nAdtServ+nInsal+nAdcConf+nAdcTransf+nOutros) * nTotMes) / 12 , 2)
	
	// PONTO DE ENTRADA PARA ALTERRAR VALORES 13o. E  ADICIONAL
	If ExistBlock("GP090DEC")
		EXECBLOCK("GP090DEC",.F.,.F.)
	Endif
	
	// BASE PARA CALCULO DO FGTS E PIS
	nB_FGTS := nV_Valor + nV_Adic
	nB_PIS  := nV_Valor + nV_Adic
	
	// CALCULA FGTS COM BASE TOTAL SE AFAST. POR AUXILIO MATERNIDADE
	nAvos_OQ := 0
	nAvos_B	 := 0

    Aeval(aAfast ,{ |X| nAvos_OQ += If(X[16] $ "O*Q*B", X[1], 0) })
    Aeval(aAfast ,{ |X| nAvos_B += If(X[16] $ "B", X[1], 0) })

	If nAvos_OQ > 0
		nB_FGTS := NoRound(fRetDec(@nB_FGTS) / nTotMes * (nTotMes + nAvos_OQ), 2)
	EndIf

	nV_Tot13  := (nV_Valor + nV_Adic)
	nV_Tot13o := (nV_Valor + nV_Adic) - nV_1Par13

	If nAvos_B > 0
		//QUANDO FOR RECOLHIMENTO DA CONTRIBUICAO PATRONAL SOBRE A RECEITA BRUTA E TER QUE EFETUAR A 
		//PROPORCIONALIZACAO DOS AVOS ADQUIRIDOS ANTES E APOS O INICIO DA DESONERACAO
		If cRecFatEmp $ "S*M*C" .And. lDesAtiv
			nInssAnt  := NoRound(((nV_Tot13 / nTotMes * (nTotMes + nAvos_OQ)) / nTotMes ) * nAvosAnt)
			nInssAnt  := NoRound(nInssAnt * (nPercAnt + nPercTer + nPercAcTrab))
			nInssDes  := NoRound(((nV_Tot13 / nTotMes * (nTotMes + nAvos_OQ)) / nTotMes ) * (nTotMes - nAvosAnt))
			nInssDes  := NoRound(nInssDes * (nPerEmp13 + nPercTer + nPercAcTrab))
			nV_Inss   := NoRound(nInssAnt + nInssDes)
			//CASO CONTRARIO, APENAS APLICA O PERCENTUAL NO VALOR TOTAL DO 13o
		Else
			nV_Inss   := NoRound((nV_Tot13 / nTotMes * (nTotMes + nAvos_OQ))* (nPerEmp13 + nPercTer + nPercAcTrab))
		EndIf
	Else
		//QUANDO FOR RECOLHIMENTO DA CONTRIBUICAO PATRONAL SOBRE A RECEITA BRUTA E TER QUE EFETUAR A 
		//PROPORCIONALIZACAO DOS AVOS ADQUIRIDOS ANTES E APOS O INICIO DA DESONERACAO
		If cRecFatEmp $ "S*M*C" .And. lDesAtiv
			nInssAnt  := NoRound((nV_Tot13 / nTotMes) * nAvosAnt)
			nInssAnt  := NoRound(nInssAnt * (nPercAnt + nPercTer + nPercAcTrab))
			nInssDes  := NoRound((nV_Tot13 / nTotMes) * (nTotMes - nAvosAnt))
			nInssDes  := NoRound(nInssDes * (nPerEmp13 + nPercTer + nPercAcTrab))
			nV_Inss   := NoRound(nInssAnt + nInssDes)
			//CASO CONTRARIO, APENAS APLICA O PERCENTUAL NO VALOR TOTAL DO 13o
		Else
			nV_Inss   := NoRound(nV_Tot13 * (nPerEmp13 + nPercTer + nPercAcTrab))
		EndIf
	EndIf
		
	// VERIFICA SE O CODIGO DE 1o PARCELA ESTA COM INCIDENCIA	FGTS
	If PosSrv(aCodFol[022,1],TPR->PR_FILIAL,"RV_FGTS") == "S"
		nV_Fgts := NoRound(((nB_FGTS) - nV_1Par13) * nPercFgts)
	Else
		nV_Fgts := NoRound((nB_FGTS) * nPercFgts)
	EndIf

	// VERIFICA SE O CODIGO DE 1o PARCELA ESTA COM INCIDENCIA DE PIS
	If PosSrv(aCodFol[022,1],TPR->PR_FILIAL,"RV_PIS") == "S"
		nV_Pis := NoRound(((nB_PIS) - nV_1Par13) * nPercPis)
	Else
		nV_Pis := NoRound((nB_PIS) * nPercPis)
	EndIf
	
	// PONTO DE ENTRADA PARA ALTERAR VALORES DE INSS E FGTS DO 13o
	If ExistBlock("GP090DIF")
		EXECBLOCK("GP090DIF",.F.,.F.)
	EndIf

	// GRAVA O VALOR DA BAIXA DA 1A. PARCELA SE HOUVER BAIXA DA 2ª
	If a13Salar[_Bx13O,_Prov]+a13Salar[_Bx13O,_Adic] > 0
		a13Salar[_Bx13O,_1Par] := nV_1Par13
	EndIf
	
	// SE HOUVER IDENTIFCADORES DE BXA DE 13o, FOR MES 12 E FUNCIONARIO NAO FOR DEMITIDO, ZERAR 
	// OS VALORES "ATUAL" PARA QUE O AJUSTE "NO MES" SEJA FEITO PELA BAIXA
	If lBx13Pgt .And. Month(dDataRef) == 12 .and. !lDemitido
		If n14Salario > 0    
			a14Salar[_Atual,_Avos] := 0
			a14Salar[_Atual,_Prov] := (nV_Valor  	* n14Salario)
			a14Salar[_Atual,_Adic] := (nV_Adic   	* n14Salario)
			a14Salar[_Atual,_1Par] := 0
			a14Salar[_Atual,_INSS] := (nV_Inss  	* n14Salario)
			a14Salar[_Atual,_FGTS] := (nV_Fgts  	* n14Salario)
		EndIf
		a13Salar[_Atual,_Avos] := 0
  		a13Salar[_Atual,_Prov] := 0
  		a13Salar[_Atual,_Adic] := 0
  		a13Salar[_Atual,_1Par] := 0
  		a13Salar[_Atual,_INSS] := 0
  		a13Salar[_Atual,_FGTS] := 0
  		a13Salar[_Atual,_PIS]  := 0
	Else
		// GRAVA OS VALORES CALCULADOS NO ARRAY (SUBTRAINDO AS BAIXAS)
		a13Salar[_Atual,_Avos] := nTotMes   - a13Salar[_Bx13O,_Avos]
		a13Salar[_Atual,_Prov] := nV_Valor  - a13Salar[_Bx13O,_Prov]
		a13Salar[_Atual,_Adic] := nV_Adic   - a13Salar[_Bx13O,_Adic]
		a13Salar[_Atual,_1Par] := nV_1Par13 - a13Salar[_Bx13O,_1Par]
		a13Salar[_Atual,_INSS] := nV_Inss - a13Salar[_Bx13O,_INSS]
		a13Salar[_Atual,_FGTS] := nV_Fgts - a13Salar[_Bx13O,_FGTS]
		a13Salar[_Atual,_PIS]  := nV_Pis- a13Salar[_Bx13O,_PIS]
	EndIf
	
	// Calcula o 14o. salario a partir dos valores de 13o
	If n14Salario > 0
		If  !(lBx13Pgt .And. Month(dDataRef) == 12 .and. !lDemitido) 
			a14Salar[_Atual,_Avos] := 0
			a14Salar[_Atual,_Prov] := (a13Salar[_Atual,_Prov] * n14Salario)
			a14Salar[_Atual,_Adic] := (a13Salar[_Atual,_Adic] * n14Salario)
			a14Salar[_Atual,_1Par] := 0
			a14Salar[_Atual,_INSS] := (a13Salar[_Atual,_INSS] * n14Salario)
			a14Salar[_Atual,_FGTS] := (a13Salar[_Atual,_FGTS] * n14Salario)
			a14Salar[_Atual,_PIS]  := (a13Salar[_Atual,_PIS]  * n14Salario)
		EndIf 
	EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Congela Provisao Para Funcionario Afastado  				 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fBxa13oPro³ Autor ³ Emerson Rosa de Souza	³ Data ³ 26.06.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Busca a Baixa de 13o Salario                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fBxa13oProv(cArqBxa13,cFilBxa13,cMatBxa13cDatBxa13,...)    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fBxa13oProv(cArqBxa13,cFilBxa13,cMatBxa13,cDatBxa13,cPdBxa13,cValBxa13)
Local nDif13     := 0
Local nInssAnt   := 0
Local nInssDes   := 0
Local nTotMes    := 0
Local nV_Inss    := 0
Local nCnt1
Local cRot13Aux  := fGetCalcRot('6')
Local lSomaAdic
Local aAux       := {}
// Variaveis utilizadas nos pontos de entrada				     ³
Private nBxa13o := nBxaAdi := nBxaIns := nBxaFgt := nBxaPis := 0

// RETORNA AS VERBAS DO FUNCIONARIO A PARTIR DO SRC E SRD
aVerbasFunc	:= RetornaVerbasFunc(	TPR->PR_FILIAL					,; // Filial do funcionario corrente
									TPR->PR_MAT	  					,; // Matricula do funcionario corrente
									NIL								,; // 
									""								,; // Roteiro para busca das verbas
									NIL			  					,; // Array com as verbas que deverão ser listadas. Se NIL retorna todas as verbas.
									aPerAb13	  					,; // Array com os Periodos e Numero de pagamento abertos
									aPerFe13	 	 				) // Array com os Periodos e Numero de pagamento fechados
									
If !lFechouMes
	// Adicionar a verba de Dif. de 13º no aVerbasFunc
	aAux := RetornaVerbasFunc( TPR->PR_FILIAL, TPR->PR_MAT , Nil, "", Nil, aPerAberto, aPerFechado)
	
	For nCnt1 := 1 To Len(aAux)
		If aAux[nCnt1,nPosPd] == aCodFol[028,1]//Dif 13o. Sal
			Aadd(aVerbasFunc, aAux[nCnt1])
		EndIf
	Next nCnt1
EndIf

For nCnt1 := 1 To Len(aVerbasFunc)
	// VERIFICA SE DEVE CONSIDERAR DIFERENCA DE 13o SAL. NEGATIVA
	If aVerbasFunc[nCnt1,nPosPd] == aCodFol[028,1] .And. aVerbasFunc[nCnt1,nPosValor] < 0 .And. !lDif13Neg
		Loop
	EndIf
	If aVerbasFunc[nCnt1,nPosPd] $ cCod13o .and. aVerbasFunc[nCnt1,nPosRoteiro] == cRot13Aux
		nBxa13o += aVerbasFunc[nCnt1,nPosValor]  //-- Baixa Valor do 13o Salario
	EndIf
	If aVerbasFunc[nCnt1,nPosPd] == aCodFol[024,1] .and. aVerbasFunc[nCnt1,nPosRoteiro] == cRot13Aux//Parcela Final 13o. Sal
		nTotMes := aVerbasFunc[nCnt1,nPosHoras]
	EndIf	
	If aVerbasFunc[nCnt1,nPosPd] == aCodFol[028,1]//Dif 13o. Sal
		nDif13 += aVerbasFunc[nCnt1,nPosValor]
	EndIf	
	// SE A VERBA ATUAL NAO ESTIVER CONTIDA NOS CODIGOS DE ADICIONAIS VERIFICA SE E UM PROVENTO E SE 
	//INCIDE MEDIA P/ QUE SEJA SOMADA NA COLUNA DE ADICIONAIS E UTILIZADA PARA EFETUAR A BAIXA
	lSomaAdic := .F.
	If !(aVerbasFunc[nCnt1,nPosPd] $ cCodAd13o) .and. aVerbasFunc[nCnt1,nPosRoteiro] == cRot13Aux
	    PosSrv(aVerbasFunc[nCnt1,nPosPd], TPR->PR_FILIAL)
	    If SRV->RV_TIPOCOD == "1" .And. !Empty(SRV->RV_MED13) .And. SRV->RV_MED13 # "N "
			lSomaAdic := .T.
	    EndIf
	EndIf
	If ( aVerbasFunc[nCnt1,nPosPd] $ cCodAd13o .And. aVerbasFunc[nCnt1,nPosRoteiro] == cRot13Aux ) .Or. lSomaAdic
		nBxaAdi += aVerbasFunc[nCnt1,nPosValor]  //-- Baixa de Adicionais
	EndIf
	If ( (aVerbasFunc[nCnt1,nPosPd] $ cCod13o .Or. aVerbasFunc[nCnt1,nPosPd] $ cCodAd13o) .And. aVerbasFunc[nCnt1,nPosRoteiro] == cRot13Aux ) .Or. lSomaAdic
		If PosSrv(aVerbasFunc[nCnt1,nPosPd],TPR->PR_FILIAL,"RV_INSS") == "S"
			nBxaIns += aVerbasFunc[nCnt1,nPosValor]  //-- I.N.S.S.
		EndIf
		If PosSrv(aVerbasFunc[nCnt1,nPosPd],TPR->PR_FILIAL,"RV_FGTS") == "S"
			nBxaFgt += aVerbasFunc[nCnt1,nPosValor]  //-- F.G.T.S.
		EndIf
		If PosSrv(aVerbasFunc[nCnt1,nPosPd],TPR->PR_FILIAL,"RV_PIS") == "S"
			nBxaPis += aVerbasFunc[nCnt1,nPosValor]  //-- P.I.S.
		EndIf
	EndIf
	If aVerbasFunc[nCnt1,nPosPd] == aCodFol[183,1]
		nBxaFgt -= If(lDesc1Parc,aVerbasFunc[nCnt1,nPosValor],0.00)  //-- Subtrai 1a parcela
	EndIf
	// BUSCA A DIFERENCA DE 13o NO MOVIMENTO MENSAL - SRC
	If aVerbasFunc[nCnt1,nPosPd] == aCodFol[028,1] .Or.;
	   aVerbasFunc[nCnt1,nPosPd] == aCodFol[348,1] 
	   If aVerbasFunc[nCnt1,nPosValor] > 0 .Or. lDif13Neg
			nBxa13o += aVerbasFunc[nCnt1,nPosValor]    //-- Baixa Valor do 13o Salario
			nBxaIns += aVerbasFunc[nCnt1,nPosValor]    //-- I.N.S.S.
			nBxaFgt += aVerbasFunc[nCnt1,nPosValor]    //-- F.G.T.S.
			nBxaPis += aVerbasFunc[nCnt1,nPosValor]    //-- P.I.S.
		EndIf
	EndIf
	//Outras verbas que incorporam
	If aVerbasFunc[nCnt1,nPosTpo2] == 'K' .and. (!(aVerbasFunc[nCnt1,nPosPd] $ cCod13o .Or. aVerbasFunc[nCnt1,nPosPd] == aCodFol[024,1]) .And.;
		!(aVerbasFunc[nCnt1,nPosPd] == aCodFol[028,1] .Or. aVerbasFunc[nCnt1,nPosPd] $ cCodAd13o) .And.;
		PosSrv(aVerbasFunc[nCnt1,nPosPd],TPR->PR_FILIAL,"RV_INCORP") == "S")
		nBxaAdi += aVerbasFunc[nCnt1,nPosValor]       //-- Baixa de Adicionais
		If PosSrv(aVerbasFunc[nCnt1,nPosPd],TPR->PR_FILIAL,"RV_INSS") == "S"
			nBxaIns += aVerbasFunc[nCnt1,nPosValor]      // I.N.S.S.
		EndIf
		If PosSrv(aVerbasFunc[nCnt1,nPosPd],TPR->PR_FILIAL,"RV_FGTS") == "S"
			nBxaFgt += aVerbasFunc[nCnt1,nPosValor]      // F.G.T.S.
		EndIf
		If PosSrv(aVerbasFunc[nCnt1,nPosPd],TPR->PR_FILIAL,"RV_PIS") == "S"
			nBxaPis  += aVerbasFunc[nCnt1,nPosValor]      // P.I.S.
		EndIf
	EndIf

Next nCnt1

// PONTO DE ENTRADA PARA ALTERAR VALORES DE INSS E FGTS BAIXAS
If ExistBlock("GP090BIF")
	EXECBLOCK("GP090BIF",.F.,.F.)
EndIf

// GRAVA NO ARRAY OS VALORES DE BAIXA
a13Salar[_Bx13O,_Prov] := nBxa13o
a13Salar[_Bx13O,_Adic] := nBxaAdi
a13Salar[_Bx13O,_1Par] := 0.00 //-- Busca o valor da 1a.parcela atraves do GPEXMED
If cPaisLoc == "BRA" .And. cRecFatEmp $ "S*M*C" .And. lDesAtiv
	nInssAnt  := NoRound( ( ( nBxaIns - nDif13 ) / nTotMes ) * nAvosAnt )
	nInssAnt  := NoRound( nInssAnt * ( nPercAnt + nPercTer + nPercAcTrab ) )
	nInssDes  := NoRound( ( ( nBxaIns - nDif13 ) / nTotMes ) * ( nTotMes - nAvosAnt ) )
	nInssDes  := NoRound( ( nInssDes + nDif13 ) * ( nPerEmp13 + nPercTer + nPercAcTrab ) )
	nV_Inss   := NoRound( nInssAnt + nInssDes )
	a13Salar[_Bx13o,_INSS] := nV_Inss
Else
	a13Salar[_Bx13O,_INSS] := NoRound(nBxaIns * (nPercEmp+nPercTer+nPercAcTrab))
EndIf
a13Salar[_Bx13O,_FGTS] := NoRound(nBxaFgt * nPercFgts)
a13Salar[_Bx13O,_PIS]  := NoRound(nBxaPis * nPercPis)

Return (nBxa13o + nBxaAdi + nBxaIns + nBxaFgt + nBxaPis > 0)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ GPM070Ok ³ Autor ³ Emerson R. de Souza	³ Data ³ 26.06.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Grava os valores da provisao no arquivo SRT                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPM070Ok()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±± */
Static Function GPM070Ok()
Return (MsgYesNo(OemToAnsi(STR0013),OemToAnsi(STR0012))) //"Confirma configura‡„o dos parƒmetros?"###"ATENCAO"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fEncargEmp³ Autor ³ Emerson R. de Souza	³ Data ³ 26.06.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Carrega variaveis para calculo dos encargos                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fEncargEmp(nPercEmp, nPercTer, nPercAcTrab, nPercFgts)     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ nPercEmp      - Percentual da Empresa                      ³±±
±±³          ³ nPerTerc      - Percentual de Terceiros                    ³±±
±±³          ³ nPercAcTrab   - Percentual de Acidente de Trabalho         ³±±
±±³          ³ nPercFgts     - Percentual para o FGTS funcionario  8%     ³±±
±±³          ³ nPercFgC      - Percentual para o FGTS contribuicao 0.5%   ³±±
±±³          ³ cRecFatEmp    - Recolhe contribuicao sobre faturamento     ³±±
±±³          ³ nPerEmp13     - Percentual da Empresa p/ 13o. Salario      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso	 	 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±± */
Static Function fEncargEmp(nPercEmp, nPercTer, nPercAcTrab, nPercFgts, nPercFgC, cRecFatEmp, nPerEmp13)
//SE PAIS BRASIL, VERIFICA SE CALCULA INSS PARA O FUNCIONÁRIO
If lINSSAut .And. TPR->PR_INSSAUT == "N"
	nPercEmp 	:= 0.00
	nPerEmp13 	:= 0.00
	nPercTer    := 0.00
	nPercAcTrab := 0.00
Else
	// VERIFICAR TIPO DE CONTRATO PARA CALCULO DE ENCARGOS
	cRecFatEmp	:= If (TPR->PR_TPCONTR$ " *1", aInssEmp[27, 1] , aInssEmp[27, 2])
	If  cRecFatEmp $ "S*M*C"
		fPercEmp(cRecFatEmp, @nPercEmp, @nPerEmp13)
	Else
		nPercEmp	:= If (TPR->PR_TPCONTR$ " *1", aInssEmp[01, 1] , aInssEmp[01, 2])
		nPerEmp13   := If (TPR->PR_TPCONTR$ " *1", aInssEmp[01, 1] , aInssEmp[01, 2])
	EndIf
	nPercTer 	:= If (TPR->PR_TPCONTR$ " *1", aInssEmp[02, 1] , aInssEmp[02, 2])
	nPercAcTrab := If (TPR->PR_TPCONTR$ " *1", aInssEmp[03, 1] , aInssEmp[03, 2])
	// BUSCA O % ACID. TRAB. NO FUNCIONARIO/CENTRO CUSTO E TERCEIROS NO CENTRO DE CUSTO
	fBuscaAci(@nPercAcTrab,@nPercTer)
	//DEDUZIR O % DE TERCEIROS DEFINIDO NA TABELA AUXILIAR S035
	nPercTer -= fP15Terc(TPR->PR_CC,aGPSPer,"*")	
EndIf
nPercFgts 	:= If (TPR->PR_TPCONTR$ " *1", aInssEmp[04, 1] , aInssEmp[04, 2])

If SRA->RA_REGIME == '2'
	nPercFgts := 0
EndIf

// BUSCA O % DE FGTS DO CADASTRO DE FUNCIONARIOS
If Type("SRA->RA_PERFGTS") # "U" .And. SRA->RA_PERFGTS > 0.00
   nPercFgts := SRA->RA_PERFGTS / 100
Endif

// SOMA O % DE FGTS REFERENTE A CONTRIBUICAO DE 0.5
nPercFgts += nPercFgC

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fBusCodBx ³ Autor ³ Emerson R. de Souza	³ Data ³ 27.06.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Carrega Codigos de Ferias Para Baixa da Provisao           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fBusCodBx()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±± */
Static Function fBusCodBx()
// CODIGOS PARA BAIXA DA PROVISAO DE FERIAS
cCodFer 	:= aCodFol[072,1] + "*" + aCodFol[074,1] + "*" + aCodFol[088,1] + "*" + aCodFol[094,1] + "*" + aCodFol[0891,1] + "*"
cCodUmTer	:= aCodFol[077,1] + "*" + aCodFol[079,1] + "*" + aCodFol[090,1] + "*" + aCodFol[095,1] + "*"
cCodFerMs	:= aCodFol[073,1] + "*" + aCodFol[089,1] + "*" + aCodFol[205,1] + "*" + aCodFol[207,1] + "*"
cCodUmTerMs := aCodFol[078,1] + "*" + aCodFol[091,1] + "*" + aCodFol[206,1] + "*" + aCodFol[208,1] + "*"
cCodAbono	:= aCodFol[074,1] + "*" + aCodFol[079,1] + "*" + aCodFol[094,1] + "*" + aCodFol[095,1] + "*"
cCodAboMs	:= aCodFol[205,1] + "*" + aCodFol[207,1] + "*" + aCodFol[206,1] + "*" + aCodFol[208,1] + "*"
cCodAdicMs  := aCodFol[076,1] + "*" + aCodFol[081,1] + "*" + aCodFol[083,1] + "*" + aCodFol[085,1] + "*" +;
               aCodFol[093,1] + "*" + aCodFol[097,1] + "*" + aCodFol[099,1] + "*" + aCodFol[344,1] + "*" +;
			   aCodFol[346,1] + "*" + aCodFol[637,1] + "*" + aCodFol[1297,1] + "*" + aCodFol[1299,1] + "*" + aCodFol[1313,1];
			   + "*" + aCodFol[1315,1] + "*" + aCodFol[1301,1] + "*" + aCodFol[1303,1] + "*" + aCodFol[1317,1] + "*" + aCodFol[1319,1];
			   + "*" + aCodFol[1305,1] + "*" + aCodFol[1307,1] + "*" + aCodFol[1321,1] + "*" + aCodFol[1323,1] + "*" + aCodFol[1309,1];
			   + "*" + aCodFol[1325,1] + "*" + aCodFol[1311,1] + "*" + aCodFol[1327,1] + "*" + aCodFol[633,1] + "*" + aCodFol[634,1];
			   + "*" + aCodFol[1331,1]
cCodAdFer	:= aCodFol[075,1] + "*" + aCodFol[080,1] + "*" + aCodFol[082,1] + "*" + aCodFol[084,1] + "*" +;
			   aCodFol[092,1] + "*" + aCodFol[096,1] + "*" + aCodFol[098,1] + "*" + aCodFol[343,1] + "*" +;
			   aCodFol[345,1] + "*" + aCodFol[636,1] + "*" + aCodFol[1296,1] + "*" + aCodFol[1298,1] + "*" + aCodFol[1312,1];
			   + "*" + aCodFol[1314,1] + "*" + aCodFol[1300,1] + "*" + aCodFol[1302,1] + "*" + aCodFol[1316,1] + "*" + aCodFol[1318,1];
			   + "*" + aCodFol[1304,1] + "*" + aCodFol[1320,1] + "*" + aCodFol[1322,1] + "*" + aCodFol[1308,1] + "*" + aCodFol[1324,1];
			   + "*" + aCodFol[1306,1] + "*" + aCodFol[1310,1] + "*" + aCodFol[1326,1] + "*" + aCodFol[622,1] + "*" + aCodFol[1330,1];
			   + "*" + aCodFol[623,1]
cCodSalV    := aCodFol[361,1]
cCodSalVMs  := aCodFol[362,1]
//CODIGOS PARA BAIXA DA PROVISAO DE 13o. SALARIO
cCod13o	  := aCodFol[024,1] + "*" + aCodFol[028,1] + "*" + aCodFol[348,1]
cCodAd13o := aCodFol[001,1] + "*" + aCodFol[002,1] + "*" + aCodFol[003,1] + "*" + aCodFol[004,1] + "*"+;
		     aCodFol[005,1] + "*" + aCodFol[036,1] + "*" + aCodFol[037,1] + "*" + aCodFol[038,1] + "*"+;
			 aCodFol[039,1] + "*" + aCodFol[123,1] + "*" + aCodFol[124,1] + "*" + aCodFol[181,1] + "*"+;
			 aCodFol[182,1] + "*" + aCodFol[1288,1] + "*" + aCodFol[1289,1] + "*" + aCodFol[1290,1] + "*" +;
			 aCodFol[1291,1] + "*" + aCodFol[1292,1] + "*" + aCodFol[1293,1] + "*" + aCodFol[1294,1] + "*" +;
			 aCodFol[1295,1]
cInss13o  := aCodFol[148,1]

// PONTO DE ENTRADA PARA ACRESCENTAR VERBAS AOS CODIGOS PADRAO
If ExistBlock("GP070COD")
	ExecBlock("GP070COD",.F.,.F.)
Endif

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fLimpaArra³ Autor ³ Emerson R. de Souza	³ Data ³ 27.06.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Limpa o array de provisao para calculo                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fLimpaArray(aProvisao)                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aProvisao  - Array que sera zerado                         ³±±
±±³          ³ uConteudo  - Conteudo que sera gravado no array            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±*/
Function fLimpaArray(aProvisao,uConteudo,cNaoLimpar,nLinProv)
Local nCnt1, nCnt2

cNaoLimpar := If(cNaoLimpar == Nil, "", cNaoLimpar)

DEFAULT nLinProv := _Linhas

For nCnt1 := 1 To nLinProv
	If !Str(nCnt1,1) $ cNaoLimpar
		For nCnt2 := 1 To _Colunas
			aProvisao[nCnt1,nCnt2] := uConteudo
		Next nCnt2
	EndIf	
Next nCnt1

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fIdentProv³ Autor ³ Emerson R. de Souza	³ Data ³ 27.06.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Carrega Codigos de Provisao Para Lancamentos no SRT        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fIdentProv(aVerba,aCodFol)                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±± */
Function fIdentProv(aVerba,aCodFol)

aVerba := {}

Aadd(aVerba, { _FerVenc, _Atual, _Prov, aCodFol[130,1], "130" }) // Provisao de Ferias
Aadd(aVerba, { _FerVenc, _Atual, _Adic, aCodFol[254,1], "254" }) // Adicional Provisao de Ferias	
Aadd(aVerba, { _FerVenc, _Atual, _1Ter, aCodFol[255,1], "255" }) // Um Terco Provisao de Ferias	
Aadd(aVerba, { _FerVenc, _Atual, _INSS, aCodFol[131,1], "131" }) // INSS Provisao de Ferias
Aadd(aVerba, { _FerVenc, _Atual, _FGTS, aCodFol[132,1], "132" }) // FGTS Provisao de Ferias
Aadd(aVerba, { _FerVenc, _Atual, _PIS,  aCodFol[416,1], "416" }) // PIS Provisao de Ferias

Aadd(aVerba, { _FerVenc, _Corre, _Prov, aCodFol[133,1], "133" }) // Correcao Provisao de Ferias
Aadd(aVerba, { _FerVenc, _Corre, _Adic, aCodFol[256,1], "256" }) // Correcao Adicional Provisao de Ferias
Aadd(aVerba, { _FerVenc, _Corre, _1Ter, aCodFol[257,1], "257" }) // Correcao Um Terco Provisao de Ferias
Aadd(aVerba, { _FerVenc, _Corre, _INSS, aCodFol[134,1], "134" }) // Correcao INSS Provisao de Ferias
Aadd(aVerba, { _FerVenc, _Corre, _FGTS, aCodFol[135,1], "135" }) // Correcao FGTS Provisao de Ferias
Aadd(aVerba, { _FerVenc, _Corre, _PIS,  aCodFol[417,1], "417" }) // Correcao PIS Provisao de Ferias

Aadd(aVerba, { _FerVenc, _BxFer, _Prov, aCodFol[233,1], "233" }) // Baixa Provisao Ferias
Aadd(aVerba, { _FerVenc, _BxFer, _Adic, aCodFol[258,1], "258" }) // Baixa Adicional Provisao de Ferias
Aadd(aVerba, { _FerVenc, _BxFer, _1Ter, aCodFol[259,1], "259" }) // Baixa Um Terco Provisao de Ferias
Aadd(aVerba, { _FerVenc, _BxFer, _INSS, aCodFol[234,1], "234" }) // Baixa Inss Provisao Ferias
Aadd(aVerba, { _FerVenc, _BxFer, _FGTS, aCodFol[235,1], "235" }) // Baixa Fgts Provisao Ferias
Aadd(aVerba, { _FerVenc, _BxFer, _PIS,  aCodFol[418,1], "418" }) // Baixa PIS Provisao Ferias

Aadd(aVerba, { _FerVenc, _BxTrf, _Prov, aCodFol[239,1], "239" }) // Baixa Provisao Ferias Transferidos
Aadd(aVerba, { _FerVenc, _BxTrf, _Adic, aCodFol[260,1], "260" }) // Baixa Adicional Provisao de Ferias Transferidos
Aadd(aVerba, { _FerVenc, _BxTrf, _1Ter, aCodFol[261,1], "261" }) // Baixa Um Terco Provisao de Ferias Transferidos
Aadd(aVerba, { _FerVenc, _BxTrf, _INSS, aCodFol[240,1], "240" }) // Baixa Inss Provisao Ferias Transferidos
Aadd(aVerba, { _FerVenc, _BxTrf, _FGTS, aCodFol[241,1], "241" }) // Baixa Fgts Provisao Ferias Transferidos
Aadd(aVerba, { _FerVenc, _BxTrf, _PIS,  aCodFol[419,1], "419" }) // Baixa PIS Provisao Ferias Transferidos
                                                
Aadd(aVerba, { _FerVenc, _BxRes, _Prov, aCodFol[262,1], "262" }) // Baixa Provisao Ferias Rescisao
Aadd(aVerba, { _FerVenc, _BxRes, _Adic, aCodFol[263,1], "263" }) // Baixa Adicional Provisao de Ferias Rescisao
Aadd(aVerba, { _FerVenc, _BxRes, _1Ter, aCodFol[264,1], "264" }) // Baixa Um Terco Provisao de Ferias Rescisao
Aadd(aVerba, { _FerVenc, _BxRes, _INSS, aCodFol[265,1], "265" }) // Baixa Inss Provisao Ferias Rescisao
Aadd(aVerba, { _FerVenc, _BxRes, _FGTS, aCodFol[266,1], "266" }) // Baixa Fgts Provisao Ferias Rescisao
Aadd(aVerba, { _FerVenc, _BxRes, _PIS,  aCodFol[420,1], "420" }) // Baixa PIS Provisao Ferias Rescisao

Aadd(aVerba, { _FerVMes, _Atual, _Prov, aCodFol[960,1], "960" }) // Prov. Mês Férias
Aadd(aVerba, { _FerVMes, _Atual, _Adic, aCodFol[962,1], "962" }) // Prov. Mês Adicional de Férias
Aadd(aVerba, { _FerVMes, _Atual, _1Ter, aCodFol[961,1], "961" }) // Prov. Mês 1/3 de Férias
Aadd(aVerba, { _FerVMes, _Atual, _INSS, aCodFol[963,1], "963" }) // Prov. Mês INSS de Férias
Aadd(aVerba, { _FerVMes, _Atual, _FGTS, aCodFol[964,1], "964" }) // Prov. Mês FGTS de Férias
Aadd(aVerba, { _FerVMes, _Atual, _PIS,  aCodFol[965,1], "965" }) // Prov. Mês PIS de Férias

If Len(aCodFol) > 1399
	Aadd(aVerba, { _RecVenc, _Atual, _Prov, aCodFol[1400,1], "1400" }) // Provisao de Recesso
	Aadd(aVerba, { _RecVenc, _Corre, _Prov, aCodFol[1401,1], "1401" }) // Correcao Provisao de Recesso
	Aadd(aVerba, { _RecVenc, _BxFer, _Prov, aCodFol[1402,1], "1402" }) // Baixa Provisao Recesso
	Aadd(aVerba, { _RecVenc, _BxTrf, _Prov, aCodFol[1403,1], "1403" }) // Baixa Provisao Recesso Transferidos
	Aadd(aVerba, { _RecVenc, _BxRes, _Prov, aCodFol[1404,1], "1404" }) // Baixa Provisao Recesso Rescisao
Endif

Aadd(aVerba, { _13Salar, _Atual, _Prov, aCodFol[136,1], "136" }) // Provisao de 13o Salario
Aadd(aVerba, { _13Salar, _Atual, _Adic, aCodFol[267,1], "267" }) // Adicional Provisao de 13o Salario
Aadd(aVerba, { _13Salar, _Atual, _1Par, aCodFol[268,1], "268" }) // 1a. Parcela 13o Provisao
Aadd(aVerba, { _13Salar, _Atual, _INSS, aCodFol[137,1], "137" }) // INSS Provisao 13o Salario
Aadd(aVerba, { _13Salar, _Atual, _FGTS, aCodFol[138,1], "138" }) // FGTS Provisao 13o Salario
Aadd(aVerba, { _13Salar, _Atual, _PIS,  aCodFol[421,1], "421" }) // PIS Provisao 13o Salario

Aadd(aVerba, { _13Salar, _Corre, _Prov, aCodFol[139,1], "139" }) // Correcao Provisao 13o Salario
Aadd(aVerba, { _13Salar, _Corre, _Adic, aCodFol[269,1], "269" }) // Correcao Adicional Provisao de 13o Salario
Aadd(aVerba, { _13Salar, _Corre, _INSS, aCodFol[140,1], "140" }) // Correcao INSS Provisao 13o Salario
Aadd(aVerba, { _13Salar, _Corre, _FGTS, aCodFol[141,1], "141" }) // Correcao FGTS Provisao 13o Salario
Aadd(aVerba, { _13Salar, _Corre, _PIS,  aCodFol[422,1], "422" }) // Correcao PIS Provisao 13o Salario

Aadd(aVerba, { _13Salar, _Bx13O, _Prov, aCodFol[332,1], "332" }) // Baixa Provisao 13o Salario
Aadd(aVerba, { _13Salar, _Bx13O, _Adic, aCodFol[333,1], "333" }) // Baixa Adicional Provisao de 13o Salario
Aadd(aVerba, { _13Salar, _Bx13O, _1Par, aCodFol[334,1], "334" }) // Baixa Antecipacao 1a Parcela do 13o Salario
Aadd(aVerba, { _13Salar, _Bx13O, _INSS, aCodFol[335,1], "335" }) // Baixa Inss Provisao 13o Salario
Aadd(aVerba, { _13Salar, _Bx13O, _FGTS, aCodFol[336,1], "336" }) // Baixa Fgts Provisao 13o Salario
Aadd(aVerba, { _13Salar, _Bx13O, _PIS,  aCodFol[423,1], "423" }) // Baixa PIS Provisao 13o Salario

Aadd(aVerba, { _13Salar, _BxTrf, _Prov, aCodFol[270,1], "270" }) // Baixa Provisao 13o Salario Transferido
Aadd(aVerba, { _13Salar, _BxTrf, _Adic, aCodFol[271,1], "271" }) // Baixa Adicional Provisao de 13o Salario Transferido
Aadd(aVerba, { _13Salar, _BxTrf, _INSS, aCodFol[272,1], "272" }) // Baixa Inss Provisao 13o Salario Transferido
Aadd(aVerba, { _13Salar, _BxTrf, _FGTS, aCodFol[273,1], "273" }) // Baixa Fgts Provisao 13o Salario Transferido
Aadd(aVerba, { _13Salar, _BxTrf, _PIS,  aCodFol[424,1], "424" }) // Baixa PIS Provisao 13o Salario Transferido

Aadd(aVerba, { _13Salar, _BxRes, _Prov, aCodFol[274,1], "274" }) // Baixa Provisao 13o Salario Rescisao
Aadd(aVerba, { _13Salar, _BxRes, _Adic, aCodFol[275,1], "275" }) // Baixa Adicional Provisao de 13o Salario Rescisao
Aadd(aVerba, { _13Salar, _BxRes, _INSS, aCodFol[276,1], "276" }) // Baixa Inss Provisao 13o Salario Rescisao
Aadd(aVerba, { _13Salar, _BxRes, _FGTS, aCodFol[277,1], "277" }) // Baixa Fgts Provisao 13o Salario Rescisao
Aadd(aVerba, { _13Salar, _BxRes, _PIS,  aCodFol[425,1], "425" }) // Baixa PIS Provisao 13o Salario Rescisao

Aadd(aVerba, { _13SVMes, _Atual, _Prov, aCodFol[966,1], "966" }) // 966 - Prov. Mês 13o Salário
Aadd(aVerba, { _13SVMes, _Atual, _Adic, aCodFol[967,1], "967" }) // 967 - Prov. Mês Adcional de 13o Salário
Aadd(aVerba, { _13SVMes, _Atual, _1Par, aCodFol[968,1], "968" }) // 968 - 1a. Parcela 13o Provisao
Aadd(aVerba, { _13SVMes, _Atual, _INSS, aCodFol[969,1], "969" }) // 969 - Prov. Mês INSS de 13o Salário
Aadd(aVerba, { _13SVMes, _Atual, _FGTS, aCodFol[970,1], "970" }) // 970 - Prov. Mês FGTS de 13o Salário
Aadd(aVerba, { _13SVMes, _Atual, _PIS,  aCodFol[971,1], "971" }) // 971 - Prov. Mês PIS de 13o Salário

Aadd(aVerba, { _14Salar, _Atual, _Prov, aCodFol[142,1], "142" }) // Provisao de 14o Salario
Aadd(aVerba, { _14Salar, _Atual, _Adic, aCodFol[278,1], "278" }) // Adicional Provisao de 14o Salario
Aadd(aVerba, { _14Salar, _Atual, _INSS, aCodFol[143,1], "143" }) // INSS Provisao 14o Salario
Aadd(aVerba, { _14Salar, _Atual, _FGTS, aCodFol[144,1], "144" }) // FGTS Provisao 14o Salario
Aadd(aVerba, { _14Salar, _Atual, _PIS,  aCodFol[426,1], "426" }) // PIS Provisao 14o Salario

Aadd(aVerba, { _14Salar, _Corre, _Prov, aCodFol[145,1], "145" }) // Correcao Provisao 14o Salario
Aadd(aVerba, { _14Salar, _Corre, _Adic, aCodFol[279,1], "279" }) // Correcao Adicional Provisao de 14o Salario
Aadd(aVerba, { _14Salar, _Corre, _INSS, aCodFol[146,1], "146" }) // Correcao INSS Provisao 14o Salario
Aadd(aVerba, { _14Salar, _Corre, _FGTS, aCodFol[147,1], "147" }) // Correcao FGTS Provisao 14o Salario
Aadd(aVerba, { _14Salar, _Corre, _PIS,  aCodFol[427,1], "427" }) // Correcao PIS Provisao 14o Salario

Aadd(aVerba, { _14Salar, _BxTrf, _Prov, aCodFol[280,1], "280" }) // Baixa Provisao 14o Salario Transferido
Aadd(aVerba, { _14Salar, _BxTrf, _Adic, aCodFol[281,1], "281" }) // Baixa Adicional Provisao de 14o Salario Transferido
Aadd(aVerba, { _14Salar, _BxTrf, _INSS, aCodFol[282,1], "282" }) // Baixa Inss Provisao 14o Salario Transferido
Aadd(aVerba, { _14Salar, _BxTrf, _FGTS, aCodFol[283,1], "283" }) // Baixa Fgts Provisao 14o Salario Transferido
Aadd(aVerba, { _14Salar, _BxTrf, _PIS,  aCodFol[428,1], "428" }) // Baixa PIS Provisao 14o Salario Transferido

Aadd(aVerba, { _14Salar, _BxRes, _Prov, aCodFol[284,1], "284" }) // Baixa Provisao 14o Salario Rescisao
Aadd(aVerba, { _14Salar, _BxRes, _Adic, aCodFol[285,1], "285" }) // Baixa Adicional Provisao de 14o Salario Rescisao
Aadd(aVerba, { _14Salar, _BxRes, _INSS, aCodFol[286,1], "286" }) // Baixa Inss Provisao 14o Salario Rescisao
Aadd(aVerba, { _14Salar, _BxRes, _FGTS, aCodFol[287,1], "287" }) // Baixa Fgts Provisao 14o Salario Rescisao
Aadd(aVerba, { _14Salar, _BxRes, _PIS,  aCodFol[429,1], "429" }) // Baixa PIS Provisao 14o Salario Rescisao

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fBusCabSRT³ Autor ³ Emerson R. de Souza	³ Data ³ 21.06.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Busca informacoes de cabecalho no SRT                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fBusCabSRT(dDtBusca,aCabProv)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fBusCabSRT(dDtBusca,aCabProv)
Local lRet
Local aArea	:= GetArea()
Local cClvl	:= ""
Local cCusto:= ""
Local cItem	:= ""

If Type("lItemClVl") == "U"
	lItemClVl   := SuperGetMv( "MV_ITMCLVL", .F., "2" ) $ "13"
Endif

If !lItemCLVL
	aCabProv := Array(11)
Else	 
	aCabProv := Array(14)
EndIF

If !Empty(cTpRtProv)
	dbSelectArea("RHT")
	cCusto	:= TPR->PR_CCMVTO
	If lItemClVl
		cItem	:= TPR->PR_ITMMVTO 
		cClvl	:= TPR->PR_CLVMVTO
	EndIf
Else 
	dbSelectArea("SRT")
	cCusto	:= TPR->PR_CC
	If lItemClVl
		cItem	:= TPR->PR_ITEM 
		cClvl	:= TPR->PR_CLVL
	EndIf
Endif

If lItemClvl
	dbSetOrder(4)
	lRet := dbSeek( TPR->PR_FILIAL + TPR->PR_MAT + cCusto + cItem + cClvl + MesAno(dDtBusca) )
Else
	dbSetOrder(1)
	lRet := dbSeek( TPR->PR_FILIAL + TPR->PR_MAT + cCusto + MesAno(dDtBusca) )
EndIf

aCabProv[_DatCalc] := If (!Empty(cTpRtProv), RHT->RHT_DTCALC, SRT->RT_DATACAL)
aCabProv[_CentroC] := If (!Empty(cTpRtProv), RHT->RHT_CC, SRT->RT_CC)
aCabProv[_DBsProv] := If (!Empty(cTpRtProv), RHT->RHT_DTBASE, SRT->RT_DATABAS)
aCabProv[_DFerVen] := If (!Empty(cTpRtProv), RHT->RHT_DFERVE, SRT->RT_DFERVEN)
aCabProv[_DFerPro] := If (!Empty(cTpRtProv), RHT->RHT_DFERPR, SRT->RT_DFERPRO)
aCabProv[_DFalVen] := If (!Empty(cTpRtProv), RHT->RHT_DFALVE, SRT->RT_DFALVEN)
aCabProv[_DFalPro] := If (!Empty(cTpRtProv), RHT->RHT_DFALPR, SRT->RT_DFALPRO)
aCabProv[_DFerAnt] := If (!Empty(cTpRtProv), RHT->RHT_DFERAN, SRT->RT_DFERANT)
aCabProv[_Avos13S] := If (!Empty(cTpRtProv), RHT->RHT_AVOS13, SRT->RT_AVOS13S)
aCabProv[_MovProv] := If (!Empty(cTpRtProv), RHT->RHT_TIPMOV, SRT->RT_TIPMOVI)
aCabProv[_SalProv] := If (!Empty(cTpRtProv), RHT->RHT_SALAR, SRT->RT_SALARIO)
If LItemCLVL
	aCabProv[_cItem] := If (!Empty(cTpRtProv), RHT->RHT_ITEM, SRT->RT_ITEM)
	aCabProv[_Clvl] := If (!Empty(cTpRtProv), RHT->RHT_CLVL, SRT->RT_CLVL)
EndIf

RestArea(aArea)
Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fQryDetSRT| Autor ³ Ricardo Duarte Costa  ³ Data ³ 22.01.07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Busca os valores da provisao no arquivo SRT                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ < vide parametros abaixo >                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aVerba     - Verbas de Ferias / 13o Sal / 14o Sal          ³±±
±±³          ³ aTransf    - Funcionarios Transferidos no mes              ³±±
±±³          ³ dDataRef   - Data de referencia para busca dos valores     ³±±
±±³          ³ nTipoProv  - Tipo de provisao a buscar (Venc,Prop,13o)     |±±
±±³          ³ lTrataTrf  - Indica se deve tratar os transferidos         |±±
±±³          ³ lCalcula   - Indica se deve calcular o valor do mes        |±±
±±³          ³ lFerias    - Indica se deve buscar as ferias               |±±
±±³          ³ l13oSal    - Indica se deve buscar o 13o salario           |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fQryDetSRT(aVerba,aTransf,dDataRef,lTrataTrf,lCalcula,lFerias,l13oSal,lSoMes)
Local cBusMesAnt,cBusMesAtu,nCnt1
Local cFilQryAnt,cMatQryAnt,cCCQryAnt,cFilQryAtu,cMatQryAtu,cCCQryAtu,cDtIQryAnt,cDtFQryAnt,cDtIQryAtu,cDtFQryAtu  
Local cItmQryAnt,cClvQryAnt,cItmQryAtu,cClvQryAtu
Local nTpMov      := 0
Local nPosTrf	  := 0
Local nPosVerba	  := 0
Local nreg		  := 0
Local nTipoProv   := 0
Local nCntSRT	  := 0
Local cAlias      := ALIAS()
Local cAnoMesAnt  := MesAno(dDataRef - Day(dDataRef))
Local cAnoMesAtu  := MesAno(dDataRef)
Local cOrdemAnt   := &("{ || (cAliasAnt)->RT_FILIAL + (cAliasAnt)->RT_MAT + (cAliasAnt)->RT_CC + MesAno((cAliasAnt)->RT_DATACAL) }")
Local cOrdemBusca := &("{ || (cAliasSRT)->RT_FILIAL + (cAliasSRT)->RT_MAT + (cAliasSRT)->RT_CC + MesAno((cAliasSRT)->RT_DATACAL) }")
Local cCposQuery  := ""
Local aProvisao   := {}
Local axFerVenc   := {}
Local axFerProp   := {}
Local ax13Salar   := {}
Local ax14Salar   := {}
Local lInAS400	  := ExeInAS400()
Local lTrfEmpAmes := .F.
Local lAdiant13	  := .F. 
Local cFilMatCC 
Local cAliasArq	  := If (Empty(cTpRtProv), "SRT", "RHT")    
Local cAliasAnt   := "QSRT2"
Local cTpProvMes  := ""
Local cEmpQryAnt  := ""
Local cSvEmpAnt   := ""
Local cTabAnt     := ""
Local lQryAnt	  := .F.

Default lSoMes := .F.

If Type("lProvResc") == "U"
	lProvResc := (SuperGetMv("MV_PROVRES",,"N") == "S") //-- Indica se devera provisionar no mes da rescisao
EndIf 

If Type("lItemClVl") == "U"
	lItemClVl   := SuperGetMv( "MV_ITMCLVL", .F., "2" ) $ "13"
Endif

If Type("lTrfAMES") == "U"
	cTrfAMES     := (SuperGetMv("MV_TRFAMES",, Space(6))) //-- Ano/Mes para inicio das demonstracoes de entrada e saida de transferencias no conceito 1 (transfere saldo origem p/ destino)
	lTrfAMES     := (!Empty(cTrfAMES) .And. MesAno(dDataRef) >= cTrfAMES)
EndIf

If Type("lTrfSld") == "U" .Or. lTrfSld
	lTrfSld := .F.
EndIf

lCalcula   := If(lCalcula  == Nil, .F., lCalcula)
lFerias	   := If(lFerias   == Nil, .T., lFerias)
l13oSal    := If(l13oSal   == Nil, .T., l13oSal) 

If !Empty(cTpRtProv)
	If lItemClVl
		cOrdemAnt   := &("{ || (cAliasAnt)->RHT_FILIAL + (cAliasAnt)->RHT_MAT + (cAliasAnt)->RHT_CC + (cAliasAnt)->RHT_ITEM + (cAliasAnt)->RHT_CLVL + MesAno((cAliasAnt)->RHT_DTCALC) }")    
		cOrdemBusca := &("{ || (cAliasSRT)->RHT_FILIAL + (cAliasSRT)->RHT_MAT + (cAliasSRT)->RHT_CC + (cAliasSRT)->RHT_ITEM + (cAliasSRT)->RHT_CLVL + MesAno((cAliasSRT)->RHT_DTCALC) }")   
	Else
		cOrdemAnt   := &("{ || (cAliasAnt)->RHT_FILIAL + (cAliasAnt)->RHT_MAT + (cAliasAnt)->RHT_CC + MesAno((cAliasAnt)->RHT_DTCALC) }")    
		cOrdemBusca := &("{ || (cAliasSRT)->RHT_FILIAL + (cAliasSRT)->RHT_MAT + (cAliasSRT)->RHT_CC + MesAno((cAliasSRT)->RHT_DTCALC) }")    
	EndIf
	cTpProvMes := If(lFerias, "'1','2'", "'3'")
ElseIf lItemClVl
	cOrdemAnt   := &("{ || (cAliasAnt)->RT_FILIAL + (cAliasAnt)->RT_MAT + (cAliasAnt)->RT_CC + (cAliasAnt)->RT_ITEM + (cAliasAnt)->RT_CLVL + MesAno((cAliasAnt)->RT_DATACAL) }")	
	cOrdemBusca := &("{ || (cAliasSRT)->RT_FILIAL + (cAliasSRT)->RT_MAT + (cAliasSRT)->RT_CC + (cAliasSRT)->RT_ITEM + (cAliasSRT)->RT_CLVL + MesAno((cAliasSRT)->RT_DATACAL) }")
Endif

If !lSoMes
	If !lProvResc .And. TPR->PR_TIPMOVI == _Demitido
		nPosTrf		:= Ascan(aTransf, { |X| X[_TAtual,_TEmp]+X[_TAtual,_TFil]+X[_TAtual,_TCC]+X[_TAtual,_TMat] == cEmpAnt+TPR->PR_FILIAL+TPR->PR_CC+TPR->PR_MAT })
		If nPosTrf > 0 .and. ;
			aTransf[nPosTrf,_TAnter,_TEmp]+aTransf[nPosTrf,_TAnter,_TFil]+aTransf[nPosTrf,_TAnter,_TCC]+aTransf[nPosTrf,_TAnter,_TMat]==;
			aTransf[nPosTrf,_TDest ,_TEmp]+aTransf[nPosTrf,_TDest ,_TFil]+aTransf[nPosTrf,_TDest ,_TCC]+aTransf[nPosTrf,_TDest ,_TMat]
			nPosTrf		:= 0
	  	EndIf
		lTrfSld		:= nPosTrf > 0
		nTpMov		:= TPR->PR_TIPMOVI //Gravar tipo de movimento de demissao
	EndIf
	
	//	CHAVE DE BUSCA DO MES ANTERIOR E ATUAL
	If lItemClVl
		cBusMesAnt  := TPR->PR_FILIAL + TPR->PR_MAT + TPR->PR_CC + TPR->PR_ITEM + TPR->PR_CLVL + cAnoMesAnt 
	Else
		cBusMesAnt  := TPR->PR_FILIAL + TPR->PR_MAT + TPR->PR_CC + cAnoMesAnt
	EndIf
	cFilQryAnt	:= "'"+TPR->PR_FILIAL+"'"
	cMatQryAnt	:= "'"+TPR->PR_MAT+"'"
	cCCQryAnt	:= "'"+TPR->PR_CC+"'"
	cEmpQryAnt	:= cEmpAnt
	If lItemClVl
		cItmQryAnt	:= "'"+TPR->PR_ITEM+"'"
		cClvQryAnt	:= "'"+TPR->PR_CLVL+"'"
	EndIf
	
	If lItemClVl
		cBusMesAtu := TPR->PR_FILIAL + TPR->PR_MAT + TPR->PR_CC + TPR->PR_ITEM + TPR->PR_CLVL + cAnoMesAtu
	Else
		cBusMesAtu := TPR->PR_FILIAL + TPR->PR_MAT + TPR->PR_CC + cAnoMesAtu
	EndIf
	cFilQryAtu	:= "'"+TPR->PR_FILIAL+"'"
	cMatQryAtu	:= "'"+TPR->PR_MAT+"'"
	cCCQryAtu	:= "'"+TPR->PR_CC+"'"
	If lItemClVl
		cItmQryAtu	:= "'"+TPR->PR_ITEM+"'"
		cClvQryAtu	:= "'"+TPR->PR_CLVL+"'"
	EndIf
	
	// SE NAO TRATA TRANSFERENCIA BUSCAR C. DE CUSTO DO MES ANTERIOR
	If !lTrataTrf .Or. lTrfAMES .Or. lTrfSld
		If !lTrfSld
            If lItemClVl
				nPosTrf := Ascan(aTransf, { |X| X[_TAtual,_TEmp]+X[_TAtual,_TFil]+X[_TAtual,_TCC]+X[_TAtual,_TMat]+X[_TAtual,_TItem]+X[_TAtual,_TClvl] == FWGrpCompany(cAliasArq)+TPR->PR_FILIAL+TPR->PR_CC+TPR->PR_MAT+TPR->PR_ITEM+TPR->PR_CLVL })
			Else
				nPosTrf := Ascan(aTransf, { |X| X[_TAtual,_TEmp]+X[_TAtual,_TFil]+X[_TAtual,_TCC]+X[_TAtual,_TMat] == FWGrpCompany(cAliasArq)+TPR->PR_FILIAL+TPR->PR_CC+TPR->PR_MAT })
			EndIf
		    //Se anterior igual a destino entao nao houve transferencia.
			If nPosTrf > 0 .and. ;
				( ( lItemClVl .And.;
				aTransf[nPosTrf,_TAnter,_TEmp]+aTransf[nPosTrf,_TAnter,_TFil]+aTransf[nPosTrf,_TAnter,_TCC]+aTransf[nPosTrf,_TAnter,_TMat]+aTransf[nPosTrf,_TAnter,_TDta]+aTransf[nPosTrf,_TAnter,_TItem]+aTransf[nPosTrf,_TAnter,_TClvl] == ;
				aTransf[nPosTrf,_TDest ,_TEmp]+aTransf[nPosTrf,_TDest ,_TFil]+aTransf[nPosTrf,_TDest ,_TCC]+aTransf[nPosTrf,_TDest ,_TMat]+aTransf[nPosTrf,_TDest ,_TDta]+aTransf[nPosTrf,_TDest ,_TItem]+aTransf[nPosTrf,_TDest ,_TClvl] ) .Or.;
				( !lItemClVl .And.;
				aTransf[nPosTrf,_TAnter,_TEmp]+aTransf[nPosTrf,_TAnter,_TFil]+aTransf[nPosTrf,_TAnter,_TCC]+aTransf[nPosTrf,_TAnter,_TMat]+aTransf[nPosTrf,_TAnter,_TDta] == ;
				aTransf[nPosTrf,_TDest ,_TEmp]+aTransf[nPosTrf,_TDest ,_TFil]+aTransf[nPosTrf,_TDest ,_TCC]+aTransf[nPosTrf,_TDest ,_TMat]+aTransf[nPosTrf,_TDest ,_TDta] ) )
				nPosTrf:=0
			EndIf
		EndIf
		If nPosTrf > 0
			If lItemClVl
				cBusMesAnt	:= aTransf[nPosTrf,_TAnter,_TFil]+aTransf[nPosTrf,_TAnter,_TMat]+aTransf[nPosTrf,_TAnter,_TCC]+aTransf[nPosTrf,_TAnter,_TItem]+aTransf[nPosTrf,_TAnter,_TClvl]+cAnoMesAnt
			Else
				cBusMesAnt	:= aTransf[nPosTrf,_TAnter,_TFil]+aTransf[nPosTrf,_TAnter,_TMat]+aTransf[nPosTrf,_TAnter,_TCC]+cAnoMesAnt
			EndIf
			cFilQryAnt	:= "'"+aTransf[nPosTrf,_TAnter,_TFil]+"'"
			cMatQryAnt	:= "'"+aTransf[nPosTrf,_TAnter,_TMat]+"'"
			cCCQryAnt	:= "'"+aTransf[nPosTrf,_TAnter,_TCC]+"'"
			cEmpQryAnt	:= aTransf[nPosTrf,_TAnter,_TEmp]
			If lItemClVl
				cItmQryAnt	:= "'"+aTransf[nPosTrf,_TAnter,_TItem]+"'"
				cClvQryAnt	:= "'"+aTransf[nPosTrf,_TAnter,_TClvl]+"'"
			EndIf			
			// Se transferencia ocorreu apos mes atual preserva mes anterior|
			If aTransf[nPosTrf,_TAnter,_TDta] > cAnoMesAtu
				If lItemClVl
					cBusMesAtu	:= aTransf[nPosTrf,_TAnter,_TFil]+aTransf[nPosTrf,_TAnter,_TMat]+aTransf[nPosTrf,_TAnter,_TCC]+aTransf[nPosTrf,_TAnter,_TItem]+aTransf[nPosTrf,_TAnter,_TClvl]+cAnoMesAtu
				Else
					cBusMesAtu	:= aTransf[nPosTrf,_TAnter,_TFil]+aTransf[nPosTrf,_TAnter,_TMat]+aTransf[nPosTrf,_TAnter,_TCC]+cAnoMesAtu
				EndIf
				cFilQryAtu	:= "'"+aTransf[nPosTrf,_TAnter,_TFil]+"'"
				cMatQryAtu	:= "'"+aTransf[nPosTrf,_TAnter,_TMat]+"'"
				cCCQryAtu	:= "'"+aTransf[nPosTrf,_TAnter,_TCC]+"'"
				If lItemClVl
					cItmQryAtu	:= "'"+aTransf[nPosTrf,_TAnter,_TItem]+"'"
					cClvQryAtu	:= "'"+aTransf[nPosTrf,_TAnter,_TClvl]+"'"
				EndIf
	
				lTrfSld := .F.
			EndIf
			lTrfEmpAmes := aTransf[nPosTrf,_TAnter,_TEmp] <> aTransf[nPosTrf,_TAtual,_TEmp]
		ElseIf TPR->PR_TIPMOVI == _Trfe_Sai
			If lItemClVl
				nPosTrfAmes	:= Ascan(aTransf, { |X| X[_TAnter,_TEmp]+X[_TAnter,_TFil]+X[_TAnter,_TCC]+X[_TAnter,_TMat]+X[_TAnter,_TItem]+X[_TAnter,_TClvl] == FWGrpCompany(cAliasArq)+TPR->PR_FILIAL+TPR->PR_CC+TPR->PR_MAT+TPR->PR_ITEM+TPR->PR_CLVL })
			Else
				nPosTrfAmes	:= Ascan(aTransf, { |X| X[_TAnter,_TEmp]+X[_TAnter,_TFil]+X[_TAnter,_TCC]+X[_TAnter,_TMat] == FWGrpCompany(cAliasArq)+TPR->PR_FILIAL+TPR->PR_CC+TPR->PR_MAT })
			EndIf
			If nPosTrfAmes > 0
				lTrfEmpAmes := aTransf[nPosTrfAmes,_TAnter,_TEmp] <> aTransf[nPosTrfAmes,_TAtual,_TEmp]
			Endif
		Endif
	EndIf
	
	cAliasSRT	:= "QSRT" 
	If Empty(cTpRtProv)	
		aStruSRT 	:= SRT->(dbStruct())
	Else
		aStruSRT	:= RHT->(dbStruct())
	Endif
	
	For nCntSRT := 1 To Len(aStruSRT)
		If nCntSRT < Len(aStruSRT)
			cCposQuery	+= aStruSRT[nCntSRT,1] + ", "
		Else
			cCposQuery	+= aStruSRT[nCntSRT,1]
		EndIf
	Next nCntSRT
	
	//MONTA A QUERY PARA SELECAO DOS DADOS
	If lInAS400
		cCposQuery	+=	", RRN(SRT) RECNO_ "
	Else
		cCposQuery	+=	", R_E_C_N_O_ AS RECNO_"
	EndIF
	cCposQuery	:=	"% "+cCposQuery+" %"
	If lItemClVl
		cOrdem		:= If (Empty(cTpRtProv), SqlOrder( SRT->( IndexKey(4) ) ), SqlOrder( RHT->( IndexKey(4) ) ) )
	Else
		cOrdem		:= If (Empty(cTpRtProv), SqlOrder( SRT->( IndexKey(1) ) ), SqlOrder( RHT->( IndexKey(1) ) ) )
	EndIf
	cOrdem		:= "% "+cOrdem+" %"
	cDtIQryAnt	:= "'"+cAnoMesAnt+"01'"
	cDtFQryAnt	:= "'"+Dtos(dDataRef-Day(dDataRef))+"'"
	cDtIQryAtu	:= "'"+cAnoMesAtu+"01'"
	cDtFQryAtu	:= "'"+cAnoMesAtu+strzero(f_Ultdia(dDataRef),2)+"'"
	
	If !Empty(cTpRtProv)
		If lItemClVl
			cWhere		:=	"(RHT.RHT_FILIAL = "+cFilQryAtu+" AND RHT.RHT_MAT = "+cMatQryAtu+;
							" AND RHT.RHT_DTCALC BETWEEN "+cDtIQryAtu+" AND "+cDtFQryAtu+" ) "
			cWhere		+= " AND RHT.RHT_CC = '"+TPR->PR_CCMVTO+ "' AND RHT.RHT_ITEM = '"+TPR->PR_ITMMVTO+ "' AND RHT.RHT_CLVL = '"+TPR->PR_CLVMVTO+ "' AND RHT.RHT_TPPROV IN ("+ cTpProvMes+ " )"								
		Else
			cWhere		:=	"(RHT.RHT_FILIAL = "+cFilQryAtu+" AND RHT.RHT_MAT = "+cMatQryAtu+;
							" AND RHT.RHT_DTCALC BETWEEN "+cDtIQryAtu+" AND "+cDtFQryAtu+" ) "
			cWhere		+= " AND RHT.RHT_CC = '"+TPR->PR_CCMVTO+ "' AND RHT.RHT_TPPROV IN ("+ cTpProvMes+ " )"
		EndIf
	Else
		If lItemClVl
			cWhere		:=	"(SRT.RT_FILIAL = "+cFilQryAnt+" AND SRT.RT_MAT = "+cMatQryAnt+" AND "+;
							"SRT.RT_CC = "+cCCQryAnt+" AND SRT.RT_ITEM = "+cItmQryAnt+" AND SRT.RT_CLVL = "+cClvQryAnt+" AND SRT.RT_DATACAL BETWEEN "+cDtIQryAnt+" AND "+cDtFQryAnt+" )"
			cWhere		:=	"( "+cWhere+" OR (SRT.RT_FILIAL = "+cFilQryAtu+" AND SRT.RT_MAT = "+cMatQryAtu+" AND "+;
							"SRT.RT_CC = "+cCCQryAtu+" AND SRT.RT_ITEM = "+cItmQryAtu+" AND SRT.RT_CLVL = "+cClvQryAtu+" AND SRT.RT_DATACAL BETWEEN "+cDtIQryAtu+" AND "+cDtFQryAtu+" ) )"		
		Else
			cWhere		:=	"(SRT.RT_FILIAL = "+cFilQryAnt+" AND SRT.RT_MAT = "+cMatQryAnt+" AND "+;
							"SRT.RT_CC = "+cCCQryAnt+" AND SRT.RT_DATACAL BETWEEN "+cDtIQryAnt+" AND "+cDtFQryAnt+" )"
			cWhere		:=	"( "+cWhere+" OR (SRT.RT_FILIAL = "+cFilQryAtu+" AND SRT.RT_MAT = "+cMatQryAtu+" AND "+;
							"SRT.RT_CC = "+cCCQryAtu+" AND SRT.RT_DATACAL BETWEEN "+cDtIQryAtu+" AND "+cDtFQryAtu+" ) )"
		EndIf
	EndIf

	//FILTRA PELOS IDENTIFICADORES DE CALCULO DO RATEIO DE PROVISAO DE FERIAS OU 13 SALARIO
	//FERIAS: 960 - 961 - 962 - 963 - 964 - 965                                           
	//13 SAL: 966 - 967 - 968 - 969 - 970                                                 
	If cTpRtProv == "RPF" //RPF - Rateio Provisao Férias
		cWhere		+= " AND RHT.RHT_VERBA IN ('"+aCodFol[960,1]+"','"+aCodFol[961,1]+"','"+aCodFol[962,1]+"','"+;
												  aCodFol[963,1]+"','"+aCodFol[964,1]+"','"+aCodFol[965,1]+"') "
	ElseIf cTpRtProv == "RP13" //RP13 - Rateio Provisao 13 Salario
		cWhere		+= " AND RHT.RHT_VERBA IN ('"+aCodFol[966,1]+"','"+aCodFol[967,1]+"','"+aCodFol[968,1]+"','"+;
												  aCodFol[969,1]+"','"+aCodFol[970,1]+"','"+aCodFol[971,1]+"') "
	EndIf
	
	cWhere		:= "%"+cWhere+"%"

	If Empty(cTpRtProv)	
		BeginSql alias cAliasSRT
			SELECT %exp:cCposQuery% 
			FROM %table:SRT% SRT 
			WHERE %exp:cWhere% AND SRT.%notDel% 
			ORDER BY %exp:cOrdem%
		EndSql
		If cEmpQryAnt != cEmpAnt
			lQryAnt   := .T.
			cSvEmpAnt := cEmpAnt			
			cEmpAnt   := cEmpQryAnt
			cTabAnt   := "%" + RetFullName("SRT") + "%"
			BeginSql alias cAliasAnt
				SELECT %exp:cCposQuery% 
				FROM %exp:cTabAnt% SRT 
				WHERE %exp:cWhere% AND SRT.%notDel% 
				ORDER BY %exp:cOrdem%
			EndSql
			cEmpAnt := cSvEmpAnt			
		EndIf
	Else
		BeginSql alias cAliasSRT
			SELECT %exp:cCposQuery% 
			FROM %table:RHT% RHT 
			WHERE %exp:cWhere% AND RHT.%notDel% 
			ORDER BY %exp:cOrdem%
		EndSql	
	Endif
	//AJUSTA A ESTRUTURA DOS CAMPOS
	For nReg := 1 To Len(aStruSRT)
		If (aStruSRT[nReg][2] <> "C")
			TcSetField(cAliasSRT,aStruSRT[nReg][1],aStruSRT[nReg][2],aStruSRT[nReg][3],aStruSRT[nReg][4])
		EndIf
	Next nReg
	If lQryAnt
		For nReg := 1 To Len(aStruSRT)
			If (aStruSRT[nReg][2] <> "C")
				TcSetField(cAliasAnt,aStruSRT[nReg][1],aStruSRT[nReg][2],aStruSRT[nReg][3],aStruSRT[nReg][4])
			EndIf
		Next nReg	
	EndIf

	//	LIMPA OS ARRAYS DE PROVISAO
	If Type("aFerVenc") # "U"
		fLimpaArray(@aFerVenc, 0)
		fLimpaArray(@aFerProp, 0)
		If !lCalcula
			fLimpaArray(@aRecVenc, 0)
			fLimpaArray(@aRecProp, 0)
		Endif
	EndIf
	If Type("a13Salar") # "U"
		fLimpaArray(@a13Salar, 0)
		fLimpaArray(@a14Salar, 0)
		If !lCalcula
			fLimpaArray(@aRec13Sl, 0)
			fLimpaArray(@aRec14Sl, 0)
		Endif
	EndIf
	
	dbSelectArea(cAliasSRT)
	//ENTRA NESTE BLOCO NA IMPRESSAO DO RELATORIO DE VALORES DO MES
	If !Empty(cTpRtProv)
		
			If cTpRtProv == 'RPF'
				While !Eof()
					If (cAliasSRT)->RHT_TPPROV == '1' // Férias Vencidas
						If (cAliasSRT)->RHT_VERBA == aCodFol[960,1]
							aFerVenc[3,_Prov] := (cAliasSRT)->RHT_VALOR
							aFerVenc[3,_Avos] := 0 
							aFerVenc[3,_SalV] := 0
						ElseIf (cAliasSRT)->RHT_VERBA == aCodFol[962,1] 
							aFerVenc[3,_Adic] := (cAliasSRT)->RHT_VALOR
						ElseIf (cAliasSRT)->RHT_VERBA == aCodFol[961,1]
							aFerVenc[3,_1Ter] := (cAliasSRT)->RHT_VALOR
						ElseIf (cAliasSRT)->RHT_VERBA == aCodFol[963,1]
							aFerVenc[3,_INSS] := (cAliasSRT)->RHT_VALOR
						ElseIf (cAliasSRT)->RHT_VERBA == aCodFol[964,1]
							aFerVenc[3,_FGTS] := (cAliasSRT)->RHT_VALOR
						ElseIf (cAliasSRT)->RHT_VERBA == aCodFol[965,1]
							aFerVenc[3,_PIS] := (cAliasSRT)->RHT_VALOR	
						EndIf 
					Else 
						If (cAliasSRT)->RHT_VERBA == aCodFol[960,1]
							aFerProp[3,_Prov] := (cAliasSRT)->RHT_VALOR
							aFerProp[3,_Avos] := 0 
							aFerProp[3,_SalV] := 0
						ElseIf (cAliasSRT)->RHT_VERBA == aCodFol[962,1] 
							aFerProp[3,_Adic] := (cAliasSRT)->RHT_VALOR
						ElseIf (cAliasSRT)->RHT_VERBA == aCodFol[961,1]
							aFerProp[3,_1Ter] := (cAliasSRT)->RHT_VALOR
						ElseIf (cAliasSRT)->RHT_VERBA == aCodFol[963,1]
							aFerProp[3,_INSS] := (cAliasSRT)->RHT_VALOR
						ElseIf (cAliasSRT)->RHT_VERBA == aCodFol[964,1]
							aFerProp[3,_FGTS] := (cAliasSRT)->RHT_VALOR
						ElseIf (cAliasSRT)->RHT_VERBA == aCodFol[965,1]
							aFerProp[3,_PIS] := (cAliasSRT)->RHT_VALOR	
						EndIf 					
					Endif
					dbSkip()
				EndDo
			Else
				While !Eof()
					If (cAliasSRT)->RHT_VERBA == aCodFol[966,1]
						a13Salar[3,_Prov] := (cAliasSRT)->RHT_VALOR
						a13Salar[3,_Avos] := 0 
						a13Salar[3,_SalV] := 0
					ElseIf (cAliasSRT)->RHT_VERBA == aCodFol[967,1] 
						a13Salar[3,_Adic] := (cAliasSRT)->RHT_VALOR
					ElseIf (cAliasSRT)->RHT_VERBA == aCodFol[968,1]
						a13Salar[3,_1Par] := (cAliasSRT)->RHT_VALOR
					ElseIf (cAliasSRT)->RHT_VERBA == aCodFol[969,1]
						a13Salar[3,_INSS] := (cAliasSRT)->RHT_VALOR
					ElseIf (cAliasSRT)->RHT_VERBA == aCodFol[970,1]
						a13Salar[3,_FGTS] := (cAliasSRT)->RHT_VALOR
					ElseIf (cAliasSRT)->RHT_VERBA == aCodFol[971,1]
						a13Salar[3,_PIS] := (cAliasSRT)->RHT_VALOR
					EndIf
					dbSkip()
				Enddo
			Endif
			(cAliasSRT)->(dbCloseArea())
			dbSelectArea(cAlias)
			Return
	EndIf
	
	While !Eof()
		// CARREGA O ARRAY COM OS VALORES DO MES ANTERIOR. NAO CARREGAR SE PROVISAO DE 13o SALARIO E MES DE REFERENCIA FOR JANEIRO
		If !lQryAnt .And. Eval(cOrdemBusca) == cBusMesAnt
			//SOMENTE PARA O REGISTRO DE CABECALHO
			If !lTrfSld
				If !((cAliasSRT)->RT_TIPPROV == str(_13Salar,1) .And. Month(dDataRef) == 1) .And. !Empty((cAliasSRT)->RT_DATABAS)
					If lFerias
						aFerVenc[_Anter,_Dias] := (cAliasSRT)->RT_DFERVEN
						aFerProp[_Anter,_Dias] := (cAliasSRT)->RT_DFERPRO
					Endif
					If l13oSal
						a13Salar[_Anter,_Avos] := (cAliasSRT)->RT_AVOS13S
					Endif
				Endif
			EndIf
		    nPosVerba := Ascan(aVerba, { |X| X[4] == (cAliasSRT)->RT_VERBA })
			lAdiant13 := (cAliasSRT)->RT_VERBA == aCodFol[268,1]   //1.Parcela 13.Sal
			// BUSCA O ATUAL DO MES ANTERIOR E GRAVA NO ANTERIOR DO MES
		    If nPosVerba > 0 .and. aVerba[nPosVerba,2] == _Atual
	   		    nPosCol := aVerba[nPosVerba,3]
				If lFerias
					If (cAliasSRT)->RT_TIPPROV == Str(_FerVenc,1) .Or. (cAliasSRT)->RT_TIPPROV == Str(_RecVenc,1) 
						aFerVenc[_Anter,nPosCol] := (cAliasSRT)->RT_VALOR
					ElseIf (cAliasSRT)->RT_TIPPROV == Str(_FerProp,1) .Or. (cAliasSRT)->RT_TIPPROV == Str(_RecProp,1)
						aFerProp[_Anter,nPosCol] := (cAliasSRT)->RT_VALOR
					Endif
				Endif
				If l13oSal
					If (cAliasSRT)->RT_TIPPROV == Str(_13Salar,1) .and. !(Month(dDataRef) == 1)
						a13Salar[_Anter,nPosCol] := (cAliasSRT)->RT_VALOR   
						//Se for demitido e tiver verba de 1Parcela, gerar movimento de baixa 
						// de rescisao da 1.parcela
						If lAdiant13 .and. nTpMov == _Demitido
							a13Salar[_BxRes,nPosCol] := (cAliasSRT)->RT_VALOR   						             
						EndIf
					ElseIf (cAliasSRT)->RT_TIPPROV == Str(_14Salar,1) .and. !(Month(dDataRef) == 1)
						a14Salar[_Anter,nPosCol] := (cAliasSRT)->RT_VALOR
						//Se for demitido e tiver verba de 1Parcela, gerar movimento de baixa 
						// de rescisao da 1.parcela
						If lAdiant13 .and. nTpMov == _Demitido
							a14Salar[_BxRes,nPosCol] := (cAliasSRT)->RT_VALOR   						             
						EndIf
					EndIf
				Endif
			EndIf
		EndIf
		//CARREGA O ARRAY COM OS VALORES DO MES ATUAL
		If Eval(cOrdemBusca) == cBusMesAtu
			//SOMENTE PARA O REGISTRO DE CABECALHO
			If !lTrfSld
				If !Empty((cAliasSRT)->RT_DATABAS)
					nTpMov := (cAliasSRT)->RT_TIPMOVI
					If lFerias .And. ( ((cAliasSRT)->RT_TIPPROV == Str(_FerProp,1) .or. ((cAliasSRT)->RT_TIPPROV == Str(_FerVenc,1))) .Or. ((cAliasSRT)->RT_TIPPROV == Str(_RecProp,1) .or. ((cAliasSRT)->RT_TIPPROV == Str(_RecVenc,1))) )
						aFerVenc[_NoMes,_Dias] := (cAliasSRT)->RT_DFALVEN
						aFerVenc[_Atual,_Dias] := (cAliasSRT)->RT_DFERVEN-If((cAliasSRT)->RT_DFERVEN > 0, (cAliasSRT)->RT_DFERANT,0)-(cAliasSRT)->RT_DFALVEN
						aFerProp[_NoMes,_Dias] := (cAliasSRT)->RT_DFALPRO
						aFerProp[_Atual,_Dias] := (cAliasSRT)->RT_DFERPRO-(cAliasSRT)->RT_DFALPRO
					Endif
					If l13oSal
						a13Salar[_NoMes,_Avos] := (cAliasSRT)->RT_AVOS13S	
					Endif
				Endif
			EndIf
			
	    	nPosVerba := Ascan(aVerba, { |X| X[4] == (cAliasSRT)->RT_VERBA })
	    	lAdiant13 := (cAliasSRT)->RT_VERBA == aCodFol[268,1]  //1.Parcela 13.Sal
	    	
	    	If nPosVerba > 0
				nPosLin := aVerba[nPosVerba,2]
				nPosCol := aVerba[nPosVerba,3]
				If lFerias
					If (cAliasSRT)->RT_TIPPROV == Str(_FerVenc,1) .Or. (cAliasSRT)->RT_TIPPROV == Str(_RecVenc,1)
						If lCalcula
							aFerVenc[nPosLin,nPosCol] := (cAliasSRT)->RT_VALOR
						Else
							aRecVenc[nPosLin,nPosCol]   := (cAliasSRT)->RECNO_
						Endif
					ElseIf (cAliasSRT)->RT_TIPPROV == Str(_FerProp,1) .Or. (cAliasSRT)->RT_TIPPROV == Str(_RecProp,1)
						If lCalcula 
							aFerProp[nPosLin,nPosCol] := (cAliasSRT)->RT_VALOR
						Else
							aRecProp[nPosLin,nPosCol]   := (cAliasSRT)->RECNO_
						Endif
					Endif
				Endif
				If l13oSal
	 				If (cAliasSRT)->RT_TIPPROV == Str(_13Salar,1)
						If lCalcula
							a13Salar[nPosLin,nPosCol] := (cAliasSRT)->RT_VALOR   
							//SE FOR DEMITIDO E TIVER VERBA DE 1PARCELA, GERAR MOVIMENTO DE BAIXA DE RESCISAO DA 1.PARCELA
							If lAdiant13 .and. nTpMov == _Demitido
								a13Salar[_BxRes,nPosCol] := (cAliasSRT)->RT_VALOR   						             
							EndIf
						Else
							aRec13Sl[nPosLin,nPosCol]   := (cAliasSRT)->RECNO_
						Endif
					ElseIf (cAliasSRT)->RT_TIPPROV == Str(_14Salar,1)
						If lCalcula 
							a14Salar[nPosLin,nPosCol] := (cAliasSRT)->RT_VALOR
							//Se for demitido e tiver verba de 1Parcela, gerar movimento de baixa 
							// de rescisao da 1.parcela
							If lAdiant13 .and. nTpMov == _Demitido
								a14Salar[_BxRes,nPosCol] := (cAliasSRT)->RT_VALOR   						             
							EndIf
						Else
							aRec14Sl[nPosLin,nPosCol]   := (cAliasSRT)->RECNO_
						Endif
					EndIf
				Endif
			EndIf
		EndIf
		dbSkip()
	EndDo
	
	If lQryAnt
		dbSelectArea(cAliasAnt)
		While !Eof()
			// CARREGA O ARRAY COM OS VALORES DO MES ANTERIOR. NAO CARREGAR SE PROVISAO DE 13o SALARIO E MES DE REFERENCIA FOR JANEIRO
			If Eval(cOrdemAnt) == cBusMesAnt
				//SOMENTE PARA O REGISTRO DE CABECALHO
				If !lTrfSld
					If !((cAliasAnt)->RT_TIPPROV == str(_13Salar,1) .And. Month(dDataRef) == 1) .And. !Empty((cAliasAnt)->RT_DATABAS)
						If lFerias
							aFerVenc[_Anter,_Dias] := (cAliasAnt)->RT_DFERVEN
							aFerProp[_Anter,_Dias] := (cAliasAnt)->RT_DFERPRO
						Endif
						If l13oSal
							a13Salar[_Anter,_Avos] := (cAliasAnt)->RT_AVOS13S
						Endif
					Endif
				EndIf
				nPosVerba := Ascan(aVerba, { |X| X[4] == (cAliasAnt)->RT_VERBA })
				lAdiant13 := (cAliasAnt)->RT_VERBA == aCodFol[268,1]   //1.Parcela 13.Sal
				// BUSCA O ATUAL DO MES ANTERIOR E GRAVA NO ANTERIOR DO MES
			    If nPosVerba > 0 .And. aVerba[nPosVerba,2] == _Atual
		   		    nPosCol := aVerba[nPosVerba,3]
					If lFerias
						If (cAliasAnt)->RT_TIPPROV == Str(_FerVenc,1) .Or. (cAliasAnt)->RT_TIPPROV == Str(_RecVenc,1)
							aFerVenc[_Anter,nPosCol] := (cAliasAnt)->RT_VALOR
						ElseIf (cAliasAnt)->RT_TIPPROV == Str(_FerProp,1) .Or. (cAliasAnt)->RT_TIPPROV == Str(_RecProp,1)
							aFerProp[_Anter,nPosCol] := (cAliasAnt)->RT_VALOR
						Endif
					Endif
					If l13oSal
						If (cAliasAnt)->RT_TIPPROV == Str(_13Salar,1) .and. !(Month(dDataRef) == 1)
							a13Salar[_Anter,nPosCol] := (cAliasAnt)->RT_VALOR   
							//SE FOR DEMITIDO E TIVER VERBA DE 1PARCELA, GERAR MOVIMENTO DE BAIXA DE RESCISAO DA 1.PARCELA
							If lAdiant13 .and. nTpMov == _Demitido
								a13Salar[_BxRes,nPosCol] := (cAliasAnt)->RT_VALOR   						             
							EndIf
						ElseIf (cAliasAnt)->RT_TIPPROV == Str(_14Salar,1) .and. !(Month(dDataRef) == 1)
							a14Salar[_Anter,nPosCol] := (cAliasAnt)->RT_VALOR
							//Se for demitido e tiver verba de 1Parcela, gerar movimento de baixa 
							// de rescisao da 1.parcela
							If lAdiant13 .and. nTpMov == _Demitido
								a14Salar[_BxRes,nPosCol] := (cAliasAnt)->RT_VALOR   						             
							EndIf
						EndIf
					Endif
				EndIf
			EndIf
			dbSkip()
		EndDo
	EndIf
EndIf //Fim do IF lSoMes

// CALCULA OS VALORES DE BAIXA E O VALOR NO MES
If lCalcula
	//SALVA A INFORMACAO ANTES DE PROCESSAR O CALCULO DA PROVISAO NO MES
	If lFerias
		axFerVenc	:= aClone(aFerVenc)
		axFerProp	:= aClone(aFerProp)
	Endif
	If l13oSal
		ax13Salar	:= aClone(a13Salar)
		ax14Salar	:= aClone(a14Salar)
	Endif
	For nTipoProv := 1 to 4
		If (l13oSal .And. (nTipoProv == 3 .Or. nTipoProv == 4)) .Or.;
		   (lFerias .And. ( (nTipoProv == 1 .Or. nTipoProv == 2) .Or. (nTipoProv == 7 .Or. nTipoProv == 8) ) )
			//-- Verifica o tipo de provisao a ser processado
			If nTipoProv == 1 .Or. nTipoProv == 7
				aProvisao	:= aFerVenc
			ElseIf nTipoProv == 2 .Or. nTipoProv == 8
				aProvisao	:= aFerProp
			ElseIf nTipoProv == 3
				aProvisao	:= a13Salar
			ElseIf nTipoProv == 4
				aProvisao	:= a14Salar
			Endif
			// GRAVAR VALORES COLUNA DE RESCISAO QUANDO NAO EXISTIR IDENTIF. DE RESCISAO POIS,NESTE CASO, A BAIXA ESTARA NA COLUNA FERIAS
			If nTpMov == _Demitido
				fChkDemit(aProvisao,aVerba,nTipoProv,_BxRes,_BxFer)
			EndIf
			//TOTAL DE BAIXAS
			aProvisao[_BxTot,_Prov] := aProvisao[_BxTrf,_Prov]+aProvisao[_BxFer,_Prov]+aProvisao[_BxRes,_Prov]
			aProvisao[_BxTot,_Adic] := aProvisao[_BxTrf,_Adic]+aProvisao[_BxFer,_Adic]+aProvisao[_BxRes,_Adic]
			aProvisao[_BxTot,_1Ter] := aProvisao[_BxTrf,_1Ter]+aProvisao[_BxFer,_1Ter]+aProvisao[_BxRes,_1Ter]
			aProvisao[_BxTot,_INSS] := aProvisao[_BxTrf,_INSS]+aProvisao[_BxFer,_INSS]+aProvisao[_BxRes,_INSS]
			aProvisao[_BxTot,_FGTS] := aProvisao[_BxTrf,_FGTS]+aProvisao[_BxFer,_FGTS]+aProvisao[_BxRes,_FGTS]
			aProvisao[_BxTot,_PIS]  := aProvisao[_BxTrf,_PIS] +aProvisao[_BxFer,_PIS] +aProvisao[_BxRes,_PIS]
			//PROVISAO DO MES
			aProvisao[_NoMes,_Prov] := aProvisao[_Atual,_Prov]-aProvisao[_Anter,_Prov]+If(nTpMov == _Demitido .And. lProvResc, aProvisao[_Bx13O,_Prov], aProvisao[_BxTot,_Prov])
			aProvisao[_NoMes,_Adic] := aProvisao[_Atual,_Adic]-aProvisao[_Anter,_Adic]+If(nTpMov == _Demitido .And. lProvResc, aProvisao[_Bx13O,_Adic], aProvisao[_BxTot,_Adic])
			aProvisao[_NoMes,_1Ter] := aProvisao[_Atual,_1Ter]-aProvisao[_Anter,_1Ter]+If(nTpMov == _Demitido .And. lProvResc, aProvisao[_Bx13O,_1Ter], aProvisao[_BxTot,_1Ter])
			aProvisao[_NoMes,_INSS] := aProvisao[_Atual,_INSS]-aProvisao[_Anter,_INSS]+If(nTpMov == _Demitido .And. lProvResc, aProvisao[_Bx13O,_INSS], aProvisao[_BxTot,_INSS])
			aProvisao[_NoMes,_FGTS] := aProvisao[_Atual,_FGTS]-aProvisao[_Anter,_FGTS]+If(nTpMov == _Demitido .And. lProvResc, aProvisao[_Bx13O,_FGTS], aProvisao[_BxTot,_FGTS])
			aProvisao[_NoMes,_PIS]  := aProvisao[_Atual,_PIS] -aProvisao[_Anter,_PIS] +If(nTpMov == _Demitido .And. lProvResc, aProvisao[_Bx13O,_PIS],  aProvisao[_BxTot,_PIS] )
			If lProvResc .And. nTpMov == _Demitido
				// Zera a linha de provisao atual quando o funcionario estiver demitido e o parametro MV_PROVRES estiver configurado com "S"
				For nCnt1 := 1 To _Colunas
					aProvisao[_Atual,nCnt1]	:= 0
				Next nCnt1
			Endif
			If lTrfAMES
				//TRANSFERE O SALDO ANTERIOR PARA A COLUNA TRANSF. ENTRADA
				If TPR->PR_TIPMOVI == _Trfe_Ent .Or. ((TPR->PR_TIPMOVI == _Anter) .And. (aProvisao[_BxRes,_Prov]+aProvisao[_BxRes,_Adic]+aProvisao[_Bx13O,_Prov]+aProvisao[_Bx13O,_Adic] <> 0) .And. (nPosTrf > 0))
					For nCnt1 := 1 To _Colunas
						If !lTrfEmpAmes 
							aProvisao[_TrfEnt,nCnt1] := aProvisao[_Anter,nCnt1]
							aProvisao[_Anter,nCnt1]  := 0
						Else
							aProvisao[_NoMes,nCnt1] += aProvisao[_Anter,nCnt1]
							aProvisao[_Anter,nCnt1]  := 0	
						EndIf
					Next nCnt1    
					
					If aProvisao[_Bx13O,_Prov]+aProvisao[_Bx13O,_Adic] > 0
						aProvisao[_BxRes,_1Par] := 0
					EndIf	
				ElseIf  (nPosTrf > 0 .and. (nTipoProv==2 .Or. nTipoProv==8) .and. TPR->PR_TIPMOVI == _Anter)
		   			For nCnt1 := 1 To _Colunas
						aProvisao[_TrfEnt,nCnt1] := aProvisao[_Anter,nCnt1]
						aProvisao[_Anter,nCnt1]  := 0
					Next nCnt1  
				//TRANSFERE O SALDO DE BAIXA POR TRANSF. P/COLUNA TRANSF. SAIDA
				ElseIf TPR->PR_TIPMOVI == _Trfe_Sai
					For nCnt1 := 1 To _Colunas
						aProvisao[_TrfSai,nCnt1] := aProvisao[_BxTrf,nCnt1]
						aProvisao[_BxTrf,nCnt1]  := 0
					Next nCnt1
					//AJUSTA O VALOR DA BAIXA DA PRIMEIRA PARCELA NA LINHA DE TRANSFERENCIA DE SAIDA
					//DEVOLVE O VALOR PARA A LINHA NO MES
					//TRATAMENTO EXCLUSIVO PARA 13o SALARIO - 1A PARCELA
					If !lTrfEmpAmes .And. nTipoProv == _13Salar // NAO E TRANSFERENCIA ENTRE EMPRESAS
						aProvisao[_TrfSai,_1Par]  := aProvisao[_Anter,_1Par]
						aProvisao[_NoMes,_1Par]   += aProvisao[_Anter,_1Par]
					Endif
				EndIf
			EndIf
		  	If lTrfSld .And. fChkIdent(aVerba,If(nTipoProv==2,1,If(nTipoProv==8, 7, nTipoProv)),{_BxRes},.F.) .and. fChkIdent(aVerba,If(nTipoProv==2,1,If(nTipoProv==8, 7, nTipoProv)),{_BxTrf},.F.)
				For nCnt1 := 1 To _Colunas
					aProvisao[_NoMes,nCnt1] := aProvisao[_Anter,nCnt1]
					aProvisao[_Anter,nCnt1]  := 0
				Next nCnt1
			EndIf
			//SALVA O ARRAY PROCESSADO
			If nTipoProv == 1 
				axFerVenc	:= aClone(aProvisao)
			ElseIf nTipoProv == 2
				axFerProp	:= aClone(aProvisao)
			ElseIf nTipoProv == 3
				ax13Salar	:= aClone(aProvisao)
			ElseIf nTipoProv == 4
				ax14Salar	:= aClone(aProvisao)
			Endif
		Endif

	Next nTipoProv
	//DEVOLVE O ARRAY PARA O PROCESSO NORMAL
	If lFerias
		aFerVenc	:= aClone(axFerVenc)
		aFerProp	:= aClone(axFerProp)
	Endif
	If l13oSal
		a13Salar	:= aClone(ax13Salar)
		a14Salar	:= aClone(ax14Salar)
	Endif
Endif 

If !lSoMes
	(cAliasSRT)->(dbCloseArea())
	If lQryAnt
		(cAliasAnt)->(dbCloseArea())
	EndIf
	dbSelectArea(cAlias)
Endif

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ fGeraSRT ³ Autor ³ Emerson R. de Souza	³ Data ³ 26.06.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Grava os valores da provisao no arquivo SRT                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fGeraSRT(aProvisao,aVerba,dDataRef,nTipoMovMes,nSalMes...) ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aProvisao   - Array que contera os valores da provisao	  ³±±
±±³          ³ aVerba      - Verbas de Ferias / 13oSal / 14oSal           ³±±
±±³          ³ aRecnos     - Indica os recnos que serao avaliados         ³±±
±±³          ³ dDataRef    - Data de referencia p/ gravacao dos valores   ³±±
±±³          ³ nTipoMovMes - Tipo de movimentacao no mes                  |±±
±±³          ³ nSalMes     - Valor do salario no mes                      ³±±
±±³          ³ dDtBasFer   - Data base de ferias                          ³±±
±±³          ³ nDiaFeAnt   - Dias de ferias antecipadas                   ³±±
±±³          ³ nTipoProv   - Tipo de provisao a buscar (Venc,Prop,13o)    |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fGeraSRT(aProvisao,aRecnos,aVerba,dDataRef,nTipoMovMes,nSalMes,dDtBasFer,nDiaFeAnt,nTipoProv)
Local cAlias    := Alias(), nTipo2, nTipo3, nCnt
Local nProvBus  := If(nTipoProv == _FerProp, _FerVenc, If(nTipoProv == _RecProp, _RecVenc, nTipoProv)) 
Local cChave	:= ""
Local cFilAux   := cFilAnt
Local lCabProp  := .F.
Local lGp070Grv := ExistBlock("GP070GRV")
Local lTemCab   := .F.

// POSICIONA CFILANT NA FILIAL CORRENTE P/ GARANTIR INTEGRIDADE
cFilAnt := TPR->PR_FILIAL

// SE NAO EXISTIR IDENTIFICADORES DE BAIXA DE RESCISAO,SOMAR NA COLUNA DE BAIXA DE FERIAS E ZERAR COLUNA DE BAIXA DE RESCISAO
fChkDemit(aProvisao,aVerba,nTipoProv,_BxFer,_BxRes)

// PONTO DE ENTRADA PARA CHECAR REGISTROS PERDIDOS NO SRT
If ExistBlock("GP070CHK")
	ExecBlock("GP070CHK",.F.,.F.)
Endif

// GRAVA O ARRAY PROVISAO NO ARQUIVO SRT
dbSelectArea("SRT")
If lItemClVl
	dbSetOrder(4)
Else
	dbSetOrder(1)
EndIf

For nTipo2 := 1 To _Linhas
	// LINHAS QUE NAO DEVERAO SER GRAVADAS NO ARQUIVO
	If nTipo2 == _Anter .Or. nTipo2 == _NoMes .Or. nTipo2 == _BxTot
		Loop
	EndIf
	// GRAVA AS COLUNAS DE VALORES NO ARQUIVO DETALHE
	For nTipo3 := 1 To _Colunas
		// BUSCA A VERBA E GRAVA OS LANCAMENTOS NO ARQUIVO DETALHE
        nPosVerba := Ascan(aVerba, { |X| X[1] == nProvBus .And. X[2] == nTipo2 .And. X[3] == nTipo3 })
        cCodVerba := If(nPosVerba > 0, aVerba[nPosVerba,4], Space(3))
        If !Empty(cCodVerba)
	        If ValType(aRecnos[nTipo2,nTipo3]) <> "U" .And. aRecnos[nTipo2,nTipo3] > 0
    	    	dbGoTo(aRecnos[nTipo2,nTipo3])
        		RecLock("SRT", .F.)
		    	If aProvisao[nTipo2,nTipo3] == 0
					dbDelete()
					MsUnlock()
					Loop
				EndIf
			Else
				If aProvisao[nTipo2,nTipo3] # 0
					RecLock("SRT", .T.)
				Else
					Loop
				EndIf        
        	EndIf
			SRT->RT_FILIAL  := TPR->PR_FILIAL
			SRT->RT_MAT     := TPR->PR_MAT
			SRT->RT_CC      := TPR->PR_CC
			If lItemClVl
				SRT->RT_ITEM    := TPR->PR_ITEM
				SRT->RT_CLVL    := TPR->PR_CLVL
	        EndIf
			SRT->RT_TIPPROV := Str(nTipoProv,1)
			SRT->RT_VERBA   := cCodVerba
			SRT->RT_VALOR   := aProvisao[nTipo2,nTipo3]
			SRT->RT_DATACAL := dDataRef
		    
			// PONTO DE ENTRADA PARA GRAVAR CAMPOS PERSONALIZADOS DO SRT
			If lGp070Grv
				ExecBlock("GP070GRV", .F., .F., {TPR->PR_FILIAL, TPR->PR_MAT, nTipoProv})
			Endif

			MsUnlock()
			//Gravação das baixas no arquivo de provisão mensal - RHT
			If lGeraPmes .And. (nTipo2 == _BxTrf .Or. nTipo2 == _BxFer .Or. nTipo2 == _Bx13O .Or. nTipo2 == _BxRes)
				fGeraBaixaMes()
			Endif

		    //INTEGRACAO COM MODULO SIGAPCO
			PcoDetLan("000091","01","GPEA070")

		EndIf
	Next nTipo3
Next nTipo2
If lItemClVl
	cChave := TPR->PR_FILIAL + TPR->PR_MAT + TPR->PR_CC + TPR->PR_ITEM +  TPR->PR_CLVL + MesAno(dDataRef)
Else
	cChave := TPR->PR_FILIAL + TPR->PR_MAT + TPR->PR_CC + MesAno(dDataRef)
EndIf

// GRAVA AS INFORMACOES DE CABECALHO
If dbSeek( cChave )
	While SRT->(!EOF()) .AND. (SRT->RT_FILIAL + SRT->RT_MAT + SRT->RT_CC + MesAno(SRT->RT_DATACAL) ==   cChave)
		If !Empty(SRT->RT_DATABAS) 
			lTemCab := .T.
			Exit
		EndIf
		DbSkip()
	EndDo
	If !lTemCab
		dbSeek( cChave )
	EndIf

	RecLock("SRT", .F.)
	SRT->RT_DATABAS := dDtBasFer

	IF Empty(SRT->RT_DFERANT)
		SRT->RT_DFERANT := nDiaFeAnt
	EndIf
	IF Empty(SRT->RT_DFALVEN)
		SRT->RT_DFALVEN := nV_DFalFer
	EndIf
	IF Empty(SRT->RT_DFALPRO)
		SRT->RT_DFALPRO := nP_DFalFer
	EndIf
	IF Empty(SRT->RT_TIPMOVI)
		SRT->RT_TIPMOVI := nTipoMovMes
	EndIf
	SRT->RT_SALARIO := nSalMes
	If Empty(SRT->RT_DFERVEN) .and. (nTipoProv == _FerVenc .Or. nTipoProv == _RecVenc) 
		SRT->RT_DFERVEN := aProvisao[_Atual,_Dias]
	ElseIf Empty(SRT->RT_DFERPRO) .and. (nTipoProv == _FerProp .Or. nTipoProv == _RecProp)
		SRT->RT_DFERPRO := aProvisao[_Atual,_Dias]
	ElseIf Empty(SRT->RT_AVOS13S) .AND. nTipoProv == _13Salar
		SRT->RT_AVOS13S := aProvisao[_Atual,_Avos]
	EndIf
	MsUnLock()
	
ElseIf ( lCabProp := ( ( nTipoProv == _FerProp .Or. nTipoProv == _RecProp .OR. nTipoProv == _13Salar ) .And. aProvisao[_Anter,_Dias] > 0 ) )
	RecLock("SRT", .T.)
	SRT->RT_FILIAL  := TPR->PR_FILIAL
	SRT->RT_MAT     := TPR->PR_MAT
	SRT->RT_CC      := TPR->PR_CC
	If lItemClVl
		SRT->RT_ITEM    := TPR->PR_ITEM
		SRT->RT_CLVL    := TPR->PR_CLVL
        EndIf
	SRT->RT_TIPPROV := Str(nTipoProv,1)
	SRT->RT_VERBA   := ""
	SRT->RT_VALOR   := 0.00
	SRT->RT_DATACAL := dDataRef
		
	SRT->RT_DATABAS := dDtBasFer
	SRT->RT_DFERANT := nDiaFeAnt
	SRT->RT_DFALVEN := nV_DFalFer
	SRT->RT_DFALPRO := nP_DFalFer
	SRT->RT_TIPMOVI := nTipoMovMes
	SRT->RT_SALARIO := nSalMes
	If nTipoProv == _FerVenc .Or. nTipoProv == _RecVenc 
		SRT->RT_DFERVEN := aProvisao[_Atual,_Dias]
	ElseIf nTipoProv == _FerProp .Or. nTipoProv == _RecProp
		SRT->RT_DFERPRO := aProvisao[_Atual,_Dias]
	ElseIf nTipoProv == _13Salar
		SRT->RT_AVOS13S := aProvisao[_Atual,_Avos]
	EndIf
	MsUnLock()
EndIf
dbSelectArea(cAlias)

// RETORNA FILIAL ORIGINAL PARA O cFilAnt                       
cFilAnt := cFilAux

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao	 ³ fGeraMes ³ Autor ³ Kelly Soares       	³ Data ³ 15.06.12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Grava os valores mensais da provisao no arquivo SRT       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fGeraMes(aProvisao,aVerba,dDataRef,nTipoMovMes,nSalMes...) ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aProvisao	 - Array que contera os valores da provisao	  ³±±
±±³          ³ aVerba   	 - Verbas de Ferias / 13oSal / 14oSal         ³±±
±±³          ³ aRecnos   	 - Indica os recnos que serao avaliados       ³±±
±±³          ³ dDataRef   	 - Data de referencia p/ gravacao dos valores ³±±
±±³          ³ nTipoMovMes 	 - Tipo de movimentacao no mes                |±±
±±³          ³ nSalMes  	 - Valor do salario no mes                    ³±±
±±³          ³ dDtBasFer  	 - Data base de ferias                        ³±±
±±³          ³ nDiaFeAnt  	 - Dias de ferias antecipadas                 ³±±
±±³          ³ nTipoProv	 - Tipo de provisao a buscar (Venc,Prop,13o)  |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso	 	 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fGeraMes()
Local nX		:= 0
Local nC		:= 0
Local nPos		:= 0
Local nQtd		:= 0
Local nPerc		:= 0
Local nValDiv 	:= 0
Local nVlComp	:= 0
Local nValor  	:= 0
Local aArea	:= GetArea()
Private aRateio	:= {}

DbSelectArea("RHT")
DbSetOrder(1)

// CARREGA O RATEIO POR CENTRO DE CUSTO DA TABELA DE RATEIOS - RHQ
aRateio	:= {}

// CARREGA OS REGISTROS DA TABELA RHQ-PROGRAMACAO DE RATEIO PARA PEGAR OS PERCENTUAIS X CENTRO DE CUSTO
fRateio({},2, cAno+cMes)

// VERIFICA O TAMANHO DO RETORNO DO ARRAY PARA CRIAR AO MENOS UM REGISTRO PARA A GERACAO
nQtd := Len(aRateio)

If nQtd == 0
	If lItemClVl
	   	aAdd(aRateio,{TPR->PR_CC, TPR->PR_ITEM, TPR->PR_CLVL, 100, "S", 0, ""})
	Else
	   	aAdd(aRateio,{TPR->PR_CC, Space(GetSx3Cache("RHQ_ITEM","X3_TAMANHO")), Space(GetSx3Cache("RHQ_CLVL","X3_TAMANHO")), 100, "S", 0, ""})
	Endif
	nQtd := 1
Else
	// VERIFICA SE EXISTEM ORIGENS SISTEMAS (CAMPO RHQ_ORIGEM = 'S') E TAMBEM GERADAS PELO
	// USUARIO PARA CONSIDERAR APENAS OS REGISTROS DO TIPO USUARIO (CAMPOS RHQ_ORIGEM = 'U'),
	// CASO EXISTIR APENAS UM TIPO, IRA CONSIDERAR O QUE EXISTIR
    If aScan(aRateio, { |X| X[5] == "S" })	> 0 .And. aScan(aRateio, { |X| X[5] == "U" })	> 0
	    For nC := 1 to Len(aRateio)
			If  (nPosRateio := aScan(aRateio, { |X| X[5] == "S" })) > 0
				aDel(aRateio , nPosRateio)
				aSize(aRateio,Len(aRateio)-1)
			Endif
	    Next
		nQtd := Len(aRateio)
	Endif
Endif

For nX := 2 to _Colunas
	// FERIAS VENCIDAS
	nPos := aScan(aVerba,{|X| X[1] = _FerVMes .and. X[2] = _Atual .and. X[3] = nX})	
	If nPos > 0
		cCodVerba := aVerba[nPos,4]
		If !Empty(cCodVerba)
			//Rateio
			nValDiv := 0
			nVlComp := 0
			nValor  := aFerVenc[_NoMes,nX]
			If !nValor == 0
				For nC := 1 to nQtd
					If nC # nQtd
						nValDiv := Round(nValor * aRateio[nC,4],2)
						nVlComp += nValDiv
					Else
						nValDiv := nValor-nVlComp
					Endif 
					
					nPerc := aRateio[nC,4]
					If nPerc < 1
						nPerc := nPerc * 100
					Endif
					// Ordem 4 - RHT_FILIAL+RHT_MAT+RHT_CC+RHT_ITEM+RHT_CLVL+DTOS(RHT_DTCALC)+RHT_TPPROV+RHT_VERBA
					RHT->(dbSetOrder(RetOrdem("RHT","RHT_FILIAL+RHT_MAT+RHT_CC+RHT_ITEM+RHT_CLVL+DTOS(RHT_DTCALC)+RHT_TPPROV+RHT_VERBA")))	
					If !RHT->(dbSeek(TPR->PR_FILIAL+TPR->PR_MAT+aRateio[nC,1]+aRateio[nC,2]+aRateio[nC,3]+DTOS(dDataRef)+"1"+cCodVerba))
						RecLock("RHT", .T.)
						RHT->RHT_FILIAL  := TPR->PR_FILIAL
						RHT->RHT_MAT     := TPR->PR_MAT
						RHT->RHT_CC      := aRateio[nC,1]
						If lItemClVl
							RHT->RHT_ITEM := aRateio[nC,2]
							RHT->RHT_CLVL := aRateio[nC,3]
						EndIf
						RHT->RHT_TPPROV := "1"
						RHT->RHT_VERBA   := cCodVerba
					Else
						RecLock("RHT", .F.)
					Endif
					RHT->RHT_SALAR  := nSalMes
					RHT->RHT_DTCALC := dDataRef
					RHT->RHT_VALOR  := nValDiv
					RHT->RHT_PERC	:= nPerc  
					If cCodVerba == aCodFol[960,1]
						RHT->RHT_DTBASE := dDtBasFer
					Endif
					MsUnlock()
				Next nC   
			Endif
		Endif		
	Endif
	//FERIAS PROPORCIONAIS
	nPos := aScan(aVerba,{|X| X[1] = _FerVMes .and. X[2] = _Atual .and. X[3] = nX})	
	If nPos > 0
		cCodVerba := aVerba[nPos,4]
		If !Empty(cCodVerba)
			//RATEIO
			nValDiv := 0
			nVlComp := 0
			nValor  := aFerProp[_NoMes,nX]
			If !nValor == 0
				For nC := 1 to nQtd
					If nC # nQtd
						nValDiv := Round(nValor * aRateio[nC,4],2)
						nVlComp += nValDiv
					Else
						nValDiv := nValor-nVlComp
					Endif
					  
					nPerc := aRateio[nC,4]
					If nPerc < 1
						nPerc := nPerc * 100
					Endif					
					// Ordem 4 - RHT_FILIAL+RHT_MAT+RHT_CC+RHT_ITEM+RHT_CLVL+DTOS(RHT_DTCALC)+RHT_TPPROV+RHT_VERBA
					RHT->(dbSetOrder(RetOrdem("RHT","RHT_FILIAL+RHT_MAT+RHT_CC+RHT_ITEM+RHT_CLVL+DTOS(RHT_DTCALC)+RHT_TPPROV+RHT_VERBA")))	
					If !RHT->(dbSeek(TPR->PR_FILIAL+TPR->PR_MAT+aRateio[nC,1]+aRateio[nC,2]+aRateio[nC,3]+DTOS(dDataRef)+"2"+cCodVerba))
						RecLock("RHT", .T.)
						RHT->RHT_FILIAL  := TPR->PR_FILIAL
						RHT->RHT_MAT     := TPR->PR_MAT
						RHT->RHT_CC      := aRateio[nC,1]
						If lItemClVl
							RHT->RHT_ITEM := aRateio[nC,2]
							RHT->RHT_CLVL := aRateio[nC,3]
						EndIf
						RHT->RHT_TPPROV := "2"
						RHT->RHT_VERBA   := cCodVerba
					Else
						RecLock("RHT", .F.)
					Endif
					RHT->RHT_SALAR  := nSalMes
					RHT->RHT_DTCALC := dDataRef
					RHT->RHT_VALOR  := nValDiv
					RHT->RHT_PERC	:= nPerc
					If cCodVerba == aCodFol[960,1]
						RHT->RHT_DTBASE := dDtBasFer
					Endif					
					MsUnlock()
				Next nC   
			Endif
		Endif		
	Endif
	// 13 SALARIO
	nPos := aScan(aVerba,{|X| X[1] = _13SVMes .and. X[2] = _Atual .and. X[3] = nX})	
	If nPos > 0
		cCodVerba := aVerba[nPos,4]
		If !Empty(cCodVerba)
			//RATEIO
			nValDiv := 0
			nVlComp := 0
			nValor  := a13Salar[_NoMes,nX] 
			If !nValor == 0
				For nC := 1 to nQtd
					If nC # nQtd
						nValDiv := Round(nValor * aRateio[nC,4],2)
						nVlComp += nValDiv
					Else
						nValDiv := nValor-nVlComp
					Endif  
					nPerc := aRateio[nC,4]
					If nPerc < 1
						nPerc := nPerc * 100
					Endif
					// Ordem 4 - RHT_FILIAL+RHT_MAT+RHT_CC+RHT_ITEM+RHT_CLVL+DTOS(RHT_DTCALC)+RHT_TPPROV+RHT_VERBA
					RHT->(dbSetOrder(RetOrdem("RHT","RHT_FILIAL+RHT_MAT+RHT_CC+RHT_ITEM+RHT_CLVL+DTOS(RHT_DTCALC)+RHT_TPPROV+RHT_VERBA")))	
					If !RHT->(dbSeek(TPR->PR_FILIAL+TPR->PR_MAT+aRateio[nC,1]+aRateio[nC,2]+aRateio[nC,3]+DTOS(dDataRef)+"3"+cCodVerba))
						RecLock("RHT", .T.)
						RHT->RHT_FILIAL  := TPR->PR_FILIAL
						RHT->RHT_MAT     := TPR->PR_MAT
						RHT->RHT_CC      := aRateio[nC,1]
						If lItemClVl
							RHT->RHT_ITEM := aRateio[nC,2]
							RHT->RHT_CLVL := aRateio[nC,3]
						EndIf
						RHT->RHT_TPPROV := "3"
						RHT->RHT_VERBA   := cCodVerba
					Else
						RecLock("RHT", .F.)
					Endif
					RHT->RHT_SALAR  := nSalMes
					RHT->RHT_DTCALC := dDataRef
					RHT->RHT_VALOR  := nValDiv
					RHT->RHT_PERC	:= nPerc
					MsUnlock()
				Next nC      
			Endif
		Endif
	Endif
Next nX

RestArea(aArea)

Return nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ fChkIdent   ³ Autor ³ Emerson R. de Souza³ Data ³ 15.07.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Verifica a existencia de identificadores                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fChkIdent(aVerba,nTipProv,aTipId,lGeraLog)                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³  aVerba   -Array com os identificadores a ser pesquisado   ³±±
±±³          ³  nTipProv -Tipo de provisao (Ferias,13o,14o)               ³±±
±±³          ³  aTipId   -Identificadores pesquisados(Trf,Res,Correcao)   ³±±
±±³          ³  lGeraLog -Indica se deve incluir o ident. em aIdProvis    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³  Generico                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fChkIdent(aVerba,nTipProv,aTipId,lGeraLog)
Local nCnt1, nCnt2, nPosElem, aElem := { _Prov, _Adic, _1Ter, _INSS, _FGTS }
Local aIdAuxTrf   := {}, lRet := .T.

// BUSCA O TIPO DE PROVISAO, TIPO DE IDENTIFICADOR E COLUNA DA PROVISAO. SE ENCONTROU, VERIFICA SE CADASTROU O IDENTIFICADOR
For nCnt1  := 1 To Len(aTipId)
	nTipId := aTipId[nCnt1]
	For nCnt2 := 1 To Len(aElem)
		nPosElem := Ascan(aVerba, { |X| X[1] == nTipProv .And. X[2] == nTipId .And. X[3] == aElem[nCnt2] })
		// ARRAY CONTENDO AS VERBAS NAO CADASTRADAS OU IDENT. DE TRANSF
		If nPosElem > 0
			If (Type("aIdProvis") == "A" .And. lGeraLog)
				Aadd(aIdProvis, { aVerba[nPosElem,4], aVerba[nPosElem,5], nTipProv, nTipId })
			EndIf
			If Empty(aVerba[nPosElem,4])
				lRet := .F.
			EndIf
		EndIf
	Next nCnt2
Next nCnt1

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fGrvArrPrv| Autor ³ Emerson Rosa de Souza ³ Data ³ 15.07.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Trata as baixas de transferencia e rescisao                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fGrvArrPrv(aProvisao, nElem1, nElem2, nIndice, aZerar)     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aProvisao - Array contendo os valores da provisao          ³±±
±±³          ³ nElem1    - Elemento que recebera o conteudo de gravacao   |±±
±±³          ³ nElem2    - Elemento que sera gravado em nElem1            |±±
±±³          ³ nIndCorr  - Indice de correcao                             |±±
±±³          ³ aZerar    - Elementos que deverao ser zerados              |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fGrvArrPrv(aProvisao, nElem1, nElem2, nIndCorr, aZerar)
Local  nCnt1	:= 0 

aProvisao[nElem1,_Dias] := Round( aProvisao[nElem2,_Dias] * nIndCorr, 2 )
aProvisao[nElem1,_Prov] := Round( aProvisao[nElem2,_Prov] * nIndCorr, 2 )
aProvisao[nElem1,_Adic] := Round( aProvisao[nElem2,_Adic] * nIndCorr, 2 )
aProvisao[nElem1,_1Ter] := Round( aProvisao[nElem2,_1Ter] * nIndCorr, 2 )
aProvisao[nElem1,_INSS] := Round( aProvisao[nElem2,_INSS] * nIndCorr, 2 )
aProvisao[nElem1,_FGTS] := Round( aProvisao[nElem2,_FGTS] * nIndCorr, 2 )
aProvisao[nElem1,_PIS]  := Round( aProvisao[nElem2,_PIS]  * nIndCorr, 2 )

For nCnt1 := 1 To Len(aZerar)
	aProvisao[nElem1,aZerar[nCnt1] ] := 0
Next nCnt1

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao	 ³fMonta_TPR³ Autor ³ Emerson Rosa de Souza	³ Data ³ 02.03.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Monta Arquivo de Trabalho para impressao da provisao       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³fMonta_TPR(cTPRDbf,cTPRNtx,nOrdem,dDataRef,lSalInc,lTrataTrf³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cTPRDbf   - Arquivo DBF temporario                         ³±±
±±³          ³ cTPRNtx   - Arquivo NTX temporario                         ³±±
±±³          ³ nOrdem    - Ordem de Calculo ou Impressao                  ³±±
±±³          ³ dDataRef  - Data de referencia para Calculo ou Impressao   ³±±
±±³          ³ lSalInc   - Indica se existe salario incorporado           ³±±
±±³          ³ lTrataTrf - Indica se deve tratar transferidos             ³±±
±±³          ³ aTransf   - Array que contera os transferidos no mes       ³±±
±±³          ³ lIncDemit - Indica se deve incluir os demitidos            ³±±
±±³          ³ cAcessaArq- Indica se tem dereito de acesso                ³±±
±±³          ³ lIncTodos - Indica se deve incluir todos os funcionarios   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fMonta_TPR(cTPRDbf,cTPRNtx,nOrdem,dDataRef,lSalInc,lTrataTrf,aTransf,lIncDemit,cAcessaArq,lIncTodos,lTodosCpos, cTpc)
Local nTpMv,cInicio,cFim,cIndCond,nK1,nK2,aVerbaProv,nPosFunc,nTrfProc, nPosFunc1
Local cSitFolh,cTipAfas,cCposQuery, cCusto, cCustoAtu
Local cClvl 		:= ""
Local cClvlAtu 		:= ""
Local cItem 		:= ""
Local cItemAtu 		:= ""
Local dAfaIni		:= CtoD("")
Local dAfaFim		:= CtoD("")
Local aCodFol := {},aStruSRA := {}
Local bChkSRA,bChkSRE
Local cAliasSRA		:= "SRA"
Local cOrdem		:= ""
Local nReg			:= 0
Local lTransfEmp	:= .F.
Local cAnoMesAtu,cDtIQryAtu,cDtFQryAtu,cLisVerbas,cCposGroup,cCposnOrd5,nK3,nTamArray,lTranfFil
Local aSRASRT		:= {}
Local nPR_TIPMOVI 	:= 0
Local cVerbaRPF 	:= ""
Local cVerbaRP13	:= ""
Local aVerbasTPR	:= {}
Local nVB			:= 0

Default cTpc		:= ""

lIncDemit  := If(lIncDemit  == Nil,  .T., lIncDemit)
lIncTodos  := If(lIncTodos  == Nil, .F., lIncTodos)
If Type("lItemClVl") == "U"
	lItemClVl := SuperGetMv("MV_ITMCLVL", .F., "2") $ "13"
EndIf
lTodosCpos := If(lTodosCpos == Nil, .F., lTodosCpos)

// VERIFICA SE FOI PASSADO BLOCO, STRING OU NIL
If cAcessaArq == Nil .Or. Empty(cAcessaArq)
	bChkSRA := &("{ || .T.}")
	bChkSRE := &("{ || .T.}")
ElseIf ValType(cAcessaArq) == "B"
	cAcessaArq	:= StrTran(cAcessaArq, "SRA->","(cAliasSRA)->")   
	bChkSRA 	:= cAcessaArq
	bChkSRE 	:= &("{ || .T.}")
ElseIf ValType(cAcessaArq) == "C"
	cAcessaArq	:= StrTran(cAcessaArq, "SRA->","(cAliasSRA)->")   
	bChkSRA 	:= &(cAcessaArq)
	// SE HOUVE C.CUSTO NO FILTRO TROCAR PELO ARRAY ATRANSF
	If AT("RA_CC", cAcessaArq) > 0 
		cAcessaArq := STRTRAN( cAcessaArq, "SRA->RA_CC", "aTransf[nK1,nTrfProc,_TCC]")
	EndIf
	bChkSRE := &(cAcessaArq)
EndIf

// VERIFICA A EXISTENCIA DOS IDENTIFICADORES DE TRANSFERENCIA
lTrataTrf := .F.
If Fp_CodFol(@aCodFol,cFilAnt)
	// CARREGA OS IDENTIFICADORES DA PROVISAO E TESTA SE EXISTEM
	fIdentProv(@aVerbaProv,aCodFol)
	// VERIFICA A EXISTENCIA DOS IDENTIFICADORES DE TRANSFERENCIA
	lTrataTrf := (fChkIdent(aVerbaProv,_FerVenc,{_BxTrf},.F.) .And. fChkIdent(aVerbaProv,_13Salar,{_BxTrf},.F.))
	// VERIFICA A EXISTENCIA DOS IDENTIFICADORES DA PROVISAO MES
	If lGeraPMes .And. lTrataTrf
		lTrataTrf := (	fChkIdent(aVerba,_FerVMes,{_Atual},.F.) .And.;
						fChkIdent(aVerba,_13SVMes,{_Atual},.F.))
	Endif
EndIf


// ATUALIZA VARIAVEIS COM OS CODIGOS DAS VERBAS DE RATEIO PROVISAO MES
If !Empty(cTpRtProv)
	cVerbaRPF 	:= "'"+aCodFol[960,1]+"','"+aCodFol[961,1]+"','"+aCodFol[962,1]+"','"+aCodFol[963,1]+"','"+aCodFol[964,1]+"','"+aCodFol[965,1]+"'"
	cVerbaRP13	:= "'"+aCodFol[966,1]+"','"+aCodFol[967,1]+"','"+aCodFol[968,1]+"','"+aCodFol[969,1]+"','"+aCodFol[970,1]+"','"+aCodFol[971,1]+"'"
Endif

// VERIFICAR SE EXISTE CAMPO SAL.INCORPORADO NO CADASTRO
dbSelectArea("SX3")
dbSetOrder(2)
If dbSeek("RA_SALINCO")
	lSalInc := .T.
Endif
lINSSAut := dbSeek("RA_INSSAUT")
dbsetOrder(1)

//CAMPOS DA QUERY
If lTodosCpos
	cCposQuery	:= "% * %"
Else
	cCposQuery	:=	"RA_FILIAL, RA_MAT, RA_NOME, RA_CC, RA_CATFUNC, RA_ADMISSA, RA_SITFOLH, RA_AFASFGT, "+;
					"RA_DEMISSA, RA_TPCONTR, RA_HRSMES, RA_PERICUL, RA_ADTPOSE, RA_INSMAX"
	cCposQuery	+= If(lSalInc,", RA_SALINCO","")
	cCposQuery	+= If(lItemClVl,", RA_ITEM, RA_CLVL","")
	If !Empty(cTpRtProv) .And. lItemClVl 
		cCposQuery	+= ", RHT_CC, RHT_ITEM, RHT_CLVL"
	ElseIf !Empty(cTpRtProv) .And. !lItemClVl 
		cCposQuery	+= ", RHT_CC"
	EndIf
	cCposQuery	+= If(lINSSAut,", RA_INSSAUT","")
	cCposQuery  += ", SRA.R_E_C_N_O_"
	If !Empty(cTpRtProv)
		cCposnOrd5	:= If(nOrdem == 5 .AND. lItemClVl,", RHT_FILIAL, RHT_ITEM, RHT_CLVL, RHT_MAT",If(nOrdem == 5 .AND. !lItemClVl,", RHT_FILIAL, RHT_MAT",""))
	Else
		cCposnOrd5	:= If(nOrdem == 5 .AND. lItemClVl,", RT_FILIAL, RT_ITEM, RT_CLVL, RT_MAT",If(nOrdem == 5 .AND. !lItemClVl,", RT_FILIAL, RT_MAT",""))
	Endif
	cCposGroup	:=	"% "+cCposQuery+cCposnOrd5+" %"		
	cCposQuery	:=	"% "+cCposQuery+" %"
Endif
cAliasSRA	:= "QSRA"
If (Select(cAliasSRA) > 0)
	(cAliasSRA)->(dbCloseArea())
EndIf 

aStruSRA 	:= SRA->(dbStruct())
If !Empty(cTpRtProv)
	cOrdem		:= sqlorder(;
		If(nOrdem == 5 .AND. lItemClVl, "RHT_FILIAL, RHT_CC, RHT_ITEM, RHT_CLVL, RHT_MAT";
		, If(nOrdem == 5 .AND. !lItemClVl, "RHT_FILIAL, RHT_CC, RHT_MAT";
		, SRA->(IndexKey(If(nOrdem==4,8,nOrdem)));
		);
		);
		)
Else
	cOrdem		:= sqlorder(;
		If(nOrdem == 5 .AND. lItemClVl, "RT_FILIAL, RT_CC, RT_ITEM, RT_CLVL, RT_MAT";
		, If(nOrdem == 5 .AND. !lItemClVl, "RT_FILIAL, RT_CC, RT_MAT";
		, SRA->(IndexKey(If(nOrdem==4,8,nOrdem)));
		);
		);
		)
Endif
cOrdem		:= "% "+cOrdem+" %"
cCatQuery := ""
For nReg:=1 to Len(cCateg)
	cCatQuery += "'"+Subs(cCateg,nReg,1)+"'"
	If (nReg+1) <= Len(cCateg)
		cCatQuery += "," 
	Endif
Next nReg
cCatQuery	:= "%" + cCatQuery + "%"
cDemissa	:= "(SRA.RA_DEMISSA = '        ' OR SRA.RA_DEMISSA > '"+DTOS(dDataRef-day(dDataRef))+"')"
cDemissa	:= "% "+cDemissa+" %"
If !Empty(cTpRtProv)
	cAnoMesAtu  := MesAno(dDataRef)
	cDtIQryAtu	:= cAnoMesAtu+"01"
	cDtFQryAtu	:= cAnoMesAtu+strzero(f_Ultdia(dDataRef),2)
	If cTpRtProv == 'RPF'
		cLisVerbas	:= "%"+cVerbaRPF+"%"
	ElseIf cTpRtProv == 'RP13'
		cLisVerbas	:= "%"+cVerbaRP13+"%"
	EndIf
	
	BeginSql alias cAliasSRA
		    SELECT %exp:cCposQuery%
		      FROM %table:SRA% SRA
		INNER JOIN %table:RHT% RHT
		        ON RHT.RHT_FILIAL = SRA.RA_FILIAL
  			   AND RHT.RHT_MAT = SRA.RA_MAT
		 WHERE SRA.RA_FILIAL >= %exp:cFilDe% AND SRA.RA_FILIAL <= %exp:cFilAte% AND
				   SRA.RA_MAT >= %exp:cMatDe% AND SRA.RA_MAT <= %exp:cMatAte% AND
				   SRA.RA_CC >= %exp:cCCDe% AND SRA.RA_CC <= %exp:cCCAte% AND
				   SRA.RA_NOME >= %exp:cNomeDe% AND SRA.RA_NOME <= %exp:cNomeAte% AND
			       SRA.RA_CATFUNC IN (%exp:Upper(cCatQuery)%) AND
			       SRA.RA_ADMISSA <= %exp:dtos(dDataRef)% AND
			       %exp:cDemissa% AND				       
     			   RHT.RHT_VERBA IN (%exp:cLisVerbas%) AND
     			   RHT.RHT_DTCALC BETWEEN %exp:cDtIQryAtu% AND %exp:cDtFQryAtu% AND
				   SRA.%notDel% AND
				   RHT.%notDel%
		  GROUP BY %exp:cCposGroup%  
	      ORDER BY %exp:cOrdem%
 	EndSql	
Else	
	If Empty(cTpc)
		BeginSql alias cAliasSRA
			SELECT %exp:cCposQuery%
			FROM %table:SRA% SRA
			WHERE  SRA.RA_FILIAL >= %exp:cFilDe% AND SRA.RA_FILIAL <= %exp:cFilAte% AND
				   SRA.RA_MAT >= %exp:cMatDe% AND SRA.RA_MAT <= %exp:cMatAte% AND
				   SRA.RA_CC >= %exp:cCCDe% AND SRA.RA_CC <= %exp:cCCAte% AND
				   SRA.RA_NOME >= %exp:cNomeDe% AND SRA.RA_NOME <= %exp:cNomeAte% AND
			       SRA.RA_CATFUNC IN (%exp:Upper(cCatQuery)%) AND
			       SRA.RA_ADMISSA <= %exp:dtos(dDataRef)% AND
			       %exp:cDemissa% AND
				   SRA.%notDel%   
			ORDER BY %exp:cOrdem%
	 	EndSql
 	Else
		BeginSql alias cAliasSRA
			SELECT %exp:cCposQuery%
			FROM %table:SRA% SRA
			WHERE  SRA.RA_FILIAL >= %exp:cFilDe% AND SRA.RA_FILIAL <= %exp:cFilAte% AND
				   SRA.RA_MAT >= %exp:cMatDe% AND SRA.RA_MAT <= %exp:cMatAte% AND
				   SRA.RA_CC >= %exp:cCCDe% AND SRA.RA_CC <= %exp:cCCAte% AND
				   SRA.RA_NOME >= %exp:cNomeDe% AND SRA.RA_NOME <= %exp:cNomeAte% AND
			       SRA.RA_CATFUNC IN (%exp:Upper(cCatQuery)%) AND
			       SRA.RA_ADMISSA <= %exp:dtos(dDataRef)% AND SRA.RA_TPCONTR = %exp:cTpc% AND
			       %exp:cDemissa% AND
				   SRA.%notDel%   
			ORDER BY %exp:cOrdem%
	 	EndSql
 	EndIf
EndIf

//AJUSTA A ESTRUTURA DOS CAMPOS
For nReg := 1 To Len(aStruSRA)
	If (aStruSRA[nReg][2] <> "C") .And. If(!lTodosCpos,aStruSRA[nReg][1]$cCposQuery,.T.)
		TcSetField(cAliasSRA,aStruSRA[nReg][1],aStruSRA[nReg][2],aStruSRA[nReg][3],aStruSRA[nReg][4])
	EndIf
Next nReg
//ORDEM DO ARQUIVO TEMPORARIO
If nOrdem == 1
	cInicio  := (cAliasSRA)+"->RA_FILIAL+"+(cAliasSRA)+"->RA_MAT"
	cFim	 := cFilAte + cMatAte
	cIndCond := "PR_FILIAL + PR_MAT"
ElseIf nOrdem == 2
	cInicio  := (cAliasSRA)+"->RA_FILIAL+"+(cAliasSRA)+"->RA_CC+"+(cAliasSRA)+"->RA_MAT"
	cFim	 := cFilAte + cCcAte + cMatAte
	cIndCond := "PR_FILIAL + PR_CC + PR_MAT"	
ElseIf nOrdem == 3
	cInicio  := (cAliasSRA)+"->RA_FILIAL+"+(cAliasSRA)+"->RA_NOME+"+(cAliasSRA)+"->RA_MAT"	
	cFim	 := cFilAte + cNomeAte + cMatAte
	cIndCond := "PR_FILIAL + PR_NOME + PR_MAT"	
ElseIf nOrdem == 4
	cInicio  := (cAliasSRA)+"->RA_FILIAL+"+(cAliasSRA)+"->RA_CC+"+(cAliasSRA)+"->RA_NOME"
	cFim	 := cFilAte + cCcAte + cNomeAte
	cIndCond := "PR_FILIAL + PR_CC + PR_NOME"
ElseIf nOrdem == 5
	If !Empty(cTpRtProv)
		cInicio  :=  (cAliasSRA)+"->RA_FILIAL+"+(cAliasSRA)+"->RHT_CC+"+(cAliasSRA)+"->RA_MAT"
   	Else
   		cInicio  :=  (cAliasSRA)+"->RA_FILIAL+"+(cAliasSRA)+"->RT_CC+"+(cAliasSRA)+"->RA_MAT"
   	Endif
   	cFim	 :=  cFilAte + cCcAte + cMatAte
   	cIndCond :=	 "PR_FILIAL + PR_CCMVTO + PR_MAT"	
ElseIf nOrdem == 6//"C.Custo + Item + Classe"
	cInicio  := (cAliasSRA)+"->RA_FILIAL+"+(cAliasSRA)+"->RA_CC+"+(cAliasSRA)+"->RA_MAT"
	cFim	 := cFilAte + cCcAte + cMatAte
	cIndCond := "PR_FILIAL + PR_CC + PR_ITEM + PR_CLVL + PR_MAT"
Endif


// MONTA MATRIZ COM OS FUNCIONARIOS TRANSFERIDOS NO MES CONTEUDO DA MATRIZ - EMPRESA + FILIAL + CC + MATRICULA
aTransf := {}
fSeleTransf(@aTransf, dDataRef,cTpc)

// CRIA O ARQUIVO TEMPORARIO "TPR" PARA IMPRESSAO DA PROVISAO
Cria_TPR(@cTPRDbf,@cTPRNtx,cIndCond)

// SELECIONA O SRA PARA A MONTAGEM DO ARQUIVO TPR
dbSelectArea(cAliasSRA)
    
// CARREGA REGUA DE PROCESSAMENTO
ProcRegua(RecCount())

While !Eof() .And. &cInicio <= cFim
	// MOVIMENTA REGUA DE PROCESSAMENTO
  	IncProc(STR0014) // "Selecionando Registros..."

	// INDICA O TIPO DE MOVIMENTO DO FUNCIONARIO
	nTpMv 		:= 0
	lTransfEmp	:= .F.
	aSRASRT	:= {}
	aVerbasTPR	:= {}
	//Posiciona o SRA para a correta leitura dos afastamentos
	SRA->(DbGoTo((cAliasSRA)->R_E_C_N_O_))
	
	If !Empty(cTpRtProv)
		// VERIFICA SE ESTA AFASTADO NA DATA DE REFERENCIA DO CALCULO
		cSitFolh := cTipAfas := ""
		dAfaIni  := dAfaFim	 := CtoD("")
		fChkAfas((cAliasSRA)->RA_FILIAL,(cAliasSRA)->RA_MAT,dDataRef,@dAfaIni,@dAfaFim,@cTipAfas)
		cSitFolh := If(!Empty(cTipAfas) .And. cTipAfas # "F" .and. ( Empty(dAfaFim) .or. dAfaFim >= dDataRef) , "A", cSitFolh)
 
		If lItemClVl
			aAdd(aSRASRT,{(cAliasSra)->RA_FILIAL,(cAliasSra)->RA_MAT,(cAliasSra)->RA_CC,(cAliasSra)->RA_NOME,;
			(cAliasSra)->RA_ADMISSA,(cAliasSra)->RA_DEMISSA,cSitFolh,cTipAfas,(cAliasSra)->RA_TPCONTR,;
			(cAliasSra)->RA_HRSMES,(cAliasSra)->RA_PERICUL,If (lSalInc,(cAliasSra)->RA_SALINCO,""),(cAliasSra)->RHT_CC,;
			(cAliasSra)->RA_INSMAX,(cAliasSra)->RA_ADTPOSE,(cAliasSra)->RHT_ITEM,(cAliasSra)->RHT_CLVL})		
		Else
			aAdd(aSRASRT,{(cAliasSra)->RA_FILIAL,(cAliasSra)->RA_MAT,(cAliasSra)->RA_CC,(cAliasSra)->RA_NOME,;
			(cAliasSra)->RA_ADMISSA,(cAliasSra)->RA_DEMISSA,cSitFolh,cTipAfas,(cAliasSra)->RA_TPCONTR,;
			(cAliasSra)->RA_HRSMES,(cAliasSra)->RA_PERICUL,If (lSalInc,(cAliasSra)->RA_SALINCO,""),(cAliasSra)->RHT_CC,;
			(cAliasSra)->RA_INSMAX,(cAliasSra)->RA_ADTPOSE})
		EndIf
	EndIf 
	
	If !lIncTodos
		cCusto 		:=  ""
		cCustoAtu	:=  ""
		cClvl 		:=  ""
		cClvlAtu 	:=  ""
		cItem 		:=  ""
		cItemAtu 	:=  ""

		// CONSISTE CONTROLE DE ACESSOS E FILIAIS VALIDAS
		If !((cAliasSRA)->RA_FILIAL $ fValidFil()) .Or. !Eval(bChkSRA)
			dbSkip()
			Loop
		EndIf
		
		// CONSISTE DEMISSAO E TRANSFERENCIA (SE FOR TRANSFERENCIA SAIDA DESPREZA O FUNCIONARIO POIS SERA TRATADO NO ARRAY aTransf
		If (cAliasSRA)->RA_SITFOLH == "D"                                     
			nPosFunc := Ascan(aTransf,{ |X| X[_TAnter,_TFil]+X[_TAnter,_TMat]+X[_TAnter,_TCC] == (cAliasSRA)->RA_FILIAL+(cAliasSRA)->RA_MAT+(cAliasSRA)->RA_CC })
			If nPosFunc > 0
				DbSkip()
				Loop
			EndIf                                   
			// SE TRANSF. SAIDA NO MES/ANO E NAO ENCONTRAR NO ARRAY OU O LTRANSFEMP FOR .T. (EMPRESA ANTERIOR E EMPRESA DESTINO 
			// FOR DIFERENTE) INDICA QUE E UMA TRANSFERENCIA ENTRE EMPRESAS E DEVERA SER INCLUIDO MOV. SAIDA
	     	nPosFunc := Ascan(aTransf, { |X| X[_TAnter,_TEmp]+X[_TAnter,_TFil]+X[_TAnter,_TMat] == FWGrpCompany("SRA")+(cAliasSRA)->RA_FILIAL+(cAliasSRA)->RA_MAT })
  	  	  	If nPosFunc > 0
				lTransfEmp	:= aTransf[nPosFunc,_TAnter,_TEmp] <> aTransf[nPosFunc,_TDest,_TEmp] .And. aTransf[nPosFunc,_TAnter,_TEmp] <> aTransf[nPosFunc,_TAtual,_TEmp]
  				cCusto  	:= aTransf[nPosFunc,_TAnter,_TCC] // Centro de custo anterior
			    cCustoAtu	:= aTransf[nPosFunc,_TAtual,_TCC] // Centro de custo atual				
  				If lItemClVl
					cItem  		:= aTransf[nPosFunc,_TAnter,_TItem]// Item anterior
					cItemAtu	:= aTransf[nPosFunc,_TAtual,_TItem]// Item atual
					cClvl   	:= aTransf[nPosFunc,_TAnter,_TClvl]// Classe anterior
					cClvlAtu	:= aTransf[nPosFunc,_TAtual,_TClvl]// Classe atual
				EndIf
 	 	  	Else
				lTransfEmp	:= .F.       
			// SE NAO FOR TRANSFERENCIA EMPRESA, FILIAL+MATRICULA ANTERIOR IGUAL RA_FILIAL+RA_MAT,    
			// EMPRESA + FILIAL ANTERIOR DIFERENTE DA EMPRESA + FILIAL DESTINO, E MES\ANO ATUAL       
			//	IGUAL AO MES\ANO DESTINO,  UTILIZAR C.CUSTO DO ATRANSF  ANTERIOR, E MARCAR FUNCIONARIO 
			// PARA QUE NAO SER INCLUIDO (ANALISE FEITA NA PARTE "INCLUI OS TRANSFERIDOS ENTRE CC	   
 			//  NO ARQUIVO DE PROVISAO" - ANALISE DAS TRANSFERENCIAS)		                           
 				nPosFunc1 := Ascan(aTransf, { |X| X[_TAnter,_TEmp] == FWGrpCompany("SRA") .And. X[_TAnter,_TFil]+X[_TAnter,_TMat] == (cAliasSRA)->RA_FILIAL+(cAliasSRA)->RA_MAT })
				If nPosFunc1 > 0 .and. aTransf[nPosFunc1,_TAnter,_TEmp]+aTransf[nPosFunc1,_TAnter,_TFil] <> aTransf[nPosFunc1,_TDest,_TEmp]+aTransf[nPosFunc1,_TDest,_TFil] .and. ;
									   aTransf[nPosFunc1,_TAtual,_TDta] == aTransf[nPosFunc1,_TDest,_TDta]
					cCusto := aTransf[nPosFunc1,_TAnter,_TCC] // Centro de custo anterior
					If lItemClVl
						cItem  := aTransf[nPosFunc1,_TAnter,_TItem]// Item anterior
						cClvl  := aTransf[nPosFunc1,_TAnter,_TClvl]// Classe anterior
					EndIf 
					aTransf[nPosFunc1,_TAnter,_TInc] := .T.  // Marca funcionario para que nao seja incluido
					If (aTransf[nPosFunc1,_TAnter,_TEmp] <> aTransf[nPosFunc1,_TDest,_TEmp])
						lTransfEmp	:= .T.       
            		EndIf
				Else	
					cCusto:= (cAliasSRA)->RA_CC
					If lItemClVl
						cItem := (cAliasSRA)->RA_ITEM 
						cClvl := (cAliasSRA)->RA_CLVL                    
					EndIf                    
				EndIf	                
			EndIf
			// NAO GRAVAR MOVIMENTO SE FOR MES\ANO DA TRANSFERENCIA, NAO UTILIZA IDENTIFICADOR
			// DE BAIXA DE TRANSFERENCIA E NAO SEJA TRANSFERENCIA ENTRE EMPRESAS
			If !lTrataTrf .and. !lTransfEmp .and. ; 
				MesAno((cAliasSRA)->RA_DEMISSA) == MesAno(dDataRef) .And. (AllTrim((cAliasSRA)->RA_AFASFGT) $ "5*N") .And. (nPosFunc == 0 .Or. (nPosFunc > 0 .And. lTransfEmp)) 
				dbSkip()
				Loop
			EndIf
  		  // NA TRANSFERENCIA POR EMPRESA, VERIFICAR SE MES\ANO TRANSF. E O MESMA QUE MES\ANO DE REF. PARA  
		  //GARANTIR NA EMPRESA ORIGEM A MOVIMENTACAO DE SAIDA, INDEPENDENTE DO DESTINO (ULTIMA TRANSF)    
		If	(((nPosFunc > 0 .And.lTransfEmp).And. ;
						(aTransf[nPosFunc,_TAnter,_TDta] == aTransf[nPosFunc,_TAtual,_TDta]) .and. ;
						(aTransf[nPosFunc,_TAtual,_TDta] == MesAno(dDataRef))	);
					.Or. (MesAno((cAliasSRA)->RA_DEMISSA) == MesAno(dDataRef) .And. AllTrim((cAliasSRA)->RA_AFASFGT) $ "5*N" .And. (nPosFunc == 0 .Or.	(nPosFunc > 0 .And. lTransfEmp))))
				nTpMv := _Trfe_Sai
			ElseIf MesAno((cAliasSRA)->RA_DEMISSA) == MesAno(dDataRef) .And. !(AllTrim((cAliasSRA)->RA_AFASFGT) $ "5*N") .And. lIncDemit
				nTpMv := _Demitido
			ElseIf MesAno((cAliasSRA)->RA_DEMISSA) < MesAno(dDataRef) .Or. ((AllTrim((cAliasSRA)->RA_AFASFGT) $ "5*N") .And. nPosFunc > 0 .And. !lTransfEmp) .Or.;
				   (MesAno((cAliasSRA)->RA_DEMISSA) == MesAno(dDataRef) .And. !lIncDemit) .OR. ;
				   (MesAno((cAliasSRA)->RA_DEMISSA) > MesAno(dDataRef) .and. AllTrim((cAliasSRA)->RA_AFASFGT) $ "5*N")
				dbSkip()
				Loop
			EndIf
		EndIf
	EndIf

	// VERIFICA SE FUNCIONARIO FOI TRANSFERIDO APOS DATA REFERENCIA
	nPosFunc := Ascan(aTransf, { |X| X[_TDest,_TEmp]+X[_TDest,_TFil]+X[_TDest,_TCC]+X[_TDest,_TMat] == FWGrpCompany("SRA")+(cAliasSRA)->RA_FILIAL+(cAliasSRA)->RA_CC+(cAliasSRA)->RA_MAT })
	If nPosFunc > 0
		If !(;
			aTransf[nPosFunc,_TAtual,_TEmp]+aTransf[nPosFunc,_TAtual,_TFil]+aTransf[nPosFunc,_TAtual,_TCC]+aTransf[nPosFunc,_TAtual,_TMat] == ;
			aTransf[nPosFunc,_TDest ,_TEmp]+aTransf[nPosFunc,_TDest ,_TFil]+aTransf[nPosFunc,_TDest ,_TCC]+aTransf[nPosFunc,_TDest ,_TMat]  ;
			)
			If aTransf[nPosFunc,_TDest,_TDta] > MesAno(dDataRef) // data de transferencia maior que data de processamento
				aTransf[nPosFunc,_TDest,_TInc] := .T.  // Marca funcionario para que nao seja incluido
				dbSelectArea(cAliasSRA)
				dbSkip()
				Loop
			EndIf
		EndIf
		aTransf[nPosFunc,_TDest,_TInc] := .T.  // Marca funcionario para que nao seja incluido
	EndIf	
	// VERIFICA SE E TRANSFERENCIA ENTRADA
	If nTpMv == 0
		nPosFunc := Ascan(aTransf, { |X| X[_TAtual,_TEmp]+X[_TAtual,_TFil]+X[_TAtual,_TCC]+X[_TAtual,_TMat] == FWGrpCompany("SRA")+((cAliasSRA)->RA_FILIAL)+(cAliasSRA)->RA_CC+(cAliasSRA)->RA_MAT })
		//SE TRANSFERIDO APOS DATA DE REFERENCIA, DESPREZA FUNCIONARIO, CASO CONTRARIO, MARCA TRANSF. ENTRADA PARA CALCULO/IMPRESSAO
		If nPosFunc > 0
			If !(;
				( lItemClVl .And.;
				aTransf[nPosFunc,_TAtual,_TEmp]+aTransf[nPosFunc,_TAtual,_TFil]+aTransf[nPosFunc,_TAtual,_TCC]+aTransf[nPosFunc,_TAtual,_TMat]+aTransf[nPosFunc,_TAtual,_TItem]+aTransf[nPosFunc,_TAtual,_TClvl] == ;
				aTransf[nPosFunc,_TAnter,_TEmp]+aTransf[nPosFunc,_TAnter,_TFil]+aTransf[nPosFunc,_TAnter,_TCC]+aTransf[nPosFunc,_TAnter,_TMat]+aTransf[nPosFunc,_TAnter,_TItem]+aTransf[nPosFunc,_TAnter,_TClvl] .And. ;
				aTransf[nPosFunc,_TAtual,_TEmp]+aTransf[nPosFunc,_TAtual,_TFil]+aTransf[nPosFunc,_TAtual,_TCC]+aTransf[nPosFunc,_TAtual,_TMat]+aTransf[nPosFunc,_TAtual,_TItem]+aTransf[nPosFunc,_TAtual,_TClvl] == ;
				aTransf[nPosFunc,_TDest ,_TEmp]+aTransf[nPosFunc,_TDest ,_TFil]+aTransf[nPosFunc,_TDest ,_TCC]+aTransf[nPosFunc,_TDest ,_TMat]+aTransf[nPosFunc,_TDest ,_TItem]+aTransf[nPosFunc,_TDest ,_TClvl]  ;
				);				
				.Or.;
				( !lItemClVl .And.; 
				aTransf[nPosFunc,_TAtual,_TEmp]+aTransf[nPosFunc,_TAtual,_TFil]+aTransf[nPosFunc,_TAtual,_TCC]+aTransf[nPosFunc,_TAtual,_TMat] == ;
				aTransf[nPosFunc,_TAnter,_TEmp]+aTransf[nPosFunc,_TAnter,_TFil]+aTransf[nPosFunc,_TAnter,_TCC]+aTransf[nPosFunc,_TAnter,_TMat] .And. ;
				aTransf[nPosFunc,_TAtual,_TEmp]+aTransf[nPosFunc,_TAtual,_TFil]+aTransf[nPosFunc,_TAtual,_TCC]+aTransf[nPosFunc,_TAtual,_TMat] == ;
				aTransf[nPosFunc,_TDest ,_TEmp]+aTransf[nPosFunc,_TDest ,_TFil]+aTransf[nPosFunc,_TDest ,_TCC]+aTransf[nPosFunc,_TDest ,_TMat]  ;
				);
				)
				If aTransf[nPosFunc,_TAtual,_TDta] == MesAno(dDataRef)
					nTpMv := _Trfe_Ent 						 // Transferencia Entrada
					aTransf[nPosFunc,_TAtual,_TInc] := .T.  // Marca funcionario para que nao seja incluido
				ElseIf !(aTransf[nPosFunc,_TAtual,_TEmp] <> aTransf[nPosFunc,_TDest,_TEmp]) 
					dbSelectArea( cAliasSRA )
					dbSkip()
					Loop
				EndIf
			EndIf
		EndIf
	EndIf
	If Empty(cTpRtProv)
		// VERIFICA SE ESTA AFASTADO NA DATA DE REFERENCIA DO CALCULO
		cSitFolh := cTipAfas := ""
		dAfaIni  := dAfaFim	 := CtoD("")
		fChkAfas((cAliasSRA)->RA_FILIAL,(cAliasSRA)->RA_MAT,dDataRef,@dAfaIni,@dAfaFim,@cTipAfas)
		cSitFolh := If(!Empty(cTipAfas) .And. cTipAfas # "F" .and. ( Empty(dAfaFim) .or. dAfaFim >= dDataRef) , "A", cSitFolh)
	Else
		If (nTpMv <> 6 .And. nTpMv <> 0)   // Trans.Entrada ou Demissao	
			dbSelectArea(cAliasSRA)
			dbSkip()
			Loop		
		Endif
	EndIf

	cCusto	:= ""
	cItem	:= ""
	cClvl	:= ""
	
	nPos	:= aScan(aTransf, { |X| X[_TAtual,_TEmp]+X[_TAtual,_TFil]+X[_TAtual,_TMat]+X[_TAtual,_TDta] == FWGrpCompany("SRA")+(cAliasSRA)->RA_FILIAL+(cAliasSRA)->RA_MAT+AnoMes(dDataRef) } ) 

	If nPos == 0
		nPos	:= aScan(aTransf, { |X| X[_TAtual,_TEmp]+X[_TAtual,_TFil]+X[_TAtual,_TMat] == FWGrpCompany("SRA")+(cAliasSRA)->RA_FILIAL+(cAliasSRA)->RA_MAT } ) 
		If nPos > 0
			cCusto:= aTransf[nPos][_TAtual][_TCC]
			If lItemClVl
				cItem	:= aTransf[nPos][_TAtual][_TItem]
				cClvl	:= aTransf[nPos][_TAtual][_TClvl]
			EndIf
		Endif		
	Else
		cCusto:= aTransf[nPos][_TAtual][_TCC]
		If lItemClVl
			cItem	:= aTransf[nPos][_TAtual][_TItem]
			cClvl	:= aTransf[nPos][_TAtual][_TClvl]
		EndIf
	Endif

	dbSelectArea("TPR")
    RecLock("TPR", .T.)
	TPR->PR_FILIAL  := (cAliasSRA)->RA_FILIAL
	TPR->PR_MAT     := (cAliasSRA)->RA_MAT
	TPR->PR_NOME    := (cAliasSRA)->RA_NOME

	If Empty(cCusto)
		TPR->PR_CC	:= (cAliasSRA)->RA_CC
	Else
		If nTpMv==6 .or. nTpMv==1   // Trans.Entrada ou Demissao
			TPR->PR_CC	:= If(Empty(cCustoAtu), cCusto, cCustoAtu)  //Gravar c.custo atual
  		Else
			TPR->PR_CC	:= cCusto     //Gravar c.custo da origem 
		EndIf
	EndIf
		
	If lItemClVl
		If Empty(cItem)
			TPR->PR_ITEM    := (cAliasSRA)->RA_ITEM
		Else
			If nTpMv==6 .or. nTpMv==1   // Trans.Entrada ou Demissao
				TPR->PR_ITEM	:= If(Empty(cItemAtu), cItem, cItemAtu)  //Gravar item atual
	  		Else
				TPR->PR_ITEM	:= cItem     //Gravar item da origem 
			EndIf			
		EndIf  
		If Empty(cClvl)
			TPR->PR_CLVL    := (cAliasSRA)->RA_CLVL
		Else
			If nTpMv==6 .or. nTpMv==1   // Trans.Entrada ou Demissao
				TPR->PR_CLVL	:= If(Empty(cClvlAtu), cClvl, cClvlAtu)  //Gravar classe atual
	  		Else
				TPR->PR_CLVL	:= cClvl     //Gravar classe da origem 
			EndIf				
		EndIf  
	EndIf
	TPR->PR_ADMISSA := (cAliasSRA)->RA_ADMISSA
	TPR->PR_DEMISSA := (cAliasSRA)->RA_DEMISSA
	TPR->PR_SITFOLH := cSitFolh
	TPR->PR_AFASFGT := cTipAfas
	TPR->PR_TPCONTR := (cAliasSRA)->RA_TPCONTR
	TPR->PR_HRSMES  := (cAliasSRA)->RA_HRSMES
	TPR->PR_PERICUL := (cAliasSRA)->RA_PERICUL
	TPR->PR_INSMAX	:= (cAliasSra)->RA_INSMAX
	TPR->PR_ADTPOSE	:= (cAliasSra)->RA_ADTPOSE
	TPR->PR_TIPMOVI := nTpMv  // Demitido, Transf. Saida ou Transf. Entrada
	TPR->PR_CATFUNC := (cAliasSRA)->RA_CATFUNC
	If lSalInc
		TPR->PR_SALINCO := (cAliasSRA)->RA_SALINCO
	EndIf
	If !Empty(cTpRtProv) 
		TPR->PR_CCMVTO := (cAliasSRA)->RHT_CC
		If lItemClVl
			TPR->PR_ITMMVTO := (cAliasSRA)->RHT_ITEM
			TPR->PR_CLVMVTO := (cAliasSRA)->RHT_CLVL			
		EndIf
	EndIf	
	If lINSSAut
		TPR->PR_INSSAUT := (cAliasSRA)->RA_INSSAUT	
	EndIf
	MsUnlock()

	dbSelectArea(cAliasSRA)
	dbSkip()
EndDo

// GARANTE ORDEM 1 PARA BUSCA DOS FUNCIONARIOS
dbSelectArea("SRA")
dbSetOrder(1)

// INCLUI OS TRANSFERIDOS ENTRE CC NO ARQUIVO DE PROVISAO
For nK1 := 1 To Len(aTransf)
	dbSelectArea("SRA")     
	If (dbSeek(aTransf[nK1,_TDest,_TFil] + aTransf[nK1,_TDest,_TMat]) .And. ;
			 aTransf[nK1,_TDest,_TEmp] == FWGrpCompany("SRA")) .OR. ;
			 (dbSeek(aTransf[nK1,_TAtual,_TFil] + aTransf[nK1,_TAtual,_TMat]) .And. ;
			 aTransf[nK1,_TAtual,_TEmp] == FWGrpCompany("SRA"))

		If (SRA->RA_SITFOLH == "D" .And. (MesAno(SRA->RA_DEMISSA) <= MesAno(dDataRef) .and.;
			MesAno(SRA->RA_DEMISSA) < aTransf[nK1,_TAtual,_TDta]) .and. !(AllTrim(SRA->RA_AFASFGT) $ "5*N")) 
			Loop
		EndIf
		// CONSISTE CATEGORIA E DEMISSAO DO FUNCIONARIO
		If !(SRA->RA_CATFUNC $ cCateg) .Or. (SRA->RA_SITFOLH == "D" .And.;
		   MesAno(SRA->RA_DEMISSA) < MesAno(dDataRef))
			Loop
		EndIf                
	
		For nK2 := 1 To 2
			nTrfProc := If(nK2 == 1, _TAnter, _TAtual)
			// VERIFICACAO DO ELEMENTO ANTERIOR DO array aTransf
			If nK2 == 1
				// SE NAO TRATA TRANSFERIDO, NAO INCLUIR TRANSFERENCIA ORIGEM EFETUADA NO MES DE PROCESSAMENTO. SOMENTE INCLUIR POSTERIORES
				If !lTrataTrf .And. aTransf[nK1,_TAnter,_TDta] == MesAno(dDataRef)
					Loop
				EndIf   
				// VERIFICA SE OS ELEMENTOS ANTERIOR E ATUAL SAO IGUAIS OU SE A TRANSFERENCIA ANTERIOR PARA ATUAL FOI ENTRE FILIAL
				If ( ( lItemClVl .And. aTransf[nK1,_TAnter,_TFil]+aTransf[nK1,_TAnter,_TCC]+aTransf[nK1,_TAnter,_TMat]+aTransf[nK1,_TAnter,_TItem]+aTransf[nK1,_TAnter,_TClvl] ==;
				    aTransf[nK1,_TAtual,_TFil]+aTransf[nK1,_TAtual,_TCC]+aTransf[nK1,_TAtual,_TMat]+aTransf[nK1,_TAtual,_TItem]+aTransf[nK1,_TAtual,_TClvl] ) ;
					.Or.;
					( !lItemClVl .And. aTransf[nK1,_TAnter,_TFil]+aTransf[nK1,_TAnter,_TCC]+aTransf[nK1,_TAnter,_TMat]==;
				    aTransf[nK1,_TAtual,_TFil]+aTransf[nK1,_TAtual,_TCC]+aTransf[nK1,_TAtual,_TMat] ) ) .or. ;
				    aTransf[nK1,_TAnter,_TInc] .or. ;
				    aTransf[nK1,_TAnter,_TEmp] <> FWGrpCompany("SRA") 
				    Loop
				EndIf
			Else
				// VERIFICA SE ATUAL == DESTINO E JA INCLUI DESTINO OU SE A EMPRESA ATUAL E DIFERENTE DA EMPRESA CORRENTE
				If (;
					( lItemClVl .And. aTransf[nK1,_TAtual,_TFil]+aTransf[nK1,_TAtual,_TCC]+aTransf[nK1,_TAtual,_TMat]+aTransf[nK1,_TAtual,_TItem]+aTransf[nK1,_TAtual,_TClvl]==;
				     aTransf[nK1,_TDest,_TFil]+aTransf[nK1,_TDest,_TCC]+aTransf[nK1,_TDest,_TMat]+aTransf[nK1,_TDest,_TItem]+aTransf[nK1,_TDest,_TClvl] .And. aTransf[nK1,_TDest,_TInc]) .Or. ;					 
					( !lItemClVl .And. aTransf[nK1,_TAtual,_TFil]+aTransf[nK1,_TAtual,_TCC]+aTransf[nK1,_TAtual,_TMat]==;
				     aTransf[nK1,_TDest,_TFil]+aTransf[nK1,_TDest,_TCC]+aTransf[nK1,_TDest,_TMat] .And. aTransf[nK1,_TDest,_TInc]) ) .Or. ;
				     ( aTransf[nK1,_TAtual,_TEmp] <> FWGrpCompany("SRA") )
				    Loop
				EndIf
			EndIf
			// CONSISTE PARAMETROS SELECIONADO PELO USUARIO
			If 	(aTransf[nK1,nTrfProc,_TFil]  < cFilDe) .Or. (aTransf[nK1,nTrfProc,_TFil] > cFilAte) .Or. ;
				(aTransf[nK1,nTrfProc,_TCC]  < cCcDe)	.Or. (aTransf[nK1,nTrfProc,_TCC]  > cCCAte)  .Or. ;
				(aTransf[nK1,nTrfProc,_TMat] < cMatDe)	.Or. (aTransf[nK1,nTrfProc,_TMat] > cMatAte) .Or. ;
				(SRA->RA_NOME < cNomeDe) .Or. (SRA->RA_NOME > cNomeAte)
				Loop
			EndIf
			// CONSISTE CONTROLE DE ACESSOS E FILIAIS VALIDAS
			If  !(SRA->RA_FILIAL $ fValidFil()) .Or. !SRA->(Eval(bChkSRE))
				Loop
			EndIf

			// VERIFICA SE FUNCIONARIO JA FOI INCLUIDO
		   	dbSelectArea("TPR")
			If !aTransf[nK1,nTrfProc,_TInc]
				nPR_TIPMOVI := 0
				// Se transferido no mes/ano da referencia, indica saida.³
				If nK2 == 1 .And. aTransf[nK1,_TAnter,_TDta] == MesAno(dDataRef)
					nPR_TIPMOVI := _Trfe_Sai
				ElseIf nK2 == 2 .And. aTransf[nK1,_TAtual,_TDta] == MesAno(dDataRef) .And.;
				  aTransf[nK1,_TAnter,_TEmp] + aTransf[nK1,_TAnter,_TFil] + aTransf[nK1,_TAnter,_TCC] + aTransf[nK1,_TAnter,_TMat] #;
				  aTransf[nK1,_TAtual,_TEmp] + aTransf[nK1,_TAtual,_TFil] + aTransf[nK1,_TAtual,_TCC] + aTransf[nK1,_TAtual,_TMat]
					nPR_TIPMOVI := _Trfe_Ent
				EndIf

				//Verifica a situacao folha e o tipo de afastamento na competencia de calculo
				cSitFolh := cTipAfas := ""
				dAfaIni  := dAfaFim	 := CtoD("")
				fChkAfas( aTransf[nK1,nTrfProc,_TFil], aTransf[nK1,nTrfProc,_TMat], dDataRef, @dAfaIni,@dAfaFim, @cTipAfas)
				cSitFolh := If(!Empty(cTipAfas) .And. cTipAfas # "F" .and. ( Empty(dAfaFim) .or. dAfaFim >= dDataRef) , "A", cSitFolh)

				If !Empty(cTpRtProv) 
					If (nPR_TIPMOVI == 0 .Or. nPR_TIPMOVI == 6)
						nTamArray := Len(aSRASRT) 
						For nK3 := 1 To nTamArray
							If SRA->RA_FILIAL == aSRASRT[nK3,1] .AND. SRA->RA_MAT == aSRASRT[nK3,2] .OR.;
								(SRA->RA_FILIAL <> aSRASRT[nK3,1] .AND. aTransf[nK1,nTrfProc,_TFil] == aSRASRT[nK3,1] .AND.;
								aTransf[nK1,nTrfProc,_TMat] == aSRASRT[nK3,2])							
				    			RecLock("TPR", .T.) 
								TPR->PR_FILIAL  := aTransf[nK1,nTrfProc,_TFil] // Filial De
								TPR->PR_MAT     := aTransf[nK1,nTrfProc,_TMat] // Matricula De
								TPR->PR_CC      := aTransf[nK1,nTrfProc,_TCC]  // Centro de Custo De
								TPR->PR_NOME    := aSRASRT[nK3,4]//(cAliasSra)->RA_NOME
								TPR->PR_ADMISSA := aSRASRT[nK3,5]//(cAliasSra)->RA_ADMISSA
								TPR->PR_DEMISSA := aSRASRT[nK3,6]//(cAliasSra)->RA_DEMISSA
								TPR->PR_SITFOLH := cSitFolh
								TPR->PR_AFASFGT := cTipAfas
								TPR->PR_TPCONTR := aSRASRT[nK3,9]//(cAliasSra)->RA_TPCONTR
								TPR->PR_HRSMES  := aSRASRT[nK3,10]//(cAliasSra)->RA_HRSMES
								TPR->PR_PERICUL := aSRASRT[nK3,11]//(cAliasSra)->RA_PERICUL
								If lSalInc
									TPR->PR_SALINCO := aSRASRT[nK3,12]//(cAliasSra)->RA_SALINCO
								EndIf
								TPR->PR_CCMVTO := aSRASRT[nK3,13]//(cAliasSra)->RT_CC
								If lItemClVl
									TPR->PR_ITMMVTO := aSRASRT[nK3,16]//(cAliasSra)->RT_ITEM
									TPR->PR_CLVMVTO := aSRASRT[nK3,17]//(cAliasSra)->RT_CLVL
								EndIf
								TPR->PR_TIPMOVI := nPR_TIPMOVI
								MsUnlock()							
							EndIf
						Next
					Endif
				Else					
			    	RecLock("TPR", .T.)
					TPR->PR_FILIAL  := aTransf[nK1,nTrfProc,_TFil] // Filial De
					TPR->PR_MAT     := aTransf[nK1,nTrfProc,_TMat] // Matricula De
					TPR->PR_CC      := aTransf[nK1,nTrfProc,_TCC]  // Centro de Custo De
					TPR->PR_NOME    := SRA->RA_NOME
					TPR->PR_ADMISSA := SRA->RA_ADMISSA
					TPR->PR_DEMISSA := SRA->RA_DEMISSA
					TPR->PR_SITFOLH := cSitFolh
					TPR->PR_AFASFGT := cTipAfas
					TPR->PR_TPCONTR := SRA->RA_TPCONTR
					TPR->PR_HRSMES  := SRA->RA_HRSMES
					TPR->PR_PERICUL := SRA->RA_PERICUL
					TPR->PR_TIPMOVI := nPR_TIPMOVI
					If lSalInc
						TPR->PR_SALINCO := SRA->RA_SALINCO
					EndIf   
					If lItemClVl
						TPR->PR_ITEM    := aTransf[nK1,nTrfProc,_TItem] // Item Contabil
						TPR->PR_CLVL    := aTransf[nK1,nTrfProc,_TClvl] // Classe de valor
					EndIf					
					MsUnlock()
				EndIf				
				aTransf[nK1,nTrfProc,_TInc] := .T.
			EndIf
		Next nK2
	EndIf
Next nK1

(cAliasSRA)->(dbCloseArea())
If !Empty(cTpRtProv)
	aSRASRT := {}
EndIf
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Cria_TPR ³ Autor ³ R.H.                ³Data ³  02.03.00   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Cria  Arquivo de Trabalho para impressao da provisao       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ CRIA_TPR(cTPRDbf,cTPRNtx,cIndCond)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cTPRDbf  - Nome do arquivo de dados                        ³±±
±±³          ³ cTPRNtx  - Nome do arquivo de indice						    ³±±
±±³          ³ cIndCond - Chave do indice condicional					    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
STATIC Function Cria_TPR(cTPRDbf,cTPRNtx,cIndCond)
Local aFields
Local aTamCC	:= TamSX3("RA_CC")
Local aTamItem 
Local aTamClVl 
Local aTamCCMvto 
Local nTry		:= 20
Local cMsgExc	:= ""

If Type("lGrid") == "U"
	Private lGrid	:= .F.
EndIf

If lItemClVl
	aTamItem := TamSX3("RA_ITEM")
	aTamClVl := TamSX3("RA_CLVL")
Endif	

cTPRDBF := Subst(CriaTrab(NIL,.f.),1,7) + "B" // Evita criacao com mesmo nome
aFields := {}

AADD(aFields,{"PR_FILIAL" ,"C",FWGETTAMFILIAL,0})
AADD(aFields,{"PR_MAT"    ,"C",06,0})
AADD(aFields,{"PR_CC"     ,"C",aTamCC[1],0})
AADD(aFields,{"PR_NOME"   ,"C",30,0})
AADD(aFields,{"PR_ADMISSA","D",08,0})
AADD(aFields,{"PR_DEMISSA","D",08,0})
AADD(aFields,{"PR_SITFOLH","C",01,0})
AADD(aFields,{"PR_AFASFGT","C",01,0})
AADD(aFields,{"PR_TPCONTR","C",01,0})
AADD(aFields,{"PR_HRSMES" ,"N",06,2})
AADD(aFields,{"PR_SALINCO","N",12,2})
AADD(aFields,{"PR_PERICUL","N",06,2})
AADD(aFields,{"PR_INSMIN" ,"N",06,2})
AADD(aFields,{"PR_INSMED" ,"N",06,2})
AADD(aFields,{"PR_INSMAX" ,"N",06,2})
AADD(aFields,{"PR_ADTPOSE","C",06,0})
AADD(aFields,{"PR_TIPMOVI","N",01,0})
AADD(aFields,{"PR_INSSAUT","C",01,0})
AADD(aFields,{"PR_CATFUNC","C",01,0})
//SE EXISTIR ITEM E CLASSE CRIA OS CAMPOS NA TABELA TEMPORARIA
If lItemClVl
	AADD(aFields,{"PR_ITEM"   ,"C",aTamItem[1],0})
	AADD(aFields,{"PR_CLVL"   ,"C",aTamClVl[1],0})
Endif
If !Empty(cTpRtProv)
	aTamCCMvto	:= TamSX3("RT_CC") 
	AADD(aFields,{"PR_CCMVTO"     ,"C",aTamCCMvto[1],0})
	If lItemClVl
		AADD(aFields,{"PR_ITMMVTO"    ,"C",aTamItem[1],0})
		AADD(aFields,{"PR_CLVMVTO"    ,"C",aTamClVl[1],0})
	EndIf
EndIf
	
DbCreate(cTPRDbf,aFields)

If (Select("TPR") > 0)
	dbSelectArea("TPR")
	USE
EndIf

USE (cTPRDbf) ALIAS TPR EXCLUSIVE NEW

// NO LINUX EXISTE UM TIME PARA LIBERACAO DA CRIACAO DO ARQUIVO. QUANDO NAO ENCONTRAR 
// O ARQUIVO PARA ABRIR, DEVER AGUARDAR UM TEMPO. ESPERAR NO MAXIMO 20 SEGUNDOS
While NetErr() .and. nTry > 0 
	nTry--
	sleep(1000)	//-- 1 segundo
	USE (cTPRDbf) ALIAS TPR EXCLUSIVE NEW
Enddo

If NetErr()
	cMsgExc := CRLF + OemToAnsi(STR0053)// "Falha na criacao do arquivo temporario (TPR)!"
	UserException(cMsgExc)
EndIf

// CRIA NOME PARA INDICE PERMANENTE 
cTPRNtx := Subst(CriaTrab(NIL,.f.),1,7) + "C"

// CRIA INDICE PERMANENTE PARA A TABELA
INDEX ON &(cIndCond) TO (cTPRNtx + OrdBagExt())

If lGrid
	// FECHA A TABELA 
	USE
	// E REABRE EM MODO COMPARTILHADO, COM O INDICE
	Open_TPR(cTPRDbf,cTPRNtx)
EndIf

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao	 ³ fSeleTransf ³ Autor ³ Emerson R. de Souza³ Data ³ 17.09.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Seleciona Funcionarios Transferidos	no Mes			      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fSeleTransf(aTransf,dDataProvisao)						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aTransf       = Array que contera os transferido do mes    ³±±
±±³          ³ dDataProvisao = Data Provisao P/ Selecao dos Transferidos  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso	 	 ³ Generico   												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
STATIC Function fSeleTransf(aTransf, dDataProvisao, cTpc)
Local aArqSRE    	:= {}
Local aTrfAux    	:= {}
Local nPosTrf    	:= 0
Local nPosArq    	:= 0
Local cAlias     	:= ALIAS()
Local nTamRA_FILIAL := ""
Local cBusca     	:= ""
Local cAnoMesAux 	:= ""
Local nSeqFor

DEFAULT cTpc		:= ""

cAnoMesR := MesAno(dDataProvisao)

dbSelectArea("SRE")
cIndCond:= "RE_DATA"
cFor:='Left(DTOS(RE_DATA),6) >= "'+cAnoMesR+'"'
cArqNtx1  := Subs(CriaTrab(NIL,.F.),1,7)+"A"
IndRegua("SRE",cArqNtx1,cIndCond,,cFor,STR0014)		//"Selecionando Registros..."
DbGoTop()

nTamRA_FILIAL := GetSx3Cache("RA_FILIAL", "X3_TAMANHO") 

While ! Eof()
	cAnoMesAux := MesAno(SRE->RE_DATA)
	If SRE->RE_EMPD == FWGrpCompany("SRE") .And. SRE->RE_EMPP == FWGrpCompany("SRE")
		
		If lItemClVl
			aAdd( aArqSRE, { { SRE->RE_EMPD, Substr(SRE->RE_FILIALD,1,nTamRA_FILIAL), SRE->RE_CCD, SRE->RE_MATD, cAnoMesAux, .F., SRE->RE_ITEMD, SRE->RE_CLVLD },;
					          {           "", 			   "",      "",           "", 	       "", .F., 	       "", 	       "" },;
	  						  { SRE->RE_EMPP, Substr(SRE->RE_FILIALP,1,nTamRA_FILIAL), SRE->RE_CCP, SRE->RE_MATP, cAnoMesAux, .F., SRE->RE_ITEMP, SRE->RE_CLVLP } } )		
		Else
			Aadd( aArqSRE, { { SRE->RE_EMPD, Substr(SRE->RE_FILIALD,1,nTamRA_FILIAL), SRE->RE_CCD, SRE->RE_MATD, cAnoMesAux, .F. },;
					          {           "", 			   "",      "",           "", 	       "", .F. },;
	  						  { SRE->RE_EMPP, Substr(SRE->RE_FILIALP,1,nTamRA_FILIAL), SRE->RE_CCP, SRE->RE_MATP, cAnoMesAux, .F. } } )
		EndIf					  
	EndIf
	dbSkip()
EndDo
dbSelectArea("SRE")
dbSetOrder(1)
RetIndex("SRE")
dbClearFilter()
fErase(cArqNtx1 + OrdBagExt())

For nSeqFor := 1 To Len(aArqSRE)
	If !Empty(aArqSRE[nSeqFor,_TAnter,_TEmp])
		// SE A TRANSFERENCIA OCORREU NO MES DE CALCULO SIGNIFICA QUE O
		// FUNCIONARIO ESTA ATUALMENTE NO CENTRO DE CUSTO DESTINO, CASO
		// CONTRARIO, INDICA QUE FOI PROCESSADO NO C. DE CUSTO ORIGEM.
		Aadd(aTrfAux, { ACLONE(aArqSRE[nSeqFor,_TAnter]), If(aArqSRE[nSeqFor,_TAnter,_TDta] == cAnoMesR,;
					      ACLONE(aArqSRE[nSeqFor,_TDest]), ACLONE(aArqSRE[nSeqFor,_TAnter])), ACLONE(aArqSRE[nSeqFor,_TDest]) })

		If !lItemClvl
			aArqSRE[nSeqFor,_TAnter] := { "", "", "", "", "", .F. } // Nao deve ser lido novamente
		Else
			aArqSRE[nSeqFor,_TAnter] := { "", "", "", "", "", .F., "", "" } // Nao deve ser lido novamente
		EndIf
		nPosTrf ++
		While .T.
			If !lItemClvl	
				cBusca  := aTrfAux[nPosTrf,_TDest,_TEmp]+aTrfAux[nPosTrf,_TDest,_TFil]+aTrfAux[nPosTrf,_TDest,_TCC]+aTrfAux[nPosTrf,_TDest,_TMat]
				nPosSre := Ascan(aArqSRE, { |X| X[_TAnter,_TEmp]+X[_TAnter,_TFil]+X[_TAnter,_TCC]+X[_TAnter,_TMat] == cBusca })
			Else
				cBusca  := aTrfAux[nPosTrf,_TDest,_TEmp]+aTrfAux[nPosTrf,_TDest,_TFil]+aTrfAux[nPosTrf,_TDest,_TCC]+aTrfAux[nPosTrf,_TDest,_TItem]+aTrfAux[nPosTrf,_TDest,_TClvl]+aTrfAux[nPosTrf,_TDest,_TMat]
				nPosSre := Ascan(aArqSRE, { |X| X[_TAnter,_TEmp]+X[_TAnter,_TFil]+X[_TAnter,_TCC]+X[_TAnter,_TItem]+X[_TAnter,_TClvl]+X[_TAnter,_TMat] == cBusca })
			EndIf		
	
			If nPosSre > 0
				If aArqSRE[nPosSre,_TAnter,_TDta] == cAnoMesR
					aTrfAux[nPosTrf,_TAtual] := ACLONE(aArqSRE[nPosSre,_TDest])
				EndIf
				aTrfAux[nPosTrf,_TDest]  := ACLONE(aArqSRE[nPosSre,_TDest])
				If !lItemClvl
					aArqSRE[nPosSre,_TAnter] := { "", "", "", "", "", .F. } // NAO DEVE SER LIDO NOVAMENTE
				Else
					aArqSRE[nPosSre,_TAnter] := { "", "", "", "", "", .F., "", "" } // NAO DEVE SER LIDO NOVAMENTE
				EndIf
			Else // SE ESTIVER FORA DO INTERVALO DE IMPRESSAO LIMPA MATRIZ
				If (((aTrfAux[nPosTrf,_TAnter,_TFil] < cFilDe .Or. aTrfAux[nPosTrf,_TAnter,_TFil] > cFilAte ) .Or.;  
				     (aTrfAux[nPosTrf,_TAnter,_TCC]  < cCcDe  .Or. aTrfAux[nPosTrf,_TAnter,_TCC]  > cCcAte )  .Or.;
				     (aTrfAux[nPosTrf,_TAnter,_TMat] < cMatDe .Or. aTrfAux[nPosTrf,_TAnter,_TMat] > cMatAte)) .and. aTrfAux[nPosTrf,_TEmp,_TDta] > cAnoMesR); 
				    .And.;
				    ((aTrfAux[nPosTrf,_TAtual,_TFil] < cFilDe .Or. aTrfAux[nPosTrf,_TAtual,_TFil] > cFilAte ) .Or.;
				    (aTrfAux[nPosTrf,_TAtual,_TCC]  < cCcDe  .Or. aTrfAux[nPosTrf,_TAtual,_TCC]  > cCcAte )  .Or.;
				    (aTrfAux[nPosTrf,_TAtual,_TMat] < cMatDe .Or. aTrfAux[nPosTrf,_TAtual,_TMat] > cMatAte));
				    .And.;
					((aTrfAux[nPosTrf,_TDest,_TFil] < cFilDe .Or. aTrfAux[nPosTrf,_TDest,_TFil] > cFilAte ) .Or.;
				    (aTrfAux[nPosTrf,_TDest,_TCC]  < cCcDe  .Or. aTrfAux[nPosTrf,_TDest,_TCC]  > cCcAte )  .Or.;
				    (aTrfAux[nPosTrf,_TDest,_TMat] < cMatDe .Or. aTrfAux[nPosTrf,_TDest,_TMat] > cMatAte) );
				    .Or.;
				    ( If( Empty(cTpc),.F., !fChkMatTpc( aTrfAux[nPosTrf,_TAtual,_TFil],aTrfAux[nPosTrf,_TAtual,_TMat],cTpc ) ) )
					If !lItemClvl
						aTrfAux[nPosTrf,_TAnter] := { "", "", "", "", "", .F. }
					Else
						aTrfAux[nPosTrf,_TAnter] := { "", "", "", "", "", .F., "", "" }
					EndIf
				EndIf
				Exit
			EndIf
		EndDo
	EndIf
Next nSeqFor
Aeval(aTrfAux,{ |x| If(!Empty(x[_TAnter,_TMat]), Aadd(aTransf, { x[_TAnter], x[_TAtual], x[_TDest] }), "") })
dbSelectArea(cAlias)

Return Nil


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fGpeProvis³ Autor ³ Emerson Rosa de Souza ³ Data ³ 10.08.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Cria constantes p/ utilizacao em GPEA070,GPER070 e GPEM070.³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GpeProvisao(uPar1,uPar2,uPar3,nPar4,uPar5)                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GpeProvisao(uPar1,uPar2,uPar3,uPar4,uPar5,aPar,cTpRtPr)

uPar5 := If(uPar5 == Nil, 1, uPar5)

// INDICA O TIPO DE PROVISAO (SERA GRAVADO NO CAMPO RT_TIPPROV)
Private _FerVenc  := 1  // Ferias Vencidas
Private _FerProp  := 2  // Ferias Proporcionais
Private _13Salar  := 3  // 13o Salario
Private _14Salar  := 4  // 14o Salario
Private _FerVMes  := 5	// Ferias Provisao Mes
Private _13SVMes  := 6  // 13o Provisao Mes
Private _RecVenc  := 7	// Recesso Vencido
Private _RecProp  := 8	// Recesso Proporcional	 
// INDICA A LINHA NA ORDEM EM QUE SERA APRESENTADA NO RELATORIO
Private _Anter    := 01  // Mes Anterior
Private _Corre    := 02  // Correcao
Private _NoMes    := 03  // No Mes
Private _Atual    := 04  // Mes Atual
Private _BxTrf    := 05  // Baixa de Transferencia
Private _BxFer    := 06  // Baixa de Ferias
Private _BX13O    := 06  // Baixa de 13o Salario
Private _Bx14o    := 06  // Baixa de 14o Salario
Private _BxRes    := 07  // Baixa de Rescisao
Private _TrfEnt   := 08  // Transferencia de Entrada
Private _TrfSai   := 09  // Transferencia de Saida
Private _BxTot    := 10  // Baixa Total
// INDICA A COLUNA NA ORDEM EM QUE SERA APRESENTADA NO RELATORIO
Private _Dias     := 1  // Dias de Ferias
Private _Avos     := 1  // Avos de 13o Salario
Private _Prov     := 2  // Valor da Provisao de Ferias ou Decimo Terceiro Salario
Private _Adic     := 3  // Adicionais
Private _1Ter     := 4  // Um Terco de Ferias
Private _1Par     := 4  // 1o Parcela do 13o Salario
Private _INSS     := 5  // INSS
Private _FGTS     := 6  // FGTS
Private _SalV	  := 7  // Media Salario Vac
Private _PIS      := 8  // PIS
// CONSTANTES QUE DEFINEM O NUMERO DE LINHAS E COLUNAS DO ARRAY
Private _Linhas   := 10  // Quantidade de Linhas ou Elementos
Private _Colunas  := 08  // Quantidade de colunas para cada Linha ou Elemento
// INDICA A POSICAO DAS INFORMACOES DE CABECALHO EM "aCabProv"  
Private _DatCalc  :=  1  // Data do calculo
Private _CentroC  :=  2  // Data do calculo
Private _DBsProv  :=  3  // Data base de ferias
Private _DFerVen  :=  4  // Dias de ferias vencidas
Private _DFerPro  :=  5  // Dias de ferias proporcionais
Private _DFerAnt  :=  6  // Dias de ferias antecipadas
Private _DFalVen  :=  7  // Dias de faltas vencidas
Private _DFalPro  :=  8  // Dias de faltas proporcionais
Private _MovProv  :=  9  // Movimentacao no mes
Private _SalProv  := 10  // Salario da provisao no mes
Private _Avos13S  := 11  // Avos de 13o salario 
Private _PStatus  := 12  // Status (Ativo/Excluido)
Private _CItem	  := 13  // Item Contabil
Private _Clvl	  := 14  // Classe de Valor
// INDICA OS TIPOS DE MOVIMENTACAO DO FUNCIONARIO NO MES
Private _Demitido := 1  // Demitido
Private _Cong_Fer := 2  // Congelado Ferias
Private _Cong_13s := 3  // Congelado 13 Salario
Private _Cong_F13 := 4  // Congelado Ferias e 13 Salario
Private _Trfe_Sai := 5  // Transferencia Saida 
Private _Trfe_Ent := 6  // Transferencia Entrada
// INDICA AS POSICOES DENTRO DO ARRAY aTransf
Private _TAnter   := 1  // Centro de Custo Anterior
Private _TAtual   := 2  // Centro de Custo Atual
Private _TDest    := 3  // Centro de Custo Destino
Private _TEmp     := 1  // Empresa
Private _TFil     := 2  // Filial
Private _TCC      := 3  // Centro de Custo
Private _TMat     := 4  // Matricula
Private _TDta     := 5  // Data da Transferencia
Private _TInc     := 6  // Funcionario ja incluido no arquivo temporario
Private _TItem	  := 7  // Item Contabil
Private _TClvl	  := 8  // Classe de Valor
Private cTpRtProv
Private lGeraPMes := .F.

If SuperGetMv("MV_RATPROV",,"N") == "S"
	lGeraPMes	  := fChkRHQBase()	// Verifica a existencia da tabela RHQ
Endif

If ValType(cTpRtPr) == "U" 
	cTpRtProv	:= Nil 
Else
	cTpRtProv	:= cTpRtPr
EndIf

If uPar5 == 1 		// Cadastro
	gp070Atu(uPar1,uPar2,uPar3)
ElseIf uPar5 == 2  // Relatorio de Ferias
	RptStatus({|lEnd| GP070Imp(@lEnd,uPar1,uPar2)},uPar3)
ElseIf uPar5 == 3  // Relatorio de 13o Salario
	RptStatus({|lEnd| GP090Imp(@lEnd,uPar1,uPar2)},uPar3)
ElseIf uPar5 == 4  // Calculo
	// EM GRID HAVERA UMA BARRA DE PROCESSAMENTO DA LIB //
	If lGrid
		MsAguarde({|lEnd| GPM070Processa(aPar)}, OemToAnsi(STR0051), OemToAnsi(STR0052)) //"Aguarde..."###"Preparando Informações para o GRID..."
	Else
		Processa({|| GPM070Processa(aPar)},uPar1,,.T.)
	EndIf
ElseIf uPar5 == 5  // Importacao do arquivo SRF para o novo SRT
//    Processa({|| fConvSRF()},uPar1)
ElseIf uPar5 == 6  // Geracao de lancamentos contabeis da provisao no arquivo SRZ
    Processa({|| fGeraProvSRZ(uPar2)},uPar1)
ElseIf uPar5 == 7  // Processa a diferencas do calculo de provisa
	Processa({|| fProvProc()},uPar1)
EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fChkDemit ³ Autor ³ Emerson Rosa de Souza ³ Data ³ 29.08.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Se nao existir identif. de rescisao,acerta posicao no array³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fChkDemit(aProvisao,aVerba,nTipoProv,nGravar,nZerar)       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fChkDemit(aProvisao,aVerba,nTipoProv,nGravar,nZerar)
Local aElem     := { _Prov, _Adic, _1Ter, _INSS, _FGTS, _PIS },nCnt
Local nProvBus  := If(nTipoProv == _FerProp,_FerVenc,nTipoProv)

If !fChkIdent(aVerba,nProvBus,{_BxRes},.F.)
   For nCnt := 1 To Len(aElem)
      aProvisao[nGravar,aElem[nCnt]] += aProvisao[nZerar,aElem[nCnt]]
      aProvisao[nZerar,aElem[nCnt]]  := 0
   Next nCnt
EndIf

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fChkProv()³ Autor ³ Equipe R.H.           ³ Data ³ 18/12/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Encontra diferencas entre dois meses na provisao           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso      ³ Esta funcao nao é utilizada no Protheus, mas é de uso       ³±±
±±³ 	     ³ externo para conferências.                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±*/
Function fChkProv()
nOrdem      := 2
cFilDe      := Space(FWGETTAMFILIAL)
cFilAte     := Replicate("z",FWGETTAMFILIAL)
cCcDe       := Space(9)
cCcAte      := Replicate("z",09)
cMatDe      := Space(6)
cMatAte     := Replicate("z",06)
cNomeDe     := Space(30)
cNomeAte    := Replicate("z",30)
cCateg      := "ACDEGHIJMPST"
Private aCodFol   := {}
Private aLogFile  := {}
Private aLogTitle := {}

nTipProv  := 1
dDtRefIni := CTOD("")
dDtRefFim := CTOD("")

If fSeleProv(@nTipProv,@dDtRefIni,@dDtRefFim) == 0 .Or. Empty(dDtRefIni) .Or. Empty(dDtRefFim)
	Return
EndIf

//CARREGA VARIAVEIS PRIVATES COMUNS A GPEA070,GPER070 E GPEM070
GPEProvisao(OemToAnsi(STR0083) + Substr(DTOC(dDtRefIni),4,7)+; // "PROCESSANDO DIFERENCAS - MESES "
            OemToAnsi(STR0084) + Substr(DTOC(dDtRefFim),4,7),,,,7) // " E "
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fProvProc ³ Autor ³ Equipe R.H.           ³ Data ³ 18/12/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Decricao ³ Processa as diferencas entre dois meses na provisao		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±*/
Static Function fProvProc()
Local cDbfIni := cNtxIni := cDbfFim := cNtxFim := ""
Local lSalInc     := .F.
Local lTrataTrf   := .F.
Local aTransf     := {}
Local aProvAux    := {}
Local aFer13o     := {}
Local lAchou,lProvOk,nCnt,nValProv,nValTot,cTitulo,cIndCond

// MONTA O ARQUIVO TEMPORARIO "TPRINI"  DO MES INICIAL
If !File("TPRINI.DBF")
	Processa({ || fMonta_TPR(@cDbfIni,@cNtxIni,nOrdem,dDtRefIni,@lSalInc,@lTrataTrf,@aTransf,)},OemToAnsi(STR0085) + Substr(DTOC(dDtRefIni),4,7)) //"GERANDO ARQUIVO DA PROVISAO - MES "
	dbSelectArea("TPR")
	dbCloseArea()
	DbUseArea(.T.,,cDbfIni,"TPRINI",.F.)
	dbSetIndex(cNtxIni+OrdBagExt())
Else
	DbUseArea(.T.,,"TPRINI","TPRINI",.F.)
	cIndCond := "PR_FILIAL + PR_CC + PR_MAT"
	cNtxIni  := Subst(CriaTrab(NIL,.f.),1,7)+"C"
	IndRegua("TPRINI",cNtxIni,cIndCond,,, OemToAnsi(STR0086)) // "Selecionando Registros..."
EndIf

// MONTA O ARQUIVO TEMPORARIO "TPRFIM"  DO MES FINAL
If !File("TPRFIM.DBF")
	Processa({ || fMonta_TPR(@cDbfFim,@cNtxFim,nOrdem,dDtRefFim,@lSalInc,@lTrataTrf,@aTransf,)}, OemToAnsi(STR0085) + Substr(DTOC(dDtRefFim),4,7)) //"GERANDO ARQUIVO DA PROVISAO - MES "
	dbSelectArea("TPR")
	dbCloseArea()
	DbUseArea(.T.,,cDbfFim,"TPRFIM",.F.)
	dbSetIndex(cNtxFim+OrdBagExt())
Else
	DbUseArea(.T.,,"TPRFIM","TPRFIM",.F.)
	cIndCond := "PR_FILIAL + PR_CC + PR_MAT"
	cNtxFim  := Subst(CriaTrab(NIL,.f.),1,7)+"C"
	IndRegua("TPRFIM",cNtxFim,cIndCond,,, OemToAnsi(STR0086)) // "Selecionando Registros..."
EndIf

cFilialAnt := Replicate("!", FWGETTAMFILIAL)

dbSelectArea("SRT")
dbSetOrder(2)

dbSelectArea("TPRINI")
dbGoTop()

// CARREGA REGUA DE PROCESSAMENTO
ProcRegua(RecCount())
While !Eof()
	// MOVIMENTA REGUA DE PROCESSAMENTO
	IncProc(OemToAnsi(STR0087) + TPRINI->PR_FILIAL+OemToAnsi(STR0088)+AllTrim(TPRINI->PR_CC)+OemToAnsi(STR0089)+TPRINI->PR_MAT) //"Filial: " ### " - C.Custo: " ### " - Mat: "
	If TPRINI->PR_FILIAL # cFilialAnt
		If !Fp_CodFol(@aCodFol,TPRINI->PR_FILIAL)
			Exit
		Endif
		cFilialAnt := TPRINI->PR_FILIAL
		// MONTA ARRAY COM O TIPO DE PROVISAO A PROCESSAR
		aFer13o := {}
		If nTipProv == 1 .Or. nTipProv == 3
			aAdd(aFer13o, { "1", aCodFol[130,1] })
		EndIf
		If nTipProv == 2 .Or. nTipProv == 3
			aAdd(aFer13o, { "3", aCodFol[136,1] })
		EndIf
	EndIf

	For nCnt := 1 To Len(aFer13o)
		lProvOk   := .F.
		nValProv  := 0
		If aFer13o[nCnt,1] == "1"
			//Provisao de Ferias Vencidas
			If SRT->(dbSeek(TPRINI->PR_FILIAL+TPRINI->PR_CC+TPRINI->PR_MAT+DTOS(dDtRefIni)+"1"+aFer13o[nCnt,2]))
				nValProv += SRT->RT_VALOR
				lProvOk  := .T.
			EndIf
			//Provisao de Ferias Proporcionais
			If SRT->(dbSeek(TPRINI->PR_FILIAL+TPRINI->PR_CC+TPRINI->PR_MAT+DTOS(dDtRefIni)+"2"+aFer13o[nCnt,2]))
				nValProv += SRT->RT_VALOR
				lProvOk  := .T.
			EndIf
		Else 			                // Provisao de 13o Salario
			If SRT->(dbSeek(TPRINI->PR_FILIAL+TPRINI->PR_CC+TPRINI->PR_MAT+DTOS(dDtRefIni)+"3"+aFer13o[nCnt,2]))
				nValProv += SRT->RT_VALOR
				lProvOk  := .T.
			EndIf
		EndIf
		If lProvOk
			lAchou  := .T.
			If !TPRFIM->(dbSeek(TPRINI->PR_FILIAL+TPRINI->PR_CC+TPRINI->PR_MAT))
				If !(MesAno(TPRINI->PR_ADMISSA) == MesAno(dDtRefFim)) .Or.;
				   !(MesAno(TPRINI->PR_DEMISSA) == MesAno(dDtRefIni))
				   	lAchou := .F.
				EndIf
			Else
				If aFer13o[nCnt,1] == "1"
					//Provisao de Ferias Vencidas/Proporcionais
					If !(SRT->(dbSeek(TPRFIM->PR_FILIAL+TPRFIM->PR_CC+TPRFIM->PR_MAT+DTOS(dDtRefFim)+"1"))) .And.;
					   !(SRT->(dbSeek(TPRFIM->PR_FILIAL+TPRFIM->PR_CC+TPRFIM->PR_MAT+DTOS(dDtRefFim)+"2")))
						lAchou := .F.
					EndIf
				Else
					//Provisao de 13o. Salario
					If !(SRT->(dbSeek(TPRFIM->PR_FILIAL+TPRFIM->PR_CC+TPRFIM->PR_MAT+DTOS(dDtRefFim)+"3")))
						lAchou := .F.
					EndIf
				EndIf
			EndIf
			If !lAchou
				aAdd(aProvAux, { aFer13o[nCnt,1], nValProv,;
								 OemToAnsi(STR0087)	+ TPRINI->PR_FILIAL +; // "Filial: "
							     OemToAnsi(STR0088)	+ TPRINI->PR_CC +; // " - C.Custo: "
						         OemToAnsi(STR0089)	+ TPRINI->PR_MAT+; // " - Mat: " 
						         OemToAnsi(STR0089)	+ DTOC(TPRINI->PR_ADMISSA)+; // " - Admissao: "
							     OemToAnsi(STR0090)	+ DTOC(TPRINI->PR_DEMISSA)+; // " - Demissao: "
		   					     OemToAnsi(STR0091)	+ Transform(nValProv,"@E 9,999,999.99") }) // " - Valor: "
			EndIf			
		EndIf
	Next nCnt
	dbSelectArea("TPRINI")
	dbSkip()
EndDo
aLogTitle := { OemToAnsi(STR0093)+; // "LOG DE OCORRENCIAS DO CALCULO DE PROVISOES - Entre os meses "
				Substr(DTOC(dDtRefIni),4,7) +  OemToAnsi(STR0084) + Substr(DTOC(dDtRefFim),4,7) }
				
// MONTA LOG DA PROVISAO DE FERIAS
If nTipProv == 1 .Or. nTipProv == 3
	nValTot := 0
	Aeval(aProvAux, { |X| If(X[1] == "1", nValTot += X[2], "") })
	cTitulo := OemToAnsi(STR0094) + LTRIM(Transform(nValTot,"@E 9,999,999.99")) // "PROVISAO DE FERIAS - VALOR DA DIFERENCA: "	
	aAdd(aLogFile, cTitulo)
	aAdd(aLogFile, Replicate("-", Len(cTitulo)))
	Aeval(aProvAux, { |X| If(X[1] == "1", aAdd(aLogFile, X[3]), "") })
	aAdd(aLogFile, "")
EndIf

// MONTA LOG DA PROVISAO DE 13o SALARIO
If nTipProv == 2 .Or. nTipProv == 3
	nValTot := 0
	Aeval(aProvAux, { |X| If(X[1] == "3", nValTot += X[2], "") })
	cTitulo := OemToAnsi(STR0095) + LTRIM(Transform(nValTot,"@E 9,999,999.99")) // "PROVISAO DE 13o. SALARIO - VALOR DA DIFERENCA: "
	aAdd(aLogFile, cTitulo)
	aAdd(aLogFile, Replicate("-", Len(cTitulo)))
	Aeval(aProvAux, { |X| If(X[1] == "3", aAdd(aLogFile, X[3]), "") })
	aAdd(aLogFile, "")
EndIf

// VERIFICA OS REGISTROS PERDIDOS DO SRT
Processa({|| fProcSRT()}, OemToAnsi(STR0096)) //"PROCURANDO REGISTROS PERDIDOS NO SRT..."

// APRESENTA TELA COM LOG DE ERROS
fMakeLog({aLogFile}, aLogTitle,, .T.)

// DELETA ARQUIVOS TEMPORARIOS		
dbSelectArea("TPRINI")
dbCloseArea()
fErase(cNtxIni + OrdBagExt())
fErase(cDbfIni + ".DBF")
dbSelectArea("TPRFIM")
dbCloseArea()
fErase(cNtxFim + OrdBagExt())
fErase(cDbfFim + ".DBF")

dbSelectArea("SRT")
dbSetOrder(1)

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ fProcSRT ³ Autor ³ Equipe R.H.           ³ Data ³ 26/10/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Verifica os registros perdidos no SRT                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±*/
Static Function fProcSRT()
Local cAlias   := ALIAS()
Local cTitPerd,cChavePerd

cTitPerd := OemToAnsi(STR0097) //"REGISTRO PERDIDOS ENCONTRADOS NO SRT - VERIFICAR CADA CASO"
aAdd(aLogFile, cTitPerd)
aAdd(aLogFile, Replicate("-", Len(cTitPerd)))

dbSelectArea("SRT")
dbSetOrder(1)

dbSelectArea("TPRFIM")
dbGoTop()

// CARREGA REGUA DE PROCESSAMENTO						 
ProcRegua(RecCount())

While !Eof()
	// MOVIMENTA REGUA DE PROCESSAMENTO
	IncProc(OemToAnsi(STR0087) + TPRFIM->PR_FILIAL+ OemToAnsi(STR0088)+AllTrim(TPRFIM->PR_CC)+OemToAnsi(STR0089)+TPRFIM->PR_MAT) //"Filial: " ### " - C.Custo: " ### " - Mat: "
	// BUSCA FUNCIONARIOS TRANSFERENCIA ENTRADA NO MES ANTERIOR, SE  ENCONTRADOS, DEVERAO SER EXCLUIDOS
	If TPRFIM->PR_TIPMOVI == 6 // Transferencia Entrada
		cChavePerd := TPRFIM->PR_FILIAL+TPRFIM->PR_MAT+TPRFIM->PR_CC+Left(DTOS(dDtRefIni),6)
		dbSelectArea("SRT")
		If dbSeek(cChavePerd)
			While SRT->RT_FILIAL+SRT->RT_MAT+SRT->RT_CC+Left(DTOS(RT_DATACAL),6)==cChavePerd
				aAdd(aLogFile, 	OemToAnsi(STR0087) + SRT->RT_FILIAL +; //  "Filial: "
							    	OemToAnsi(STR0088) + SRT->RT_CC +; // " - C.Custo: "
						         	OemToAnsi(STR0089) + SRT->RT_MAT+; // " - Mat: "
						            OemToAnsi(STR0098) + DTOC(SRT->RT_DATACAL)+; //" - Dt.Calculo: "
	   						     	OemToAnsi(STR0099) + Transform(RECNO(),"@E 9,999,999")+; // " - Registro do SRT: "
	   						     	OemToAnsi(STR0100) + If(SRT->RT_TIPPROV $ "1*2*7*8", "FERIAS", "13.SAL")+; // " - Provisao: "
   	   						        OemToAnsi(STR0101) + Transform(SRT->RT_VERBA,"999")+; // " - Verba: "
   	   						     	OemToAnsi(STR0092) + Transform(SRT->RT_VALOR,"@E 9,999,999.99")) // " - Valor: "
		 		dbSkip()
			EndDo		
		EndIf
	EndIf
	dbSelectArea("TPRFIM")
	dbSkip()
EndDo
dbSelectArea(cAlias)

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fSeleProv ³ Autor ³ Equipe R.H.           ³ Data ³ 26/10/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Decricao  ³ Monta dialogo para selecao com botoes de radio             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Usada somente pela função fChkProv                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±*/
Static Function fSeleProv(nOpcRadio,dDatIni,dDatFim)
Local nOpcAux
Local oRadio
Local oDlg
Local oGroup
Local oFont

nOpcAux   := nOpcRadio
nOpcRadio := 0

DEFINE FONT oFont1  NAME "Arial" SIZE 0,-12 BOLD
DEFINE FONT oFont2  NAME "Arial" SIZE 0,-13

DEFINE MSDIALOG oDlg FROM  094,001 TO 255,415 TITLE OemToAnsi(STR0067) PIXEL //"PROCESSAR DIFERENCAS NA PROVISAO"

@ 005,005 GROUP oGroup TO 060,080 LABEL OemToAnsi(STR0068) OF oDlg PIXEL COLOR CLR_BLUE //"PROVISAO"
oGroup:SetFont(oFont1)

@ 005,090 GROUP oGroup TO 060,200 LABEL OemToAnsi(STR0069) OF oDlg PIXEL COLOR CLR_BLUE //"DATA DO CALCULO"
oGroup:SetFont(oFont1)

@ 020,010 RADIO oRadio VAR nOpcAux ITEMS OemToAnsi(STR0064), OemToAnsi(STR0065),; //"FERIAS" ### "13O.SALARIO" 
		  OemToAnsi(STR0070) SIZE 60,010 OF oDlg PIXEL //"AMBAS"

@ 020,095 SAY OemtoAnsi(STR0071) SIZE 50,10 OF oDlg PIXEL FONT oFont2 COLOR CLR_BLUE //"DATA INICIAL: "
@ 018,145 MSGET oDtCalIni VAR dDatIni VALID !Empty(dDatIni)  SIZE 40,10 OF oDlg PIXEL
@ 040,095 SAY OemtoAnsi(STR0072)   SIZE 50,10 OF oDlg   PIXEL FONT oFont2 COLOR CLR_BLUE //"DATA FINAL: "
@ 038,145 MSGET oDtCalFim VAR dDatFim VALID !Empty(dDatFim)  SIZE 40,10 OF oDlg PIXEL

DEFINE SBUTTON FROM 065, 135 TYPE 1 ENABLE OF oDlg ACTION (nOpcRadio := nOpcAux, oDlg:End())
DEFINE SBUTTON FROM 065, 170 TYPE 2 ENABLE OF oDlg ACTION (nOpcRadio := 0,       oDlg:End())

ACTIVATE MSDIALOG oDlg CENTERED

Return nOpcRadio

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ fAdicLog ³ Autor ³ Emerson Rosa de Souza ³ Data ³ 03.05.02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Adiciona as ocorrencias do array aIdProvis no aLogFile     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fAdicLog(aIdProvis,cFilProc)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fAdicLog(aIdProvis,cFilProc)
Local lRet := .F.
Local nCnt,lPriVez
Local aIdObr,aIdNObr,cCondProv

//TESTA A EXISTENCIA DO PARAMETRO MV_TRFAMES
//ESTA SITUACAO OCORRE SOMENTE NA IMPORTACAO DO SALDO ANTERIOR
If Type("lTrfAMES") == "U"
	dDataRef	 := If(Type("dDataRef") == "U" , dDataBase , dDataRef)
	cTrfAMES     := (SuperGetMv("MV_TRFAMES",, Space(6))) //-- Ano/Mes para inicio das demonstracoes de entrada e saida de transferencias no conceito 1 (transfere saldo origem p/ destino)
	lTrfAMES     := (!Empty(cTrfAMES) .And. MesAno(dDataRef) >= cTrfAMES)
EndIf

// VERIFICAR SE EXISTE AO MENOS UM IDENTIFICADOR NAO CADASTRADO
If Ascan(aIdProvis, { |X| Empty(X[1]) }) > 0
	// ADICIONA AVISO INFORMANDO A FILIAL CANCELADA
	Aadd(aLogFile, { STR0020 + " [" + cFilProc + "] " + STR0021, "" }) //"CALCULO DA FILIAL [XX] CANCELADO !!!"
	// DEFINICAO DOS IDENTIFICADORES OBRIGATORIOS
   	aIdObr  := { { _FerVenc, _Atual }, { _13Salar, _Atual }, { _14Salar, _Atual },;
		         { _FerVenc, _Corre }, { _13Salar, _Corre }, { _14Salar, _Corre },;
	  	     	     { _FerVenc, _BxFer }}
	If lTrfAMES
		AADD(aIdObr, { 0, _BxTrf })
	EndIf
	// DEFINICAO DOS IDENTIFICADORES NAO OBRIGATORIOS. O ZERO INDICA QUE A BAIXA DE TRANSF. DEVERA SER AVALIADA PARA FERIAS/13o
	aIdNObr := { { 0, _BxTrf }, { _13Salar, _Bx13O }, { _14Salar, _Bx14o } }
	// Adiciona Identificadores Obrigatorios no LOG de ocorrencias  ³
	
	lPriVez  := .T.
	For nCnt := 1 To Len(aIdObr)
		If Ascan(aIdProvis, { |X| If(aIdObr[nCnt,1]==0,.T.,X[3]==aIdObr[nCnt,1]) .And. X[4]==aIdObr[nCnt,2] .And. Empty(X[1])}) > 0
			If lPriVez
				Aadd(aLogFile[1], "")
				Aadd(aLogFile[1], STR0022)          //"Cadastrar a verba correspondente para cada um dos identificadores relacionados abaixo"
				Aadd(aLogFile[1], "")
				If lTrfAMES
					Aadd(aLogFile[1], STR0026)       //"Os identificadores de transferencia devem obrigatoriamente ser criados pois o parametro MV_TRFAMES esta habilitado"
					Aadd(aLogFile[1], "")
				EndIf
				Aadd(aLogFile[1], "    " + STR0025) // "Verba   Ident   Descricao do Identificador"
				Aadd(aLogFile[1], "    -----   -----   --------------------------------------------------")
				lPriVez  := .F.
			EndIf
			Aeval(aIdProvis, { |X| If(If(aIdObr[nCnt,1]==0,.T.,X[3]==aIdObr[nCnt,1]) .And. X[4]==aIdObr[nCnt,2] .And. Empty(X[1]),;
   				   Aadd(aLogFile[1],Space(4)+X[1]+Space(5)+X[2]+Space(5)+fDesc("SX5","35"+X[2],"X5DESCRI()",50)), "")})
		EndIf
	Next nCnt
	
	// IDENTIFICADORES NAO OBRIGATORIOS, O USUARIO DEVERA CADASTRAR
	// TODOS OU EXCLUIR TODOS, NAO PODE HAVER MEIO TERMO.
	
	For nCnt := 1 To Len(aIdNObr)
		If Ascan(aIdProvis, { |X| If(aIdNObr[nCnt,1]==0,.T.,X[3]==aIdNObr[nCnt,1]) .And. X[4]==aIdNObr[nCnt,2] .And.  Empty(X[1])}) > 0 .And.;
		   Ascan(aIdProvis, { |X| If(aIdNObr[nCnt,1]==0,.T.,X[3]==aIdNObr[nCnt,1]) .And. X[4]==aIdNObr[nCnt,2] .And. !Empty(X[1])}) > 0
			If lPriVez
				Aadd(aLogFile[1], "")
				Aadd(aLogFile[1], STR0023)          //"Para que seja efetuado o tratamento das baixas de transferencia, cadastre a verba correspondente para"
				Aadd(aLogFile[1], STR0024)          //"cada identificador. Para que nao seja efetuado tratamento das baixas, exclua as verbas ja cadastradas."
				Aadd(aLogFile[1], "")
				Aadd(aLogFile[1], "    " + STR0025) // "Verba   Ident   Descricao do Identificador"
				Aadd(aLogFile[1], "    -----   -----   --------------------------------------------------")
				lPriVez := .F.
			EndIf
			Aeval(aIdProvis, { |X| If(If(aIdNObr[nCnt,1]==0,.T.,X[3]==aIdNObr[nCnt,1]) .And. X[4]==aIdNObr[nCnt,2],;
				   Aadd(aLogFile[1],Space(4)+X[1]+Space(5)+X[2]+Space(5)+fDesc("SX5","35"+X[2],"X5DESCRI()",50)), "")})
		EndIf
	Next nCnt
	// OS DOIS PRIMEIROS ELEMENTOS SE REFEREM AO SUBTITULO
	If Len(aLogFile[1]) > 2
		// SE FOR GRID ADICIONA O LOG E ABORTA O CALCULO
		If lGrid
			MsgLogGrid(aClone(aLogFile[1]), .T.)
		EndIf
		lRet := .T.
	Else
		aLogFile := {}
	EndIf
EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ fQrySRD  ³ Autor ³ Ricardo Duarte Costa  ³ Data ³ 01.02.07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Executa query filtrando verbas de salario e adicionais.    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ vide abaixo                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fQrySRD(cFilProc , cMatProc , cMesAnoProc)
Local nReg	:= 0
Local cCodAdicMs  := aCodFol[1297,1] + aCodFol[1299,1] + aCodFol[1313,1] + aCodFol[1315,1]; //ATS
					 + aCodFol[1301,1] + aCodFol[1303,1] + aCodFol[1317,1] + aCodFol[1319,1]; //PERICULOSIDADE
			   		 + aCodFol[1305,1] + aCodFol[1307,1] + aCodFol[1321,1] + aCodFol[1323,1]; //INSALUBRIDADE
			   		 + aCodFol[1309,1]  + aCodFol[1325,1]; // ADICIONAL CARGO
			   		 + aCodFol[1311,1] + aCodFol[1327,1] // ADICIONAL TRANSFERENCIA

Local cCodAdFer	:= aCodFol[1296,1] + aCodFol[1298,1] + aCodFol[1312,1] + aCodFol[1314,1]; //ATS
			   + aCodFol[1300,1] + aCodFol[1302,1] + aCodFol[1316,1] + aCodFol[1318,1]; //PERICULOSIDADE
			   + aCodFol[1304,1] + aCodFol[1320,1] + aCodFol[1322,1] + aCodFol[1306,1];//INSALUBRIDADE
			   	+ aCodFol[1308,1] + aCodFol[1324,1]; // Adicional Cargo
			    + aCodFol[1310,1] + aCodFol[1326,1] //ADICIONAL TRANSFERENCIA

Local cCodAdFol	:= aCodFol[318,1] + aCodFol[671,1] + aCodFol[672,1] + aCodFol[673,1] ; //BASE SAL MES
					 + aCodFol[001,1] + aCodFol[002,1] + aCodFol[003,1] + aCodFol[004,1] + aCodFol[005,1]; // ATS
					 + aCodFol[36,1]; // Pericul
					 + aCodFol[37,1] + aCodFol[38,1] + aCodFol[39,1];// Insalubridade
					 + aCodFol[984,1]; //Adcional Cargo
					 + aCodFol[988,1] //Adicional Transf
					 
Local cInAds	:= fSqlIN(StrTran(cCodAdicMs + cCodAdFer + cCodAdFol,Space(TamSX3("RV_COD")[1]),""),3)
//MONTA A QUERY PARA SELECAO DOS DADOS
If lFechouMes
	aStruSRD := SRD->(dbStruct())
	cWhere   := "SRD.RD_FILIAL = '"+cFilProc+"' AND SRD.RD_MAT = '"+cMatProc+"' AND "+;
				"SRD.RD_PD IN (" + cInAds + ") AND "+;
				"SRD.RD_DATARQ = '"+cMesAnoProc+"'"
	cWhere   := "%"+cWhere+"%"
	
	BeginSql alias cAliasPROC
		SELECT SRD.RD_FILIAL, SRD.RD_MAT, SRD.RD_PD, SRD.RD_DATARQ, SUM(SRD.RD_VALOR) AS RD_VALOR 
		FROM %table:SRD% SRD
		WHERE %exp:cWhere% AND SRD.%notDel% 
		GROUP BY RD_FILIAL, RD_MAT, RD_DATARQ, RD_PD
		ORDER BY RD_FILIAL, RD_MAT, RD_DATARQ, RD_PD
	EndSql

	//AJUSTA A ESTRUTURA DOS CAMPOS
	For nReg := 1 To Len(aStruSRD)
		If (aStruSRD[nReg][2] <> "C")
			TcSetField(cAliasPROC,aStruSRD[nReg][1],aStruSRD[nReg][2],aStruSRD[nReg][3],aStruSRD[nReg][4])
		EndIf
	Next nReg
Else	
	aStruSRC := SRC->(dbStruct())
	cWhere   := "SRC.RC_FILIAL = '"+cFilProc+"' AND SRC.RC_MAT = '"+cMatProc+"' AND "+;
				"SRC.RC_PD IN (" + cInAds + ") "
	cWhere   := "%"+cWhere+"%"
	
	BeginSql alias cAliasPROC
		SELECT SRC.RC_FILIAL, SRC.RC_MAT, SRC.RC_PD, SUM(SRC.RC_VALOR) AS RC_VALOR 
		FROM %table:SRC% SRC
		WHERE %exp:cWhere% AND SRC.%notDel% 
		GROUP BY RC_FILIAL, RC_MAT, RC_PD
		ORDER BY RC_FILIAL, RC_MAT, RC_PD
	EndSql

	//AJUSTA A ESTRUTURA DOS CAMPOS
	For nReg := 1 To Len(aStruSRC)
		If (aStruSRC[nReg][2] <> "C")
			TcSetField(cAliasPROC,aStruSRC[nReg][1],aStruSRC[nReg][2],aStruSRC[nReg][3],aStruSRC[nReg][4])
		EndIf
	Next nReg
Endif	

return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ fCarAdics³ Autor ³ Ricardo Duarte Costa  ³ Data ³ 04.04.07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Inicializa as variaveis de Adic.Tpo.Servico e Insalubridade³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ vide abaixo                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fCarAdics()
Local cAdtPoSer	:= SRA->RA_ADTPOSER
Local cCod		:= Space(3)
Local nx		:= 0

//TRATAMENTO DO ADICIONAL DE TEMPO DE SERVICO
cAdtPoSer	:= alltrim(cAdtPoSer)
cAdtPoSer	:= strtran(cAdtPoSer,"*","")
For nx := 1 to len(cAdtPoSer)
   If Substr(cAdtPoSer,nx,1) == "A"
		cCodAdt := aCodfol[1,1]
	ElseIf Substr(cAdtPoSer,nx,1) == "B"
		cCodAdt := aCodfol[2,1]
	ElseIf Substr(cAdtPoSer,nx,1) == "T"
		cCodAdt := aCodfol[3,1]
	ElseIf Substr(cAdtPoSer,nx,1) == "D"
		cCodAdt := aCodfol[4,1]
	ElseIf Substr(cAdtPoSer,nx,1) == "Q"
		cCodAdt := aCodfol[5,1]
	EndIf
Next nx

//TRATAMENTO DA INSALUBRIDADE
// INSAL. MINIMA
If SRA->RA_ADCINS == "2"
	cCod   := aCodfol[37,1]
EndIf
// INSAL. MEDIA
If SRA->RA_ADCINS == "3"
	cCod   := aCodfol[38,1]
EndIf
// INSAL. MAXIMA
If SRA->RA_ADCINS == "4"
	cCod   := aCodfol[39,1]
EndIf
// DEFINE O CODIGO DA INSALUBRIDADE UTILIZADA PELO FUNCIONARIO
If cCod # Space(3)
	cCodIns := cCod
Endif

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ CalcGrid ºAutor  ³Jonatas A. T. Alves º Data ³  18/08/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Inclusao do Calculo Atraves de GRID - Multiprocessamento    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Gpem070                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function CalcGrid(aCall,aParametros,aArqTPR,aAreaSRA)
Local aParAmb			:= {}
Local lRet 			:= .T.
Local cMsgGrid 		:= ''	// Variavel para retorno de Log
Local nI, nT, nX, nZ
Local nTimeFim
Local nQtde
Local nIni
Local nTam
Local lNewGrid			:= FindFunction("GridCliVersion")
Local aProcessoTit  	:= {}
Local aProcessoLog  	:= {}
Local aGpem070TitLog	:= {}
Local aGpem070Log		:= {}
Local aLogErro 		:= {}
Local nLoop
//NO NOVO BUILD DO GRID, O TERCEIRO ELEMENTO DO ARRAY AINFOPROC E UM IDENTIFICADOR DO AGENTE
//UTILIZADO E O QUARTO ELEMENTO, CASO EXISTA, EH O VALOR CORRESPONDENTE AO RETORNO DO PROCESSO
Local nGridLog		:= If(lNewGrid, 4, 3) // Se for GRID antigo o log fica na posicao 3, senao fica na posicao 4
Private oGrid

//ADICIONA INFORMACOES NO LOG DE PROCESSAMENTO PARA GRID
aAdd(aProcessoTit , STR0056) // "Informacoes do Processo de Calculo com GRID"
aAdd(aProcessoLog , STR0057 + " " + SecsToTime(nTimeIni) + " " + STR0046 + " " + Dtoc(MsDate())) // "Inicio do Processo de Calculo:" ## "de"

// ARRAY DE LOG:											       
//  1-ERROS NA ROTINA DE PREPARACAO DO GRID					   
//  2-ERROS FATAIS DO GRID - METODO AERRORPROC				   
//  3-ERRO EM UM LOTE DO GRID - METODO ASENDPROC				   
//  4-ERROS NO LOTE DO GRID - TRATAMENTO FEITO NA FUNCAO 	   
//	 GPM070GRD	- RETORNO PELO METODO AINFOPROC
aAdd(aLogErro, { STR0058, {} }) // "Erros na preparacao do GRID"
aAdd(aLogErro, { STR0059, {} }) // "Erros fatais no processamento do GRID"
aAdd(aLogErro, { STR0060, {} }) // "Requisições não processadas"
aAdd(aLogErro, { STR0061, {} }) // "Log dos registros processados"

//CRIACAO DO OBJETO GRID E PROCESSAMENTO
dbSelectArea("TPR")
oGrid := GridClient():New()         

If ValType(oGrid) == "U"
	aAdd(aLogErro[1,2], OemToAnsi(STR0038)) // "FALHA NA CRIACAO DO OBJETO GRIDCLIENT. IMPOSSIVEL EXECUTAR PROCESSO EM GRID."
	lRet := .F.
EndIf
aTransf := {} // LIMPA ARRAY ATRANSF, POIS SERÁ CARREGADO VIA TABELA TEMPORARIA ASSOCIADA
If lRet
	aParAmb	:=	{	FWGrpCompany("SRA"),;
					FwCodFil(),;
					dDataBase,;
					aEmpresas,;
					cUsuario,;																	// Parametros Public do Sistema
			  		{aParametros,lFechouMes,lItemClVl,Nil,nFec131,cFilialAnt,;
			  		cNameFile,cDbfLog,cChaveLog,aIdProvis,lGrid,cFileTrf,cTrfDbf,nTamTrf,lFechou13},; 								// Parametros Privates de GPEM070
			  		{aTabFer,,cTpBxFer,cTpCongAf,cTrfAMES,lTrfAMES,cAbatAfas,;
			  		cPgSalMat,lDif13Neg,aTransf,lSalInc,lTrataTrf,c__Roteiro,cPerFeAc,;
			  		lProvResc,,cCalcSalInc,lCalcSalInc,cNomeDe,cNomeAte,aArqTPR,;
			  		aInssEmp,aCodFol,aInfo,aGPSPer,aVerba,nPerc1T,nPercPis,lTrfSld,dDataDem1,lGeraPMes,cTpRtProv,lSabDom,cAnoMes,;
			  		aPerFechado,cMes,cAno,lTemVenc,aPerAb13,aPrAb131,aPrAb132,aPerFe13,aPrFe131,aPrFe132},}
			  		
	// CHAMA A EXECUCAO EM GRID. CASO ELA RETORNE .F., OU O GRID NAO PODE SER INICIADO, 
	// OU ALGUM PROCESSO DEU ERRO, OU UM OU MAIS ITENS NAO FORAM PROCESSADOS.	
	lRet := oGrid:BatchExec("GPM070AMB",aParAmb,"GPM070GRD",aCall,"GPM070END")

	// FECHA A AREA 'TPR' E EXCLUI OS ARQUIVOS TEMPORARIOS DE INDICE E DE DADOS A ELA ASSOCIADOS
	TPR->(dbCloseArea())
	fErase(aArqTPR[2] + OrdBagExt())		// Indice TPR
	fErase(aArqTPR[1] + GetDbExtension())	// Tabela TPR
	// EFETUA DROP TABLE DA TABELA TEMPORARIA DO aTransf
	(cTrfDbf)->(dbCloseArea())
	If TCCanOpen(cFileTrf)
		MsErase(cFileTrf)
	EndIf
	If !lRet 
		If !Empty(oGrid:cErrorMsg)
			aAdd(aLogErro[1,2], oGrid:cErrorMsg) // CARREGA MENSAGENS GERADAS PELO GRID
		EndIf
		If !Empty(oGrid:aGridThreads)
			aAdd(aLogErro[1,2], OemToAnsi(STR0039)) // "NENHUM AGENTE DO GRID DISPONIVEL NO MOMENTO."
		EndIf
	EndIf
	// ERRO NO PROCESSAMENTO DO GRID - ERROS FATAIS
	If !Empty(oGrid:aErrorProc)
		// [1] : Numero sequencial da instrucao enviada que nao foi processada
		// [2] : Parametro enviado para processamento 
		// [3] : String contendo informacoes do ERRO
		nT := len(oGrid:aErrorProc)
		For nI := 1 to nT
			aAdd(aLogErro[2,2], OemToAnsi(STR0040) + " " + Str(oGrid:aErrorProc[nI,1],5)) // "Requisicao: "
			cMsgGrid := oGrid:aErrorProc[nI][3]
			nTamanho := Len(cMsgGrid)
			If nTamanho > 225
	            nTam := nTamanho / 225
                nQtde 	:= Int(nTam) + If((nTam - Int(nTam)) > 0, 1, 0)
                nIni 	:= 1
				For nX := 1 To nQtde
					aAdd(aLogErro[2,2], Substr(oGrid:aErrorProc[nI][3],nIni,225))
					nIni := nIni + 225
				Next nX
			Else
				aAdd(aLogErro[2,2], oGrid:aErrorProc[nI][3])
			EndIf
		Next nI
	Endif

	// REQUISICOES QUE NAO FORAM PROCESSADAS
	If !Empty(oGrid:aSendProc)
		// [1] : NUMERO SEQUENCIAL DA INSTRUCAO ENVIADA QUE NAO FOI PROCESSADA
		// [2] : PARAMETRO ENVIADO PARA PROCESSAMENTO 
		// [3] : RETORNO DA EXECUÇAO 
		nT := len(oGrid:aSendProc)
		For nI := 1 to nT
			aAdd(aLogErro[3,2], OemToAnsi(STR0040) + " " + Str(oGrid:aSendProc[nI,1],5)) // "Requisição: "
			aAdd(aLogErro[3,2], Space(10) + OemToAnsi(STR0041) + " " +; // "Parametros De/Ate:"
			  					  OemToAnsi(STR0033) + " " + oGrid:aSendProc[nI,2,1,2] + " " + OemToAnsi(STR0042) + " " + oGrid:aSendProc[nI,2,Len(oGrid:aSendProc[nI,2]),2] + "   "+; // "Filial" ## "a"
			  					  OemToAnsi(STR0034) + " " + oGrid:aSendProc[nI,2,1,3] + " " + OemToAnsi(STR0042) + " " + oGrid:aSendProc[nI,2,Len(oGrid:aSendProc[nI,2]),3]) // "Matrícula" ## "a"
		Next nI
	EndIf

	// RECUPERA RETORNOS DAS CHAMADAS - MENSAGENS DE LOG DO CALCULO (RETORNO DIFERENTE DE NIL DA FUNCAO GPM070GRD)
	If !Empty(oGrid:aInfoProc)
		// [1] : NUMERO SEQUENCIAL DA INSTRUCAO ENVIADA QUE NAO FOI PROCESSADA
		// [2] : PARAMETRO ENVIADO PARA PROCESSAMENTO 
		// [3] : RETORNO DA EXECUÇAO 
		nT := Len(oGrid:aInfoProc)
		For nI := 1 To nT
			aAdd(aLogErro[4,2], oGrid:aInfoProc[nI][nGridLog][1][1])	// GRAVACAO DO CABECALHO - FUNCIONARIO 
			For nZ := 1 To Len(oGrid:aInfoProc[nI][nGridLog][1][2])
				aAdd(aLogErro[4,2], oGrid:aInfoProc[nI][nGridLog][1][2][nZ][1]) // LOG DO ERRO
			Next nZ
		Next nI
	Endif
EndIf

//EXCLUIR OS PROCESSOS QUE TERMINARAM COM SUCESSO
cQuery := " DELETE FROM "
cQuery += " "+ cNameFile +" "
CQuery += " WHERE CHAVE = '" + cChaveLog + "'"
TcSqlExec(cQuery)

nTimeFim := Seconds()

aAdd(aProcessoLog , STR0045 + " " + SecsToTime(nTimeFim) + " " + STR0046 + " "  + Dtoc(MsDate())) // "Final do Processo de Calculo:" ## "de"
aAdd(aProcessoLog , STR0047 + " " + SecsToTime(nTimeFim - nTimeIni) ) // "Duracao do Processo de Calculo:"
aAdd(aProcessoLog , "")
aAdd(aProcessoLog , STR0048 + " " + Str(oGrid:nSendSeq,5)) // "Nro de Requisições Enviadas para Processamento:"
aAdd(aProcessoLog , STR0049 + " " + Str(oGrid:nSendSeq-Len(oGrid:aSendProc),5)) // "Nro de Requisições Processadas:"
aAdd(aProcessoLog , STR0050 + " " + Str(Len(oGrid:aSendProc),5)) // "Nro de Requisições Nao Processadas:"

//ADICIONA INFORMACOES DO PROCESSAMENTO NO LOG GERAL
aAdd(aGpem070TitLog	, aProcessoTit[1])
aAdd(aGpem070Log		, aClone(aProcessoLog))

//CARREGANDO INFORMACOES PARA O LOG
For nLoop := 1 To Len(aLogErro)
	If Len(aLogErro[nLoop,2]) > 0
   		aAdd(aGpem070TitLog	, aLogErro[nLoop , 1])
   		aAdd(aGpem070Log		, aClone(aLogErro[nLoop , 2]))
	EndIf
Next nLoop
	
//MOSTRAR O LOG
fMakeLog(aGpem070Log, aGpem070TitLog, NIL, NIL, FunName(), STR0062, NIL, NIL, NIL, .F.)

RestArea(aAreaSRA)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPM070AMB ºAutor  ³Jonatas A. T. Alves º Data ³  18/08/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Preparacao do ambiente para GRID - Multiprocessamento       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Gpem070                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GPM070AMB(aParms)
Local cEmpParm  	:= aParms[1]	// Empresa conectada 	--> cEmpAnt
Local cFilParm  	:= aParms[2]	// Filial conectada 	--> cFilAnt
Local dDataParm 	:= aParms[3]	// Data Base			--> dDataBase
Local aEmpParm		:= aParms[4]	// Array com empresas 	--> aEmpresas
Local cUsuaParm 	:= aParms[5]	// Usuario conectado	--> cUsuario
Local aMnemonicos	:= {}
Local lRet 		:= .T.
Local lShowErr	:= .F.
Local lSetDef
Local nMnemo
Local nMnemos
Local uVar

//PREPARACAO DE NOVO AMBIENTE - TABELAS E VARIAVEIS
RpcSetType(3)
PREPARE ENVIRONMENT EMPRESA (cEmpParm) FILIAL (cFilParm) MODULO "GPE" FUNNAME "GPEM070"

If (lRet := fMontaMnemo(cFilParm , @aMnemonicos))
	If (nMnemos:= Len(aMnemonicos)) > 0
		For nMnemo := 1 to nMnemos
			lSetDef := .F.
			//INICIALIZA E CARREGA OS VALORES PADROES DECLARANDO COMO PRIVATES NA FUNCAO QUE CHAMOU(1 NIVEL ACIMA)
			IF !Empty(uVar := AllTrim(aMnemonicos[nMnemo , 03]))
				IF (aMnemonicos[nMnemo , 04] == "D" .and. ("/" $ uVar))
					IF CheckExecForm({ || uVar := Ctod(uVar) },lShowErr)
						IF ((cType := ValType(uVar)) == aMnemonicos[nMnemo , 04])
							_SetOwnerPrvt(aMnemonicos[nMnemo , 01], uVar)
						Else
							lSetDef := .T.
						EndIF
					Else
						lSetDef := .T.
					EndIF
				ElseIF CheckExecForm(@uVar , lShowErr) 
					_SetOwnerPrvt(aMnemonicos[nMnemo , 01], uVar)
				Else
					lSetDef := .T.
				EndIF
			Else
				lSetDef := .T.
			EndIF
			If lSetDef
				_SetOwnerPrvt(aMnemonicos[nMnemo , 01], GetValType(aMnemonicos[nMnemo , 04]))
			EndIf
		Next nMnemo
	Endif     
	//VARIAVEIS PUBLICAS DO SISTEMA
	aEmpresas := aClone(aEmpParm)
	dDataBase := dDataParm
	cUsuario  := cUsuaParm
Endif	
// DECLARACAO DAS VARIAVEIS PRIVATE UTILIZADAS NA PREPARACAO DO AMBIENTE PARA PROCESSAMENTO EM GRID                
//| CARREGANDO PARAMETROS MV_PAR DA ROTINA
_SetOwnerPrvt('lFerias'		, aParms[6,1,1])	//  Tipo de Provisao 1 - Ferias 2 - 13o  3 - Ambas
_SetOwnerPrvt('l13oSal'		, aParms[6,1,2])	//  Tipo de Provisao 1 - Ferias 2 - 13o  3 - Ambas
_SetOwnerPrvt('dDataRef'	, aParms[6,1,3])	//  Data de Referencia para Calculo
_SetOwnerPrvt('nOrdem'		, aParms[6,1,4])	//  Ordem de Calculo 1 - Matricula  2 - Centro de Custo
_SetOwnerPrvt('cFilDe'		, aParms[6,1,5])	//	Filial De
_SetOwnerPrvt('cFilAte'		, aParms[6,1,6])	//	Filial Ate
_SetOwnerPrvt('cCcDe'		, aParms[6,1,7])	//	Centro de Custo De
_SetOwnerPrvt('cCcAte'		, aParms[6,1,8])	//	Centro de Custo Ate
_SetOwnerPrvt('cMatDe'		, aParms[6,1,9])	//	Matricula De
_SetOwnerPrvt('cMatAte'		, aParms[6,1,10])	//	Matricula Ate
_SetOwnerPrvt('nCorrecao'	, aParms[6,1,11])	//	Indice de Correcao
_SetOwnerPrvt('lDesc1parc'	, aParms[6,1,12])	//	Indice de Correcao
_SetOwnerPrvt('lIncluiDem'	, aParms[6,1,13])	//  Incluir Demit. 13o
_SetOwnerPrvt('n14Salario'	, aParms[6,1,14])	//  Indice de 14o Sal.
_SetOwnerPrvt('cCateg'		, aParms[6,1,15])	//	Categorias
_SetOwnerPrvt('nVerFatFin'	, aParms[6,1,16])	//	Verifica integracao
//CARREGANDO VARIAVEIS PRIVATE GENERICAS
_SetOwnerPrvt('lFechouMes'	, aParms[6,2])	//	Verifica se esta calculando provisao com mes fechado
_SetOwnerPrvt('lItemClVl'	, aParms[6,3])	//	Verifica se utiliza item contabil
// Posicao 4 livre para utilizacao de outra variavel - controle de item e classe em uma unica variavel
_SetOwnerPrvt('nFec131'		, aParms[6,5])	//	Mes de pagamento da 1a. parcela do 13o. salario
_SetOwnerPrvt('cFilialAnt'	, aParms[6,6])	//	
_SetOwnerPrvt('aIdProvis'	, aParms[6,10])	//	
_SetOwnerPrvt('lFechou13'	, aParms[6,15])	//	Verifica se esta calculando provisao com mes fechado
//Indica o tipo de provisao (sera gravado no campo RT_TIPPROV)
_SetOwnerPrvt('_FerVenc'	, 1 ) // Ferias Vencidas
_SetOwnerPrvt('_FerProp'	, 2 ) // Ferias Proporcionais
_SetOwnerPrvt('_13Salar'	, 3 ) // 13o Salario
_SetOwnerPrvt('_14Salar'	, 4 ) // 14o Salario  
_SetOwnerPrvt('_FerVMes'   	, 5	) // Ferias Provisao Mes
_SetOwnerPrvt('_13SVMes'   	, 6 ) // 13o Provisao Mes
_SetOwnerPrvt('_RecVenc'  	, 7	) // Recesso Vencido
_SetOwnerPrvt('_RecProp'  	, 8	) // Recesso Proporcional	 
//INDICA A LINHA NA ORDEM EM QUE SERA APRESENTADA NO RELATORI
_SetOwnerPrvt('_Anter'		, 01) // Mes Anterior
_SetOwnerPrvt('_Corre'		, 02) // Correcao
_SetOwnerPrvt('_NoMes'		, 03) // No Mes
_SetOwnerPrvt('_Atual'		, 04) // Mes Atual
_SetOwnerPrvt('_BxTrf'		, 05) // Baixa de Transferencia
_SetOwnerPrvt('_BxFer'		, 06) // Baixa de Ferias
_SetOwnerPrvt('_Bx13O'		, 06) // Baixa de 13o Salario
_SetOwnerPrvt('_Bx14o'		, 06) // Baixa de 14o Salario
_SetOwnerPrvt('_BxRes'		, 07) // Baixa de Rescisao
_SetOwnerPrvt('_TrfEnt'	, 08) // Transferencia de Entrada
_SetOwnerPrvt('_TrfSai'	, 09) // Transferencia de Saida
_SetOwnerPrvt('_BxTot'		, 10) // Baixa Total
//INDICA A COLUNA NA ORDEM EM QUE SERA APRESENTADA NO RELATORIO
_SetOwnerPrvt('_Dias'		, 1 ) // Dias de Ferias
_SetOwnerPrvt('_Avos'		, 1 ) // Avos de 13o Salario
_SetOwnerPrvt('_Prov'		, 2 ) // Valor da Provisao de Ferias ou Decimo Terceiro Salario
_SetOwnerPrvt('_Adic'		, 3 ) // Adicionais
_SetOwnerPrvt('_1Ter'		, 4 ) // Um Terco de Ferias
_SetOwnerPrvt('_1Par'		, 4 ) // 1o Parcela do 13o Salario
_SetOwnerPrvt('_INSS'		, 5 ) // INSS
_SetOwnerPrvt('_FGTS'		, 6 ) // FGTS
_SetOwnerPrvt('_SalV'		, 7 ) // Media Salario Vac
_SetOwnerPrvt('_PIS'		, 8 ) // PIS
//CONSTANTES QUE DEFINEM O NUMERO DE LINHAS E COLUNAS DO ARRAY
_SetOwnerPrvt('_Linhas'	, 10) // Quantidade de Linhas ou Elementos
_SetOwnerPrvt('_Colunas'	, 08) // Quantidade de colunas para cada Linha ou Elemento
//INDICA A POSICAO DAS INFORMACOES DE CABECALHO EM "aCabProv"   
_SetOwnerPrvt('_DatCalc'	,  1) // Data do calculo
_SetOwnerPrvt('_CentroC'	,  2) // Data do calculo
_SetOwnerPrvt('_DBsProv'	,  3) // Data base de ferias
_SetOwnerPrvt('_DFerVen'	,  4) // Dias de ferias vencidas
_SetOwnerPrvt('_DFerPro'	,  5) // Dias de ferias proporcionais
_SetOwnerPrvt('_DFerAnt'	,  6) // Dias de ferias antecipadas
_SetOwnerPrvt('_DFalVen'	,  7) // Dias de faltas vencidas
_SetOwnerPrvt('_DFalPro'	,  8) // Dias de faltas proporcionais
_SetOwnerPrvt('_MovProv'	,  9) // Movimentacao no mes
_SetOwnerPrvt('_SalProv'	, 10) // Salario da provisao no mes
_SetOwnerPrvt('_Avos13S'	, 11) // Avos de 13o salario 
_SetOwnerPrvt('_PStatus'	, 12) // Status (Ativo/Excluido)
_SetOwnerPrvt('_CItem'		, 13) // Item Contabil
_SetOwnerPrvt('_Clvl'		, 14) // Classe de Valor
//INDICA OS TIPOS DE MOVIMENTACAO DO FUNCIONARIO NO MES
_SetOwnerPrvt('_Demitido'	, 1 ) // Demitido
_SetOwnerPrvt('_Cong_Fer'	, 2 ) // Congelado Ferias
_SetOwnerPrvt('_Cong_13s'	, 3 ) // Congelado 13 Salario
_SetOwnerPrvt('_Cong_F13'	, 4 ) // Congelado Ferias e 13 Salario
_SetOwnerPrvt('_Trfe_Sai'	, 5 ) // Transferencia Saida 
_SetOwnerPrvt('_Trfe_Ent'	, 6 ) // Transferencia Entrada
// INDICA AS POSICOES DENTRO DO ARRAY aTransf
_SetOwnerPrvt('_TAnter'		, 1 ) // Centro de Custo Anterior
_SetOwnerPrvt('_TAtual'		, 2 ) // Centro de Custo Atual
_SetOwnerPrvt('_TDest'		, 3 ) // Centro de Custo Destino
_SetOwnerPrvt('_TEmp'		, 1 ) // Empresa
_SetOwnerPrvt('_TFil'		, 2 ) // Filial
_SetOwnerPrvt('_TCC'		, 3 ) // Centro de Custo
_SetOwnerPrvt('_TMat'		, 4 ) // Matricula
_SetOwnerPrvt('_TDta'		, 5 ) // Data da Transferencia
_SetOwnerPrvt('_TInc'		, 6 ) // Funcionario ja incluido no arquivo temporario  
_SetOwnerPrvt( '_TItem'		, 7  ) // Item Contabil 
_SetOwnerPrvt( '_TClvl'		, 8  ) // Classe de valor 
// VARIAVEIS PARA CRIACAO DOS ARQUIVOS TEMPORARIOS LOG/TRP/TPR
_SetOwnerPrvt('lGrid'		, aParms[6,11])	//	Mes de pagamento da 1a. parcela do 13o. salario
_SetOwnerPrvt('aLogFile'	, {}         )		// Nome do Arquivo de Log de identif. - fAdicLog()
_SetOwnerPrvt('cNameFile'	, aParms[6,7])		// Nome do Arquivo de Log
_SetOwnerPrvt('cDbfLog'		, aParms[6,8])		// Alias do Arquivo de Log
_SetOwnerPrvt('cChaveLog'	, aParms[6,9])		// Chave do Arquivo de Log
_SetOwnerPrvt('cFileTrf'	, aParms[6,12])		// Tabela temporaria de transferencias
_SetOwnerPrvt('cTrfDbf'		, aParms[6,13])		// Alias de transferencia
_SetOwnerPrvt('nTamTrf'		, aParms[6,14])		// No. de elementos total de aTransf
_SetOwnerPrvt('cArqDbf'		, "") 				// Arquivo DBF para Medias
_SetOwnerPrvt('cArqNtx'		, "") 				// Arquivo temporario de indice para Medias
_SetOwnerPrvt('cTPRDbf'		, aParms[7,21,1]) // Arquivo DBF para Medias
_SetOwnerPrvt('cTPRNtx'		, aParms[7,21,2]) // Arquivo temporario de indice para Medias
// VARIAVEIS PRIVATE DE GPM070PROCESSA
_SetOwnerPrvt('aTabFer'		, aParms[7, 1]) // Tabela para calculo dos dias de ferias
_SetOwnerPrvt('cTpBxFer'	, aParms[7, 3]) // Baixa Ferias Total ou Mes e Mes Seguinte
_SetOwnerPrvt('cTpCongAf'	, "2") 			// Trata congelamento por periodo Aquisitivo ou Por afastamento
_SetOwnerPrvt('cTrfAMES'	, aParms[7, 5]) // Ano/Mes para inicio das demonstracoes de entrada e saida de transferencias no conceito 1 (transfere saldo origem p/ destino)
_SetOwnerPrvt('lTrfAMES'	, aParms[7, 6]) // Indica se utilizara se utilizara demonstracoes de entrada e saida de transferencias no conceito 1 (transfere saldo origem p/ destino)
_SetOwnerPrvt('cAbatAfas'	, aParms[7, 7]) // Indica se abate avos por Afast. no 13o Salario
_SetOwnerPrvt('cPgSalMat'	, aParms[7, 8]) // Abater Afastamento Auxilio Maternidade no 13o
_SetOwnerPrvt('lDif13Neg'	, aParms[7, 9]) // Gerar diferenca de 13o. negativa S/N
_SetOwnerPrvt('aTransf'  	, aParms[7,10]) // Array com as transferencias do funcionario
_SetOwnerPrvt('lSalInc'		, aParms[7,11]) // Utiliza salario incorporado         
_SetOwnerPrvt('lTrataTrf'	, aParms[7,12]) // Utiliza conceito de transferencia de saldo demonstrando saida e entrada
_SetOwnerPrvt('c__Roteiro'	, aParms[7,13]) // Roteiro de calculo a ser processado
_SetOwnerPrvt('cPerFeAc'	, aParms[7,14]) // Usada na Funcao FCalcFimAq-(GpexMed).
_SetOwnerPrvt('lProvResc'	, aParms[7,15]) // Indica se devera provisionar no mes da rescisao
_SetOwnerPrvt('cCalcSalInc'	, aParms[7,17]) // Ano/Mes para inicio da busca dos salarios e adicionais no acumulado, sem a utilizacao da fSalInc.
_SetOwnerPrvt('lCalcSalInc'	, aParms[7,18]) // Define se busca os salarios e adicionais no acumulado, sem a utilizacao da fSalInc.
_SetOwnerPrvt('cNomeDe'		, aParms[7,19]) // Nome inicial para filtro na montagem do arquivo TPR
_SetOwnerPrvt('cNomeAte'	, aParms[7,20]) // Nome final para filtro na montagem do arquivo TPR
_SetOwnerPrvt('aInssEmp'	, aParms[7,22]) // Array com os percentuais e informacoes dos encargos da empresa
_SetOwnerPrvt('aCodFol'		, aParms[7,23]) // Array com os codigos de verbas que possuem identificador de calculo
_SetOwnerPrvt('aInfo'		, aParms[7,24]) // Arary com informacoes da empresa
_SetOwnerPrvt('aGPSPer'		, aParms[7,25]) // 
_SetOwnerPrvt('aVerba'		, aParms[7,26]) // 
_SetOwnerPrvt('nPerc1T'		, aParms[7,27]) // 
_SetOwnerPrvt('nPercPis'	, aParms[7,28]) // 
_SetOwnerPrvt('lTrfSld'		, aParms[7,29]) // Indica se eh demissao com transferencia e MV_PROVRES = N
_SetOwnerPrvt('dDataDem1'	, aParms[7,30]) // Data de demissao considerando MV_SABDOM
_SetOwnerPrvt('lGeraPmes'	, aParms[7,31]) // Gera rateio mensal
_SetOwnerPrvt('cTpRtProv'	, aParms[7,32]) // Determina Tipo de rateio / Alias a ser utilziado ("SRT*RHT")
_SetOwnerPrvt('lSabDom'		, aParms[7,33]) // Se pagara o sab e domingo qdo demissao na sexta
_SetOwnerPrvt('cAnoMes'		, aParms[7,34]) 
_SetOwnerPrvt('aPerFechado'	, aParms[7,35] 	) //
_SetOwnerPrvt('cMes'			, aParms[7,36] 	) //
_SetOwnerPrvt('cAno'			, aParms[7,37] 	) //
_SetOwnerPrvt('lTemVenc'		, aParms[7,38] 	) //
_SetOwnerPrvt('aPerAb13'		, aParms[7,39] 	) //
_SetOwnerPrvt('aPrAb131'		, aParms[7,40] 	) //
_SetOwnerPrvt('aPrAb132'		, aParms[7,41] 	) //
_SetOwnerPrvt('aPerFe13'		, aParms[7,42] 	) //
_SetOwnerPrvt('aPrFe131'		, aParms[7,43] 	) //
_SetOwnerPrvt('aPrFe132'		, aParms[7,44] 	) //

// VARIAVEIS PRIVATE DE GPM070PROCESSA CARREGADAS POR IniVarGrd
_SetOwnerPrvt('aFerVenc'	, Array(_Linhas,_Colunas)	) // 
_SetOwnerPrvt('aFerProp'	, Array(_Linhas,_Colunas)	) // 
_SetOwnerPrvt('aRecVenc'	, Array(_Linhas,_Colunas)	) // 
_SetOwnerPrvt('aRecProp'	, Array(_Linhas,_Colunas)	) // 
_SetOwnerPrvt('nV_DFalFer'	, 0		) // 
_SetOwnerPrvt('nP_DFalFer'	, 0		) // 
_SetOwnerPrvt('nTFaltaV'	, 0		) // 
_SetOwnerPrvt('nTFaltaP'	, 0		) // 
_SetOwnerPrvt('nDFerAnt'	, 0		) // 
_SetOwnerPrvt('cCodFer'		, ""	) // 
_SetOwnerPrvt('cCodAdFer'	, ""	) // 
_SetOwnerPrvt('cCodUmTer'	, ""	) // 
_SetOwnerPrvt('cCodFerMs'	, ""	) // 
_SetOwnerPrvt('cCodAdicMs'	, ""	) // 
_SetOwnerPrvt('cCodUmTerMs'	, ""	) // 
_SetOwnerPrvt('cCodAbono'	, ""	) // 
_SetOwnerPrvt('cCodAboMs'	, ""	) // 
_SetOwnerPrvt('cCodSalV'	, ""	) // 
_SetOwnerPrvt('cCodSalVMs'	, ""	) // 
_SetOwnerPrvt('dDtBasFer'	, CTOD("")	) // 
_SetOwnerPrvt('a13Salar'	, Array(_Linhas,_Colunas)	) // 
_SetOwnerPrvt('a14Salar'	, Array(_Linhas,_Colunas)	) // 
_SetOwnerPrvt('aRec13Sl'	, Array(_Linhas,_Colunas)	) // 
_SetOwnerPrvt('aRec14Sl'	, Array(_Linhas,_Colunas)	) // 
_SetOwnerPrvt('cCod13o'		, ""	) // 
_SetOwnerPrvt('cCodAd13o'	, ""	) // 
_SetOwnerPrvt('cInss13o'	, ""	) // 
_SetOwnerPrvt('lBx13Pgt'	, .F.	) // 
_SetOwnerPrvt('lDesAtiv'	, .F.	) // 
_SetOwnerPrvt('nAvosAnt'	, 0		) // 
_SetOwnerPrvt('nPercAnt'	, 0		) // 
_SetOwnerPrvt('nSalario'	, 0		) // 
_SetOwnerPrvt('nSalMes'		, 0		) // 
_SetOwnerPrvt('nSalDia'		, 0		) // 
_SetOwnerPrvt('nSalHora'	, 0		) // 
_SetOwnerPrvt('nPerEmp13'	, 0		) // 
_SetOwnerPrvt('nPercEmp'	, 0		) // 
_SetOwnerPrvt('nPercTer'	, 0		) // 
_SetOwnerPrvt('nPercAcTrab'	, 0		) // 
_SetOwnerPrvt('nPercFgts'	, 0		) // 
_SetOwnerPrvt('lDemitido'	, .F.	) // 
_SetOwnerPrvt('lTransfSai'	, .F.	) // 
_SetOwnerPrvt('aRoteiro'	, {}	) // 
_SetOwnerPrvt('nAdtServ'	, 0.00	) // Valor do Adicional por Tempo de Servico
_SetOwnerPrvt('nPeric'		, 0.00	) // Valor do Adicional de Periculosidade
_SetOwnerPrvt('nInsal'		, 0.00	) // Valor do Adicional de Insalubridade
_SetOwnerPrvt('nAdcConf'	, 0.00	) // Valor do Adicional de Cargo de Confiança
_SetOwnerPrvt('nAdcTransf'	, 0.00	) // Valor do Adicional de Transferência
_SetOwnerPrvt('nOutros'		, 0.00	) // Valor de outras verbas que incorporam
_SetOwnerPrvt('nSalHInc'	, 0.00  ) 
_SetOwnerPrvt('cCodAdt'		, Space(3)	) // 
_SetOwnerPrvt('cCodIns'		, Space(3)	) // 
_SetOwnerPrvt('nSalMin'		, 0		) // 
_SetOwnerPrvt('cRecFatEmp'	, ""	) // X14_RECFAT
_SetOwnerPrvt('lDissidio'	, .F. 	) //
_SetOwnerPrvt('lINSSAut'	, .F. 	) //

//CRIA ARQUIVO DE MEDIAS TEMPORARIO
Cria_TRP(@cArqDbf,@cArqNtx)

Return(lRet)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPM070END ºAutor  ³Jonatas A. T. Alves º Data ³  18/08/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Finalizacao da Thread na provisao                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Gpem070                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GPM070END()

// EXCLUSAO DOS ARQUIVOS TEMPORARIOS CRIADOS PARA MEDIA
TRP->(dbCloseArea())
fErase(cArqNtx + OrdBagExt())
fErase(cArqDbf + GetDbExtension())

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPM070GRD ºAutor  ³Mauricio Takakura   º Data ³  31/01/07   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao para Encapsulamento de Multiprocessamento            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Gpem070                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GPM070GRD(aCall)
Local aLog 		:= {} 	// Log por Lote enviado
Local aSvLog		:= {}
Local cFilCalc 			// Filial 
Local cMatrCalc			// Matricula
Local cCCCalc			// C.Custo
Local cChave
Local cCond		:= If(nOrdem == 1, 'TPR->(PR_FILIAL + PR_MAT)', 'TPR->(PR_FILIAL + PR_CC + PR_MAT)')
Local cRequis
Local cQuery
Local cMsgLog
Local lRet			:= .T.
Local nX
Local nY
Local nT
Local nTotReg
Local nTpMvCalc			// Tipo do movimento
Local nTrf
Local nEleTrf
Local uRetCalc

//TRATAMENTO PARA ARQUIVO DE LOG
If !(lRet := MsOpenDbf(.T. , "TOPCONN", cNameFile , cDbfLog , .T. , .F. , .F. , .F.))
	cMsgLog := CRLF + OemToAnsi(STR0043) // "Nao foi possivel abrir o Arquivo de Log de Registros"
	UserException(cMsgLog)		// Abortado o calculo pela execucao do Calculo
EndIf

IF !(lRet := (Select(cDbfLog) > 0))
	cMsgLog := CRLF + OemToAnsi(STR0043) // "Nao foi possivel abrir o Arquivo de Log de Registros"
	UserException(cMsgLog)		// Abortado o calculo pela execucao do Calculo
EndIF

If !(Select("TPR") > 0) //So abre o alias no primeiro calculo da Thread
	Open_TPR(cTPRDbf,cTPRNtx)
EndIf

If !(Select(cTrfDbf) > 0) //So abre o alias no primeiro calculo da Thread
	If !(lRet := MsOpenDbf(.T. , "TOPCONN", cFileTrf , cTrfDbf , .T. , .F. , .F. , .F.))
		cMsgLog := CRLF + OemToAnsi(STR0063) // "Nao foi possivel abrir o Arquivo de Log de Registros"
		UserException(cMsgLog)		// Abortado o calculo pela execucao do Calculo
	EndIf
EndIf

//PREPARACAO DO ARRAY ATRANSF A PARTIR DA TABELA TEMPORARIA
If Empty(aTransf)
	aTransf := Array(nTamTrf, 3, 6)
	dbSelectArea(cTrfDbf)
	While !(cTrfDbf)->(Eof())
		nTrf 	:= (cTrfDbf)->TRFPOS	 // Posicao no aTransf original
		nEleTrf := (cTrfDbf)->TRFELE	 // Posicao no aTransf original
		aTransf[nTrf, nEleTrf, 1] := (cTrfDbf)->TRFEMPP
		aTransf[nTrf, nEleTrf, 2] := (cTrfDbf)->TRFFILIALP
		aTransf[nTrf, nEleTrf, 3] := (cTrfDbf)->TRFCCP
		aTransf[nTrf, nEleTrf, 4] := (cTrfDbf)->TRFMATP
		aTransf[nTrf, nEleTrf, 5] := (cTrfDbf)->TRFDATE
		aTransf[nTrf, nEleTrf, 6] := (cTrfDbf)->TRFNLIDO
		(cTrfDbf)->(dbSkip())
	EndDo
EndIf

//INCLUIR O REGISTRO DA REQUISICAO NO LOG
DbSelectArea(cDbfLog)
cRequis := StrZero(aCall[1,1] , 8)
RecLock(cDbfLog , .T., .T.)
(cDbfLog)->CHAVE 	:= cChaveLog
(cDbfLog)->REQUIS 	:= cRequis 
(cDbfLog)->(MsUnLock())

nTotReg := Len(aCall)
For nX := 1 to nTotReg
	cFilCalc 	:= aCall[nX,2]	// Filial 
	cMatrCalc	:= aCall[nX,3]	// Matricula
	cCCCalc		:= aCall[nX,4]	// C.Custo
	nTpMvCalc	:= aCall[nX,5]	// Tipo do movimento

	//GARANTE SELECAO DAS AREAS UTILIZADAS
	dbSelectArea("SRT")
	SRT->(dbGoTop())
	
	dbSelectArea("SRC")
	dbSetOrder(1)
	SRC->(dbGoTop())
	
	dbSelectArea("SRA")
	dbSetOrder(1)
	SRA->(dbGoTop())
	dbSeek(cFilCalc + cMatrCalc, .F.)

	dbSelectArea("TPR")
	dbSetOrder(1)
	TPR->(dbGoTop())
	
	// O TPR PODE ESTAR NAS ORDENS 1 OU 2 NO CALCULO DA PROVISAO
	If nOrdem == 1
		cChave := cFilCalc + cMatrCalc
		dbSeek(cFilCalc + cMatrCalc, .F.)
	Else
		cChave := cFilCalc + cCCCalc + cMatrCalc
		dbSeek(cFilCalc + cCCCalc + cMatrCalc, .F.)
	EndIf

	//PODE HAVER MAIS DE UM REGISTRO DO FUNCIONARIO NO TPR, COM C.CUSTO E/OU TIPO DE MOVIMENTACAO DIFERENTES
	While !TPR->(Eof()) .And. &(cCond) == cChave
		If	TPR->(PR_CC == cCCCalc .And. PR_TIPMOVI	== nTpMvCalc)
			Exit
		Else
			TPR->(dbSkip())
			Loop
		EndIf
	EndDo
	
	//EXECUTA O CALCULO DA PROVISAO
	uRetCalc := GPM070CALG()

	// VERIFICA SE EXISTE MENSAGEM PARA ENVIO AO FINAL DO PROCESSAMENTO
	aSvLog 		:= GetMsgLogGrid()		// Retorna o Log de Erros
	lBreakCalc 	:= AbortCalc()			// Verifica se foi solicitado o cancelamento do calculo no LOG
	
	//VERIFICA OCORRENCIA DE MENSAGENS DE LOG DE RETORNO DA GPM070CALG()
	If (ValType(uRetCalc) == "L" .And. !uRetCalc) .Or. lBreakCalc
		nX := nTotReg
		cMsgLog := CRLF + CRLF 
		For nT := 1 To Len(aSvLog)
			For nY := 1 To Len(aSvLog[nT])
				cMsgLog += aSvLog[nT,nY] + CRLF
			Next nY
		Next nT
		If !lBreakCalc
			cMsgLog += CRLF + "Aborted by UserException" // Complemento da Mensagem que sera enviado pelo Gerenciamento do GRID
		Else
			cMsgLog += CRLF + "Calculate Process Aborted with UserException by" // Complemento da Mensagem que sera enviado pelo Gerenciamento do GRID
		EndIf
		UserException(cMsgLog)		// Abortado o calculo pela execucao do Calculo
	EndIf
	
	// OCORRENCIAS DE LOG QUE NAO ABORTARAM O CALCULO
	If !Empty(aSvLog)
		aAdd(aLog, { SRA->RA_FILIAL + "  " + SRA->RA_MAT + "  " + SRA->RA_NOME, aClone(aSvLog) })
		RstMsgLogGrid()
	EndIf

	//ATUALIZAR O LOG COM O ULTIMO FUNCION. PROCESSADO DA REQUISICAO
	cQuery := " UPDATE "
	cQuery += " "+ cNameFile +" "
	cQuery += " SET   CONTROL = '1',  "
	cQuery += " FILIAL = '" + SRA->RA_FILIAL + "', "
	cQuery += " MAT = '" + SRA->RA_MAT + "', "
	cQuery += " DT_GRAVA = '" + DTOC(MsDate()) + "', "
    cQuery += " HR_GRAVA = '" + Time() + "' "
    cQuery += " WHERE CHAVE = '" + cChaveLog + "'"
    cQuery += "   AND REQUIS = '" + cRequis + "'"
	TcSqlExec(cQuery)
Next nX

If Empty(aLog)
	aLog := NIL
EndIf

//ATUALIZAR O LOG COM O TIPO 3-FINALIZADO COM SUCESSO
cQuery := " UPDATE "
cQuery += " "+ cNameFile +" "
cQuery += " SET   CONTROL = '3' "
cQuery += " WHERE CHAVE = '" + cChaveLog + "'"
cQuery += "   AND REQUIS = '" + cRequis + "'"
TcSqlExec(cQuery)

(cDbfLog)->(DbCloseArea())
TPR->(DbCloseArea())

Return(aLog)	

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPM070CALGºAutor  ³Jonatas A. T. Alves º Data ³  18/08/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao para calculo da provisao utilizando GRID             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Gpem070                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
STATIC Function GPM070CALG()
Local cCongFeR 		:= "1" 						 		// Indica se congela por Afast. Acidente 
Local cCong13		:= "1" 							 	// Indica se congela por Afast. Doenca
Local cProcMed		:= ""
Local cArqBxa13,cFilBxa13,cMatBxa13,cDatBxa13,cPdBxa13,cValBxa13
Local nTipoMovMes  := 0
Local nPercFgC     := 0
Local aCabProv     := {}
Local aPerFerAux   := {}
Local cNomeCpo,cArqBxaPr,cFilBxaPr,cMatBxaPr,cDatBxaPr,cPdBxaPr,cSeqBxaPr,cValBxaPr,cHrsBxaPr
Local lTemCab		:= .F.  
Local dRefMed		:= dDataRef
Local dBkpData		:= dDataRef
Local aTabBPC       := {}
Local nValBpc       := 0
Local lDepen        := .F.
Local Nx            := 0
Local nY            := 0
Local nPosAux		:= 0
Local nRecnoSRF		:= 0  
Local nDiasAf		:= 0 
Local lDiasAfAcum   := .T.
Local lPriVez       := .T.
Local lRCHExcl      := !( Empty( xFilial("RCH") ) )
Local cPdAdic      := ""
Local cPdsAts		:= ""
Local cPdsPer		:= ""
Local cPdsIns		:= ""
Local cPdsAdC		:= ""
Local cPdsAdT		:= ""
Local cFolAts		:= ""
Local cFolPer		:= ""
Local cFolIns		:= ""
Local cFolAdC		:= ""
Local cFolAdT		:= ""

IniVarGrd() // INICIALIZA VARIAVEIS PRIVATE COM CONTEUDOS PADROES

PcoIniLan("000091") // INICIALIZA A GRAVACAO DOS LANCAMENTOS DO SIGAPCO        

// QUEBRA FILIAL PARA BUSCAR AS TABELAS

If TPR->PR_FILIAL # cFilialAnt
	If lRCHExcl .Or. (!lRCHExcl .And. cFilialAnt == Replicate("!", FWGETTAMFILIAL) )
		// Carregar os periodos abertos (aPerAberto) e/ou fechados (aPerFechado), de acordo com a competencia de calculo
		fRetPerComp(cMes, cAno, xFilial("RCH",TPR->PR_FILIAL), Nil, fGetCalcRot("5"), @aPrAb131, @aPrFe131, Nil, Nil, Nil, Nil, .F.)//131
		fRetPerComp(cMes, cAno, xFilial("RCH",TPR->PR_FILIAL), Nil, fGetCalcRot("6"), @aPrAb132, @aPrFe132, Nil, Nil, Nil, Nil, .F.)//132
		aPerFe13 := aClone(aPrFe131)
		For nX := 1 to Len(aPrFe132)
			Aadd(aPerFe13,aClone(aPrFe132[nX]))
		Next nX
	
		aPerAb13 := aClone(aPrAb131)
		For nX := 1 to Len(aPrAb132)
			Aadd(aPerAb13,aClone(aPrAb132[nX]))
		Next nX
		
		fRetPerComp(cMes, cAno, xFilial("RCH",TPR->PR_FILIAL), Nil, fGetRotOrdinar(), @aPerAberto, @aPerFechado)
	EndIf
	
	If !Fp_CodFol(@aCodFol,TPR->PR_FILIAL)		 .Or.;
	   !fInfo(@aInfo,TPR->PR_FILIAL)
           	Return(.T.)
	Endif
	If !fInssEmp(TPR->PR_FILIAL,@aInssEmp,.F.,MesAno(dDataRef))
		MsgLogGrid(Ap5GetHelp("GR240SEMP"),.F.)
           	Return(.T.)
	EndIf
	//-- Volta o conteúdo dDataRef que foi informado nos parâmetros
	dDataRef := dBkpData
	//RESGATA OS PERCENTUAIS DE TERCEIROS ARMAZENADOS NA TABELA AUXILIAR S035
	fGPSVal(TPR->PR_FILIAL,"999999",@aGPSPer,"1")
	// LIMPA ARRAY DE IDENTIFICADORES UTILIZADOS NA CADASTRADOS
	aIdProvis := {}
	// CARREGA CODIGOS DE FERIAS/13o SALARIO PARA BAIXA DA PROVISAO
	fBusCodBx()
	// CARREGA OS IDENTIFICADORES DA PROVISAO
	fIdentProv(@aVerba,aCodFol)
	// VERIFICA A EXISTENCIA DOS IDENTIFICADORES DA PROVISAO
	fChkIdent(aVerba,_FerVenc,{_Atual,_BxFer,_BxTrf},.T.)
	fChkIdent(aVerba,_13Salar,{_Atual,_BxTrf},.T.)
	// VERIFICA A EXISTENCIA DOS IDENTIFICADORES DA PROVISAO MES
	If lGeraPMes
		fChkIdent(aVerba,_FerVMes,{_Atual},.T.)
		fChkIdent(aVerba,_13SVMes,{_Atual},.T.)
	Endif

	// VERIFICA A EXISTENCIA DOS IDENTIFICADORES DE BAIXA DE 13o
	lBx13Pgt := fChkIdent(aVerba,_13Salar,{_Bx13O},.T.)
	
	// VERIFICA A EXISTENCIA DOS IDENTIFICADORES DE CORRECAO
	If nCorrecao > 0
		fChkIdent(aVerba,_FerVenc,{_Corre},.T.)
		fChkIdent(aVerba,_13Salar,{_Corre},.T.)
	EndIf
	
	// VERIFICA A EXISTENCIA DOS IDENTIFICADORES DA PROVISAO 14 SAL
	If n14Salario > 0
		fChkIdent(aVerba,_14Salar,{_Atual},.T.)
	EndIf
	
	// BUSCA PERCENTUAL DO IDENTIFICADOR DE 1/3 DE FERIAS
	dbSelectArea("TPR")
	cFilAnt := cFilialAnt := TPR->PR_FILIAL
	nPerc1T := PosSrv(aCodFol[77,1],TPR->PR_FILIAL,"RV_PERC")

	// BUSCA O PERCENTUAL DO IDENTIFICADOR DO PIS
	nPercPis := PosSrv(aCodFol[229,1],TPR->PR_FILIAL,"RV_PERC") / 100
	
	// SE GEROU OCORRENCIAS ABORTA PARA APRESENTACAO DO LOG
	If fAdicLog(aIdProvis,TPR->PR_FILIAL)
		Return(.T.)
	EndIf
	
	cProcMed := ""
Endif

If cProcMed <> SRA->RA_PROCES
	cProcMed   := SRA->RA_PROCES
	lFechou13  := .F.
	lFechouMes := .F.
	For nY := 1 To Len(aPerFe13)
		If aPerFe13[ny,3] + aPerFe13[ny,4] == cMes + cAno .and. aPerFe13[ny,7] == cProcMed
			lFechou13 := .T.
		EndIf
	Next nY
	For nY := 1 To Len(aPerFechado)
		If aPerFechado[ny,3] + aPerFechado[ny,4] == cMes + cAno .and. aPerFechado[ny,7] == cProcMed
			lFechouMes := .T.
		EndIf
	Next nY
EndIf

// VARIAVEIS UTILIZADAS PARA BAIXA DE FERIAS E DE RESCISAO NAS PROVISOES DE FERIAS E DE 13o SALARIO (SRC OU SRD)
cArqBxaPr := "SRC"
cNomeCpo  := "SRC->RC_"
cDatBxaPr := &("{ || MesAno(dDataRef) }")
If lFechouMes
	cNomeCpo  := "SRD->RD_"
	cArqBxaPr := "SRD"
	cDatBxaPr := &("{ || " + cNomeCpo + "DATARQ }")
EndIf
cFilBxaPr := &("{ || " + cNomeCpo + "FILIAL }")
cMatBxaPr := &("{ || " + cNomeCpo + "MAT }"   )
cPdBxaPr  := &("{ || " + cNomeCpo + "PD }"    )
cSeqBxaPr := &("{ || " + cNomeCpo + "SEQ }"   )
cValBxaPr := &("{ || " + cNomeCpo + "VALOR }" )
cHrsBxaPr := &("{ || " + cNomeCpo + "HORAS }" )

// VARIAVEIS UTILIZADAS PARA BAIXA DE 13o SALARIO (SRI OU SRD)
If l13oSal
	cArqBxa13 := "SRC"
	cNomeCpo  := "SRC->RC_"
	cDatBxa13 := &("{ || Strzero(Year(SRC->RC_DATA),4) }")
	If lFechou13
		cNomeCpo  := "SRD->RD_"
		cArqBxa13 := "SRD"
		cDatBxa13 := &("{ || " + cNomeCpo + "DATARQ }")
	EndIf
	cFilBxa13 := &("{ || " + cNomeCpo + "FILIAL }")
	cMatBxa13 := &("{ || " + cNomeCpo + "MAT }"   )
	cPdBxa13  := &("{ || " + cNomeCpo + "PD }"    )
	cValBxa13 := &("{ || " + cNomeCpo + "VALOR }" )
EndIf

//| CARREGA TABELA PARA APURACAO DOS DIAS DE FERIAS - ATABFER    |
//| 1-MESES PERIODO    2-NRO PERIODOS   3-DIAS DO MES    4-FATOR |
fTab_Fer(@aTabFer)

// CARREGA VARIAVEIS PARA CALCULO DOS ENCARGOS
fEncargEmp(@nPercEmp, @nPercTer, @nPercAcTrab, @nPercFgts, nPercFgC, @cRecFatEmp, @nPerEmp13)

nRecnoSRF := 0
nDFerAnt  := 0								  // Dias de ferias antecipadas
dDtBasFer := TPR->PR_ADMISSA			      // Data Base Ferias Funcionario
dDtBasCal := dDtBasFer                        // Data Base utilizada no calculo (de acordo com data de referencia)
nDiasVenc := 0
nDiasProp := 0
nTDiasAfa := 0
cTipAfa   := " "

dbSelectArea("SRF")
SRF->(dbSetOrder(2))
If !SRF->(dbSeek(TPR->(PR_FILIAL + PR_MAT + FGETCODFOL("0072")))) .Or. (SRA->RA_CATFUNC $ "E*G" .And. ((!SRF->(dbSeek(TPR->(PR_FILIAL + PR_MAT + FGETCODFOL("0891"))))) .Or. ( (SRF->(dbSeek(TPR->(PR_FILIAL + PR_MAT + FGETCODFOL("0891")))) .And. mv_par01==2) )))  
	//SE NÃO ENCONTRAR AS VERBAS DE FERIAS OU RECESSO DE ESTAGIARIO EMITE LOG
	If !SRF->(dbSeek(TPR->(PR_FILIAL + PR_MAT + FGETCODFOL("0072")))) .Or. (SRA->RA_CATFUNC $ "E*G" .And. ((!SRF->(dbSeek(TPR->(PR_FILIAL + PR_MAT + FGETCODFOL("0891")))))))
		lSemSRF := .T.
	//SE FOR ESTAGIARIO E SELECIONOU PROVISAO DE 13º SALARIO EMITE LOG
	ElseIf SRA->RA_CATFUNC $ "E*G" .And. ( (SRF->(dbSeek(TPR->(PR_FILIAL + PR_MAT + FGETCODFOL("0891")))) .And. mv_par01==2) )
		l13Estag := .T. 
	Endif
	// Adiciona aviso informando funcionario nao calculado
	If lPriVez
		Aadd(aLogFile, { OemToAnsi(STR0080), "" } ) //"FUNCIONARIOS NAO CALCULADOS!"
		If lSemSRF 
			Aadd(aLogFile[1], OemToAnsi(STR0081)) //"Nao foi encontrado registro de periodo aquisitivo no Controle de Dias de Direito ( SRF )"
			lSemSRF := .F.
		Endif
		If l13Estag
			Aadd(aLogFile[1], OemToAnsi(STR0104)) //"Foi solicitada a Provisão de 13º Salário para Estagiário, o cálculo não será efetuado."
		Endif
		Aadd(aLogFile[1], "" )
		Aadd(aLogFile[1], OemToAnsi(STR0082)) // "Filial  Matricula   Nome                               Admissao"
		Aadd(aLogFile[1], "------  ---------   ---------------------------------  ----------" )
		lPriVez := .F.
	EndIf
	Aadd(aLogFile[1], TPR->(PR_FILIAL + "      " + PR_MAT + "      " + PR_NOME + "     " + DTOC(TPR->PR_ADMISSA)))
	Return(.T.)
Else
	// CARREGA PERIODOS AQUISITIVOS PARA CALCULO
	If lFerias
		dDataAux := dDtBasFer
		dFerOpen := CtoD("")
		aPerFerAux := {}
		cChaveSRF := TPR->PR_FILIAL + TPR->PR_MAT + If(TPR->PR_CATFUNC $ "E|G" ,FGETCODFOL("0891"),FGETCODFOL("0072"))
		
		SRH->(DbSetOrder(1))
		If lFechouMes .and. SRH->(DbSeek(SRA->RA_FILIAL + SRA->RA_MAT))
			While SRH->(!Eof() .and. RH_FILIAL + RH_MAT == SRA->RA_FILIAL + SRA->RA_MAT)
				If AnoMes(SRH->RH_DATAINI) >= AnoMes(dDataRef)
					If AnoMes(SRH->RH_DATAINI) > AnoMes(dDataRef)
						dFerOpen := SRH->RH_DATABAS //Grava a data do primeiro periodo aquisitivo pago após a data de referencia
					EndIf
					Exit
				EndIf
				SRH->(DbSkip())
			EndDo
		EndIf
		
		If lFechouMes .and. Empty(dFerOpen) .and. !(Empty(TPR->PR_DEMISSA))
			DbSelectArea("SRG")
			SRG->( DbSetOrder(3) )
			If SRG->( MsSeek(TPR->PR_FILIAL + TPR->PR_MAT + DToS(TPR->PR_DEMISSA) ) )
				DbSelectArea("SRR")
				SRR->( DbSetOrder(8) )
				If SRR->( MsSeek(TPR->PR_FILIAL + TPR->PR_MAT + "R" + DToS(SRG->RG_DTGERAR) + aCodFol[0086,1] ) )
					dFerOpen := SToD( SubStr(SRR->RR_NUMID,1,8) )
				ElseIf SRR->( MsSeek(TPR->PR_FILIAL + TPR->PR_MAT + "R" + DToS(SRG->RG_DTGERAR) + aCodFol[0087,1] ) )
					dFerOpen := SToD( SubStr(SRR->RR_NUMID,1,8) )
				EndIf
			EndIf
		EndIf

		lPriSRF := .T.
		While (cChaveSRF) == (SRF->RF_FILIAL + SRF->RF_MAT + SRF->RF_PD) .And. !SRF->(EOF())
			// SE EXISTIR AO MENOS UM PERIODO ABERTO, A CALC_FER GERA OS PERIODOS
			If ( ( SRF->RF_STATUS $ " 1" .and. (lPriSRF .Or.( SRF->RF_DFERVAT > 0 .or. SRF->RF_DFERAAT > 0  .or. SRF->RF_DVENPEN > 0 )) ) .or. ;//Carrega o primeiro periodo aquisitivo com dias vencidos ou a vencer
						(!SRF->RF_STATUS $ " 1" .and. !Empty(dFerOpen) .and. dFerOpen <= SRF->RF_DATABAS))
				If lPriSRF .and. !lFechouMes
					lPriSRF := .F.
					If SRH->(DbSeek(SRA->RA_FILIAL + SRA->RA_MAT + AnoMes(dDataRef)))
						If SRF->RF_DATABAS < dDataRef .And. (SRH->RH_DFERVEN <= SRH->RH_DFERIAS + SRH->RH_DABONPE)
							SRF->(dbSkip())
							Loop
						EndIf
					EndIf
				EndIf
				aAdd(aPerFerAux,{	SRF->RF_DATABAS	,;										  		 			// 01 - Inicio Database de Ferias
									If(Empty(SRF->RF_DATAFIM),fCalcFimAq(SRF->RF_DATABAS),SRF->RF_DATAFIM),;    // 02 - Final Database de Ferias
									SRF->RF_DFERVAT	,;															// 03 - Dias de ferias vencidas
									SRF->RF_DFERAAT	,;															// 04 - Dias de ferias a vencer
									0.00			,;															// 05 - Dias totais de afastamento por periodo
									SRF->RF_OBSERVA	,;															// 06 - Descricao do tipo de afastamento do periodo
									CtoD("")		,;															// 07 - Data de original de termino do p.aquisitivo quando houver prorrogacao do mesmo RWX
									If(Empty(SRF->RF_STATUS),"1",SRF->RF_STATUS),;								// 08 - Status do periodo de ferias:  1-Ativo (Vencidos/A vencer)/2-Prescrito (Perdido)/3-Pago
									CtoD("")		,;															// 09 - Data de Inicio do Proximo periodo caso seja um periodo perdido.
									0				,;															// 10 - Quantidade dias de deducao para o direito apurado no periodo
									SRF->RF_DVENPEN ,;     														// 11 - Dias Vencidos Pendentes
									SRF->RF_IVENPEN ,;     														// 12 - Data Inicia Vencido Pendente
									SRF->RF_FVENPEN ,;															// 13 - Data Inicia Vencido Pendente
									SRF->RF_DFERANT ,;     														// 14 - Dias de Ferias Antecipadas
									SRF->RF_DFALVAT ,;     														// 15 - Dias de Faltas Vencidas
									SRF->RF_DFALAAT ,; 				    										// 16 - Dias de Faltas a Vencer
									If(cPaisLoc$"VEN|EQU",SRF->RF_DBONVAT,NIL),;				 				// 17 - Dias de bono vencido
									If(cPaisLoc$"VEN|EQU",SRF->RF_DBONAAT,NIL),; 								// 18 - Dias de bono a Vencer
									0				,;															// 19 - Total de dias de ferias
									0				,;															// 20 - Total de dias de bonificacao
									0				,;															// 21 - Dias de Faltas vencidas bonificacao
									0				,;															// 22 - Dias de ¦Faltas a Vencer bonificacao		
									0				,;															// 23 - Dias de ausencia convertidos em ferias
									0				,;      													// 24 - Total de Dias de Ferias do Periodo
									SRF->RF_DIASANT ,;					      									// 25 - Dias Gozados Vencidos
									SRF->RF_DIASANT	,;	    													// 26 - Dias Gozados a Vencer 
									0               ,;      													// 27 - Dias Subsid. Vencidos
									0               ,;   														// 28 - Dias Subsid. a Vencer
									0				,; 															// 29 - Dias de Pagto. Minimo na Adm/Dem (cpo. RF_PAGOFER desabilitado 08/2012)
									SRF->( RECNO() ),;															// 30 - Recno do aquivo
									Iif(Type("SRF->RF_FERPAGA")<>"U",  SRF->RF_FERPAGA, 0) ,;					// 31 - Dias pagos em R$ na folha
									SRF->RF_DATAATU	;															
									})
				dDtBasFer := SRF->RF_DATABAS
				If !(MesAno(SRH->RH_DATAINI) == MesAno(dDataRef)) .and. ( Empty(dFerOpen) .or. (dFerOpen > SRF->RF_DATABAS))
					nDFerAnt  += SRF->RF_DFERANT
				EndIf
				//Quando o período esta pago mas as férias ocorreram após a data de referencia da provisão, não grava os dias pagos e deixa o período como ativo. 
				If (!SRF->RF_STATUS $ " 1" .and. !Empty(dFerOpen) .and. dFerOpen <= SRF->RF_DATABAS)
					aPerFerAux[Len(aPerFerAux),8] := "1"
				EndIf
				
				If !SRF->RF_STATUS $ " 1" .and. !Empty(dFerOpen) .and. dFerOpen == SRF->RF_DATABAS .and. Len(aPerFerAux) > 0
					Exit
				EndIf	
			Else
				If SRF->RF_DATAFIM <= dDataRef
					dDtBasFer := SRF->RF_DATAFIM + 1
				EndIf
				nDFerAnt  := 0
			EndIf	
			SRF->(dbSkip())
		EndDo

		aPerFerias  := {}
		
		If Len(aPerFerAux) > 0
			aAdd(aPerFerias,aPerFerAux[Len(aPerFerAux)]) //Recalcula apenas do último periodo aquisitivo aberto em diante
			dDtBasFer := aPerFerias[1][1]
		ElseIf AnoMes(TPR->PR_ADMISSA) == AnoMes(dDataRef) .or. !(Empty(TPR->PR_DEMISSA))//Se for no mes de admissao e não tiver dias de direito, força calculo dos dias de direito desde o inicio do periodo aquisitivo.
			dDtBasFer := dDataAux
		EndIf				

		nTDiasAfa := 0
  	    Calc_Fer(aPerFerias,If(lProvResc .And. TPR->PR_TIPMOVI == _Demitido,TPR->PR_DEMISSA,dDataRef),@nDiasVenc,@nDiasProp,@nTDiasAfa,,lDiasAfAcum,.F.,dDtBasFer)
  	    
		If Len(aPerFerias) == 0 .And. !l13oSal
			Return(.T.)
		EndIf
		
		If Len(aPerFerAux) > 0
		  	dDtBasFer := aPerFerAux[1][1]
			For nPosAux := 1 to Len(aPerFerias)
				If nPosAux == 1
					aPerFerAux[Len(aPerFerAux)] := aClone(aPerFerias[nPosAux])
				Else
					aAdd(aPerFerAux, aPerFerias[nPosAux])
				EndIf
			Next nPosAux
			aPerFerias := aPerFerAux
		EndIf
		
		//Recalcula Dias Vencidos e proporcionais de incluindo os periodos que não foram enviados para calc_fer
		nDiasVenc := 0
		nDiasProp := 0
		Aeval(aPerFerias,{|X| If(X[8] == "1",(nDiasVenc += X[3], nDiasProp += X[4]),nil)})			
		
		SRF->(dbSetOrder(1))
		For nX := 1 To Len(aPerFerias)
			If aPerFerias[nX,8] <> "4"
				If !SRF->(dbSeek(TPR->(PR_FILIAL + PR_MAT + DToS(aPerFerias[nX,1]) + FGETCODFOL("0072"))))
					SRF->(RecLock("SRF",.T.))
					SRF->RF_FILIAL  := TPR->PR_FILIAL
					SRF->RF_MAT     := TPR->PR_MAT
					SRF->RF_PD      := FGETCODFOL("0072")
					SRF->RF_DATABAS := aPerFerias[nX,1]
					SRF->RF_DATAFIM := aPerFerias[nX,2]
					SRF->RF_DFERVAT := aPerFerias[nX,3]
					SRF->RF_DIASDIR := aTabFer[3]
					SRF->RF_DFERAAT := aPerFerias[nX,4]
					SRF->RF_STATUS  := aPerFerias[nX,8]
					SRF->RF_DVENPEN := aPerFerias[nX,11]
					SRF->RF_IVENPEN := aPerFerias[nX,12]
					SRF->RF_FVENPEN := aPerFerias[nX,13]
					SRF->RF_DFERANT := aPerFerias[nX,14]
					SRF->RF_DFALVAT := aPerFerias[nX,15]
					SRF->RF_DFALAAT := aPerFerias[nX,16]
				EndIf
			Endif
			If (dDataRef >= SRF->RF_DATABAS .And. dDataRef <= SRF->RF_DATAFIM)
				nRecnoSRF := SRF->(Recno()) // Preserva o Recno da Tabela SRF p/posterior gravacao
				
				//Verificar as ausências no período
				fRetAfas(CTOD("01/01/"+Str(Year(dDataRef),4), "DDMMYY"),dDataRef,"PRFE",@nAvosAf,@nDiasAf,,@aAfast,,.F.)
				If Len(aAfast)> 0
 					dIniAfa := aAfast[1,3]
 					cCongFeR := aAfast[1,9]
					cCong13  := aAfast[1,10]
				Endif
			EndIf
			dbSelectArea("SRF")
			SRF->(MsUnlock()) 

		Next nX
		SRF->(DbGoTo(nRecnoSRF))
	EndIf
Endif
SRF->(dbSetOrder(1))

dbSelectArea("TPR")

// VERIFICA O TIPO DE MOVIMENTACAO DO FUNCIONARIO
nTipoMovMes := TPR->PR_TIPMOVI
lTransfSai  := (nTipoMovMes == _Trfe_Sai)
lDemitido   := (nTipoMovMes == _Demitido)

// 1o ELEMENTO DO ARRAY - A N T E R I O R
fQryDetSRT(aVerba,aTransf,dDataRef,lTrataTrf,.F.,lFerias,l13oSal) 

// 2o ELEMENTO DO ARRAY - C O R R E C A O
If nCorrecao > 0
	If lFerias
		fGrvArrPrv(aFerVenc, _Corre, _Anter, nCorrecao, { _Dias })
		fGrvArrPrv(aFerProp, _Corre, _Anter, nCorrecao, { _Dias })
	EndIf
	If l13oSal
		fGrvArrPrv(a13Salar, _Corre, _Anter, nCorrecao, { _Avos,_1Par })
		If n14Salario > 0
			fGrvArrPrv(a14Salar, _Corre, _Anter, nCorrecao, { _Avos,_1Par })
		EndIf
	Endif
Endif

// ZERA VARIAVEIS PARA UMA NOVA BUSCA DE SALARIO E FERIAS
    nV_DFalFer := nP_DFalFer := nTFaltaV := nTFaltaP := 0
    nSalario   := nSalMes    := nSalDia  := nSalHora := 0
    nAdtServ   := nPeric     := nInsal   := nAdcConf := nAdcTranfs := 0.00

// SE MES ESTIVER FECHADO, CALCULAR COM VALORES DO SRT
If lFechouMes .And. fBusCabSRT(dDataRef,@aCabProv)
	dDtBasFer  := aCabProv[_DBsProv]
	nDFerAnt   := aCabProv[_DFerAnt]
    nSalario   := aCabProv[_SalProv]
    nSalMes    := aCabProv[_SalProv]
    nSalDia    := aCabProv[_SalProv] / 30
    nSalHora   := aCabProv[_SalProv] / TPR->PR_HRSMES
    lTemCab	:= .T.
EndIf

// 6o ELEMENTO DO ARRAY - B A I X A  D E  F E R I A S / 13o
If (!lDemitido .Or. lProvResc) .And. !lTransfSai
	If lFerias
		fBxaFerProv(@dDtBasFer,@nDFerAnt)
	EndIf
	If l13oSal .And. lBx13Pgt .And. (Month(dDataRef) == 12) 
		fBxa13oProv()
	EndIf
EndIf

// 4o ELEMENTO DO ARRAY - A T U A L
If lTransfSai .and. lCalcSalInc
	If nSalMes == 0
		// CALCULA SALARIO MES , DIA , HORA DO FUNCIONARIO
		fSalario(@nSalario,@nSalHora,@nSalDia,@nSalMes,"A",MesAno(dDataRef))
	EndIf	
Else
	// CALCULA SALARIO INCORPORADO MES , DIA , HORA DO FUNCIONARIO
	If !lCalcSalInc
		cAliasPROC	:= "QPROC"
		fQrySRD(TPR->PR_FILIAL , TPR->PR_MAT , MesAno(dDataRef))
		cPdsAts	:=	aCodFol[1297,1] + "|" + aCodFol[1299,1] + "|" + aCodFol[1313,1] + "|" + aCodFol[1315,1] + "|" + ;
					aCodFol[1296,1] + "|" + aCodFol[1298,1] + "|" + aCodFol[1312,1] + "|" + aCodFol[1314,1]	
		cPdsPer	:=	aCodFol[1301,1] + "|" + aCodFol[1303,1] + "|" + aCodFol[1317,1] + "|" + aCodFol[1319,1] + "|" + ;
					aCodFol[1300,1] + "|" + aCodFol[1302,1] + "|" + aCodFol[1316,1] + "|" + aCodFol[1318,1]
		cPdsIns	:=	aCodFol[1304,1] + "|" + aCodFol[1320,1] + "|" + aCodFol[1322,1] + "|" + aCodFol[1306,1] + "|" + ;
					aCodFol[1305,1] + "|" + aCodFol[1307,1] + "|" + aCodFol[1321,1] + "|" + aCodFol[1323,1]
		cPdsAdC	:=	aCodFol[1308,1] + "|" + aCodFol[1324,1] + "|" + aCodFol[1309,1] + "|" + aCodFol[1325,1]
		cPdsAdT	:=	aCodFol[1310,1] + "|" + aCodFol[1326,1] + "|" + aCodFol[1311,1] + "|" + aCodFol[1327,1]
		
		cFolAts		:= aCodFol[001,1] + "|" + aCodFol[002,1] + "|" + aCodFol[003,1] + "|" + aCodFol[004,1] + "|" + aCodFol[005,1]
		cFolPer		:= aCodFol[36,1]
		cFolIns		:= aCodFol[37,1] + "|" + aCodFol[38,1] + "|" + aCodFol[39,1]
		cFolAdC		:= aCodFol[984,1]
		cFolAdT		:= aCodFol[988,1]
		
		cCodAdFol	:= aCodFol[318,1]+ "|" + cFolAts + "|" + cFolPer + "|" + cFolIns + "|" + cFolAdC + "|" + cFolAdT
		cPdAdic := cCodAdFol + "|" +  cPdsAts + "|" + cPdsPer + "|" + cPdsIns + "|" + cPdsAdC + "|" + cPdsAdT
		//-- Busca o salario mes, adic.tpo.servico, insalubridade e periculosidade
		While !(cAliasPROC)->(eof())
			If lFechouMes
				If !lTemCab .And. (cAliasPROC)->RD_PD == aCodFol[318,1]
					nSalMes += (cAliasPROC)->RD_VALOR
				ElseIf (cAliasPROC)->RD_PD == aCodFol[671,1]
					nAdtServ += (cAliasPROC)->RD_VALOR
				ElseIf (cAliasPROC)->RD_PD == aCodFol[672,1]
					nInsal += (cAliasPROC)->RD_VALOR
				ElseIf (cAliasPROC)->RD_PD == aCodFol[673,1]
					nPeric += (cAliasPROC)->RD_VALOR
				ElseIf (cAliasPROC)->RD_PD $ cPdAdic
					If (cAliasPROC)->RD_PD $ cPdsAts
						If PosSrv(aCodFol[001,1],(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S" .OR.;
							PosSrv(aCodFol[002,1],(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S" .OR.;
							PosSrv(aCodFol[003,1],(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S" .OR.;
							PosSrv(aCodFol[004,1],(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S" .OR.;
							PosSrv(aCodFol[005,1],(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S"
								nSalMes -= (cAliasPROC)->RD_VALOR
						EndIf
					ElseIf (cAliasPROC)->RD_PD $ cPdsPer
						If PosSrv(cFolPer,(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S"
								nSalMes -= (cAliasPROC)->RD_VALOR
						EndIf
					ElseIf (cAliasPROC)->RD_PD $ cPdsIns
						If PosSrv(aCodFol[37,1],(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S" .OR.;
							PosSrv(aCodFol[38,1],(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S" .OR.;
							PosSrv(aCodFol[39,1],(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S"
								nSalMes -= (cAliasPROC)->RD_VALOR
						EndIf
					ElseIf (cAliasPROC)->RD_PD $ cPdsAdC
						If PosSrv(cFolAdC,(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S"
							nSalMes -= (cAliasPROC)->RD_VALOR
						EndIf
					ElseIf (cAliasPROC)->RD_PD $ cPdsAdT
						If PosSrv(cFolAdT,(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S"
							nSalMes -= (cAliasPROC)->RD_VALOR
						EndIf
					Else
						If PosSrv((cAliasPROC)->RD_PD,(cAliasPROC)->RD_FILIAL,"RV_INCORP") == "S"
							nSalMes -= (cAliasPROC)->RD_VALOR
						EndIf
					EndIf
				Endif
			Else
				If !lTemCab .And. (cAliasPROC)->RC_PD == aCodFol[318,1]
					nSalMes += (cAliasPROC)->RC_VALOR
				ElseIf (cAliasPROC)->RC_PD == aCodFol[671,1]
					nAdtServ += (cAliasPROC)->RC_VALOR
				ElseIf (cAliasPROC)->RC_PD == aCodFol[672,1]
					nInsal += (cAliasPROC)->RC_VALOR
				ElseIf (cAliasPROC)->RC_PD == aCodFol[673,1]
					nPeric += (cAliasPROC)->RC_VALOR
				ElseIf (cAliasPROC)->RC_PD $ cPdAdic
					If (cAliasPROC)->RC_PD $ cPdsAts
						If PosSrv(aCodFol[001,1],(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S" .OR.;
							PosSrv(aCodFol[002,1],(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S" .OR.;
							PosSrv(aCodFol[003,1],(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S" .OR.;
							PosSrv(aCodFol[004,1],(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S" .OR.;
							PosSrv(aCodFol[005,1],(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S"
								nSalMes -= (cAliasPROC)->RC_VALOR
						EndIf
					ElseIf (cAliasPROC)->RC_PD $ cPdsPer
						If PosSrv(cFolPer,(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S"
								nSalMes -= (cAliasPROC)->RC_VALOR
						EndIf
					ElseIf (cAliasPROC)->RC_PD $ cPdsIns
						If PosSrv(aCodFol[37,1],(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S" .OR.;
							PosSrv(aCodFol[38,1],(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S" .OR.;
							PosSrv(aCodFol[39,1],(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S"
								nSalMes -= (cAliasPROC)->RC_VALOR
						EndIf
					ElseIf (cAliasPROC)->RC_PD $ cPdsAdC
						If PosSrv(cFolAdC,(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S"
							nSalMes -= (cAliasPROC)->RC_VALOR
						EndIf
					ElseIf (cAliasPROC)->RC_PD $ cPdsAdT
						If PosSrv(cFolAdT,(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S"
							nSalMes -= (cAliasPROC)->RC_VALOR
						EndIf
					Else
						If PosSrv((cAliasPROC)->RC_PD,(cAliasPROC)->RC_FILIAL,"RV_INCORP") == "S"
							nSalMes -= (cAliasPROC)->RC_VALOR
						EndIf
					EndIf
				Endif
			Endif
			(cAliasPROC)->(dbskip())
		Enddo
		(cAliasPROC)->(dbCloseArea())
		If !lTemCab
			nSalario:= nSalMes
			nSalDia	:= nSalMes / 30
			nSalHora:= nSalMes / TPR->PR_HRSMES
		Endif
		//-- CARREGA OS CODIGOS DOS ADICIONAIS DE TEMPO DE SERVICO QUE SERAO UTILIZADOS
		//-- NOS CALCULOS DE PROVISOES DE FERIAS E 13o SALARIO.
		fCarAdics()
	Endif
	
	If !lTransfSai
	
		If lCalcSalInc .And. ( (SRA->RA_ADCINS $ "2|3|4" .And. Empty(cCodIns)) .Or. (SRA->RA_ADTPOSER # '***N**' .And. Empty(cCodAdt)) )
			fCarAdics()
		EndIf	
		
		If nSalMes == 0 .Or. !lTemCab
			If lCalcSalInc .Or. nSalMes == 0
				fSalInc(@nSalario,@nSalMes,@nSalHora,@nSalDia,.T.,,,,dDataRef,,@nPeric,@nInsal,@nAdtServ,,,,@nSalhInc)
			Endif
			//UTILIZA O SAL.INCORP. DO CADASTRO
			If lSalInc .And. TPR->PR_SALINCO > 0
				nSalMes  := TPR->PR_SALINCO
				nSalDia  := TPR->PR_SALINCO / 30
				nSalHora := TPR->PR_SALINCO / TPR->PR_HRSMES
			Endif
		Else
			// CHAMA FUNCAO PARA CARREGAR ADICIONAIS - ADTSERV/PERIC/INSAL
			If lCalcSalInc
				fSalInc(,,,,.T.,,,,dDataRef,,@nPeric,@nInsal,@nAdtServ,,,,@nSalhInc) 
			Endif
		EndIf
	
		If lProvResc .And. lDemitido
			//VERIFICA DEMISSAO E SEXTA FEIRA OU SABADO E CONS. PARAMETRO PARA PAGAR SABADO E DOMINGO
			dDataDem1	:= TPR->PR_DEMISSA
			dDataDem1	:= fDtSabDom()
		EndIf
	
		// CARREGANDO ARQUIVO TRP COM MONTA MEDIA
		If (!lDemitido .Or. lProvResc)
			dbSelectArea("TRP")
			Zap                                                                   
			dRefMed	:= If(lProvResc .And. lDemitido,dDataDem1,dDataRef)
			GpexMed(dDtBasFer,,dRefMed,,dRefMed,nSalHora,nSalMin,aCodFol,.T.,(!lFechouMes),,,,,,,nSalhInc,!lFechou13)
		Endif
		
		dbSelectArea("TPR")
		// CALCULA FERIAS VENCIDAS E PROPORCIONAIS
		If lFerias
			fProvFer(cCongFeR,@nTipoMovMes,@dDtBasFer) 
		EndIf
	
		// CALCULA O 13o SALARIO
		If l13oSal .And. (!lDemitido .Or. lProvResc)  .And. !TPR->PR_CATFUNC $ "E|G" 
			fProv13o(cCong13,@nTipoMovMes) 
		EndIf
	
	EndIf
		
EndIf

// 5o ELEMENTO DO ARRAY - B A I X A  T R A N S F E R E N C I A	 |
// 7o ELEMENTO DO ARRAY - B A I X A  R E S C I S A O			 |
If lTransfSai .Or. lDemitido
	If lFerias
		If lTransfSai
			fGrvArrPrv(@aFerVenc, _BxTrf, _Anter, 1, {})
			fGrvArrPrv(@aFerProp, _BxTrf, _Anter, 1, {})
		Else
			fGrvArrPrv(@aFerVenc, _BxRes, If(lProvResc,_Atual,_Anter), 1, {})
			fGrvArrPrv(@aFerProp, _BxRes, If(lProvResc,_Atual,_Anter), 1, {})
		EndIf
	EndIf
	If l13oSal .And. !TPR->PR_CATFUNC $ "E|G"
		If lTransfSai
			fGrvArrPrv(@a13Salar, _BxTrf, _Anter, 1, {})
			fGrvArrPrv(@a14Salar, _BxTrf, _Anter, 1, {})
		Else
			fGrvArrPrv(@a13Salar, _BxRes, If(lProvResc,_Atual,_Anter), 1, {})
			fGrvArrPrv(@a14Salar, _BxRes, If(lProvResc,_Atual,_Anter), 1, {})
		EndIf
	EndIf
EndIf

Begin Transaction
	
	// ATUALIZA O ARQUIVO DE CABECALHO - SRF
	If (!lDemitido .Or. lProvResc) .And. !lTransfSai
		If lFerias
			nDiasFv := aFerVenc[_Atual,_Dias] + nV_DFalFer
			nDiasFv += If(aFerVenc[_Atual,_Dias] > 0, nDFerAnt, 0)
			nDiasFv -= SRF->RF_DVENPEN
			If nDiasFv = 0 .And. nDFerAnt > 0
				nDiasFv := nDFerAnt * (-1)
			Endif
			aFerVenc[_Atual,_Dias] := If (nDiasFv < 0 .Or. lTrfSld, 0 , nDiasFv) + SRF->RF_DVENPEN
		EndIf
	EndIf
	    
	// PARA O NOVO CALCULO DE RATEIO, CHAMA NOVAMENTE A FUNCAO QUE APURA OS VALORES MENSAIS PARA POSTERIOR GERACAO NA TAB. SRT		
	If lGeraPMes
		fQryDetSRT(aVerba,aTransf,dDataRef,lTrataTrf,.T.,lFerias,l13oSal,.T.)
	Endif
	
	// GRAVA AS FERIAS VENCIDAS E PROPORCINAIS NO ARQUIVO DETALHE
	If lFerias
		If !TPR->PR_CATFUNC $ "E|G"
			fGeraSRT(@aFerVenc,aRecVenc,aVerba,dDataRef,nTipoMovMes,nSalMes,dDtBasFer,nDFerAnt,_FerVenc)
			fGeraSRT(@aFerProp,aRecProp,aVerba,dDataRef,nTipoMovMes,nSalMes,dDtBasFer,nDFerAnt,_FerProp)	
		Else		
			fGeraSRT(@aFerVenc,aRecVenc,aVerba,dDataRef,nTipoMovMes,nSalMes,dDtBasFer,nDFerAnt,_RecVenc)
			fGeraSRT(@aFerProp,aRecProp,aVerba,dDataRef,nTipoMovMes,nSalMes,dDtBasFer,nDFerAnt,_RecProp)
		Endif
	EndIf
	                                              
	// GRAVA O 13o E 14o SALARIO NO ARQUIVO DETALHE
	If l13oSal .And. !TPR->PR_CATFUNC $ "E|G"
		fGeraSRT(@a13Salar,aRec13Sl,aVerba,dDataRef,nTipoMovMes,nSalMes,dDtBasFer,nDFerAnt,_13Salar)
		fGeraSRT(@a14Salar,aRec14Sl,aVerba,dDataRef,nTipoMovMes,nSalMes,dDtBasFer,nDFerAnt,_14Salar)
	EndIf
	
	// SE FOR RECALCULO, DELETAR O REGISTRO DE "OK", INDICANDO QUE DEVERA SER GERADO UM NOVO SRZ NO MOMENTO DA CONTABILIZACAO
	fDelRegSRZ(3,"1",dDataRef)
	
End Transaction

// FINALIZA A GRAVACAO DOS LANCAMENTOS DO SIGAPCO
PcoFinLan("000091")

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Open_TPR  ºAutor  ³Jonatas A. T. Alves º Data ³  26/08/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao para abertura do arquivo TPR (exclusivo ou compart.).º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Gpem070                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
STATIC Function Open_TPR(cDbfProv,cNtxProv)
Local nTry		:= 20
Local cMsgExc	:= ""

// REABRE TPR EM MODO COMPARTILHADO 
USE (cDbfProv) ALIAS TPR SHARED NEW

// NO LINUX EXISTE UM TIME PARA LIBERACAO DA CRIACAO DO ARQUIVO. QUANDO NAO ENCONTRAR 
// O ARQUIVO PARA ABRIR, DEVER AGUARDAR UM TEMPO. ESPERAR NO MAXIMO 20 SEGUNDOS
While NetErr() .AND. nTry > 0 
	nTry--
	sleep(1000)	//-- 1 SEGUNDO

	// REABRE TPR EM MODO COMPARTILHADO 
	USE (cDbfProv) ALIAS TPR SHARED NEW
Enddo

If NetErr()
	cMsgExc := CRLF + OemToAnsi(STR0055)// "FALHA NA ABERTURA DO ARQUIVO TEMPORARIO (TPR) EM MODO COMPARTILHADO!"
	UserException(cMsgExc)
EndIf

dbSetIndex(cNtxProv + OrdBagExt())
dbSetOrder(1)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³IniVarGrd ºAutor  ³Jonatas A. T. Alves º Data ³  27/08/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao para inicializar variaveis Private da provisao.      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Gpem070                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
STATIC Function IniVarGrd()
aFerVenc     := Array(_Linhas,_Colunas)
aFerProp     := Array(_Linhas,_Colunas)
aRecVenc     := Array(_Linhas,_Colunas)
aRecProp     := Array(_Linhas,_Colunas)
nV_DFalFer   := 0
nP_DFalFer   := 0
nTFaltaV   	 := 0
nTFaltaP	 := 0
nDFerAnt     := 0
dDtBasFer    := CTOD("")
// VARIAVEIS PRIVATE - PROVISAO DE 13o SALARIO
a13Salar     := Array(_Linhas,_Colunas)
a14Salar     := Array(_Linhas,_Colunas)
aRec13Sl     := Array(_Linhas,_Colunas)
aRec14Sl     := Array(_Linhas,_Colunas)
// VARIAVEIS PRIVATE - PROVISAO DE FERIAS E 13o SALARIO
nSalario	 := nSalMes  := nSalDia     := nSalHora  := 0
nPercEmp	 := nPercTer := nPercAcTrab := nPercFgts := 0
nPerEmp13	 := nAvosAnt := nPercAnt    := 0
lDemitido    := .F.
lDesAtiv     := .F.
lTransfSai   := .F.
c__Roteiro   := "   "
// VARIAVEIS PARA FUNCAO FSALINC
aRoteiro 	:= {}
nAdtServ 	:= nPeric	:= nInsal :=  nAdcConf := nAdcTransf := 0.00	  // Valores dos Adicionais
nOutros 	:= 0.00	  // Valores de outras verbas que incorporam
cCodAdt  	:= cCodIns  := Space(3) 		  // Codigos que Foram Gerados
nSalMin  	:= 0

// AO PASSAR PELA fSalInc() O aPd DEVE SER ZERADO
If Type("aPd") == "A"
	aPd := {}
EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fGeraBaixaMes() ºAutor  ³Christiane Vieira º Data ³ 04/09/12º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Gravação das baixas no arquivo de provisão mensal - RHT    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Gpem070                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
STATIC Function fGeraBaixaMes() 
Local aArea		:= GetArea()
                          
DbSelectArea ("RHT")
RHT->(dbSetOrder(RetOrdem("RHT","RHT_FILIAL+RHT_MAT+RHT_CC+RHT_ITEM+RHT_CLVL+DTOS(RHT_DTCALC)+RHT_TPPROV+RHT_VERBA")))	
If !RHT->(dbSeek(SRT->RT_FILIAL+SRT->RT_MAT+SRT->RT_CC+SRT->RT_ITEM+SRT->RT_CLVL+DTOS(SRT->RT_DATACAL)+SRT->RT_TIPPROV+SRT->RT_VERBA))
	RecLock("RHT", .T.)
	RHT->RHT_FILIAL  := SRT->RT_FILIAL
	RHT->RHT_MAT     := SRT->RT_MAT
	RHT->RHT_CC      := SRT->RT_CC
	If lItemClVl
		RHT->RHT_ITEM := SRT->RT_ITEM
		RHT->RHT_CLVL := SRT->RT_CLVL
	EndIf
	RHT->RHT_TPPROV  := SRT->RT_TIPPROV
	RHT->RHT_VERBA   := SRT->RT_VERBA
Else
	RecLock("RHT", .F.)
Endif
RHT->RHT_SALAR  := nSalMes
RHT->RHT_DTCALC := SRT->RT_DATACAL
RHT->RHT_VALOR  := SRT->RT_VALOR
RHT->RHT_PERC	:= 100

MsUnlock()	

RestArea(aArea)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³CriaTransfºAutor  ³Jonatas A. T. Alves º Data ³  10/03/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Cria tabela com replica das informacoes do array aTransf.   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Gpem070                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
STATIC Function CriaTransf()
Local lRet			:= .T.
Local aFields		:= {}
Local cRdd			:= "TOPCONN"
Local cQuery		:= ""
Local nTamPos		:= Len(Alltrim(Str(nTamTrf)))

Begin Sequence
	IF (!MsFile(cFileTrf , NIL , cRdd))	
		AADD(aFields,{"TRFPOS"		,"N",nTamPos				,0})
		AADD(aFields,{"TRFELE"		,"N",01						,0})
		AADD(aFields,{"TRFEMPP"		,"C",02						,0})
		AADD(aFields,{"TRFFILIALP"	,"C",FWGETTAMFILIAL  		,0})
		AADD(aFields,{"TRFCCP"		,"C",TamSX3("RA_CC")[1]		,0})
		AADD(aFields,{"TRFMATP"		,"C",TamSX3("RA_MAT")[1]	,0})
		AADD(aFields,{"TRFDATE"		,"C",06						,0})
		AADD(aFields,{"TRFNLIDO"	,"L",01						,0})
		IF !(lRet := MsCreate(cFileTrf , aFields , cRdd))
			Break
		EndIF
	Else
		cQuery := "DELETE FROM " + cFileTrf
		TcSqlExec(cQuery)
	Endif
	IF !(lRet := MsOpenDbf(.T. , cRdd, cFileTrf , cTrfDbf , .T. , .F. , .F. , .F.))
		Break
	EndIF
	IF !(lRet := (Select(cTrfDbf) > 0))
		Break
	EndIF
End Sequence

Return (lRet)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fChkRHQBaseºAutor  ³Aldo Marini         º Data ³  07/11/11  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Verifica a existencia da tabela RHQ-Programacao de Rateio   º±±
±±º          ³para poder gerar os lancamentos rateados no SRT/Contabilizarº±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEM110/GPEM070                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fChkRHQBase()
Local lRet 		:= .F.
Local aArea		:= GetArea()
Local aSM0Area	:= SM0->(GetArea())    
Local nOrdem		

SX2->(dbSetOrder(1))
If SX2->(dbSeek("RHQ"))
	dbSelectArea("RHQ")
	If RHQ->(FieldPos("RHQ_MAT")) > 0
		lRet := .T.
	Endif
Endif

// CASO TENHA SIDO EXECUTADA A OPCAO DO COMPATIBILIZADOR QUE  |
// CRIA RATEIO POR MES DAS PROVISOES, VERIFICA A EXISTENCIA DE|
// VERBA ASSOCIADA AOS NOVOS IDS. CASO EXISTA PELO MENOS UM   |
// DOS IDENTIFICADORES ABAIXO ASSOCIADO, CONSIDERAMOS O RATEIO|
// 960 - FÉRIAS, 966 - 13o., 968 - 1A PARCELA DO 13o.		   |

IF lRet
	lRet:= .F.	
	//-- VERIFICA A AMARRACAO SRV X ID DE PROVISAO POR RATEIO
	//-- SE O ACODFOL JA TENHA SIDO CARREGADO, UTILIZA-O.
	IF Type("acodfol") <> "U" .AND. Len(aCodFol) > 0
	   IF   (;
			   	!Empty(aCodFol[0960]) .or.  	;
				!Empty(aCodFol[0961]) .or.  	;
				!Empty(aCodFol[0962]) 	.or.	;
			  	!Empty(aCodFol[0963]) .or.  	;				
			  	!Empty(aCodFol[0964]) .or.  	;			  	
			  	!Empty(aCodFol[0965]) .or.  	;			  	
			  	!Empty(aCodFol[0966]) .or.  	;
				!Empty(aCodFol[0967]) .or.  	;				  				  					  	
				!Empty(aCodFol[0968]) .or.  	;				  					
				!Empty(aCodFol[0969]) .or.  	;				  					
				!Empty(aCodFol[0970]) .or.  	;				  													
			  	!Empty(aCodFol[0971]) 		 	;			  	
			)
			lRet:= .T.
		Endif	
	Else
		//-- CORRE TODAS AS FILIAIS EM BUSCA DE VERBA ASSOCIADA A ALGUM IDENTIFICADOR DE RATEIO 
		//-- NA EXISTENCIA DE PELO MENOS UMA DAS ASSOCIACOES, O RATEIO ESTÁ EFETIVADO
		//-- PALIATIVAMENTE CONSIDERAMOS QUE SE PELO MENOS UMA FILIAL ATENDE A CODICAO DE EFETIVACAO
		//-- AS DEMAIS OBRIGATORIAMENTE ESTARAO CONFIGURADAS PARA TAL.
		SM0->(dbGoTop()) 
		nOrdem	:= RetOrdem("SRV","RV_FILIAL+RV_CODFOL")
		While SM0->(!Eof())
		     IF 	(;
						(!Empty(PosSrv("0960",  FWCodFil(), "RV_COD",nOrdem, .F.)) .or. ;
						  !Empty(PosSrv("0961",  FWCodFil(), "RV_COD",nOrdem, .F.)) .or. ;
						  !Empty(PosSrv("0962",  FWCodFil(), "RV_COD",nOrdem, .F.)) .or. ;
						  !Empty(PosSrv("0963",  FWCodFil(), "RV_COD",nOrdem, .F.)) .or. ;
						  !Empty(PosSrv("0964",  FWCodFil(), "RV_COD",nOrdem, .F.)) .or. ;
						  !Empty(PosSrv("0965",  FWCodFil(), "RV_COD",nOrdem, .F.)) .or. ;
						  !Empty(PosSrv("0966",  FWCodFil(), "RV_COD",nOrdem, .F.)) .or. ;
						  !Empty(PosSrv("0967",  FWCodFil(), "RV_COD",nOrdem, .F.)) .or. ;
						  !Empty(PosSrv("0968",  FWCodFil(), "RV_COD",nOrdem, .F.)) .or. ;
						  !Empty(PosSrv("0969",  FWCodFil(), "RV_COD",nOrdem, .F.)) .or. ;						  
						  !Empty(PosSrv("0970",  FWCodFil(), "RV_COD",nOrdem, .F.)) .or. ;						  						  
						  !Empty(PosSrv("0971",  FWCodFil(), "RV_COD",nOrdem, .F.))  	 ; 
						);
					)
					lRet:= .T.	
					Exit
			Endif
			SM0->(dbSkip())
		EndDo
	Endif
Endif

RestArea(aSM0Area)
RestArea(aArea)

Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fPercEmp  ³ Autor ³ Allyson M				³ Data ³ 29.11.12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Carrega variaveis para calculo dos encargos                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fPercEmp(cRecFatEmp, nPercEmp, nPerEmp13)                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cRecFatEmp 	 - Empresa Mista ou Exclusiva s/ faturamento  ³±±
±±³          ³ nPercEmp      - Percentual da Empresa                      ³±±
±±³          ³ nPerEmp13 	 - Percentual da Empresa p/ 13o. Salario      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEM070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±*/
Static Function fPercEmp(cRecFatEmp, nPercEmp, nPerEmp13)
Local aTabS033	:= {}
Local cAnoMes	:= AnoMes(dDataRef)
Local cDesFol	:= SuperGetMv("MV_DESFOL",,"")
Local cLimRef	:= Str(Val(SubStr(cAnoMes, 1, 4)) - 1, 4) + "12"
Local cRefAux	:= cAnoMes
Local lContinua	:= .T.
Local nFatDes	:= 0
Local nFatFol	:= 0
Local nFatTot	:= 0

If cAnoMes < cDesFol
	lDesAtiv	:= .F.
	nPercEmp	:= If (TPR->PR_TPCONTR$ " *1", aInssEmp[01, 1] , aInssEmp[01, 2])
	nPerEmp13	:= If (TPR->PR_TPCONTR$ " *1", aInssEmp[01, 1] , aInssEmp[01, 2])
Else
	//SE O ANO DE REFERENCIA FOR MAIOR DO QUE O ANO DO MV_DESFOL OU SE O MES DE INICIO DA DESONERACAO 
	//FOR '01' (JANEIRO), NAO E' NECESSARIO FAZER A  PROPORCIONALIZACAO DO INSS
	If ((SubStr(cAnoMes, 1, 4) > SubStr(cDesFol, 1, 4))) .Or. SubStr(cDesFol, 5, 2) == "01"
		lDesAtiv	:= .F.
	Else
		lDesAtiv	:= .T.	
		nAvosAnt    := Val(SubStr(cDesFol, 5, 2)) - 1
		nPercAnt	:= If (TPR->PR_TPCONTR$ " *1", aInssEmp[01, 1] , aInssEmp[01, 2])
	EndIf
	If cRecFatEmp == "S"
		nPercEmp	:= 0.00
		nPerEmp13	:= 0.00
	Else
		//FERIAS UTILIZA O FATURAMENTO DO MES DE CALCULO DA PROVISAO
		If lFerias
			//CHAMA FCARRTAB PARA RECARREGAR TABELA AUXILIAR S033
			fCarrTab(@aTabS033, "S033", dDataRef,.T., cAnoMes)
	    	nPercEmp := fPerFatEmp(cRecFatEmp, 0, aInssEmp[01, 1], aTabS033,dDataRef)[1]
	  	EndIf
	  	//13o. SALARIO UTILIZA O FATURAMENTO ACUMULADO DE DEZEMBRO/ANO ANTERIOR ATE O MES DE CALCULO DA PROVISAO
	  	//SE A DESONERACAO COMECOU NO MESMO ANO DO CALCULO EFETUADO NA PROVISAO
	  	If l13oSal .And. !TPR->PR_CATFUNC $ "E|G"
			//PRIMEIRO TENTA ENCONTRAR A REFERENCIA AAAA13 NA TABELA S033 E SE NAO ENCONTRAR,
	        //FAZ OS PROCESSOS PARA ENCONTRAR O PERCENTUAL DE 13o.
			If Month(dDataRef) == 12
				aTabS033 := {}
				//CHAMA FCARRTAB PARA RECARREGAR TABELA AUXILIAR S033
		        fCarrTab(@aTabS033, "S033", dDataRef,.T., Str(Year(dDataRef), 4) + "13")
				//SE ENCONTROU LANCAMENTOS
				If !Empty(aTabS033)
					//APURA A RECEITA BRUTA TOTAL DA EMPRESA
					aEval(aTabS033, {|aTabS033| nFatTot += aTabS033[10] })
					//Deduz as exclusoes da receita bruta total da empresa
					aEval(aTabS033, {|aTabS033| nFatTot -= aTabS033[11] } )
					If Len(aTabS033) >= 1 .And. Len(aTabS033[1]) >= 13
						//Deduz os impostos  da receita bruta total da empresa
						aEval(aTabS033, {|aTabS033| nFatTot -= aTabS033[13] } )
					EndIf					
					//APURA A RECEITA BRUTA QUE E' SOBRE AS ATIVIDADES BENEFICIADAS DA LEI NO. 12.546/2011
					aEval(aTabS033, {|aTabS033| If(aTabS033[6] == "1", nFatDes += aTabS033[10],) })
					//Deduz as exclusoes da receita bruta que e' sobre as atividades beneficiadas da Lei no. 12.546/2011
					aEval(aTabS033, {|aTabS033| If(aTabS033[6] == "1", nFatDes -= aTabS033[11], ) } )
					If Len(aTabS033) >= 1 .And. Len(aTabS033[1]) >= 13
						//Deduz os impostos da receita bruta que e' sobre as atividades beneficiadas da Lei no. 12.546/2011
						aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "1", nFatDes -= aTabS033[13], ) } )
					EndIf
					//APURA A RECEITA BRUTA QUE NAO E' SOBRE AS ATIVIDADES BENEFICIADAS DA LEI NO. 12.546/2011
					aEval(aTabS033, {|aTabS033| If(aTabS033[6] == "2", nFatFol += aTabS033[10],) })	        
					//Deduz as exclusoes da receita bruta que nao e' sobre as atividades beneficiadas da Lei no. 12.546/2011
					aEval(aTabS033, {|aTabS033| If(aTabS033[6] == "2", nFatFol -= aTabS033[11], ) } )
					If Len(aTabS033) >= 1 .And. Len(aTabS033[1]) >= 13
						//Deduz os impostos da receita bruta que nao e' sobre as atividades beneficiadas da Lei no. 12.546/2011
						aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "2", nFatFol -= aTabS033[13], ) } )
					EndIf					
					lContinua := .F.
	            Else
					cRefAux := SubMesAno(cRefAux)	
				EndIf
            EndIf
            If lContinua
            	If nVerFatFin != 1
		        	fAtuS033(cRefAux, cLimRef)
		        EndIf
		  		//ENQUANTO NAO ATINGIR A ANO/MES LIMITE PARA BUSCA DO FATURAMENTO FAZ A SOMA DO FATURAMENTO
		  		//OBTIDO NA COMEPTENCIA VERIFICADA
		  		While cRefAux >= cLimRef
					aTabS033 := {}
					//CHAMA FCARRTAB PARA RECARREGAR TABELA AUXILIAR S033
					fCarrTab(@aTabS033, "S033", dDataRef,.T., cRefAux)
					//APURA A RECEITA BRUTA TOTAL DA EMPRESA
					aEval(aTabS033, {|aTabS033| nFatTot += aTabS033[10] })
					//Deduz as exclusoes da receita bruta total da empresa
					aEval(aTabS033, {|aTabS033| nFatTot -= aTabS033[11] } )					
					If Len(aTabS033) >= 1 .And. Len(aTabS033[1]) >= 13
						//Deduz os impostos da receita bruta total da empresa
						aEval(aTabS033, {|aTabS033| nFatTot -= aTabS033[13] } )
					EndIf
					//APURA A RECEITA BRUTA QUE E' SOBRE AS ATIVIDADES BENEFICIADAS DA LEI NO. 12.546/2011
					aEval(aTabS033, {|aTabS033| If(aTabS033[6] == "1", nFatDes += aTabS033[10],) })
					//Deduz as exclusoes da receita bruta que e' sobre as atividades beneficiadas da Lei no. 12.546/2011
					aEval(aTabS033, {|aTabS033| If(aTabS033[6] == "1", nFatDes -= aTabS033[11], ) } )
					If Len(aTabS033) >= 1 .And. Len(aTabS033[1]) >= 13
						//Deduz os impostos da receita bruta que e' sobre as atividades beneficiadas da Lei no. 12.546/2011
						aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "1", nFatDes -= aTabS033[13], ) } )
					EndIf
					//APURA A RECEITA BRUTA QUE NAO E' SOBRE AS ATIVIDADES BENEFICIADAS DA LEI NO. 12.546/2011
					aEval(aTabS033, {|aTabS033| If(aTabS033[6] == "2", nFatFol += aTabS033[10],) })					
					//Deduz as exclusoes da receita bruta que nao e' sobre as atividades beneficiadas da Lei no. 12.546/2011
					aEval(aTabS033, {|aTabS033| If(aTabS033[6] == "2", nFatFol -= aTabS033[11], ) } )
					If Len(aTabS033) >= 1 .And. Len(aTabS033[1]) >= 13
						//Deduz os impostos da receita bruta que nao e' sobre as atividades beneficiadas da Lei no. 12.546/2011
						aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "2", nFatFol -= aTabS033[13], ) } )
					EndIf
					//SUBTRAI UM MES DA VARIAVAL QUE CONTROLA A COMPETENCIA
		  			cRefAux := SubMesAno(cRefAux)	
		  		End While
			EndIf
			//CALCULA A RAZAO ENTRE O FATURAMENTO DE ATIVIDADES DIVERSAS SOBRE O FATURAMENTO TOTAL
			nPerEmp13 	:= (nFatFol / nFatTot)		
			//CALCULA O NOVO PERCENTUAL DE ACORDO COM A RAZAO ENCONTRADA E O PERCENTUAL DE RECOLHIMENTO ANTIGO
			
			//Receita nao Desonerada IGUAL/MAIOR que 95% do Total - Aplicacao da aliquota padrao
			If cRecFatEmp == "M" .And. nPerEmp13 >= 0.95
				nPerEmp13 := If (TPR->PR_TPCONTR$ " *1", aInssEmp[01, 1] ,aInssEmp[01, 2] )

			//Receita nao Desonerada MAIOR que 5% e MENOR que 95% do Total - Aplicacao da aliquota reduzida
			ElseIf cRecFatEmp == "M" .And. (nPerEmp13 > 0.05 .And. nPerEmp13 < 0.95  )
		   		nPerEmp13 := ( ( nPerEmp13 * If (TPR->PR_TPCONTR$ " *1", aInssEmp[01, 1] ,aInssEmp[01, 2] ) ) )
			//Receita nao Desonerada MAIOR que 5% - Aplicacao da aliquota reduzida
			ElseIf cRecFatEmp == "C" .And. nPerEmp13 > 0.05
				nPerEmp13 := ( ( nPerEmp13 * If (TPR->PR_TPCONTR$ " *1", aInssEmp[01, 1] ,aInssEmp[01, 2] ) ) )
			Else
			    //A aliquota sera zerada
			    If aInssEmp[27, 1] $ "S*M"
			       nPerEmp13:=0
			    Else   
				   nPerEmp13 := If (TPR->PR_TPCONTR$ " *1", aInssEmp[01, 1] ,aInssEmp[01, 2])
				Endif
			EndIf
	  	EndIf
	EndIf
EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fAtuS033  ³ Autor ³ Allyson M				³ Data ³ 29.11.12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Alimenta tabela S033                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fAtuS033(cAnoMes, cLimRef)                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cRefAux	 - Ano/Mes inici                                  ³±±
±±³          ³ cLimRef 	 - Ano/Mes limite para busca da receita           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEM070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±*/
Static Function fAtuS033(cRefAux, cLimRef)
Local aArea			:= GetArea()
Local aAreaSM0	 	:= SM0->(GetArea())
Local aFilProc		:= {}
Local aTabS033		:= {}
Local cCodEmp	 	:= SM0->M0_CODIGO
Local cSvFilAnt		:= ""
Local nAliq	 		:= 0
Local nContr		:= 0
Local nFatTot	 	:= 0
Local nFatTotDev	:= 0
Local nFatTotExp	:= 0
Local nFatTotLiq	:= 0
Local nFatRecB 		:= 0
Local nFatRecL 		:= 0
Local nFatRec 		:= 0
Local nFatExc 		:= 0
Local nFatExp 		:= 0
Local nFatBas		:= 0
Local nFatRecAux	:= 0
Local nFilial		:= 0
Local nTotCLiq		:= 0
Local nTotCont 		:= 0 
Local nTotDev 		:= 0 
Local nTotExp 		:= 0 
Local nTotImp 		:= 0 
Local nValDev 		:= 0
Local nValExp 		:= 0
Local nVlAux		:= 0
Local aCargFil	:= {}

//MONTA O ARRAY DAS FILIAIS A PROCESSAR
aCargFil := FwLoadSm0()

aEval(aCargFil,{|aCargFil| If(aCargFil[1] == cCodEmp, Aadd(aFilProc,{ aCargFil[2] }),Nil )} )

While cRefAux >= cLimRef
	//REINICIALIZA VARIAVEIS
	nAliq	 	:= 0
	nContr		:= 0
	nFatRecAux 	:= 0
	nFatTot	 	:= 0
	nFatTotDev	:= 0
	nFatTotExp	:= 0
	nFatTotLiq	:= 0
	nFatRecB 	:= 0
	nFatRecL 	:= 0
	nFatRec 	:= 0
	nFatExc 	:= 0
	nFatExp 	:= 0
	nFatBas		:= 0
	nTotCont 	:= 0 
	nTotDev 	:= 0 
	nTotExp 	:= 0 
	nTotImp 	:= 0 
	nTotCLiq 	:= 0 
	nValDev 	:= 0
	nValExp 	:= 0	
	nVlAux	 	:= 0 
	aTabS033	:= {}
	//CHAMA FCARRTAB PARA RECARREGAR TABELA AUXILIAR S033
	fCarrTab(@aTabS033, "S033",,.T., cRefAux)	
	//VERIFICA TODAS AS FILIAIS QUE FORAM SELECIONADAS NO PROCESSAMENTO, 
	//PARA VALIDAR SE POSSUEM REGISTRO NA TABELA AUXILIAR S033
	For nFilial := 1 To Len(aFilProc)
		//SE NAO ENCONTRAR REGISTROS PARA A FILIAL, VERIFICAR AS FUNCOES DAS INTEGRACOES
		If (aScan(aTabS033, {|x| x[2] == aFilProc[nFilial, 1] }) == 0)
		   	//SE SOMENTE VERIFICA FINANCEIRO
		   	If nVerFatFin == 3
				//ENTRA NA FUNCAO DO FINANCEIRO PARA BUSCAR OS VALORES DOS TITULOS DE ACORDO
				//COM A FILIAL LOGADA. A FUNCAO NAO PERMITE PASSAR A FILIAL COMO PARAMETRO
				//ENTAO A VARIAVEL CFILANT SERA ALTERADA DE ACORDO COM A FILIAL EM PROCESSAMENTO.
				//A FUNCAO RETORNA O VALOR DAS RECEITAS FINANCEIRAS.					
				cSvFilAnt := cFilAnt
				cFilAnt	  := aFilProc[nFilial, 1]
				nFatTot   := FTotF100(Val(SubStr(cRefAux, 5, 2)), Val(SubStr(cRefAux, 1, 4)))
				cFilAnt	  := cSvFilAnt
		        //GRAVA O REGISTRO DA RECEITO DE TIPO 2
				If nFatTot > 0
					fGravaS033(aFilProc[nFilial, 1], cRefAux, Nil, "2", nFatTot, 0.00, 0.00, nFatTot, 0.00, 0.00, aTabS033)					
				EndIf	            	
		   	//SE VERIFICA FATURAMENTO E/OU FINANCEIRO
		   	Else
				//ENTRA NA FUNCAO DO FATURAMENTO PARA BUSCAR OS VALORES DAS NOTAS FISCAIS DE SAIDA
				//NA TABELA SD2 PARA VERIFICAR O FATURAMENTO DA FILIAL NA COMPETENCIA. A FUNCAO
				//DO FATURAMENTO RETORNA OS DADOS GRAVADOS EM UM ALIAS TEMPORARIO
				cSvFilAnt := cFilAnt
				cFilAnt	  := aFilProc[nFilial, 1]
				cAliasFat := RhInssPat(SubStr(cRefAux, 5, 2) +  SubStr(cRefAux, 1, 4), aFilProc[nFilial, 1])
				cFilAnt	  := cSvFilAnt
				If !Empty(cAliasFat)
					dbSelectArea(cAliasFat)
					(cAliasFat)->(dbGoTop())
					While (cAliasFat)->(!EoF())
				        cCodAti		:= (cAliasFat)->CODATV
				        cTipFat 	:= "1"
				        nAliq		:= SpedPCCG1(cCodAti, dDataRef)
				        nFatTot 	:= (cAliasFat)->TOTAL 		// Total
		                nFatTotLiq 	:= (cAliasFat)->TOTALLIQ	// Total
				        nFatTotDev 	:= (cAliasFat)->TOTLIQDEV 	// Total devolucao
				        nFatTotExp 	:= (cAliasFat)->TOTLIQEXP  	// Total exportacao
				        nFatRecB	:= (cAliasFat)->TOTCODAT	// Total do codigo de atividade bruto
				        nFatRecL	:= (cAliasFat)->TCODATLQ	// Total do codigo de atividade liquido
				        nFatExc		:= (cAliasFat)->TCATVDEV	// Total devolucoes do codigo de atividade
				        nFatExp		:= (cAliasFat)->TLQCATVEXP	// Total exportacoes do codigo de atividade
				        nFatBas 	:= (cAliasFat)->TCDEVEXP 	// Total codigo de atividade - devolucoes - exportacoes (base calculo)
			            //CALCULA A CONTRIBUICAO COM BASE NA ALIQUOTA ENCONTRADA
				        If nAliq > 0
					        nContr	:= (nFatBas * (nAliq/100))
					  	EndIf
				        //GRAVA A INFORMACAO DO RECOLHIMENTO DEVIDO NA TABELA AUXILIAR S033.
						If nFatBas > 0
							fGravaS033( aFilProc[nFilial, 1], cRefAux, cCodAti, cTipFat, nFatBas, nAliq, nContr, nFatRecB, nFatExc, nFatExp, aTabS033, nFatRecB - nFatRecL )
							//CHAMA FCARRTAB PARA RECARREGAR TABELA AUXILIAR S033
							aTabS033 := {}
							fCarrTab(@aTabS033, "S033",dDataRef,, cRefAux) 
						Else
							 //SENAO TIVER FATURAMENTO COM PRODUTOS COM COD ATIVIDADE, MAS TIVER ALGUM FATURAMENTO GRAVAR TABELA S033, COM TIPO 2 (PRODUTOS NAO DESONERADOS)
							If nFatTot > 0 
								If Empty(cCodAti) 
									cTipFat 	:= "2"
								Else
									cTipFat 	:= "1"
								EndIf
								fGravaS033( aFilProc[nFilial, 1], cRefAux, cCodAti, cTipFat, nFatBas, nAliq, nContr, nFatRecB, nFatExc, nFatExp, aTabS033, nFatRecB - nFatRecL )
								//CHAMA FCARRTAB PARA RECARREGAR TABELA AUXILIAR S033
								aTabS033 := {}
								fCarrTab(@aTabS033, "S033", dDataRef,, cRefAux)   
							EndIf
						EndIf

						//Caso algum Codigo de Atividade fique com valor Base zerado devido exportacoes e devolucoes, ou no caso 
						//de haver somente valores nao desonerados, o total da atividade processada sera atribuido a variavel 
						//auxiliar nFatRecAux para posterior deducao do Total Geral para que o valor da Receita Tipo 2 saia correto
						nFatRecAux += If( nFatBas == 0, nFatTotLiq, 0 )

						(cAliasFat)->(dbSkip())
					End While
				EndIf
						
				//FAZ A TOTALIZACAO DA FATURAMENTO DA FILIAL GRAVADAS NA TABELA S033
				aEval(aTabS033, {|aTabS033| If(aTabS033[2] + aTabS033[6] == aFilProc[nFilial, 1] + "1", nTotCont += aTabS033[10], Nil) })
				aEval(aTabS033, {|aTabS033| If(aTabS033[2] + aTabS033[6] == aFilProc[nFilial, 1] + "1", nTotCLiq += aTabS033[7], Nil) })
				aEval(aTabS033, {|aTabS033| If(aTabS033[2] + aTabS033[6] == aFilProc[nFilial, 1] + "1", nTotDev += aTabS033[11], Nil) })
				aEval(aTabS033, {|aTabS033| If(aTabS033[2] + aTabS033[6] == aFilProc[nFilial, 1] + "1", nTotExp += aTabS033[12], Nil) })
				If Len(aTabS033) >= 1 .And. Len(aTabS033[1]) >= 13
					aEval(aTabS033, {|aTabS033| If(aTabS033[2] + aTabS033[6] == aFilProc[nFilial, 1] + "1", nTotImp += aTabS033[13], Nil ) } )
				EndIf
				//ENTRA NA FUNCAO DO FINANCEIRO PARA BUSCAR OS VALORES DOS TITULOS DE ACORDO
				//COM A FILIAL LOGADA. A FUNCAO NAO PERMITE PASSAR A FILIAL COMO PARAMETRO
				//ENTAO A VARIAVEL CFILANT SERA ALTERADA DE ACORDO COM A FILIAL EM PROCESSAMENTO.
				//A FUNCAO RETORNA O VALOR DA RECEITA TOTAL.						
				If nVerFatFin == 4
					cSvFilAnt := cFilAnt
					cFilAnt	  := aFilProc[nFilial, 1]
					nVlAux    := FTotF100(Val(SubStr(cRefAux, 5, 2)), Val(SubStr(cRefAux, 1, 4)))
					nFatTot   += nVlAux
					nFatTotLiq+= nVlAux
					cFilAnt	  := cSvFilAnt
				EndIf
				//SE O TOTAL DE FATURAMENTO DA FILIAL FOR MAIOR DO QUE OS REGISTROS GRAVADOS
				//SIGNIFICA QUE HA FATURAMENTO DE ATIVIDADES QUE NAO ESTAO ENQUADRADAS NA LEI
				//ESSE REGISTRO DEVE SER GRAVADO NA TABELA AUXILIAR COM O TIPO DE RECEITA 2
				nValDev := If( nFatTotDev > nTotDev, nFatTotDev, nTotDev )
				nValExp := If( nFatTotExp > nTotExp, nFatTotExp, nTotExp )
				
				If nFatTotLiq - nValDev - nValExp >= nTotCLiq .And. ( ( nFatTotLiq - nTotCLiq - nFatRecAux - ( nValDev - nTotDev ) - ( nValExp - nTotExp ) ) > 0 .Or. ( nFatTot - nTotCont ) > 0 )
					fGravaS033( aFilProc[nFilial, 1], cRefAux, Nil, "2", ( nFatTotLiq - nTotCLiq - nValExp - nValDev ), 0.00, 0.00, ( nFatTot - nTotCont ), ( nValDev - nTotDev ), ( nValExp - nTotExp ), aTabS033, ( nFatTot - nFatTotLiq - nTotImp ) )
				EndIf
				(cAliasFat)->(dbCloseArea())
			EndIf
		EndIf
	Next nFilial
	//SUBTRAI UM MES DA VARIAVAL QUE CONTROLA A COMPETENCIA
	cRefAux := SubMesAno(cRefAux)		
End While

RestArea(aArea)
RestArea(aAreaSM0)

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fBusDetSRT| Autor Emerson R. de Souza  Data 21.06.00        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao Busca os valores da provisao no arquivo SRT                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   fBusDetSRT(aProvisao,aRecnos,aVerba,aTransf,dDataRef)        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros aProvisao     - Array que contera os valores da provisao    ³±±
±±    aRecnos     - Array onde sera gravado os registros                  ³±±
±±    aVerba      - Verbas de Ferias / 13o Sal / 14o Sal                  ³±±
±±    aTransf     - Funcionarios Transferidos no mes                      ³±±
±±    dDataRef    - Data de referencia para busca dos valores             ³±±
±±    nTipoProv   - Tipo de provisao a buscar (Venc,Prop,13o)             ³±±
±±    lTrataTrf   - Indica se deve tratar os transferidos                 ³±±
±±    lCalcula    - Indica se deve calcular o valor do mes                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±Uso        Generico                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fBusDetSRT(aProvisao,aRecnos,aVerba,aTransf,dDataRef,nTipoProv,lTrataTrf,lCalcula,lSoMes)
Local aArea       := GetArea()
Local cBusMesAnt,cBusMesAtu,nCnt1
Local nTpMov      := 0
Local nPosTrf           := 0
Local nPosVerba   := 0
Local cAliasSRT   := "SRT"
Local cAnoMesAnt  := MesAno( dDataRef - Day(dDataRef) )
Local cAnoMesAtu  := MesAno( dDataRef )
Local cOrdemBusca := &("{ || SRT->RT_FILIAL + SRT->RT_MAT + SRT->RT_CC + MesAno(SRT->RT_DATACAL) }")
Local lTrfEmpAmes := .F.
Local lAdiant13   := .F.
Local cAliasArq   := If (Empty(cTpRtProv), "SRT", "RHT")
Local cTpProvMes  := ""
Local cEmpQryAnt  := cEmpAnt
Local cModo       := ""
Local cSvEmpAnt   := ""
Default lSoMes          := .F.
 
If Type( "lProvResc" ) == "U"
      lProvResc := ( SuperGetMv( "MV_PROVRES",,"N" ) == "S" ) //INDICA SE DEVERA PROVISIONAR NO MES DA RESCISAO
EndIf
If Type( "lTrfAMES" ) == "U"
      cTrfAMES     := ( SuperGetMv( "MV_TRFAMES",, Space(6) ) ) //ANO/MES PARA INICIO DAS DEMONSTRACOES DE ENTRADA E SAIDA DE TRANSFERENCIAS NO CONCEITO 1 (TRANSFERE SALDO ORIGEM P/ DESTINO)
      lTrfAMES     := ( !Empty(cTrfAMES) .And. MesAno( dDataRef ) >= cTrfAMES )
EndIf
If Type( "lTrfSld" ) == "U" .Or. lTrfSld
      lTrfSld := .F.
EndIf
If Type( "lFerias" ) == "U"
	lFerias := .T.
EndIf

aRecnos    := If(aRecnos   == Nil, Array(_Linhas,_Colunas), aRecnos)
lCalcula   := If(lCalcula  == Nil, .F., lCalcula)

If !Empty(cTpRtProv)
	If lItemClVl
		cOrdemBusca := &("{ || (cAliasSRT)->RHT_FILIAL + (cAliasSRT)->RHT_MAT + (cAliasSRT)->RHT_CC + (cAliasSRT)->RHT_ITEM + (cAliasSRT)->RHT_CLVL + MesAno((cAliasSRT)->RHT_DTCALC) }")   
	Else
		cOrdemBusca := &("{ || (cAliasSRT)->RHT_FILIAL + (cAliasSRT)->RHT_MAT + (cAliasSRT)->RHT_CC + MesAno((cAliasSRT)->RHT_DTCALC) }")    
	EndIf	
	cTpProvMes := If(lFerias, "'1','2'", "'3'")
ElseIf lItemClVl
	cOrdemBusca := &("{ || SRT->RT_FILIAL + SRT->RT_MAT + SRT->RT_CC + SRT->RT_ITEM + SRT->RT_CLVL + MesAno(SRT->RT_DATACAL) }")
Endif


If !lSoMes
	If !lProvResc .And. TPR->PR_TIPMOVI == _Demitido
            nPosTrf           := Ascan(aTransf, { |X| X[_TAtual,_TEmp]+X[_TAtual,_TFil]+X[_TAtual,_TCC]+X[_TAtual,_TMat] == FWGrpCompany(cAliasArq)+TPR->PR_FILIAL+TPR->PR_CC+TPR->PR_MAT })
            If nPosTrf > 0 .and. ;
                  aTransf[nPosTrf,_TAnter,_TEmp]+aTransf[nPosTrf,_TAnter,_TFil]+aTransf[nPosTrf,_TAnter,_TCC]+aTransf[nPosTrf,_TAnter,_TMat]+aTransf[nPosTrf,_TAnter,_TDta]==;
                  aTransf[nPosTrf,_TDest ,_TEmp]+aTransf[nPosTrf,_TDest ,_TFil]+aTransf[nPosTrf,_TDest ,_TCC]+aTransf[nPosTrf,_TDest ,_TMat]+aTransf[nPosTrf,_TDest ,_TDta]
                  nPosTrf           := 0
            EndIf
            lTrfSld           := nPosTrf > 0
            nTpMov            := TPR->PR_TIPMOVI //GRAVAR TIPO DE MOVIMENTO DE DEMISSAO
    EndIf
    
    //LIMPA O ARRAY COM O CONTEUDO ESPECIFICADO NO 2o PARAMETRO
    fLimpaArray(@aProvisao, 0)
    fLimpaArray(@aRecnos,   0) 
      
    //CHAVE DE BUSCA DO MES ANTERIOR E ATUAL
	If lItemClVl
		cBusMesAnt := TPR->PR_FILIAL + TPR->PR_MAT + TPR->PR_CC + TPR->PR_ITEM + TPR->PR_CLVL + cAnoMesAnt 
		cBusMesAtu := TPR->PR_FILIAL + TPR->PR_MAT + TPR->PR_CC + TPR->PR_ITEM + TPR->PR_CLVL + cAnoMesAtu
	Else
		cBusMesAnt := TPR->PR_FILIAL + TPR->PR_MAT + TPR->PR_CC + cAnoMesAnt
		cBusMesAtu := TPR->PR_FILIAL + TPR->PR_MAT + TPR->PR_CC + cAnoMesAtu
	EndIf
      
      
      //SE NAO TRATA TRANSFERENCIA BUSCAR C. DE CUSTO DO MES ANTERIOR
 	If !lTrataTrf .Or. lTrfAMES .Or. lTrfSld
		If !lTrfSld
            If lItemClVl
				nPosTrf := Ascan(aTransf, { |X| X[_TAtual,_TEmp]+X[_TAtual,_TFil]+X[_TAtual,_TCC]+X[_TAtual,_TMat]+X[_TAtual,_TItem]+X[_TAtual,_TClvl] == FWGrpCompany(cAliasArq)+TPR->PR_FILIAL+TPR->PR_CC+TPR->PR_MAT+TPR->PR_ITEM+TPR->PR_CLVL })
			Else
				nPosTrf := Ascan(aTransf, { |X| X[_TAtual,_TEmp]+X[_TAtual,_TFil]+X[_TAtual,_TCC]+X[_TAtual,_TMat] == FWGrpCompany(cAliasArq)+TPR->PR_FILIAL+TPR->PR_CC+TPR->PR_MAT })
			EndIf		
            
            //SE ANTERIOR IGUAL A DESTINO ENTAO NAO HOUVE TRANSFERENCIA.
			If nPosTrf > 0 .and. ;
				( lItemClVl .And.;
				aTransf[nPosTrf,_TAnter,_TEmp]+aTransf[nPosTrf,_TAnter,_TFil]+aTransf[nPosTrf,_TAnter,_TCC]+aTransf[nPosTrf,_TAnter,_TMat]+aTransf[nPosTrf,_TAnter,_TDta]+aTransf[nPosTrf,_TAnter,_TItem]+aTransf[nPosTrf,_TAnter,_TClvl] == ;
				aTransf[nPosTrf,_TDest ,_TEmp]+aTransf[nPosTrf,_TDest ,_TFil]+aTransf[nPosTrf,_TDest ,_TCC]+aTransf[nPosTrf,_TDest ,_TMat]+aTransf[nPosTrf,_TDest ,_TDta]+aTransf[nPosTrf,_TDest ,_TItem]+aTransf[nPosTrf,_TDest ,_TClvl] ) .Or.;
				( !lItemClVl .And.;
				aTransf[nPosTrf,_TAnter,_TEmp]+aTransf[nPosTrf,_TAnter,_TFil]+aTransf[nPosTrf,_TAnter,_TCC]+aTransf[nPosTrf,_TAnter,_TMat]+aTransf[nPosTrf,_TAnter,_TDta] == ;
				aTransf[nPosTrf,_TDest ,_TEmp]+aTransf[nPosTrf,_TDest ,_TFil]+aTransf[nPosTrf,_TDest ,_TCC]+aTransf[nPosTrf,_TDest ,_TMat]+aTransf[nPosTrf,_TDest ,_TDta] )
				nPosTrf:=0
			EndIf	
        EndIf
        
        If nPosTrf > 0
			If lItemClVl
				cBusMesAnt	:= aTransf[nPosTrf,_TAnter,_TFil]+aTransf[nPosTrf,_TAnter,_TMat]+aTransf[nPosTrf,_TAnter,_TCC]+aTransf[nPosTrf,_TAnter,_TItem]+aTransf[nPosTrf,_TAnter,_TClvl]+cAnoMesAnt
			Else
				cBusMesAnt	:= aTransf[nPosTrf,_TAnter,_TFil]+aTransf[nPosTrf,_TAnter,_TMat]+aTransf[nPosTrf,_TAnter,_TCC]+cAnoMesAnt
			EndIf
			cEmpQryAnt := aTransf[nPosTrf,_TAnter,_TEmp]
			//SE TRANSFERENCIA OCORREU APOS MES ATUAL PRESERVA MES ANTERIOR
			If aTransf[nPosTrf,_TAnter,_TDta] > cAnoMesAtu
				If lItemClVl
					cBusMesAtu	:= aTransf[nPosTrf,_TAnter,_TFil]+aTransf[nPosTrf,_TAnter,_TMat]+aTransf[nPosTrf,_TAnter,_TCC]+aTransf[nPosTrf,_TAnter,_TItem]+aTransf[nPosTrf,_TAnter,_TClvl]+cAnoMesAtu
				Else
					cBusMesAtu	:= aTransf[nPosTrf,_TAnter,_TFil]+aTransf[nPosTrf,_TAnter,_TMat]+aTransf[nPosTrf,_TAnter,_TCC]+cAnoMesAtu
				EndIf
				lTrfSld := .F.
            EndIf
			lTrfEmpAmes := aTransf[nPosTrf,_TAnter,_TEmp] <> aTransf[nPosTrf,_TAtual,_TEmp]
   		ElseIf TPR->PR_TIPMOVI == _Trfe_Sai
			If lItemClVl
				nPosTrfAmes	:= Ascan(aTransf, { |X| X[_TAnter,_TEmp]+X[_TAnter,_TFil]+X[_TAnter,_TCC]+X[_TAnter,_TMat]+X[_TAnter,_TItem]+X[_TAnter,_TClvl] == FWGrpCompany(cAliasArq)+TPR->PR_FILIAL+TPR->PR_CC+TPR->PR_MAT+TPR->PR_ITEM+TPR->PR_CLVL })
			Else
				nPosTrfAmes	:= Ascan(aTransf, { |X| X[_TAnter,_TEmp]+X[_TAnter,_TFil]+X[_TAnter,_TCC]+X[_TAnter,_TMat] == FWGrpCompany(cAliasArq)+TPR->PR_FILIAL+TPR->PR_CC+TPR->PR_MAT })
			EndIf
			If nPosTrfAmes > 0
				lTrfEmpAmes := aTransf[nPosTrfAmes,_TAnter,_TEmp] <> aTransf[nPosTrfAmes,_TAtual,_TEmp]
			Endif
		EndIf
	EndIf
    If !Empty(cTpRtProv)
        dbSelectArea( "RHT" )   
	Else
		dbSelectArea( "SRT" )
	Endif
	If lItemClVl
		dbSetOrder(4)
	Else
		dbSetOrder(1)
	EndIf
	
      If !Empty(cTpRtProv)
            RHT->( dbSetOrder( RetOrdem( "RHT" , "RHT_FILIAL+RHT_MAT+RHT_CC+DTOS(RHT_DTCALC)+RHT_TPPROV+RHT_VERBA" ) ) )     
            If RHT->(dbSeek( TPR->PR_FILIAL + TPR->PR_MAT + TPR->PR_CCMVTO + DTOS(dDataRef) + "1" ))
                  While RHT->(!Eof()) .And. RHT->RHT_FILIAL + RHT->RHT_MAT + RHT->RHT_CC + DTOS(RHT->RHT_DTCALC) == TPR->PR_FILIAL + TPR->PR_MAT + TPR->PR_CCMVTO + DTOS(dDataRef)
                        If ( cTpRtProv == 'RPF' .And. RHT->RHT_TPPROV == "1" ) 
                             If RHT->RHT_VERBA == aCodFol[960,1]
                                   aFerVenc[1,_Prov] := RHT->RHT_VALOR
                                   aFerVenc[1,_Avos] := 0 
                                   aFerVenc[1,_SalV] := 0
                             ElseIf RHT->RHT_VERBA == aCodFol[962,1] 
                                   aFerVenc[1,_Adic] := RHT->RHT_VALOR
                             ElseIf RHT->RHT_VERBA == aCodFol[961,1]
                                   aFerVenc[1,_1Ter] := RHT->RHT_VALOR
                             ElseIf RHT->RHT_VERBA == aCodFol[963,1]
                                   aFerVenc[1,_INSS] := RHT->RHT_VALOR
                             ElseIf RHT->RHT_VERBA == aCodFol[964,1]
                                   aFerVenc[1,_FGTS] := RHT->RHT_VALOR
                             ElseIf RHT->RHT_VERBA == aCodFol[965,1]
                                   aFerVenc[1,_PIS] := RHT->RHT_VALOR 
                             EndIf
                        ElseIf ( cTpRtProv == 'RPF' .And. RHT->RHT_TPPROV == "2" )
                             If RHT->RHT_VERBA == aCodFol[960,1]
                                   aFerProp[1,_Prov] := RHT->RHT_VALOR
                                   aFerProp[1,_Avos] := 0 
                                   aFerProp[1,_SalV] := 0
                             ElseIf RHT->RHT_VERBA == aCodFol[962,1] 
                                   aFerProp[1,_Adic] := RHT->RHT_VALOR
                             ElseIf RHT->RHT_VERBA == aCodFol[961,1]
                                   aFerProp[1,_1Ter] := RHT->RHT_VALOR
                             ElseIf RHT->RHT_VERBA == aCodFol[963,1]
                                   aFerProp[1,_INSS] := RHT->RHT_VALOR
                             ElseIf RHT->RHT_VERBA == aCodFol[964,1]
                                   aFerProp[1,_FGTS] := RHT->RHT_VALOR
                             ElseIf RHT->RHT_VERBA == aCodFol[965,1]
                                   aFerProp[1,_PIS] := RHT->RHT_VALOR 
                             EndIf                   
                        ElseIf ( cTpRtProv == 'RP13' .And. RHT->RHT_TPPROV == "3" ) 
                             If RHT->RHT_VERBA == aCodFol[966,1]
                                   a13Salar[1,_Prov] := RHT->RHT_VALOR
                                   a13Salar[1,_Avos] := 0 
                                   a13Salar[1,_SalV] := 0
                             ElseIf RHT->RHT_VERBA == aCodFol[967,1] 
                                   a13Salar[1,_Adic] := RHT->RHT_VALOR
                             ElseIf RHT->RHT_VERBA == aCodFol[968,1]
                                   a13Salar[1,_1Par] := RHT->RHT_VALOR
                             ElseIf RHT->RHT_VERBA == aCodFol[969,1]
                                   a13Salar[1,_INSS] := RHT->RHT_VALOR
                             ElseIf RHT->RHT_VERBA == aCodFol[970,1]
                                   a13Salar[1,_FGTS] := RHT->RHT_VALOR
                             ElseIf RHT->RHT_VERBA == aCodFol[971,1]
                                   a13Salar[1,_PIS] := RHT->RHT_VALOR
                             EndIf
                        Endif
                        RHT->(dbSkip())
                  EndDo
            Endif
            Return
      Endif
      //SE FUNCIONARIO VEIO DE OUTRA EMPRESA, ABRE A TABELA DA EMPRESA ANTERIOR
      If cEmpQryAnt != cEmpAnt
            cSvEmpAnt := cEmpAnt
           cModo         := FWModeAccess(cAliasSRT)
            UniqueKey( NIL , cAliasSRT , .T. )
            EmpOpenFile(cAliasSRT,cAliasSRT,1,.t.,cEmpQryAnt,@cModo)
            nAT := AT(cAliasSRT,cArqTab)
            IF nAT > 0
                  cArqTab := SubStr(cArqTab,1,nAT+2)+cModo+SubStr(cArqTab,nAT+4)
            Else
                  cArqTab += cAliasSRT+cModo+"/"
            EndIF 
      EndIf
      //CARREGA O ARRAY COM OS VALORES DO MES ANTERIOR. (NAO CARREGAR SE PROVISAO DE 13o SALARIO E MES DE REFERENCIA FOR JANEIRO)
      If dbSeek( cBusMesAnt ) .And. !(nTipoProv == _13Salar .And. Month(dDataRef) == 1)
            If !lTrfSld
                  If nTipoProv == _FerVenc
                        aProvisao[_Anter,_Dias] := SRT->RT_DFERVEN
                  ElseIf nTipoProv == _FerProp
                        aProvisao[_Anter,_Dias] := SRT->RT_DFERPRO
                  ElseIf nTipoProv == _13Salar
                        aProvisao[_Anter,_Avos] := SRT->RT_AVOS13S     
                  EndIf
            EndIf
            While !Eof() .And. Eval(cOrdemBusca) == cBusMesAnt
                  If SRT->RT_TIPPROV == Str(nTipoProv,1)
                      nPosVerba := Ascan(aVerba, { |X| X[4] == SRT->RT_VERBA })
                        lAdiant13 := SRT->RT_VERBA == aCodFol[268,1]    //Verba 1.parcela do 13.sal               
                        //BUSCA O ATUAL DO MES ANTERIOR E GRAVA NO ANTERIOR DO MES
                      If nPosVerba > 0 .And. aVerba[nPosVerba,2] == _Atual
                            nPosCol := aVerba[nPosVerba,3]
                             aProvisao[_Anter,nPosCol] := SRT->RT_VALOR  
                             //SE FOR DEMITIDO E TIVER VERBA DE 1PARCELA, GERAR MOVIMENTO DE BAIXA DE RESCISAO DA 1.PARCELA
                             If lAdiant13 .and. nTpMov == _Demitido .and. nTipoProv == _13Salar
                                   aProvisao[_BxRes,nPosCol] := SRT->RT_VALOR                                                
                             EndIf
                        EndIf
                  EndIf
                  dbSkip()
            EndDo
      EndIf
      //SE FUNCIONARIO VEIO DE OUTRA EMPRESA, RESTAURA A TABELA DA EMPRESA ATUAL
      If cEmpQryAnt != cEmpAnt
           cModo         := FWModeAccess(cAliasSRT)
            UniqueKey( NIL , cAliasSRT , .T. )
            EmpOpenFile(cAliasSRT,cAliasSRT,1,.t.,cSvEmpAnt,@cModo)
            nAT := AT(cAliasSRT,cArqTab)
            IF nAT > 0
                  cArqTab := SubStr(cArqTab,1,nAT+2)+cModo+SubStr(cArqTab,nAT+4)
            Else
                  cArqTab += cAliasSRT+cModo+"/"
            EndIF 
      EndIf
      //CARREGA O ARRAY COM OS VALORES DO MES ATUAL
      If dbSeek( cBusMesAtu )
            nTpMov := SRT->RT_TIPMOVI
            If !lTrfSld
                  If nTipoProv == _FerVenc
                        aProvisao[_NoMes,_Dias] := SRT->RT_DFALVEN
                        aProvisao[_Atual,_Dias] := SRT->RT_DFERVEN-If(SRT->RT_DFERVEN > 0, SRT->RT_DFERANT,0)-SRT->RT_DFALVEN
                  ElseIf nTipoProv == _FerProp
                        aProvisao[_NoMes,_Dias] := SRT->RT_DFALPRO
                        aProvisao[_Atual,_Dias] := SRT->RT_DFERPRO-SRT->RT_DFALPRO
                  ElseIf nTipoProv == _13Salar
                        aProvisao[_NoMes,_Avos] := SRT->RT_AVOS13S     
                  EndIf
            EndIf
            While !Eof() .And. Eval(cOrdemBusca) == cBusMesAtu
                  If SRT->RT_TIPPROV == Str(nTipoProv,1)   
                  nPosVerba := Ascan(aVerba, { |X| X[4] == SRT->RT_VERBA })   
                        lAdiant13 := SRT->RT_VERBA == aCodFol[268,1]    //Verba 1.parcela do 13.sal
                  If nPosVerba > 0
                            nPosLin := aVerba[nPosVerba,2]
                            nPosCol := aVerba[nPosVerba,3]
                             aProvisao[nPosLin,nPosCol] := SRT->RT_VALOR
                             aRecnos[nPosLin,nPosCol]   := RECNO()      
                             //SE FOR DEMITIDO E TIVER VERBA DE 1PARCELA, GERAR MOVIMENTO DE BAIXA DE RESCISAO DA 1.PARCELA
                             If lAdiant13 .and. nTpMov == _Demitido .and. nTipoProv == _13Salar
                                   aProvisao[_BxRes,nPosCol] := SRT->RT_VALOR                                                
                             EndIf
                        EndIf
                  EndIf 
                  dbSkip()
            EndDo
      EndIf
Endif //Fim do IF lSoMes
 
//CALCULA OS VALORES DE BAIXA E O VALOR NO MES
If lCalcula
      //GRAVAR VALORES COLUNA DE RESCISAO QUANDO NAO EXISTIR IDENTIF DE RESCISAO POIS,NESTE CASO, A BAIXA ESTARA NA COLUNA FERIAS
      If nTpMov == _Demitido
            fChkDemit(aProvisao,aVerba,nTipoProv,_BxRes,_BxFer)
      EndIf
      //TOTAL DE BAIXAS
      aProvisao[_BxTot,_Prov] := aProvisao[_BxTrf,_Prov]+aProvisao[_BxFer,_Prov]+aProvisao[_BxRes,_Prov]
      aProvisao[_BxTot,_Adic] := aProvisao[_BxTrf,_Adic]+aProvisao[_BxFer,_Adic]+aProvisao[_BxRes,_Adic]
      aProvisao[_BxTot,_1Ter] := aProvisao[_BxTrf,_1Ter]+aProvisao[_BxFer,_1Ter]+aProvisao[_BxRes,_1Ter]
      aProvisao[_BxTot,_INSS] := aProvisao[_BxTrf,_INSS]+aProvisao[_BxFer,_INSS]+aProvisao[_BxRes,_INSS]
      aProvisao[_BxTot,_FGTS] := aProvisao[_BxTrf,_FGTS]+aProvisao[_BxFer,_FGTS]+aProvisao[_BxRes,_FGTS]
      aProvisao[_BxTot,_PIS]  := aProvisao[_BxTrf,_PIS] +aProvisao[_BxFer,_PIS] +aProvisao[_BxRes,_PIS]
      //PROVISAO DO MES
      aProvisao[_NoMes,_Prov] := aProvisao[_Atual,_Prov]-aProvisao[_Anter,_Prov]+If( nTpMov == _Demitido .And. lProvResc, aProvisao[_Bx13O,_Prov], aProvisao[_BxTot,_Prov] )
      aProvisao[_NoMes,_Adic] := aProvisao[_Atual,_Adic]-aProvisao[_Anter,_Adic]+If( nTpMov == _Demitido .And. lProvResc, aProvisao[_Bx13O,_Adic], aProvisao[_BxTot,_Adic] )
      aProvisao[_NoMes,_1Ter] := aProvisao[_Atual,_1Ter]-aProvisao[_Anter,_1Ter]+If( nTpMov == _Demitido .And. lProvResc, aProvisao[_Bx13O,_1Ter], aProvisao[_BxTot,_1Ter] )
      aProvisao[_NoMes,_INSS] := aProvisao[_Atual,_INSS]-aProvisao[_Anter,_INSS]+If( nTpMov == _Demitido .And. lProvResc, aProvisao[_Bx13O,_INSS], aProvisao[_BxTot,_INSS] )
      aProvisao[_NoMes,_FGTS] := aProvisao[_Atual,_FGTS]-aProvisao[_Anter,_FGTS]+If( nTpMov == _Demitido .And. lProvResc, aProvisao[_Bx13O,_FGTS], aProvisao[_BxTot,_FGTS] )
      aProvisao[_NoMes,_PIS]  := aProvisao[_Atual,_PIS] -aProvisao[_Anter,_PIS] +If( nTpMov == _Demitido .And. lProvResc, aProvisao[_Bx13O,_PIS],  aProvisao[_BxTot,_PIS]  )
      If lProvResc .And. nTpMov == _Demitido
            //ZERA A LINHA DE PROVISAO ATUAL QUANDO O FUNCIONARIO ESTIVER DEMITIDO E O PARAMETRO MV_PROVRES ESTIVER CONFIGURADO COM "S"
            For nCnt1 := 1 To _Colunas
                  aProvisao[_Atual,nCnt1] := 0
            Next nCnt1
      Endif
      If lTrfAMES
            //TRANSFERE O SALDO ANTERIOR PARA A COLUNA TRANSF. ENTRADA
            If TPR->PR_TIPMOVI == _Trfe_Ent .Or. ((TPR->PR_TIPMOVI == _Anter) .And. (aProvisao[_BxRes,_Prov]+aProvisao[_BxRes,_Adic]+aProvisao[_Bx13O,_Prov]+aProvisao[_Bx13O,_Adic] <> 0) .And. (nPosTrf > 0))
				For nCnt1 := 1 To _Colunas
					If !lTrfEmpAmes 
                        aProvisao[_TrfEnt,nCnt1] := aProvisao[_Anter,nCnt1]
                        aProvisao[_Anter,nCnt1]  := 0
                  	Else
                        aProvisao[_NoMes,nCnt1] += aProvisao[_Anter,nCnt1]
                        aProvisao[_Anter,nCnt1]  := 0 
					EndIf 
                Next nCnt1                  
                If aProvisao[_Bx13O,_Prov]+aProvisao[_Bx13O,_Adic] > 0
					aProvisao[_BxRes,_1Par] := 0
     			EndIf 
            ElseIf nPosTrf > 0 .and. nTipoProv == 2 .and. TPR->PR_TIPMOVI == _Anter
                  For nCnt1 := 1 To _Colunas
                        aProvisao[_TrfEnt,nCnt1] := aProvisao[_Anter,nCnt1]
                        aProvisao[_Anter,nCnt1]  := 0
                  Next nCnt1  
            //TRANSFERE O SALDO DE BAIXA POR TRANSF. P/COLUNA TRANSF. SAIDA
            ElseIf TPR->PR_TIPMOVI == _Trfe_Sai
                        For nCnt1 := 1 To _Colunas
                             If !lTrfEmpAmes         // nao e transferencia entre empresas.
                                   aProvisao[_TrfSai,nCnt1] := aProvisao[_BxTrf,nCnt1]
                                   aProvisao[_BxTrf,nCnt1]  := 0
                             Else
                                   aProvisao[_NoMes,nCnt1]  -= aProvisao[_BxTrf,nCnt1]
                                   aProvisao[_BxTrf,nCnt1]  := 0
                             Endif
                        Next nCnt1
                        //-- AJUSTA O VALOR DA BAIXA DA PRIMEIRA PARCELA NA LINHA
                        //-- DE TRANSFERENCIA DE SAIDA
                        //-- DEVOLVE O VALOR PARA A LINHA NO MES
                        //-- TRATAMENTO EXCLUSIVO PARA 13o SALARIO - 1A PARCELA
                        IF !LTRFEMPAMES .AND. NTIPOPROV == _13SALAR // NAO E TRANSFERENCIA ENTRE EMPRESAS.
                             aProvisao[_TrfSai,_1Par]  := aProvisao[_Anter,_1Par]
                             aProvisao[_NoMes,_1Par]   += aProvisao[_Anter,_1Par]
                        Endif
                  EndIf
            EndIf
            If lTrfSld .And. fChkIdent(aVerba,If(nTipoProv==2,1,nTipoProv),{_BxRes},.F.) .and. fChkIdent(aVerba,If(nTipoProv==2,1,nTipoProv),{_BxTrf},.F.)
                  For nCnt1 := 1 To _Colunas
                        aProvisao[_NoMes,nCnt1] := aProvisao[_Anter,nCnt1]
                        aProvisao[_Anter,nCnt1] := 0
                  Next nCnt1
            EndIf
      Else
			//SE NAO CALCULAR, LIMPAR AS COLUNAS DO ARRAY (MENOS O MES ANT)
			fLimpaArray( @aProvisao, 0, Str(_Anter,1) )
      EndIf
 
RestArea(aArea)
 
Return Nil
/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Funcao    ³fOpenLog	³ Autor ³Mauricio Takakura     ³ Data ³05/07/2007³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Decricao ³Cria/Abre arquivo de LOG								     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL															 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³SIGAGPE     												 ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/  
Static Function fOpenLog(cDbfLog)
Local lRet			:= .T.
Local aFields		:= {}
Local cRdd			:= "TOPCONN"

Begin Sequence
	IF ( !MsFile( cNameFile , NIL , cRdd ) )
		AADD(aFields, { "CHAVE"		,"C",20							,0} )	// Chave do Processamento
		AADD(aFields, { "REQUIS"	,"C",8							,0} )	// Nro da Requisicao em Processamento
		AADD(aFields, { "CONTROL"	,"C",1							,0} )	// Codigo de Controle -> 1-Processando/2-Finalizado
		AADD(aFields, { "FILIAL"	,"C",TamSX3("RA_FILIAL")[1]		,0} )	// Filial do ultimo Funcionario processado
		AADD(aFields, { "MAT"		,"C",TamSX3("RA_MAT")[1]		,0} )	// Matricula do ultimo Funcionario Processado
		AADD(aFields, { "NOME"		,"C",TamSX3("RA_NOME")[1]		,0} )	// Nome do ultimo Funcionario
		AADD(aFields, { "ROTEIRO"	,"C",TamSX3("RY_CALCULO")[1]	,0} )  	// Roteiro do calculo
		AADD(aFields, { "DT_GRAVA"	,"C",8							,0} )	// Data da ultima gravacao
		AADD(aFields, { "HR_GRAVA"	,"C",8							,0} )	// Hora da ultima gravacao
		IF !( lRet := MsCreate( cNameFile , aFields , cRdd ) )
			Break
		EndIf
	EndIf

	IF !( lRet := MsOpenDbf( .T. , cRdd, cNameFile , cDbfLog , .T. , .F. , .F. , .F. ) )
		Break
	EndIf

	IF !( lRet := ( Select( cDbfLog ) > 0 ) )
		Break
	EndIf
End

Return (lRet)  

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ"±±
±±ºPrograma  ³fCriaMnemoºAutor  ³Microsiga           º Data ³  07/15/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fMontaMnemo( cFil , aMnemonicos )
Local aArea			:= GetArea()
Local aAreaRCA		:= RCA->( GetArea() )
Local cFilRCA 		:= xFilial( "RCA" , cFil )   
Local lSet				:= .T.
Static lDefautsMnemos  
DEFAULT aMneMonicos        := {}
DEFAULT lDefautsMnemos     := .T.

//VERIFICA SE DEVE CARREGAR OS MNEMONICOS PADROES DO SISTEMA
IF  ( lDefautsMnemos )  
       fCarMnemo(cFilRCA) 
       lDefautsMnemos := .F.
EndIF

IF Empty(aMnemonicos) 
       RCA->( dbSetOrder( 01 ) )
       IF ( lSet := RCA->( dbSeek( cFilRCA , .F. ) ) )
             While RCA->( !Eof() .and. ( RCA_FILIAL == cFilRCA ) )
                    RCA->( aAdd( aMneMonicos , { AllTrim( RCA->RCA_MNEMON ) , RCA_DESC , RCA_CONTEUDO , RCA_TIPO , Recno() ,RCA_ACUMUL } ) )
                    RCA->( dbSkip() )
             End While
       EndIF
       ( RestArea( aAreaRCA ) , RestArea( aArea ) )
EndIF

Return( lSet )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ"±±
±±ºPrograma  ³fSalHvar ºAutor  ³Marcia Moura         º Data ³  27/09/2016 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fSalHvar()

Private dDataHorj := dDtBasFer

fSalInc(@nSalario,@nSalMes,@nSalHora,@nSalDia,.T.,,,,dDataRef,,@nPeric,@nInsal,@nAdtServ,@nAdcConf,@nAdcTransf,@nOutros,If(Type("nSalhInc") <> "U",@nSalhInc,0))

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ"±±
±±ºPrograma  ³fChkMatTpc ºAutor  ³Renan Borges       º Data ³  07/10/2016 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Static Function fChkMatTpc(cFil, cMat, cTpc)
Local lRet		:= .T.
Local aArea	:= GetArea()

DbSelectArea("SRA")
DbSetOrder(1)
If(dbSeek(cFil+cMat))
	If SRA->RA_TPCONTR <> cTpc
		lRet	:= .F. 
	EndIf
EndIf

RestArea(aArea)
Return lRet